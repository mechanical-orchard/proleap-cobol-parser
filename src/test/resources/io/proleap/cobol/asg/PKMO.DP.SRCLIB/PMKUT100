      *----------------------------------------------------------               
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------               
       PROGRAM-ID.             PMKUT100.                                        
       AUTHOR.                 ALEXIS KEIKER                                    
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           APRIL 2003.                                      
       DATE-COMPILED.                                                           
      *----------------------------------------------------------               
      *          PMKUT100 - PROMO ITEM SUMMARY EXTRACT                          
      *----------------------------------------------------------               
      * THIS PROGRAM WILL EXTRACT PROMO ITEMS FOR EITHER AN                     
      * ACTUAL/FORECAST REPORT OR A FORCAST ONLY REPORT.  IT                    
      * WILL WRITE OUT ALL RECORDS FOUND TO AN OUTPUT FILE.                     
      * THE CONTROL CARD THAT IS READ INTO THIS PROGRAM IS USED                 
      * TO DETERMINE WHETHER IT WILL BE FINDING ACTUALS/FORECAST                
      * OR JUST FORECAST RESULTS.  THERE ARE THREE DRIVING CURSORS              
      * WITHIN THIS PROGRAM.  OUT OF THE THREE ONE IS CHOSEN TO                 
      * GET THE DATA DEPENDING ON THE DATA CONTAINED IN THE CONTROL             
      * CARD.  ONE CURSOR GETS ALL PROMO ITEMS THAT WERE CONTAINED              
      * WITHIN AN AD EVENT THAT ENDED THE DAY BEFORE.  THE RECORDS              
      * THAT THIS CURSOR FETCHES ARE USED IN THE ACTUAL/FORECAST                
      * REPORT.  THE SECOND CURSOR FINDS ANY PROMO ITEMS THAT                   
      * HAVE A LOCKDOWN DATE OF TODAY.  THE RECORDS THAT THIS                   
      * CURSOR FETCHES ARE USED IN JUST FORECAST REPORTING.  THE                
      * THIRD CURSOR WILL FIND ANY PROMO ITEMS THAT ARE WITHIN                  
      * A PROMOTION THAT STARTS TODAY + THE NUMBER THAT IS CONTAINED            
      * WITHIN THE CONTROL CARD.  THIS IS ALSO USED IN JUST                     
      * FORECAST REPORTING.                                                     
      *                                                                         
      *----------------------------------------------------------               
      * HISTORY LOG:                                                            
      *----------------------------------------------------------               
      * DATE:         9/26/2003                                                 
      * WR/IR#:       WR21770                                                   
      * PROGRAMMER:   COLLEEN CUMMINGS                                          
      * DESCRIPTION:  ABENDED ON DUPLICATE AD PAGES (2500-).                    
      * ADDED DISPLAYS AND BYPASS ON ABEND TO GET THE REPORT DONE.              
      *                                                                         
      * DATE:         10/16/2003                                                
      * WR/IR#:       P000598525                                                
      * PROGRAMMER:   COLLEEN CUMMINGS                                          
      * DESCRIPTION:  CHANGED TO_CHAR TO TO_DATE ON DATE COMPARISONS.           
      * WAS NOT CALCULATING INTO THE NEW YEAR CORRECTLY.                        
      *                                                                         
      * DATE:         10/20/2003                                                
      * WR/IR#:       P000599717                                                
      * PROGRAMMER:   COLLEEN CUMMINGS                                          
      * DESCRIPTION:  CHANGED TO_DATE TO BE BETWEENS TO ACCOMODATE              
      * ORACLE DEFINITIONS.                                                     
      *                                                                         
      * DATE:         10/20/2003                                                
      * WR/IR#:       P000599733                                                
      * PROGRAMMER:   COLLEEN CUMMINGS                                          
      * DESCRIPTION:  FIXED TO_DATE AGAIN - FORMAT WRONG.                       
      *                                                                         
      * DATE:         06/20/2014                                                
      * WR/IR#:       PRB0047935                                                
      * PROGRAMMER:   COGNIZANT                                                 
      * DESCRIPTION:  MODIFIED THE CODE TO WRITE A SEAPRATE FILE OUT02          
      *               (A COPY OF OUT01) IN WHICH THE PROMO ITEMS WILL           
      *               HAVE THE ADV INDICATOR AS 'Y' THOUGH THE PAGE             
      *               NUMBERS WERE NOT ASSIGNED FOR THE PROMO ITEM.             
      *                                                                         
      *                                                                         
      * DATE:         07/25/2014                                                
      * WR/IR#:       PRB0047935 - PART2                                        
      * PROGRAMMER:   COGNIZANT                                                 
      * DESCRIPTION:  REMOVED THE CODE WHICH CREATES THE OUT02                  
      *               (A COPY OF OUT01) AS A PART OF PRB0047935 PART1.          
      *               INSTAED MODIFIED THE CODE FOR OUT01 TO                    
      *               HAVE THE ADV INDICATOR AS 'Y' AND THE PAGE NUMBER         
      *               AS 999 THOUGH THE PAGE NUMBERS WERE NOT ASSIGNED          
      *               FOR THE PROMO ITEM.                                       
      *                                                                         
      * DATE:         07/30/2018                                                
      * WR/IR#:       CHG0204126                                                
      * PROGRAMMER:   COGNIZANT                                                 
      * DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE                     
      *----------------------------------------------------------               
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------               
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT CC-RUN-DTE-FILE                                               
                  ASSIGN TO INP01.                                              
                                                                                
070941     SELECT ORACLE-LOGON-FILE                                             
070941            ASSIGN TO ORALOGON.                                           
                                                                                
           SELECT PROMO-ITM-SUMM-FILE                                           
                  ASSIGN TO OUT01.                                              
                                                                                
      *----------------------------------------------------------               
       DATA DIVISION.                                                           
      *----------------------------------------------------------               
       FILE SECTION.                                                            
                                                                                
       FD  CC-RUN-DTE-FILE                                                      
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS CC-RUN-DTE-REC.                                       
       01  CC-RUN-DTE-REC                   PIC X(80).                          
                                                                                
070941 FD  ORACLE-LOGON-FILE                                                    
070941     RECORDING MODE IS F                                                  
070941     LABEL RECORDS ARE STANDARD                                           
070941     BLOCK CONTAINS 0 RECORDS                                             
070941     RECORD CONTAINS 80 CHARACTERS                                        
070941     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
070941 01  OL-ORACLE-LOGON-RECORD           PIC X(80).                          
                                                                                
       FD  PROMO-ITM-SUMM-FILE                                                  
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS PROMO-ITM-SUMM-REC.                                   
       01  PROMO-ITM-SUMM-REC               PIC X(750).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------               
      * ABEND- ABEND VARIABLES                                                  
      *----------------------------------------------------------               
       01  ABEND-CODE                       PIC S9(04) COMP VALUE +0.           
           88  ABEND-4001-WRITE-ERROR                  VALUE +4001.             
           88  ABEND-4002-READ-ERROR                   VALUE +4002.             
           88  ABEND-4003-CTRL-CARD-ERROR              VALUE +4003.             
           88  ABEND-4004-TABLE-ERROR                  VALUE +4004.             
           88  ABEND-4005-OPEN-ERROR                   VALUE +4005.             
           88  ABEND-4006-CLOSE-ERROR                  VALUE +4006.             
           88  ABEND-4007-SEQUENCE-ERROR               VALUE +4007.             
           88  ABEND-4008-DATA-ERROR                   VALUE +4008.             
           88  ABEND-4009-KEY-DATA-ERROR               VALUE +4009.             
           88  ABEND-4013-SQL-ERROR                    VALUE +4013.             
           88  ABEND-4014-ORACLE-LOGON-ERROR           VALUE +4014.             
           88  ABEND-4019-DATE-ROUTINE-ERROR           VALUE +4019.             
           88  ABEND-4444-FORCED-ABEND                 VALUE +4444.             
                                                                                
      *----------------------------------------------------------               
      * CC- CONTROL CARD                                                        
      *----------------------------------------------------------               
       01  CC-DATE-RECORD.                                                      
           05  CC-RUN-DATE                  PIC X(08).                          
           05  FILLER                       PIC X(01).                          
           05  CC-RPT-TYP-CDE               PIC X(01).                          
           05  FILLER                       PIC X(01).                          
           05  CC-SUB-DAYS                  PIC 9(03).                          
           05  FILLER                       PIC X(01).                          
           05  CC-ADD-DAYS                  PIC 9(03).                          
           05  FILLER                       PIC X(62).                          
                                                                                
      *----------------------------------------------------------               
      * PS- PROGRAM SWITCHES                                                    
      *----------------------------------------------------------               
       01  PS-PROGRAM-SWITCHES.                                                 
070941     05  PS-END-OF-LOGON-FILE-SW      PIC X(01)  VALUE 'N'.               
070941         88  PS-END-OF-LOGON-FILE                VALUE 'Y'.               
070941     05  PS-ORA-SRC-CTL-DONE-SW       PIC X(01)  VALUE 'N'.               
070941         88  PS-ORA-SRC-EMPTY                    VALUE 'Y'.               
           05  PS-CONTROL-CARD-SW           PIC X(01)  VALUE 'N'.               
               88  PS-CONTROL-CARD-EOF                 VALUE 'Y'.               
               88  PS-CONTROL-CARD-NOT-EOF             VALUE 'N'.               
           05  PS-END-OF-ITEM-CSR-SW        PIC X(01)  VALUE 'N'.               
               88  PS-END-OF-ITEM-CSR                  VALUE 'Y'.               
               88  PS-NOT-END-OF-ITEM-CSR              VALUE 'N'.               
           05  PS-END-OF-TIER-CSR-SW        PIC X(01)  VALUE 'N'.               
               88  PS-END-OF-TIER-CSR                  VALUE 'Y'.               
               88  PS-NOT-END-OF-TIER-CSR              VALUE 'N'.               
           05  PS-END-OF-ADMIN-CSR-SW       PIC X(01)  VALUE 'N'.               
               88  PS-END-OF-ADMIN-CSR                 VALUE 'Y'.               
               88  PS-NOT-END-OF-ADMIN-CSR             VALUE 'N'.               
           05  PS-END-OF-ADVER-CSR-SW       PIC X(01)  VALUE 'N'.               
               88  PS-END-OF-ADVER-CSR                 VALUE 'Y'.               
               88  PS-NOT-END-OF-ADVER-CSR             VALUE 'N'.               
           05  PS-SALES-SW                  PIC X(01)  VALUE 'N'.               
               88  PS-NO-SALES                         VALUE 'Y'.               
               88  PS-SALES                            VALUE 'N'.               
           05  PS-PRICG-AVAIL-SW            PIC X(01)  VALUE 'N'.               
               88  PS-NO-PRICG                         VALUE 'Y'.               
               88  PS-PRICG-AVAIL                      VALUE 'N'.               
                                                                                
070941 01  CC-ORACLE-CONNECTION.                                                
070941     05  CC-ORACLE-USERID             PIC X(40).                          
070941         88 CC-AUTO-LOGON                        VALUE '/'.               
070941     05  CC-ORACLE-PASSWORD           PIC X(40).                          
                                                                                
      *----------------------------------------------------------               
      * PC- PROGRAM CONSTANTS                                                   
      *----------------------------------------------------------               
       01  PC-PROGRAM-CONSTANTS.                                                
070941     05  PC-AUTO-LOGON                PIC X(1)   VALUE '/'.               
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE                    
                                                      'PMKUT100'.               
                                                                                
      *----------------------------------------------------------               
      * PV- PROGRAM VARIABLES                                                   
      *----------------------------------------------------------               
       01  PV-PROGRAM-VARIABLES.                                                
070941     05  PV-ORACLE-USERID-LEN         PIC S9(4)  VALUE ZEROS              
070941                                                 COMP SYNC.               
070941     05  PV-ORACLE-PASSWORD-LEN       PIC S9(4)  VALUE ZEROS              
070941                                                 COMP SYNC.               
070941     05  PC-AUTO-LOGON                PIC X(01)  VALUE '/'.               
           05  PV-TABLE-NAME                PIC X(30)  VALUE SPACES.            
           05  PV-SQL-CODE                  PIC 9(09)  VALUE 0.                 
           05  PV-OPERATION-MSG             PIC X(60)  VALUE SPACES.            
           05  PV-OPERATION-MSG-1           PIC X(40)  VALUE SPACES.            
           05  PV-PARAGRAPH-NAME            PIC X(30)  VALUE SPACES.            
           05  PV-HOLD-PROMO-ID             PIC 9(10)  VALUE 0.                 
           05  PV-VOL-PCT-CHG               PIC S9(07)V9(03) COMP-3             
                                                       VALUE +0.                
           05  PV-SELL-THRU-PCT             PIC S9(07)V9(03) COMP-3             
                                                       VALUE +0.                
           05  PV-ROUND-PREP-UNT-QTY        PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-ROUND-EOH-INV-QTY         PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-ROUND-SLS-RTL-AMT         PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-ROUND-MKDN-RTL-AMT        PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-ROUND-SLS-UNT-QTY         PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-ROUND-SLS-COST-AMT        PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-RUN-TIME1.                                                    
               10  PV-RUN-DATE1             PIC X(08).                          
               10  FILLER                   PIC X(01) VALUE '-'.                
               10  PV-RUN-HH-MI-SS1         PIC X(08) VALUE '00.00.00'.         
           05  PV-RUN-TIME2.                                                    
               10  PV-RUN-DATE2             PIC X(08).                          
               10  FILLER                   PIC X(01) VALUE '-'.                
               10  PV-RUN-HH-MI-SS2         PIC X(08) VALUE '23.59.59'.         
           05  PV-RUN-TIME1-FMTD            PIC X(17).                          
           05  PV-RUN-TIME2-FMTD            PIC X(17).                          
           05  PV-NULL-IND.                                                     
               10  PV-AD-SP-NULL            PIC S9(4) VALUE +0 COMP.            
               10  PV-AD-PG-SEQ-NULL        PIC S9(4) VALUE +0 COMP.            
               10  PV-MIN-REG-RTL-NULL      PIC S9(4) VALUE +0 COMP.            
               10  PV-MAX-REG-RTL-NULL      PIC S9(4) VALUE +0 COMP.            
               10  PV-MIN-PRM-RTL-NULL      PIC S9(4) VALUE +0 COMP.            
               10  PV-MAX-PRM-RTL-NULL      PIC S9(4) VALUE +0 COMP.            
               10  PV-SLS-QTY-NULL          PIC S9(4) VALUE +0 COMP.            
               10  PV-SLS-COST-NULL         PIC S9(4) VALUE +0 COMP.            
               10  PV-SLS-RTL-NULL          PIC S9(4) VALUE +0 COMP.            
               10  PV-MKDN-RTL-NULL         PIC S9(4) VALUE +0 COMP.            
               10  PV-MKUP-PCT-NULL         PIC S9(4) VALUE +0 COMP.            
               10  PV-ABBR-NM-NULL          PIC S9(4) VALUE +0 COMP.            
               10  PV-ITM-DESC-NULL         PIC S9(4) VALUE +0 COMP.            
               10  PV-PREP-UNT-QTY-NULL     PIC S9(4) VALUE +0 COMP.            
               10  PV-EOH-INV-QTY-NULL      PIC S9(4) VALUE +0 COMP.            
               10  PV-FCST-MKUP-PCT-NULL    PIC S9(4) VALUE +0 COMP.            
               10  PV-GMA-FRST-NM-NULL      PIC S9(4) VALUE +0 COMP.            
               10  PV-GMA-LAST-NM-NULL      PIC S9(4) VALUE +0 COMP.            
               10  PV-DMA-FRST-NM-NULL      PIC S9(4) VALUE +0 COMP.            
               10  PV-DMA-LAST-NM-NULL      PIC S9(4) VALUE +0 COMP.            
               10  PV-BYR-FRST-NM-NULL      PIC S9(4) VALUE +0 COMP.            
               10  PV-BYR-LAST-NM-NULL      PIC S9(4) VALUE +0 COMP.            
                                                                                
           05  PV-TIER-NULL-IND.                                                
               10  PV-THRS-QTY-NULL         PIC S9(4) VALUE +0 COMP.            
               10  PV-THRS-AMT-NULL         PIC S9(4) VALUE +0 COMP.            
               10  PV-TIER-AMT-NULL         PIC S9(4) VALUE +0 COMP.            
               10  PV-TIER-PCT-NULL         PIC S9(4) VALUE +0 COMP.            
               10  PV-PRICG-TYP-NULL        PIC S9(4) VALUE +0 COMP.            
                                                                                
       01  PV-SELECT-VARIABLES.                                                 
           05  PV-AD-EVNT-ID                PIC 9(10)  VALUE ZEROES.            
           05  PV-AD-EVNT-NBR-TXT           PIC X(15)  VALUE SPACES.            
           05  PV-AD-EVNT-NM                PIC X(40)  VALUE SPACES.            
           05  PV-AD-EVNT-STRT-TMST         PIC X(19)  VALUE SPACES.            
           05  PV-AD-EVNT-END-TMST          PIC X(19)  VALUE SPACES.            
           05  PV-PROMO-ID                  PIC 9(10)  VALUE ZEROES.            
           05  PV-PROMO-NM                  PIC X(40)  VALUE SPACES.            
           05  PV-PROMO-ABBR-NM             PIC X(05)  VALUE SPACES.            
           05  PV-PROMO-TYP-CDE             PIC X(02)  VALUE SPACES.            
           05  PV-REG-PROMO-TYP-CDE         PIC X(02)  VALUE SPACES.            
           05  PV-PROMO-DFLT-STRT-TMST      PIC X(19)  VALUE SPACES.            
           05  PV-PROMO-DFLT-END-TMST       PIC X(19)  VALUE SPACES.            
           05  PV-PROMO-ITM-ID              PIC 9(10)  VALUE ZEROES.            
           05  PV-TIER-PROMO-ITM-ID         PIC 9(10)  VALUE ZEROES.            
           05  PV-TIER-PROMO-ID             PIC 9(10)  VALUE ZEROES.            
           05  PV-DEPT-NBR                  PIC 9(04)  VALUE ZEROES.            
           05  PV-PROMO-ITM-STAT-CDE        PIC X(01)  VALUE SPACES.            
           05  PV-PROMO-ITM-ADV-IND         PIC X(01)  VALUE SPACES.            
           05  PV-PROMO-ITM-DESC            PIC X(100) VALUE SPACES.            
           05  PV-PROMO-ITM-STRT-TMST       PIC X(19)  VALUE SPACES.            
           05  PV-PROMO-ITM-END-TMST        PIC X(19)  VALUE SPACES.            
           05  PV-ACTL-PREP-UNT-QTY         PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-ACTL-EOH-INV-QTY          PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-MIN-UNT-REG-RTL-AMT       PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-MAX-UNT-REG-RTL-AMT       PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-MIN-UNT-PRM-RTL-AMT       PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-MAX-UNT-PRM-RTL-AMT       PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-FCST-MIN-REG-RTL-AMT      PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-FCST-MAX-REG-RTL-AMT      PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-FCST-MIN-PROMO-RTL-AMT    PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-FCST-MAX-PROMO-RTL-AMT    PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-SLS-UNT-QTY               PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-SLS-COST-AMT              PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-SLS-RTL-AMT               PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-MKDN-RTL-AMT              PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-TOT-MKUP-PCT              PIC S9(03)V9(03) COMP-3             
                                                       VALUE +0.                
           05  PV-FCST-SLS-RTL-AMT          PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-FCST-SLS-UNT-QTY          PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-FCST-MKDN-RTL-AMT         PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-PROJD-PREP-UNT-QTY        PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-FCST-SLS-COST-AMT         PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-FCST-TOT-MKUP-PCT         PIC S9(03)V9(03) COMP-3             
                                                       VALUE +0.                
           05  PV-GMA-NBR                   PIC 9(04)  VALUE ZEROES.            
           05  PV-DMA-NBR                   PIC 9(04)  VALUE ZEROES.            
           05  PV-BYR-NBR                   PIC 9(04)  VALUE ZEROES.            
           05  PV-DEPT-NM                   PIC X(20)  VALUE SPACES.            
           05  PV-GMA-FRST-NAME             PIC X(40)  VALUE SPACES.            
           05  PV-GMA-LAST-NAME             PIC X(40)  VALUE SPACES.            
           05  PV-DMA-FRST-NAME             PIC X(40)  VALUE SPACES.            
           05  PV-DMA-LAST-NAME             PIC X(40)  VALUE SPACES.            
           05  PV-BUY-FRST-NAME             PIC X(40)  VALUE SPACES.            
           05  PV-BUY-LAST-NAME             PIC X(40)  VALUE SPACES.            
           05  PV-AD-PG-NBR                 PIC 9(03)  VALUE ZEROES.            
           05  PV-AD-SP-CDE                 PIC X(04)  VALUE SPACES.            
           05  PV-AD-PG-SEQ-NBR             PIC 9(03)  VALUE 0.                 
           05  PV-TIER-SLS-RTL-AMT          PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-TIER-SLS-UNT-QTY          PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-TIER-MKDN-RTL-AMT         PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-TIER-SLS-COST-AMT         PIC S9(08)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-TIER-THRS-QTY             PIC S9(05) VALUE +0.                
           05  PV-TIER-THRS-AMT             PIC S9(07)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-TIER-DISC-AMT             PIC S9(07)V9(02) COMP-3             
                                                       VALUE +0.                
           05  PV-TIER-DISC-PCT             PIC S9(03)V9(03) COMP-3             
                                                       VALUE +0.                
           05  PV-RCPT-CMMT-TXT             PIC X(40)  VALUE SPACES.            
           05  PV-PRICG-TYP-CDE             PIC X(03)  VALUE SPACES.            
           05  PV-TIER-PRICG-ID             PIC 9(10)  VALUE ZEROES.            
           05  PV-TEMP-VALUE                PIC X(01)  VALUE SPACES.            
           05  PV-ADVER-ABBR-NM             PIC X(02)  VALUE SPACES.            
                                                                                
      *----------------------------------------------------------               
      * PA- PROGRAM ACCUMULATORS                                                
      *----------------------------------------------------------               
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECS-WRITTEN              PIC 9(09) VALUE ZEROES.             
           05  PA-TIER-CNT                  PIC 9(09) VALUE ZEROES.             
           05  PA-ITEM-CNT                  PIC 9(09) VALUE ZEROES.             
           05  PA-ADMIN-CNT                 PIC 9(09) VALUE ZEROES.             
                                                                                
      *----------------------------------------------------------               
      * TBL- TABLES                                                             
      *----------------------------------------------------------               
       01  TBL-ADVERSION-ARRAY.                                                 
           05  FILLER                       OCCURS 15 TIMES                     
                                            INDEXED BY TBL-ADVER-INDX.          
               10  TBL-ADVER-ABBR-NM        PIC X(02)  VALUE SPACES.            
                                                                                
      *----------------------------------------------------------               
      * COPYBOOK FOR THE PROMO ITEM SUMMARY EXTRACT - OUTPUT                    
      *----------------------------------------------------------               
           COPY PMRD100.                                                        
                                                                                
      *----------------------------------------------------------               
      *                      O  R  A  C  L  E                                   
      *==========================================================               
      *                                                                         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *                                                                         
      *----------------------------------------------------------               
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *----------------------------------------------------------               
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *----------------------------------------------------------               
                                                                                
       01  PC-ENT01-TYPE-CDE                PIC X(02)  VALUE '01'.              
       01  PV-ORACLE-ID                     PIC X(1)   VALUE '/'.               
                                                                                
       01  USERNAME                         PIC X(10)  VARYING.                 
       01  PASSWD                           PIC X(10)  VARYING.                 
                                                                                
       01  ORACLE-RETURN                    PIC X(200) VALUE SPACES.            
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
       EJECT                                                                    
                                                                                
      *----------------------------------------------------------               
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------               
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-CURSORS                                         
                   UNTIL PS-END-OF-ITEM-CSR.                                    
                                                                                
           EVALUATE TRUE                                                        
             WHEN CC-RPT-TYP-CDE = 'A'                                          
                  PERFORM 2030-CLOSE-ACTL-ITM-CSR                               
             WHEN CC-RPT-TYP-CDE = 'F' AND CC-ADD-DAYS = ZEROES                 
                  PERFORM 2060-CLOSE-FCST-ITM-CSR                               
             WHEN CC-RPT-TYP-CDE = 'F' AND CC-ADD-DAYS NOT = ZEROES             
                  PERFORM 2090-CLOSE-FUTFCST-ITM-CSR                            
           END-EVALUATE.                                                        
                                                                                
           PERFORM 9999-WRAPUP.                                                 
                                                                                
           GOBACK.                                                              
                                                                                
      *0000-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 1000-INITIALIZATION.                                                    
      *     INITIALIZATION AREA - INITIALIZES ALL VARIABLES, OPENS              
      *     FILES, READS INPUT FILES, AND PROCESSES ANY INPUT FILE              
      *     PARAMETERS IT MAY NEED TO USE TO FETCH PROMO ITEMS                  
      *     FROM THE CURSOR.                                                    
      *----------------------------------------------------------               
       1000-INITIALIZATION.                                                     
                                                                                
           INITIALIZE PM100-PROMO-ITM-SUMM                                      
                      PV-NULL-IND                                               
                      PV-TIER-NULL-IND.                                         
           SET PS-PRICG-AVAIL TO TRUE.                                          
                                                                                
070941     DISPLAY 'ORACLE FILE OPEN'.                                          
070941     OPEN INPUT ORACLE-LOGON-FILE.                                        
070941                                                                          
070941     DISPLAY 'ORACLE FILE READ'.                                          
070941     PERFORM 7000-READ-ORACLE-LOGON.                                      
070941                                                                          
070941     DISPLAY 'ORACLE FILE CLOSE'.                                         
070941     CLOSE ORACLE-LOGON-FILE.                                             
070941                                                                          
070941     IF ((PS-END-OF-LOGON-FILE)                                           
070941             OR (CC-ORACLE-CONNECTION = SPACES))                          
070941         DISPLAY '** ABEND **'                                            
070941         DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
070941                   'NOT PROVIDED'                                         
070941         SET PS-ORA-SRC-EMPTY TO TRUE                                     
070941         CALL 'ILBOABN0' USING ABEND-CODE                                 
070941     END-IF.                                                              
070941                                                                          
070941     IF CC-AUTO-LOGON                                                     
070941         DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
070941         EXEC SQL                                                         
070941              CONNECT :USERNAME                                           
070941         END-EXEC                                                         
070941     ELSE                                                                 
070941         INITIALIZE PV-ORACLE-USERID-LEN                                  
070941                    PV-ORACLE-PASSWORD-LEN                                
070941         INSPECT CC-ORACLE-USERID                                         
070941             TALLYING PV-ORACLE-USERID-LEN                                
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      INSPECT CC-ORACLE-PASSWORD                                          
070941          TALLYING PV-ORACLE-PASSWORD-LEN                                 
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
070941                USERNAME-ARR                                              
070941      MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
070941      MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
070941                PASSWD-ARR                                                
070941      MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
070941      DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '               
070941                USERNAME-ARR                                              
070941      EXEC SQL                                                            
070941        CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
070941      END-EXEC                                                            
070941      INITIALIZE CC-ORACLE-PASSWORD                                       
070941                 PASSWD                                                   
070941      END-IF.                                                             
070941                                                                          
070941     IF (SQLCODE NOT = 0)                                                 
070941         DISPLAY PV-PARAGRAPH-NAME                                        
070941         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
070941         PERFORM Z100-ABEND                                               
070941     END-IF.                                                              
                                                                                
           OPEN INPUT  CC-RUN-DTE-FILE                                          
                OUTPUT PROMO-ITM-SUMM-FILE.                                     
                                                                                
           PERFORM 1100-READ-RUN-DTE-FILE.                                      
070941*    PERFORM PM700-A000-ORACLE-SETUP.                                     
           PERFORM 1800-DECLARE-ADVERSION-CSR.                                  
                                                                                
           EVALUATE TRUE                                                        
             WHEN CC-RPT-TYP-CDE = 'A'                                          
                  PERFORM 1300-DECLARE-ACTL-ITM-CSR                             
                  PERFORM 1600-DECLARE-TIER-PRICG-CSR                           
                  PERFORM 1700-DECLARE-ADMIN-TIER-CSR                           
                  PERFORM 2010-OPEN-ACTL-ITM-CSR                                
                  PERFORM 2020-FETCH-ACTL-ITM-CSR                               
             WHEN CC-RPT-TYP-CDE = 'F' AND CC-ADD-DAYS = ZEROES                 
                  PERFORM 1400-DECLARE-FCST-ITM-CSR                             
                  PERFORM 2040-OPEN-FCST-ITM-CSR                                
                  PERFORM 2050-FETCH-FCST-ITM-CSR                               
             WHEN CC-RPT-TYP-CDE = 'F' AND CC-ADD-DAYS NOT = ZEROES             
                  PERFORM 1500-DECLARE-FUTFCST-ITM-CSR                          
                  PERFORM 2070-OPEN-FUTFCST-ITM-CSR                             
                  PERFORM 2080-FETCH-FUTFCST-ITM-CSR                            
             WHEN OTHER                                                         
                  DISPLAY 'RUN DATE: ' CC-RUN-DATE                              
                  DISPLAY 'REPORT TYPE CODE: ' CC-RPT-TYP-CDE                   
                  DISPLAY 'SUBTRACT DAYS: ' CC-SUB-DAYS                         
                  DISPLAY 'ADD DAYS: ' CC-ADD-DAYS                              
                  SET ABEND-4003-CTRL-CARD-ERROR TO TRUE                        
                  MOVE '1000-INITIALIZATION'     TO PV-PARAGRAPH-NAME           
                  MOVE 'INVALID CONTROL CARD'    TO PV-OPERATION-MSG            
                  PERFORM 9800-ABEND                                            
           END-EVALUATE.                                                        
                                                                                
      *1000-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *---------------------------------------------------------------          
      * 1100-READ-RUN-DTE-FILE.                                                 
      *     READ IN THE RUN DATE FROM THE CONTROL CARD.                         
      *---------------------------------------------------------------          
       1100-READ-RUN-DTE-FILE.                                                  
                                                                                
           READ CC-RUN-DTE-FILE INTO CC-DATE-RECORD                             
              AT END                                                            
                 SET PS-CONTROL-CARD-EOF TO TRUE                                
           END-READ.                                                            
                                                                                
           IF PS-CONTROL-CARD-EOF                                               
               MOVE '1100-READ-RUN-DTE-FILE'   TO PV-PARAGRAPH-NAME             
               MOVE 'NO RUN DATE FILE TO PROCESS'                               
                                               TO PV-OPERATION-MSG              
               SET ABEND-4003-CTRL-CARD-ERROR  TO TRUE                          
               PERFORM 9800-ABEND                                               
           ELSE                                                                 
               DISPLAY 'RUN DATE IS: ' CC-RUN-DATE                              
               MOVE CC-RUN-DATE TO PV-RUN-DATE1                                 
                                   PV-RUN-DATE2                                 
               MOVE PV-RUN-TIME1 TO PV-RUN-TIME1-FMTD                           
               MOVE PV-RUN-TIME2 TO PV-RUN-TIME2-FMTD                           
               DISPLAY 'PV-RUN-DATE1 = ' PV-RUN-DATE1                           
               DISPLAY 'PV-RUN-DATE2 = ' PV-RUN-DATE2                           
               DISPLAY 'PV-RUN-TIME1 = ' PV-RUN-TIME1                           
               DISPLAY 'PV-RUN-TIME2 = ' PV-RUN-TIME2                           
               DISPLAY 'PV-RUN-TIME1-FMTD = ' PV-RUN-TIME1-FMTD                 
               DISPLAY 'PV-RUN-TIME2-FMTD = ' PV-RUN-TIME2-FMTD                 
           END-IF.                                                              
                                                                                
      *1100-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *---------------------------------------------------------------          
      * 1300-DECLARE-ACTL-ITM-CSR.                                              
      *     DECLARE THE ACTUAL PROMO ITEM CURSOR.                               
      *---------------------------------------------------------------          
       1300-DECLARE-ACTL-ITM-CSR.                                               
                                                                                
           EXEC SQL                                                             
                DECLARE ACTLITMCSR CURSOR FOR                                   
                  SELECT  A.PROMO_ID                                            
                         ,UPPER(A.PROMO_NM)                                     
                         ,UPPER(A.PROMO_ABBR_NM)                                
                         ,A.PROMO_TYP_CDE                                       
                         ,A.REG_PROMO_TYP_CDE                                   
                         ,TO_CHAR(A.PROMO_DFLT_STRT_TMST,                       
                                   'YYYY-MM-DD-HH24.MI.SS')                     
                         ,TO_CHAR(A.PROMO_DFLT_END_TMST,                        
                                   'YYYY-MM-DD-HH24.MI.SS')                     
                         ,A.AD_EVNT_ID                                          
                         ,A.AD_EVNT_NBR_TXT                                     
                         ,UPPER(A.AD_EVNT_NM)                                   
                         ,TO_CHAR(A.AD_EVNT_STRT_DTE,                           
                                   'YYYY-MM-DD-HH24.MI.SS')                     
                         ,TO_CHAR(A.AD_EVNT_END_DTE,                            
                                   'YYYY-MM-DD-HH24.MI.SS')                     
                         ,B.PROMO_ITM_ID                                        
                         ,B.DEPT_NBR                                            
                         ,B.STAT_CDE                                            
                         ,B.ADV_IND                                             
                         ,UPPER(B.PROMO_ITM_DESC)                               
                         ,B.ACTL_PREP_UNT_QTY                                   
                         ,B.ACTL_EOH_INV_QTY                                    
                    FROM  PMV_PROMO_DIM A                                       
                         ,PMT_PROMO_ITM B                                       
                   WHERE  A.PROMO_ID        = B.PROMO_ID                        
                     AND  A.AD_EVNT_END_DTE BETWEEN                             
                          TO_DATE(:PV-RUN-TIME1-FMTD,                           
                                  'YYYYMMDD-HH24.MI.SS')                        
                          - :CC-SUB-DAYS                                        
                     AND  TO_DATE(:PV-RUN-TIME2-FMTD,                           
                                  'YYYYMMDD-HH24.MI.SS')                        
                          - :CC-SUB-DAYS                                        
                   ORDER BY A.AD_EVNT_ID, A.PROMO_ID                            
           END-EXEC.                                                            
                                                                                
      *1300-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *---------------------------------------------------------------          
      * 1400-DECLARE-FCST-ITM-CSR.                                              
      *     DECLARE THE FORECAST PROMO ITEM CURSOR.                             
      *---------------------------------------------------------------          
       1400-DECLARE-FCST-ITM-CSR.                                               
                                                                                
           EXEC SQL                                                             
                DECLARE FCSTITMCSR CURSOR FOR                                   
                  SELECT  A.PROMO_ID                                            
                         ,UPPER(A.PROMO_NM)                                     
                         ,UPPER(A.PROMO_ABBR_NM)                                
                         ,A.PROMO_TYP_CDE                                       
                         ,A.REG_PROMO_TYP_CDE                                   
                         ,TO_CHAR(A.PROMO_DFLT_STRT_TMST,                       
                                   'YYYY-MM-DD-HH24.MI.SS')                     
                         ,TO_CHAR(A.PROMO_DFLT_END_TMST,                        
                                   'YYYY-MM-DD-HH24.MI.SS')                     
                         ,A.AD_EVNT_ID                                          
                         ,A.AD_EVNT_NBR_TXT                                     
                         ,UPPER(A.AD_EVNT_NM)                                   
                         ,TO_CHAR(A.AD_EVNT_STRT_DTE,                           
                                   'YYYY-MM-DD-HH24.MI.SS')                     
                         ,TO_CHAR(A.AD_EVNT_END_DTE,                            
                                   'YYYY-MM-DD-HH24.MI.SS')                     
                         ,B.PROMO_ITM_ID                                        
                         ,B.DEPT_NBR                                            
                         ,B.STAT_CDE                                            
                         ,B.ADV_IND                                             
                         ,UPPER(B.PROMO_ITM_DESC)                               
                         ,B.ACTL_PREP_UNT_QTY                                   
                    FROM  PMV_PROMO_DIM A                                       
                         ,PMT_PROMO_ITM B                                       
                         ,PMT_PROMO_REG_ADV C                                   
                   WHERE  A.PROMO_ID         = B.PROMO_ID                       
                     AND  B.PROMO_ID         = C.PROMO_ID                       
                     AND  A.PROMO_DFLT_STRT_TMST BETWEEN                        
                          TO_DATE(:PV-RUN-TIME1-FMTD,                           
                                  'YYYYMMDD-HH24.MI.SS')                        
                     AND  TO_DATE(:PV-RUN-TIME2-FMTD,                           
                                  'YYYYMMDD-HH24.MI.SS')                        
                   ORDER BY A.AD_EVNT_ID, A.PROMO_ID                            
           END-EXEC.                                                            
                                                                                
      *1400-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *---------------------------------------------------------------          
      * 1500-DECLARE-FUTFCST-ITM-CSR.                                           
      *     DECLARE THE FUTURE FORECAST PROMO ITEM CURSOR.                      
      *---------------------------------------------------------------          
       1500-DECLARE-FUTFCST-ITM-CSR.                                            
                                                                                
           EXEC SQL                                                             
                DECLARE FUTFCSTITMCSR CURSOR FOR                                
                  SELECT  A.PROMO_ID                                            
                         ,UPPER(A.PROMO_NM)                                     
                         ,UPPER(A.PROMO_ABBR_NM)                                
                         ,A.PROMO_TYP_CDE                                       
                         ,A.REG_PROMO_TYP_CDE                                   
                         ,TO_CHAR(A.PROMO_DFLT_STRT_TMST,                       
                                   'YYYY-MM-DD-HH24.MI.SS')                     
                         ,TO_CHAR(A.PROMO_DFLT_END_TMST,                        
                                   'YYYY-MM-DD-HH24.MI.SS')                     
                         ,A.AD_EVNT_ID                                          
                         ,A.AD_EVNT_NBR_TXT                                     
                         ,UPPER(A.AD_EVNT_NM)                                   
                         ,TO_CHAR(A.AD_EVNT_STRT_DTE,                           
                                   'YYYY-MM-DD-HH24.MI.SS')                     
                         ,TO_CHAR(A.AD_EVNT_END_DTE,                            
                                   'YYYY-MM-DD-HH24.MI.SS')                     
                         ,B.PROMO_ITM_ID                                        
                         ,B.DEPT_NBR                                            
                         ,B.STAT_CDE                                            
                         ,B.ADV_IND                                             
                         ,UPPER(B.PROMO_ITM_DESC)                               
                         ,B.ACTL_PREP_UNT_QTY                                   
                    FROM  PMV_PROMO_DIM A                                       
                         ,PMT_PROMO_ITM B                                       
                         ,PMT_PROMO_REG C                                       
                   WHERE  A.PROMO_ID        = B.PROMO_ID                        
                     AND  B.PROMO_ID        = C.PROMO_ID                        
                     AND  C.TYP_CDE         = 'A'                               
                     AND  A.PROMO_DFLT_STRT_TMST BETWEEN                        
                          TO_DATE(:PV-RUN-TIME1-FMTD,                           
                                  'YYYYMMDD-HH24.MI.SS')                        
                           + :CC-ADD-DAYS                                       
                     AND  TO_DATE(:PV-RUN-TIME2-FMTD,                           
                                  'YYYYMMDD-HH24.MI.SS')                        
                           + :CC-ADD-DAYS                                       
                   ORDER BY A.AD_EVNT_ID, A.PROMO_ID                            
           END-EXEC.                                                            
                                                                                
      *1500-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *---------------------------------------------------------------          
      * 1600-DECLARE-TIER-PRICG-CSR.                                            
      *     DECLARE THE TIERED PRICING CURSOR.                                  
      *---------------------------------------------------------------          
       1600-DECLARE-TIER-PRICG-CSR.                                             
                                                                                
           EXEC SQL                                                             
                DECLARE TIERPRICGCSR CURSOR FOR                                 
                  SELECT  A.PROMO_ITM_ID                                        
                         ,B.THRS_QTY                                            
                         ,B.THRS_AMT                                            
                         ,B.DISC_AMT                                            
                         ,B.DISC_PCT                                            
                         ,D.TIER_PRICG_ID                                       
                         ,UPPER(D.RCPT_CMMT_TXT)                                
                         ,D.PRICG_TYP_CDE                                       
                         ,SUM(A.SLS_RTL_AMT)                                    
                         ,SUM(A.SLS_UNT_QTY)                                    
                         ,SUM(A.MKDN_RTL_AMT)                                   
                         ,SUM(A.SLS_COST_AMT)                                   
                    FROM  PMT_PROMO_ITM_INTFC_SUB_ID_FCT A                      
                         ,PMT_TIER_PRICG_GP_TIER B                              
                         ,PMT_TIER_PRICG_GP C                                   
                         ,PMT_TIER_PRICG D                                      
                   WHERE  A.PROMO_ITM_ID    = :PV-PROMO-ITM-ID                  
                     AND  A.PROMO_INTFC_SUB_ID                                  
                                            = B.TIER_PROMO_INTFC_SUB_ID         
                     AND  B.TIER_PRICG_GP_ID                                    
                                            = C.TIER_PRICG_GP_ID                
                     AND  C.TIER_PRICG_ID   = D.TIER_PRICG_ID                   
                   GROUP BY A.PROMO_ITM_ID                                      
                           ,B.THRS_QTY                                          
                           ,B.THRS_AMT                                          
                           ,B.DISC_AMT                                          
                           ,B.DISC_PCT                                          
                           ,D.TIER_PRICG_ID                                     
                           ,D.RCPT_CMMT_TXT                                     
                           ,D.PRICG_TYP_CDE                                     
           END-EXEC.                                                            
                                                                                
      *1600-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *---------------------------------------------------------------          
      * 1700-DECLARE-ADMIN-TIER-CSR.                                            
      *     DECLARE THE ADMIN TIERED CURSOR.                                    
      *---------------------------------------------------------------          
       1700-DECLARE-ADMIN-TIER-CSR.                                             
                                                                                
           EXEC SQL                                                             
                DECLARE ADMINTIERCSR CURSOR FOR                                 
                  SELECT  A.PROMO_ID                                            
                         ,B.THRS_QTY                                            
                         ,B.THRS_AMT                                            
                         ,B.DISC_AMT                                            
                         ,B.DISC_PCT                                            
                         ,D.TIER_PRICG_ID                                       
                         ,UPPER(D.RCPT_CMMT_TXT)                                
                         ,D.PRICG_TYP_CDE                                       
                         ,SUM(A.SLS_RTL_AMT)                                    
                         ,SUM(A.SLS_UNT_QTY)                                    
                         ,SUM(A.MKDN_RTL_AMT)                                   
                         ,SUM(A.SLS_COST_AMT)                                   
                    FROM  PMT_MDSE_SELN_LNE_TIER_FCT A                          
                         ,PMT_TIER_PRICG_GP_TIER B                              
                         ,PMT_TIER_PRICG_GP C                                   
                         ,PMT_TIER_PRICG D                                      
                   WHERE  A.PROMO_ID        = :PV-PROMO-ID                      
                     AND  A.PROMO_INTFC_SUB_ID                                  
                                            = B.TIER_PROMO_INTFC_SUB_ID         
                     AND  B.TIER_PRICG_GP_ID                                    
                                            = C.TIER_PRICG_GP_ID                
                     AND  C.TIER_PRICG_ID   = D.TIER_PRICG_ID                   
                   GROUP BY A.PROMO_ID                                          
                           ,B.THRS_QTY                                          
                           ,B.THRS_AMT                                          
                           ,B.DISC_AMT                                          
                           ,B.DISC_PCT                                          
                           ,D.TIER_PRICG_ID                                     
                           ,D.RCPT_CMMT_TXT                                     
                           ,D.PRICG_TYP_CDE                                     
           END-EXEC.                                                            
                                                                                
      *1700-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *---------------------------------------------------------------          
      * 1800-DECLARE-ADVERSION-CSR.                                             
      *     DECLARE THE AD VERSION CURSOR.                                      
      *---------------------------------------------------------------          
       1800-DECLARE-ADVERSION-CSR.                                              
                                                                                
           EXEC SQL                                                             
                DECLARE ADVERSIONCSR CURSOR FOR                                 
                  SELECT  DISTINCT A.PRT_VER_ABBR_NM                            
                    FROM  PMT_PROMO_PRT_VER A                                   
                         ,PMT_AD_SP_ASGMT B                                     
                   WHERE  A.PROMO_PRT_VER_ID = B.PROMO_PRT_VER_ID               
                     AND  A.STAT_CDE = 'S'                                      
                     AND  B.STAT_CDE = 'S'                                      
                     AND  B.PROMO_ITM_ID = :PV-PROMO-ITM-ID                     
           END-EXEC.                                                            
                                                                                
      *1800-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 2000-PROCESS-CURSORS.                                                   
      *     FETCHES THE PROMO ITEM CURSOR AND WRITES OUT ANY RECORDS            
      *     BASED ON THE INFORMATION FOUND.  SELECTS ANY ADDITIONAL             
      *     INFORMATION NEEDED TO WRITE OUT THE RECORDS.                        
      *----------------------------------------------------------               
       2000-PROCESS-CURSORS.                                                    
                                                                                
           INITIALIZE TBL-ADVERSION-ARRAY.                                      
                                                                                
           IF CC-RPT-TYP-CDE = 'A'                                              
              INITIALIZE PV-MIN-UNT-REG-RTL-AMT                                 
                         PV-MAX-UNT-REG-RTL-AMT                                 
                         PV-MIN-UNT-PRM-RTL-AMT                                 
                         PV-MAX-UNT-PRM-RTL-AMT                                 
                         PV-SLS-UNT-QTY                                         
                         PV-SLS-COST-AMT                                        
                         PV-SLS-RTL-AMT                                         
                         PV-MKDN-RTL-AMT                                        
                         PV-TOT-MKUP-PCT                                        
              PERFORM 2100-SELECT-ACTL-SALES-RSLT                               
              IF PV-PROMO-ITM-STAT-CDE = 'S' AND PS-NO-SALES                    
                 PERFORM 2200-SELECT-PRICG-INFO                                 
              END-IF                                                            
              IF PV-PROMO-ITM-STAT-CDE = 'C' OR 'D' AND PS-NO-SALES             
                 SET PS-NO-PRICG TO TRUE                                        
                 DISPLAY 'PROMO ITEM HAS CANCELLED OR DELETED STATUS'           
                 DISPLAY 'PROMO ITEM HAS NO SALES'                              
                 DISPLAY 'PROMO ITEM ID: ' PV-PROMO-ITM-ID                      
                 DISPLAY 'PROMO ITEM DESCRIPTION: ' PV-PROMO-ITM-DESC           
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF PS-PRICG-AVAIL                                                    
              INITIALIZE PV-FCST-TOT-MKUP-PCT                                   
                         PV-GMA-FRST-NAME                                       
                         PV-GMA-LAST-NAME                                       
                         PV-DMA-FRST-NAME                                       
                         PV-DMA-LAST-NAME                                       
                         PV-BUY-FRST-NAME                                       
                         PV-BUY-LAST-NAME                                       
              PERFORM 2300-SELECT-FCST-SALES-RSLT                               
              PERFORM 2400-SELECT-MERCH-HIER                                    
              IF PV-PROMO-ITM-ADV-IND = 'Y'                                     
                 INITIALIZE PV-AD-SP-CDE                                        
                            PV-AD-PG-SEQ-NBR                                    
                 PERFORM 2500-SELECT-AD-ASGMT                                   
              ELSE                                                              
                 MOVE ZEROES    TO PV-AD-PG-NBR                                 
                                   PV-AD-PG-SEQ-NBR                             
                 MOVE SPACES    TO PV-AD-SP-CDE                                 
              END-IF                                                            
              IF PV-REG-PROMO-TYP-CDE = 'U '                                    
                 PERFORM 2600-SELECT-PMITM-EFF-DTES                             
              ELSE                                                              
                 MOVE SPACES    TO PV-PROMO-ITM-STRT-TMST                       
                                   PV-PROMO-ITM-END-TMST                        
              END-IF                                                            
              PERFORM 2700-POPULATE-SMPL-PRICG                                  
              PERFORM 9000-WRITE-PROMO-ITM-REC                                  
           END-IF.                                                              
                                                                                
           EVALUATE TRUE                                                        
             WHEN CC-RPT-TYP-CDE = 'A'                                          
                  IF PS-PRICG-AVAIL                                             
                     PERFORM 3010-OPEN-TIER-PRICG-CSR                           
                     PERFORM 3020-FETCH-TIER-PRICG-CSR                          
                     PERFORM 3000-PROCESS-TIERED-RECS                           
                             UNTIL PS-END-OF-TIER-CSR                           
                     PERFORM 3030-CLOSE-TIER-PRICG-CSR                          
                     IF PV-PROMO-ID NOT = PV-HOLD-PROMO-ID                      
                        PERFORM 4010-OPEN-ADMIN-TIER-CSR                        
                        PERFORM 4020-FETCH-ADMIN-TIER-CSR                       
                        PERFORM 4000-PROCESS-ADMIN-TIER-RECS                    
                                UNTIL PS-END-OF-ADMIN-CSR                       
                        PERFORM 4030-CLOSE-ADMIN-TIER-CSR                       
                        MOVE PV-PROMO-ID TO PV-HOLD-PROMO-ID                    
                     END-IF                                                     
                  END-IF                                                        
                  INITIALIZE  PV-PROMO-ABBR-NM                                  
                              PV-PROMO-ITM-DESC                                 
                              PV-ACTL-PREP-UNT-QTY                              
                              PV-ACTL-EOH-INV-QTY                               
                  PERFORM 2020-FETCH-ACTL-ITM-CSR                               
             WHEN CC-RPT-TYP-CDE = 'F' AND CC-ADD-DAYS = ZEROES                 
                  INITIALIZE       PV-ABBR-NM-NULL                              
                                   PV-ITM-DESC-NULL                             
                                   PV-PREP-UNT-QTY-NULL                         
                  PERFORM 2050-FETCH-FCST-ITM-CSR                               
             WHEN CC-RPT-TYP-CDE = 'F' AND CC-ADD-DAYS NOT = ZEROES             
                  INITIALIZE       PV-ABBR-NM-NULL                              
                                   PV-ITM-DESC-NULL                             
                                   PV-PREP-UNT-QTY-NULL                         
                  PERFORM 2080-FETCH-FUTFCST-ITM-CSR                            
           END-EVALUATE.                                                        
                                                                                
           SET PS-PRICG-AVAIL TO TRUE.                                          
                                                                                
      *2000-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 2010-OPEN-ACTL-ITM-CSR.                                                 
      *     OPENS THE ACTUAL PROMO ITEM CURSOR.                                 
      *----------------------------------------------------------               
       2010-OPEN-ACTL-ITM-CSR.                                                  
                                                                                
           EXEC SQL                                                             
                 OPEN ACTLITMCSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROES                                            
                    SET PS-NOT-END-OF-ITEM-CSR TO TRUE                          
               WHEN OTHER                                                       
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE SQLCODE             TO PV-SQL-CODE                     
                    MOVE '2010-OPEN-ACTL-ITM-CSR'                               
                                      TO PV-PARAGRAPH-NAME                      
                    MOVE 'OPEN ERROR ON ACTLITMCSR'                             
                                      TO PV-OPERATION-MSG                       
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      *2010-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 2020-FETCH-ACTL-ITM-CSR.                                                
      *     FETCHES THE ACTUAL PROMO ITEM CURSOR.                               
      *----------------------------------------------------------               
       2020-FETCH-ACTL-ITM-CSR.                                                 
                                                                                
           EXEC SQL                                                             
               FETCH ACTLITMCSR                                                 
                INTO  :PV-PROMO-ID                                              
                     ,:PV-PROMO-NM                                              
                     ,:PV-PROMO-ABBR-NM :PV-ABBR-NM-NULL                        
                     ,:PV-PROMO-TYP-CDE                                         
                     ,:PV-REG-PROMO-TYP-CDE                                     
                     ,:PV-PROMO-DFLT-STRT-TMST                                  
                     ,:PV-PROMO-DFLT-END-TMST                                   
                     ,:PV-AD-EVNT-ID                                            
                     ,:PV-AD-EVNT-NBR-TXT                                       
                     ,:PV-AD-EVNT-NM                                            
                     ,:PV-AD-EVNT-STRT-TMST                                     
                     ,:PV-AD-EVNT-END-TMST                                      
                     ,:PV-PROMO-ITM-ID                                          
                     ,:PV-DEPT-NBR                                              
                     ,:PV-PROMO-ITM-STAT-CDE                                    
                     ,:PV-PROMO-ITM-ADV-IND                                     
                     ,:PV-PROMO-ITM-DESC :PV-ITM-DESC-NULL                      
                     ,:PV-ACTL-PREP-UNT-QTY :PV-PREP-UNT-QTY-NULL               
                     ,:PV-ACTL-EOH-INV-QTY  :PV-EOH-INV-QTY-NULL                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    ADD 1 TO PA-ITEM-CNT                                        
               WHEN SQLCODE = 1403                                              
                    SET PS-END-OF-ITEM-CSR TO TRUE                              
                    IF PA-ITEM-CNT = ZEROES                                     
                       DISPLAY 'NO AD EVENTS ENDED ' CC-SUB-DAYS                
                       ' DAYS AGO - NO SALES RESULTS TO EXTRACT'                
                    END-IF                                                      
               WHEN OTHER                                                       
                    MOVE SQLCODE      TO PV-SQL-CODE                            
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE '2020-FETCH-ACTL-ITM-CSR'                              
                                      TO PV-PARAGRAPH-NAME                      
                    MOVE 'FETCH ERROR ON ACTLITMCSR'                            
                                     TO PV-OPERATION-MSG                        
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      *2020-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 2030-CLOSE-ACTL-ITM-CSR.                                                
      *     CLOSE THE ACTUAL PROMO ITEM CURSOR.                                 
      *----------------------------------------------------------               
       2030-CLOSE-ACTL-ITM-CSR.                                                 
                                                                                
           EXEC SQL                                                             
                CLOSE ACTLITMCSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE SQLCODE     TO PV-SQL-CODE                             
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE '2030-CLOSE-ACTL-ITM-CSR    '                          
                                     TO PV-PARAGRAPH-NAME                       
                    MOVE 'CLOSE ERROR ON ACTLITMCSR'                            
                                     TO PV-OPERATION-MSG                        
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      *2030-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 2040-OPEN-FCST-ITM-CSR.                                                 
      *     OPENS THE FORECAST PROMO ITEM CURSOR.                               
      *----------------------------------------------------------               
       2040-OPEN-FCST-ITM-CSR.                                                  
                                                                                
           EXEC SQL                                                             
                 OPEN FCSTITMCSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROES                                            
                    SET PS-NOT-END-OF-ITEM-CSR TO TRUE                          
               WHEN OTHER                                                       
                    MOVE SQLCODE     TO PV-SQL-CODE                             
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE '2040-OPEN-FCST-ITM-CSR'                               
                                      TO PV-PARAGRAPH-NAME                      
                    MOVE 'OPEN ERROR ON FCSTITMCSR'                             
                                      TO PV-OPERATION-MSG                       
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      *2040-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 2050-FETCH-FCST-ITM-CSR.                                                
      *     FETCHES THE FORECAST PROMO ITEM CURSOR.                             
      *----------------------------------------------------------               
       2050-FETCH-FCST-ITM-CSR.                                                 
                                                                                
           EXEC SQL                                                             
               FETCH FCSTITMCSR                                                 
                INTO  :PV-PROMO-ID                                              
                     ,:PV-PROMO-NM                                              
                     ,:PV-PROMO-ABBR-NM :PV-ABBR-NM-NULL                        
                     ,:PV-PROMO-TYP-CDE                                         
                     ,:PV-REG-PROMO-TYP-CDE                                     
                     ,:PV-PROMO-DFLT-STRT-TMST                                  
                     ,:PV-PROMO-DFLT-END-TMST                                   
                     ,:PV-AD-EVNT-ID                                            
                     ,:PV-AD-EVNT-NBR-TXT                                       
                     ,:PV-AD-EVNT-NM                                            
                     ,:PV-AD-EVNT-STRT-TMST                                     
                     ,:PV-AD-EVNT-END-TMST                                      
                     ,:PV-PROMO-ITM-ID                                          
                     ,:PV-DEPT-NBR                                              
                     ,:PV-PROMO-ITM-STAT-CDE                                    
                     ,:PV-PROMO-ITM-ADV-IND                                     
                     ,:PV-PROMO-ITM-DESC :PV-ITM-DESC-NULL                      
                     ,:PV-ACTL-PREP-UNT-QTY :PV-PREP-UNT-QTY-NULL               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    ADD 1 TO PA-ITEM-CNT                                        
               WHEN SQLCODE = 1403                                              
                    SET PS-END-OF-ITEM-CSR TO TRUE                              
                    IF PA-ITEM-CNT = ZEROES                                     
                       DISPLAY 'NO PROMOTIONS START MATCHING '                  
                       'THE FORECAST LOCKDOWN TIMESTAMP'                        
                       DISPLAY 'NO FORECASTS TO EXTRACT'                        
                    END-IF                                                      
               WHEN OTHER                                                       
                    MOVE SQLCODE     TO PV-SQL-CODE                             
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE '2050-FETCH-FCST-ITM-CSR'                              
                                      TO PV-PARAGRAPH-NAME                      
                    MOVE 'FETCH ERROR ON FCSTITMCSR'                            
                                     TO PV-OPERATION-MSG                        
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      *2050-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 2060-CLOSE-FCST-ITM-CSR.                                                
      *     CLOSE THE FORECAST PROMO ITEM CURSOR.                               
      *----------------------------------------------------------               
       2060-CLOSE-FCST-ITM-CSR.                                                 
                                                                                
           EXEC SQL                                                             
                CLOSE FCSTITMCSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE SQLCODE     TO PV-SQL-CODE                             
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE '2060-CLOSE-FCST-ITM-CSR'                              
                                     TO PV-PARAGRAPH-NAME                       
                    MOVE 'CLOSE ERROR ON FCSTITMCSR'                            
                                     TO PV-OPERATION-MSG                        
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      *2060-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 2070-OPEN-FUTFCST-ITM-CSR.                                              
      *     OPENS THE FUTURE FORECAST PROMO ITEM CURSOR.                        
      *----------------------------------------------------------               
       2070-OPEN-FUTFCST-ITM-CSR.                                               
                                                                                
           EXEC SQL                                                             
                 OPEN FUTFCSTITMCSR                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROES                                            
                    SET PS-NOT-END-OF-ITEM-CSR TO TRUE                          
               WHEN OTHER                                                       
                    MOVE SQLCODE     TO PV-SQL-CODE                             
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE '2070-OPEN-FUTFCST-ITM-CSR'                            
                                      TO PV-PARAGRAPH-NAME                      
                    MOVE 'OPEN ERROR ON FUTFCSTITMCSR'                          
                                      TO PV-OPERATION-MSG                       
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      *2070-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 2080-FETCH-FUTFCST-ITM-CSR.                                             
      *     FETCHES THE FUTURE FORECAST PROMO ITEM CURSOR.                      
      *----------------------------------------------------------               
       2080-FETCH-FUTFCST-ITM-CSR.                                              
                                                                                
           EXEC SQL                                                             
               FETCH FUTFCSTITMCSR                                              
                INTO  :PV-PROMO-ID                                              
                     ,:PV-PROMO-NM                                              
                     ,:PV-PROMO-ABBR-NM :PV-ABBR-NM-NULL                        
                     ,:PV-PROMO-TYP-CDE                                         
                     ,:PV-REG-PROMO-TYP-CDE                                     
                     ,:PV-PROMO-DFLT-STRT-TMST                                  
                     ,:PV-PROMO-DFLT-END-TMST                                   
                     ,:PV-AD-EVNT-ID                                            
                     ,:PV-AD-EVNT-NBR-TXT                                       
                     ,:PV-AD-EVNT-NM                                            
                     ,:PV-AD-EVNT-STRT-TMST                                     
                     ,:PV-AD-EVNT-END-TMST                                      
                     ,:PV-PROMO-ITM-ID                                          
                     ,:PV-DEPT-NBR                                              
                     ,:PV-PROMO-ITM-STAT-CDE                                    
                     ,:PV-PROMO-ITM-ADV-IND                                     
                     ,:PV-PROMO-ITM-DESC :PV-ITM-DESC-NULL                      
                     ,:PV-ACTL-PREP-UNT-QTY :PV-PREP-UNT-QTY-NULL               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    ADD 1 TO PA-ITEM-CNT                                        
               WHEN SQLCODE = 1403                                              
                    SET PS-END-OF-ITEM-CSR TO TRUE                              
                    IF PA-ITEM-CNT = ZEROES                                     
                       DISPLAY 'NO PROMOTIONS START IN ' CC-ADD-DAYS            
                               'DAYS'                                           
                       DISPLAY 'NO FORECASTS TO EXTRACT'                        
                    END-IF                                                      
               WHEN OTHER                                                       
                    MOVE SQLCODE     TO PV-SQL-CODE                             
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE '2080-FETCH-FUTFCST-ITM-CSR'                           
                                      TO PV-PARAGRAPH-NAME                      
                    MOVE 'FETCH ERROR ON FUTFCSTITMCSR'                         
                                     TO PV-OPERATION-MSG                        
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      *2080-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 2090-CLOSE-FUTFCST-ITM-CSR.                                             
      *     CLOSE THE FUTURE FORECAST PROMO ITEM CURSOR.                        
      *----------------------------------------------------------               
       2090-CLOSE-FUTFCST-ITM-CSR.                                              
                                                                                
           EXEC SQL                                                             
                CLOSE FUTFCSTITMCSR                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE SQLCODE     TO PV-SQL-CODE                             
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE '2090-CLOSE-FUTFCST-ITM-CSR'                           
                                     TO PV-PARAGRAPH-NAME                       
                    MOVE 'CLOSE ERROR ON FUTFCSTITMCSR'                         
                                     TO PV-OPERATION-MSG                        
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      *2090-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *---------------------------------------------------------------          
      * 2100-SELECT-ACTL-SALES-RSLT.                                            
      *     SELECTS THE ACTUAL SALES RESULTS OF THE PROMO ITEM.                 
      *  - 0    = A ROW WAS RETURNED                                            
      *  - 1403 = NO ROWS WERE RETURNED.                                        
      *---------------------------------------------------------------          
       2100-SELECT-ACTL-SALES-RSLT.                                             
                                                                                
           EXEC SQL                                                             
               SELECT  MIN_UNT_REG_RTL_AMT                                      
                      ,MAX_UNT_REG_RTL_AMT                                      
                      ,MIN_UNT_PROMO_RTL_AMT                                    
                      ,MAX_UNT_PROMO_RTL_AMT                                    
                      ,SLS_UNT_QTY                                              
                      ,SLS_COST_AMT                                             
                      ,SLS_RTL_AMT                                              
                      ,MKDN_RTL_AMT                                             
                      ,TOT_MKUP_PCT                                             
                 INTO  :PV-MIN-UNT-REG-RTL-AMT :PV-MIN-REG-RTL-NULL             
                      ,:PV-MAX-UNT-REG-RTL-AMT :PV-MAX-REG-RTL-NULL             
                      ,:PV-MIN-UNT-PRM-RTL-AMT :PV-MIN-PRM-RTL-NULL             
                      ,:PV-MAX-UNT-PRM-RTL-AMT :PV-MAX-PRM-RTL-NULL             
                      ,:PV-SLS-UNT-QTY :PV-SLS-QTY-NULL                         
                      ,:PV-SLS-COST-AMT :PV-SLS-COST-NULL                       
                      ,:PV-SLS-RTL-AMT :PV-SLS-RTL-NULL                         
                      ,:PV-MKDN-RTL-AMT :PV-MKDN-RTL-NULL                       
                      ,:PV-TOT-MKUP-PCT :PV-MKUP-PCT-NULL                       
                 FROM  PMV_PROMO_ITM_SUM                                        
                WHERE  PROMO_ITM_ID = :PV-PROMO-ITM-ID                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROES                                              
                  SET PS-SALES TO TRUE                                          
             WHEN SQLCODE = 1403                                                
                  SET PS-NO-SALES TO TRUE                                       
                  MOVE ZEROES TO  PV-MIN-UNT-REG-RTL-AMT                        
                                  PV-MAX-UNT-REG-RTL-AMT                        
                                  PV-MIN-UNT-PRM-RTL-AMT                        
                                  PV-MAX-UNT-PRM-RTL-AMT                        
                                  PV-SLS-UNT-QTY                                
                                  PV-SLS-COST-AMT                               
                                  PV-SLS-RTL-AMT                                
                                  PV-MKDN-RTL-AMT                               
                                  PV-TOT-MKUP-PCT                               
             WHEN OTHER                                                         
                  DISPLAY 'PROMO ITEM ID: ' PV-PROMO-ITM-ID                     
                  MOVE SQLCODE     TO PV-SQL-CODE                               
                  SET ABEND-4013-SQL-ERROR TO TRUE                              
                  MOVE 'PMV_PROMO_ITM_SUM'   TO PV-TABLE-NAME                   
                  MOVE '2100-SELECT-ACTL-SALES-RSLT'                            
                                             TO PV-PARAGRAPH-NAME               
                  MOVE 'ERROR ON SELECT ACTL SALES'                             
                                             TO PV-OPERATION-MSG                
                  PERFORM Z100-ABEND                                            
           END-EVALUATE.                                                        
                                                                                
      *2100-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *---------------------------------------------------------------          
      * 2200-SELECT-PRICG-INFO.                                                 
      *     SELECTS THE ACTUAL PRICING SET UP FOR THIS PROMO ITEM.              
      *     SELECTS THIS INFORMATION TO SEE IF THERE REALLY IS                  
      *     ANYTHING OUT THERE.                                                 
      *  - 0    = A ROW WAS RETURNED                                            
      *  - 2112 = MORE THEN ONE ROW WAS RETURNED.                               
      *  - 1403 = NO ROWS WERE RETURNED.                                        
      *---------------------------------------------------------------          
       2200-SELECT-PRICG-INFO.                                                  
                                                                                
           EXEC SQL                                                             
               SELECT  '1'                                                      
                 INTO  :PV-TEMP-VALUE                                           
                 FROM  PMT_MDSE_SELN                                            
                WHERE  PROMO_ITM_ID = :PV-PROMO-ITM-ID                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROES                                              
             WHEN SQLCODE = 2112                                                
             WHEN SQLCODE = -2112                                               
                  SET PS-PRICG-AVAIL TO TRUE                                    
             WHEN SQLCODE = 1403                                                
                  SET PS-NO-PRICG TO TRUE                                       
                  DISPLAY 'NO PRICING SET UP FOR THIS PROMO ITEM ID'            
                  DISPLAY 'PROMO ITEM ID: ' PV-PROMO-ITM-ID                     
                  DISPLAY 'PROMO ITEM DESCRIPTION: ' PV-PROMO-ITM-DESC          
             WHEN OTHER                                                         
                  MOVE SQLCODE     TO PV-SQL-CODE                               
                  SET ABEND-4013-SQL-ERROR TO TRUE                              
                  MOVE 'PMT_MDSE_SELN'       TO PV-TABLE-NAME                   
                  MOVE '2200-SELECT-PRICG-INFO'                                 
                                             TO PV-PARAGRAPH-NAME               
                  MOVE 'ERROR ON SELECT PRICG INFO'                             
                                             TO PV-OPERATION-MSG                
                  PERFORM Z100-ABEND                                            
           END-EVALUATE.                                                        
                                                                                
      *2200-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *---------------------------------------------------------------          
      * 2300-SELECT-FCST-SALES-RSLT.                                            
      *     SELECTS THE FORECASTED SALES OF THE PROMO ITEM.                     
      *  - 0    = A ROW WAS RETURNED                                            
      *  - 1403 = NO ROWS WERE RETURNED.                                        
      *---------------------------------------------------------------          
       2300-SELECT-FCST-SALES-RSLT.                                             
                                                                                
           EXEC SQL                                                             
               SELECT  SLS_RTL_AMT                                              
                      ,SLS_UNT_QTY                                              
                      ,MKDN_RTL_AMT                                             
                      ,PROJD_PREP_UNT_QTY                                       
                      ,SLS_COST_AMT                                             
                      ,MIN_UNT_REG_RTL_AMT                                      
                      ,MAX_UNT_REG_RTL_AMT                                      
                      ,MIN_UNT_PROMO_RTL_AMT                                    
                      ,MAX_UNT_PROMO_RTL_AMT                                    
                      ,TOT_MKUP_PCT                                             
                 INTO  :PV-FCST-SLS-RTL-AMT                                     
                      ,:PV-FCST-SLS-UNT-QTY                                     
                      ,:PV-FCST-MKDN-RTL-AMT                                    
                      ,:PV-PROJD-PREP-UNT-QTY                                   
                      ,:PV-FCST-SLS-COST-AMT                                    
                      ,:PV-FCST-MIN-REG-RTL-AMT                                 
                      ,:PV-FCST-MAX-REG-RTL-AMT                                 
                      ,:PV-FCST-MIN-PROMO-RTL-AMT                               
                      ,:PV-FCST-MAX-PROMO-RTL-AMT                               
                      ,:PV-FCST-TOT-MKUP-PCT :PV-FCST-MKUP-PCT-NULL             
                 FROM  PMT_PROMO_ITM_FCST                                       
                WHERE  PROMO_ITM_ID = :PV-PROMO-ITM-ID                          
                  AND  STAT_CDE = 'S'                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROES                                              
                  CONTINUE                                                      
             WHEN SQLCODE = 1403                                                
                  MOVE ZEROES TO PV-FCST-SLS-RTL-AMT                            
                                 PV-FCST-SLS-UNT-QTY                            
                                 PV-FCST-MKDN-RTL-AMT                           
                                 PV-PROJD-PREP-UNT-QTY                          
                                 PV-FCST-SLS-COST-AMT                           
                                 PV-FCST-TOT-MKUP-PCT                           
             WHEN OTHER                                                         
                  MOVE SQLCODE     TO PV-SQL-CODE                               
                  SET ABEND-4013-SQL-ERROR TO TRUE                              
                  DISPLAY 'PROMO ITEM ID: ' PV-PROMO-ITM-ID                     
                  MOVE 'PMT_PROMO_ITM_FCST'  TO PV-TABLE-NAME                   
                  MOVE '2300-SELECT-FCST-SALES-RSLT'                            
                                             TO PV-PARAGRAPH-NAME               
                  MOVE 'ERROR ON SELECT FCST SALES'                             
                                             TO PV-OPERATION-MSG                
                  PERFORM Z100-ABEND                                            
           END-EVALUATE.                                                        
                                                                                
      *2300-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *---------------------------------------------------------------          
      * 2400-SELECT-MERCH-HIER.                                                 
      *     SELECTS THE MERCHANDISE HIERARCHY.                                  
      *  - 0    = A ROW WAS RETURNED                                            
      *---------------------------------------------------------------          
       2400-SELECT-MERCH-HIER.                                                  
                                                                                
           EXEC SQL                                                             
               SELECT  GMA_NBR                                                  
                      ,GMM_FRST_NM                                              
                      ,GMM_LAST_NM                                              
                      ,DMA_NBR                                                  
                      ,DMM_FRST_NM                                              
                      ,DMM_LAST_NM                                              
                      ,DEPT_NM                                                  
                      ,BYR_NBR                                                  
                      ,BYR_FRST_NM                                              
                      ,BYR_LAST_NM                                              
                 INTO  :PV-GMA-NBR                                              
                      ,:PV-GMA-FRST-NAME :PV-GMA-FRST-NM-NULL                   
                      ,:PV-GMA-LAST-NAME :PV-GMA-LAST-NM-NULL                   
                      ,:PV-DMA-NBR                                              
                      ,:PV-DMA-FRST-NAME :PV-DMA-FRST-NM-NULL                   
                      ,:PV-DMA-LAST-NAME :PV-DMA-LAST-NM-NULL                   
                      ,:PV-DEPT-NM                                              
                      ,:PV-BYR-NBR                                              
                      ,:PV-BUY-FRST-NAME :PV-BYR-FRST-NM-NULL                   
                      ,:PV-BUY-LAST-NAME :PV-BYR-LAST-NM-NULL                   
                 FROM  PMV_PRSN_MDSE_HIER                                       
                WHERE  DEPT_NBR       = :PV-DEPT-NBR                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROES                                              
                  CONTINUE                                                      
             WHEN OTHER                                                         
                  MOVE SQLCODE     TO PV-SQL-CODE                               
                  SET ABEND-4013-SQL-ERROR TO TRUE                              
                  DISPLAY 'DEPT NBR NOT FOUND: ' PV-DEPT-NBR                    
                  MOVE 'PMV_PRSN_MDSE_HIER'  TO PV-TABLE-NAME                   
                  MOVE '2400-SELECT-MERCH-HIER'                                 
                                             TO PV-PARAGRAPH-NAME               
                  MOVE 'ERROR ON SELECT MERCH HIER'                             
                                             TO PV-OPERATION-MSG                
                  PERFORM Z100-ABEND                                            
           END-EVALUATE.                                                        
                                                                                
      *2400-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *---------------------------------------------------------------          
      * 2500-SELECT-AD-ASGMT.                                                   
      *     SELECTS THE PAGE INFORMATION IN THE ADVERTISEMENT                   
      *     ASSOCIATED WITH THIS PROMO ITEM.                                    
      *     IF THE AD PAGE IS NOT FOUND ALWAYS MOVE 'N' TO                      
      *     THE ADV-IND BECAUSE THIS PROMO ITEM IS NOT ADVERTISED.              
      *  - 0    = A ROW WAS RETURNED                                            
      *  - 1403 = NO ROWS WERE RETURNED.                                        
      *---------------------------------------------------------------          
       2500-SELECT-AD-ASGMT.                                                    
                                                                                
           EXEC SQL                                                             
               SELECT  AD_PG_NBR                                                
                      ,AD_SP_CDE                                                
                      ,AD_PG_SEQ_NBR                                            
                 INTO  :PV-AD-PG-NBR                                            
                      ,:PV-AD-SP-CDE :PV-AD-SP-NULL                             
                      ,:PV-AD-PG-SEQ-NBR :PV-AD-PG-SEQ-NULL                     
                 FROM  PMT_AD_SP_ASGMT                                          
                WHERE  PROMO_ITM_ID     = :PV-PROMO-ITM-ID                      
                  AND  PRIM_PRT_VER_IND = 'Y'                                   
                  AND  STAT_CDE         = 'S'                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROES                                              
                  PERFORM 2510-SELECT-NO-PAGE-NBRS                              
             WHEN SQLCODE = 2112                                                
             WHEN SQLCODE = -2112                                               
                  DISPLAY 'DUPLICATE ROWS 2500- KEPT GOING'                     
                  DISPLAY 'PROMO ITEM ID: ' PV-PROMO-ITM-ID                     
                  PERFORM 2510-SELECT-NO-PAGE-NBRS                              
             WHEN SQLCODE = 1403                                                
      *           MOVE 'N'    TO PV-PROMO-ITM-ADV-IND                           
                  MOVE 999    TO PV-AD-PG-NBR                                   
                  MOVE SPACES TO PV-AD-SP-CDE                                   
             WHEN OTHER                                                         
                  DISPLAY 'PROMO ITEM ID: ' PV-PROMO-ITM-ID                     
                  MOVE SQLCODE     TO PV-SQL-CODE                               
                  SET ABEND-4013-SQL-ERROR TO TRUE                              
                  MOVE 'PMT_AD_SP_ASGMT'     TO PV-TABLE-NAME                   
                  MOVE '2500-SELECT-AD-ASGMT'                                   
                                             TO PV-PARAGRAPH-NAME               
                  MOVE 'ERROR ON SELECT PAGE ASGMT'                             
                                             TO PV-OPERATION-MSG                
                  PERFORM Z100-ABEND                                            
           END-EVALUATE.                                                        
                                                                                
      *2500-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *---------------------------------------------------------------          
      * 2510-SELECT-NO-PAGE-NBRS.                                               
      *     SELECTS ANY VERSIONS FOR THE PROMO ITEM THAT DON'T HAVE             
      *     A PAGE NUMBER.                                                      
      *  - 0    = A ROW WAS RETURNED                                            
      *  - 2112 = MORE THEN ONE ROW WAS RETURNED.                               
      *  - 1403 = NO ROWS WERE RETURNED.                                        
      *---------------------------------------------------------------          
       2510-SELECT-NO-PAGE-NBRS.                                                
                                                                                
           EXEC SQL                                                             
               SELECT  '1'                                                      
                 INTO  :PV-TEMP-VALUE                                           
                 FROM  PMT_PROMO_PRT_VER A                                      
                      ,PMT_PROMO_PRIC_VER B                                     
                WHERE  A.PROMO_PRIC_VER_ID = B.PROMO_PRIC_VER_ID                
                  AND  A.STAT_CDE = 'S'                                         
                  AND  B.STAT_CDE = 'S'                                         
                  AND  B.PRMY_PROMO_ID = :PV-PROMO-ID                           
                  AND  A.PROMO_PRT_VER_ID NOT IN                                
                  (SELECT  C.PROMO_PRT_VER_ID                                   
                     FROM  PMT_AD_SP_ASGMT C                                    
                    WHERE  C.PROMO_PRT_VER_ID = A.PROMO_PRT_VER_ID              
                      AND  C.STAT_CDE = 'S'                                     
                      AND  C.PROMO_ITM_ID = :PV-PROMO-ITM-ID)                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROES                                              
             WHEN SQLCODE = 2112                                                
             WHEN SQLCODE = -2112                                               
                  SET TBL-ADVER-INDX TO +1                                      
                  PERFORM 2512-OPEN-ADVERSION-CSR                               
                  PERFORM 2515-FETCH-ADVERSION-CSR                              
                          UNTIL PS-END-OF-ADVER-CSR                             
                  PERFORM 2517-CLOSE-ADVERSION-CSR                              
             WHEN SQLCODE = 1403                                                
                  CONTINUE                                                      
             WHEN OTHER                                                         
                  DISPLAY 'PV-PROMO-ITM-ID = ' PV-PROMO-ITM-ID                  
                  MOVE SQLCODE     TO PV-SQL-CODE                               
                  SET ABEND-4013-SQL-ERROR TO TRUE                              
                  MOVE 'PMT_PROMO_PRT_VER'   TO PV-TABLE-NAME                   
                  MOVE '2510-SELECT-NO-PAGE-NBRS'                               
                                             TO PV-PARAGRAPH-NAME               
                  MOVE 'ERROR ON SELECT NO PAGE NBRS'                           
                                             TO PV-OPERATION-MSG                
                  PERFORM Z100-ABEND                                            
           END-EVALUATE.                                                        
                                                                                
      *2510-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 2512-OPEN-ADVERSION-CSR.                                                
      *     OPENS THE AD VERISION CURSOR.                                       
      *----------------------------------------------------------               
       2512-OPEN-ADVERSION-CSR.                                                 
                                                                                
           EXEC SQL                                                             
                 OPEN ADVERSIONCSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROES                                            
                    SET PS-NOT-END-OF-ADVER-CSR TO TRUE                         
               WHEN OTHER                                                       
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE SQLCODE             TO PV-SQL-CODE                     
                    MOVE '2512-OPEN-ADVERSION-CSR'                              
                                      TO PV-PARAGRAPH-NAME                      
                    MOVE 'OPEN ERROR ON ADVERSIONCSR'                           
                                      TO PV-OPERATION-MSG                       
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      *2512-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 2515-FETCH-ADVERSION-CSR.                                               
      *     FETCHES THE AD VERSION CURSOR.                                      
      *----------------------------------------------------------               
       2515-FETCH-ADVERSION-CSR.                                                
                                                                                
           EXEC SQL                                                             
               FETCH ADVERSIONCSR                                               
                INTO  :PV-ADVER-ABBR-NM                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    MOVE PV-ADVER-ABBR-NM TO                                    
                                TBL-ADVER-ABBR-NM(TBL-ADVER-INDX)               
                    SET TBL-ADVER-INDX UP BY +1                                 
               WHEN SQLCODE = 1403                                              
                    SET PS-END-OF-ADVER-CSR TO TRUE                             
               WHEN OTHER                                                       
                    MOVE SQLCODE     TO PV-SQL-CODE                             
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE '2515-FETCH-ADVERSION-CSR'                             
                                      TO PV-PARAGRAPH-NAME                      
                    MOVE 'FETCH ERROR ON ADVERSIONCSR'                          
                                     TO PV-OPERATION-MSG                        
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      *2515-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 2517-CLOSE-ADVERSION-CSR.                                               
      *     CLOSE THE AD VERSION CURSOR.                                        
      *----------------------------------------------------------               
       2517-CLOSE-ADVERSION-CSR.                                                
                                                                                
           EXEC SQL                                                             
                CLOSE ADVERSIONCSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROES                                            
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE SQLCODE     TO PV-SQL-CODE                             
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE '2517-CLOSE-ADVERSION-CSR'                             
                                     TO PV-PARAGRAPH-NAME                       
                    MOVE 'CLOSE ERROR ON ADVERSIONCSR'                          
                                     TO PV-OPERATION-MSG                        
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      *2517-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *---------------------------------------------------------------          
      * 2600-SELECT-PMITM-EFF-DTES.                                             
      *     SELECTS THE PROMO ITEM START AND END EFFECTIVE DATES.               
      *  - 0    = A ROW WAS RETURNED                                            
      *  - 2112 = MORE THEN ONE ROW WAS RETURNED.                               
      *  - 1403 = NO ROWS WERE RETURNED.                                        
      *---------------------------------------------------------------          
       2600-SELECT-PMITM-EFF-DTES.                                              
                                                                                
           EXEC SQL                                                             
               SELECT  TO_CHAR(PROMO_ITM_STRT_TMST,                             
                                'YYYY-MM-DD-HH24.MI.SS')                        
                      ,TO_CHAR(PROMO_ITM_END_TMST,                              
                                'YYYY-MM-DD-HH24.MI.SS')                        
                 INTO  :PV-PROMO-ITM-STRT-TMST                                  
                      ,:PV-PROMO-ITM-END-TMST                                   
                 FROM  PMT_PROMO_ITM_ASSN                                       
                WHERE  PROMO_ITM_ID     = :PV-PROMO-ITM-ID                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROES                                              
             WHEN SQLCODE = 2112                                                
             WHEN SQLCODE = -2112                                               
                  CONTINUE                                                      
             WHEN SQLCODE = 1403                                                
                  MOVE SPACES TO PV-PROMO-ITM-STRT-TMST                         
                                 PV-PROMO-ITM-END-TMST                          
             WHEN OTHER                                                         
                  MOVE SQLCODE     TO PV-SQL-CODE                               
                  SET ABEND-4013-SQL-ERROR TO TRUE                              
                  MOVE 'PMT_PROMO_ITM_ASSN'  TO PV-TABLE-NAME                   
                  MOVE '2600-SELECT-PMITM-EFF-DTES'                             
                                             TO PV-PARAGRAPH-NAME               
                  MOVE 'ERROR ON SELECT EFFECTIVE DATES'                        
                                             TO PV-OPERATION-MSG                
                  PERFORM Z100-ABEND                                            
           END-EVALUATE.                                                        
                                                                                
      *2600-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 2700-POPULATE-SMPL-PRICG.                                               
      *     POPULATES THE COPYBOOK FOR A SIMPLE PRICING RECORD.                 
      *----------------------------------------------------------               
       2700-POPULATE-SMPL-PRICG.                                                
                                                                                
           INITIALIZE PM100-PROMO-ITM-SUMM.                                     
                                                                                
           IF CC-RPT-TYP-CDE = 'A'                                              
              PERFORM 2710-CALCULATE-VOL-CHG                                    
              PERFORM 2720-CALCULATE-QTY-FIELDS                                 
              PERFORM 2730-CALCULATE-SELL-THRU                                  
              MOVE PV-MIN-UNT-REG-RTL-AMT  TO PM100-MIN-UNT-REG-RTL-AMT         
              MOVE PV-MAX-UNT-REG-RTL-AMT  TO PM100-MAX-UNT-REG-RTL-AMT         
              MOVE PV-MIN-UNT-PRM-RTL-AMT  TO PM100-MIN-UNT-PRM-RTL-AMT         
              MOVE PV-MAX-UNT-PRM-RTL-AMT  TO PM100-MAX-UNT-PRM-RTL-AMT         
           ELSE                                                                 
              MOVE ZEROES                  TO PV-VOL-PCT-CHG                    
                                              PV-SELL-THRU-PCT                  
                                              PV-ROUND-PREP-UNT-QTY             
                                              PV-ROUND-EOH-INV-QTY              
              MOVE PV-FCST-MIN-REG-RTL-AMT TO PM100-MIN-UNT-REG-RTL-AMT         
              MOVE PV-FCST-MAX-REG-RTL-AMT TO PM100-MAX-UNT-REG-RTL-AMT         
              MOVE PV-FCST-MIN-PROMO-RTL-AMT                                    
                                           TO PM100-MIN-UNT-PRM-RTL-AMT         
              MOVE PV-FCST-MAX-PROMO-RTL-AMT                                    
                                           TO PM100-MAX-UNT-PRM-RTL-AMT         
           END-IF.                                                              
                                                                                
           MOVE CC-RPT-TYP-CDE          TO PM100-FCST-ACTUAL-IND.               
           SET PM100-SMPL-PRICG TO TRUE.                                        
           MOVE PV-AD-EVNT-ID           TO PM100-AD-EVNT-ID.                    
           MOVE PV-AD-EVNT-NM           TO PM100-AD-EVNT-NM.                    
           MOVE PV-AD-EVNT-NBR-TXT      TO PM100-AD-EVNT-NBR-TXT.               
           MOVE PV-AD-EVNT-STRT-TMST    TO PM100-AD-EVNT-STRT-TMST.             
           MOVE PV-AD-EVNT-END-TMST     TO PM100-AD-EVNT-END-TMST.              
           MOVE PV-PROMO-ID             TO PM100-PROMO-ID.                      
           MOVE PV-PROMO-TYP-CDE        TO PM100-PROMO-TYP-CDE.                 
           MOVE PV-REG-PROMO-TYP-CDE    TO PM100-PROMO-REG-TYP-CDE.             
           MOVE PV-PROMO-NM             TO PM100-PROMO-NM.                      
           MOVE PV-PROMO-ABBR-NM        TO PM100-PROMO-ABBR-NM.                 
           MOVE PV-PROMO-DFLT-STRT-TMST TO PM100-PROMO-STRT-TMST.               
           MOVE PV-PROMO-DFLT-END-TMST  TO PM100-PROMO-END-TMST.                
           MOVE PV-GMA-NBR              TO PM100-GMA-NBR.                       
           MOVE PV-GMA-FRST-NAME        TO PM100-GMA-FRST-NM.                   
           MOVE PV-GMA-LAST-NAME        TO PM100-GMA-LAST-NM.                   
           MOVE PV-DMA-NBR              TO PM100-DMA-NBR.                       
           MOVE PV-DMA-FRST-NAME        TO PM100-DMA-FRST-NM.                   
           MOVE PV-DMA-LAST-NAME        TO PM100-DMA-LAST-NM.                   
           MOVE PV-BYR-NBR              TO PM100-BYR-NBR.                       
           MOVE PV-BUY-FRST-NAME        TO PM100-BYR-FRST-NM.                   
           MOVE PV-BUY-LAST-NAME        TO PM100-BYR-LAST-NM.                   
           MOVE PV-DEPT-NBR             TO PM100-DEPT-NBR.                      
           MOVE PV-DEPT-NM              TO PM100-DEPT-NM.                       
           MOVE PV-PROMO-ITM-ID         TO PM100-PROMO-ITM-ID.                  
           MOVE PV-PROMO-ITM-DESC       TO PM100-PROMO-ITM-DESC.                
           MOVE PV-PROMO-ITM-ADV-IND    TO PM100-PROMO-ITM-ADV-IND.             
           MOVE PV-PROMO-ITM-STRT-TMST  TO PM100-PROMO-ITM-STRT-TMST.           
           MOVE PV-PROMO-ITM-END-TMST   TO PM100-PROMO-ITM-END-TMST.            
           MOVE PV-AD-PG-NBR            TO PM100-AD-PG-NBR.                     
           MOVE PV-AD-SP-CDE            TO PM100-AD-SP-CDE.                     
           MOVE PV-AD-PG-SEQ-NBR        TO PM100-AD-PG-SEQ-NBR.                 
           MOVE PV-SLS-RTL-AMT          TO PM100-ACTL-SLS-RTL-AMT.              
           MOVE PV-MKDN-RTL-AMT         TO PM100-ACTL-MKDN-RTL-AMT.             
           MOVE PV-SLS-UNT-QTY          TO PM100-ACTL-SLS-UNT-QTY.              
           MOVE PV-SLS-COST-AMT         TO PM100-ACTL-SLS-COST-AMT.             
           MOVE PV-ROUND-PREP-UNT-QTY   TO PM100-ACTL-PREP-UNT-QTY.             
           MOVE PV-TOT-MKUP-PCT         TO PM100-ACTL-TOT-MKUP-PCT.             
           MOVE PV-ROUND-EOH-INV-QTY    TO PM100-ACTL-EOH-INV-QTY.              
           MOVE PV-FCST-SLS-RTL-AMT     TO PM100-FCST-SLS-RTL-AMT.              
           MOVE PV-FCST-MKDN-RTL-AMT    TO PM100-FCST-MKDN-RTL-AMT.             
           MOVE PV-FCST-SLS-UNT-QTY     TO PM100-FCST-SLS-UNT-QTY.              
           MOVE PV-FCST-SLS-COST-AMT    TO PM100-FCST-SLS-COST-AMT.             
           MOVE PV-PROJD-PREP-UNT-QTY   TO PM100-FCST-PROJD-UNT-QTY.            
           MOVE PV-FCST-TOT-MKUP-PCT    TO PM100-FCST-TOT-MKUP-PCT.             
           MOVE PV-VOL-PCT-CHG          TO PM100-VOL-PCT-CHG.                   
           MOVE PV-SELL-THRU-PCT        TO PM100-SELL-THRU-PCT.                 
           PERFORM VARYING TBL-ADVER-INDX FROM 1 BY 1                           
              UNTIL TBL-ADVER-INDX > 15                                         
                 OR TBL-ADVER-ABBR-NM(TBL-ADVER-INDX) = SPACES                  
                    SET AD-VER-INDX     TO TBL-ADVER-INDX                       
                    MOVE TBL-ADVER-ABBR-NM(TBL-ADVER-INDX) TO                   
                         PM100-AD-VERSION(AD-VER-INDX)                          
           END-PERFORM.                                                         
                                                                                
      *2700-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *---------------------------------------------------------------          
      * 2710-CALCULATE-VOL-CHG.                                                 
      *     CALCULATE THE VOLUME PERCENT CHANGE.                                
      *---------------------------------------------------------------          
       2710-CALCULATE-VOL-CHG.                                                  
                                                                                
           COMPUTE PV-VOL-PCT-CHG ROUNDED = ((PV-FCST-SLS-RTL-AMT -             
                     PV-SLS-RTL-AMT) /                                          
                     PV-FCST-SLS-RTL-AMT) * -100                                
                     ON SIZE ERROR                                              
                     MOVE 0 TO PV-VOL-PCT-CHG                                   
           END-COMPUTE.                                                         
                                                                                
      *2710-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *---------------------------------------------------------------          
      * 2720-CALCULATE-QTY-FIELDS.                                              
      *     DIVIDE THE ACTUAL PREP UNIT QTY AND THE ACTUAL EOH UNIT             
      *     QTY BY 1000.                                                        
      *---------------------------------------------------------------          
       2720-CALCULATE-QTY-FIELDS.                                               
                                                                                
           COMPUTE PV-ROUND-PREP-UNT-QTY ROUNDED =                              
                     PV-ACTL-PREP-UNT-QTY / 1000                                
                     ON SIZE ERROR                                              
                     MOVE 0 TO PV-ROUND-PREP-UNT-QTY                            
           END-COMPUTE.                                                         
                                                                                
           COMPUTE PV-ROUND-EOH-INV-QTY ROUNDED =                               
                     PV-ACTL-EOH-INV-QTY / 1000                                 
                     ON SIZE ERROR                                              
                     MOVE 0 TO PV-ROUND-EOH-INV-QTY                             
           END-COMPUTE.                                                         
                                                                                
      *2720-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *---------------------------------------------------------------          
      * 2730-CALCULATE-SELL-THRU.                                               
      *     CALCULATE THE SELL THRU PERCENT.                                    
      *---------------------------------------------------------------          
       2730-CALCULATE-SELL-THRU.                                                
                                                                                
           COMPUTE PV-SELL-THRU-PCT ROUNDED =                                   
                   (PV-SLS-UNT-QTY / (PV-ROUND-EOH-INV-QTY                      
                   + PV-SLS-UNT-QTY))  * 100                                    
                   ON SIZE ERROR                                                
                   MOVE 0 TO PV-SELL-THRU-PCT                                   
           END-COMPUTE.                                                         
                                                                                
      *2730-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 3000-PROCESS-TIERED-RECS.                                               
      *     FETCHES ANY TIERED INFORMATION ASSOCIATED WITH THIS                 
      *     PROMO ITEM AND WRITES OUT ANY TIERED RECORDS.                       
      *----------------------------------------------------------               
       3000-PROCESS-TIERED-RECS.                                                
                                                                                
           PERFORM 3100-POPULATE-TIER-PRICG.                                    
           PERFORM 9000-WRITE-PROMO-ITM-REC.                                    
                                                                                
           INITIALIZE PV-TIER-THRS-QTY                                          
                      PV-TIER-THRS-AMT                                          
                      PV-TIER-DISC-AMT                                          
                      PV-TIER-DISC-PCT                                          
                      PV-PRICG-TYP-CDE.                                         
                                                                                
           PERFORM 3020-FETCH-TIER-PRICG-CSR.                                   
                                                                                
      *3000-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 3010-OPEN-TIER-PRICG-CSR.                                               
      *     OPENS THE TIERED PRICING CURSOR.                                    
      *----------------------------------------------------------               
       3010-OPEN-TIER-PRICG-CSR.                                                
                                                                                
           EXEC SQL                                                             
                 OPEN TIERPRICGCSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROES                                            
                    SET PS-NOT-END-OF-TIER-CSR TO TRUE                          
               WHEN OTHER                                                       
                    MOVE SQLCODE     TO PV-SQL-CODE                             
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE '3010-OPEN-TIER-PRICG-CSR'                             
                                      TO PV-PARAGRAPH-NAME                      
                    MOVE 'OPEN ERROR ON TIERPRICGCSR'                           
                                      TO PV-OPERATION-MSG                       
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      *3010-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 3020-FETCH-TIER-PRICG-CSR.                                              
      *     FETCHES THE TIERED PRICING CURSOR.                                  
      *----------------------------------------------------------               
       3020-FETCH-TIER-PRICG-CSR.                                               
                                                                                
           EXEC SQL                                                             
               FETCH TIERPRICGCSR                                               
                INTO  :PV-TIER-PROMO-ITM-ID                                     
                     ,:PV-TIER-THRS-QTY :PV-THRS-QTY-NULL                       
                     ,:PV-TIER-THRS-AMT :PV-THRS-AMT-NULL                       
                     ,:PV-TIER-DISC-AMT :PV-TIER-AMT-NULL                       
                     ,:PV-TIER-DISC-PCT :PV-TIER-PCT-NULL                       
                     ,:PV-TIER-PRICG-ID                                         
                     ,:PV-RCPT-CMMT-TXT                                         
                     ,:PV-PRICG-TYP-CDE :PV-PRICG-TYP-NULL                      
                     ,:PV-TIER-SLS-RTL-AMT                                      
                     ,:PV-TIER-SLS-UNT-QTY                                      
                     ,:PV-TIER-MKDN-RTL-AMT                                     
                     ,:PV-TIER-SLS-COST-AMT                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROES                                            
                    ADD 1 TO PA-TIER-CNT                                        
               WHEN SQLCODE = 1403                                              
                    SET PS-END-OF-TIER-CSR TO TRUE                              
               WHEN OTHER                                                       
                    MOVE SQLCODE     TO PV-SQL-CODE                             
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE '3020-FETCH-TIER-PRICG-CSR'                            
                                      TO PV-PARAGRAPH-NAME                      
                    MOVE 'FETCH ERROR ON TIERPRICGCSR'                          
                                     TO PV-OPERATION-MSG                        
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      *3020-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 3030-CLOSE-TIER-PRICG-CSR.                                              
      *     CLOSE THE TIERED PRICING CURSOR.                                    
      *----------------------------------------------------------               
       3030-CLOSE-TIER-PRICG-CSR.                                               
                                                                                
           EXEC SQL                                                             
                CLOSE TIERPRICGCSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE SQLCODE     TO PV-SQL-CODE                             
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE '3030-CLOSE-TIER-PRICG-CSR'                            
                                     TO PV-PARAGRAPH-NAME                       
                    MOVE 'CLOSE ERROR ON TIERPRICGCSR'                          
                                     TO PV-OPERATION-MSG                        
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      *3030-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 3100-POPULATE-TIER-PRICG.                                               
      *     POPULATES THE COPYBOOK FOR A TIERED PRICING RECORD.                 
      *----------------------------------------------------------               
       3100-POPULATE-TIER-PRICG.                                                
                                                                                
           INITIALIZE PM100-SMPL-PRICG-REC                                      
                      PM100-AD-VERSION-ARRAY.                                   
                                                                                
           SET PM100-TIER-PRICG TO TRUE.                                        
           PERFORM 5000-CALCULATE-TIER-SUM-VAR.                                 
           MOVE PV-ROUND-SLS-RTL-AMT  TO PM100-ACTL-SLS-RTL-AMT.                
           MOVE PV-ROUND-MKDN-RTL-AMT TO PM100-ACTL-MKDN-RTL-AMT.               
           MOVE PV-ROUND-SLS-UNT-QTY  TO PM100-ACTL-SLS-UNT-QTY.                
           MOVE PV-ROUND-SLS-COST-AMT TO PM100-ACTL-SLS-COST-AMT.               
           MOVE PV-TIER-THRS-AMT      TO PM100-TIER-THRS-AMT.                   
           MOVE PV-TIER-DISC-PCT      TO PM100-TIER-DISC-PCT.                   
           MOVE PV-TIER-DISC-AMT      TO PM100-TIER-DISC-AMT.                   
           MOVE PV-TIER-THRS-QTY      TO PM100-TIER-THRS-QTY.                   
           MOVE PV-TIER-PRICG-ID      TO PM100-TIER-PRICG-ID.                   
           MOVE PV-RCPT-CMMT-TXT      TO PM100-TIER-RCPT-CMMT-TXT.              
           MOVE PV-PRICG-TYP-CDE      TO PM100-TIER-PRICG-TYP-CDE.              
           MOVE SPACES                TO PM100-PROMO-ITM-STRT-TMST              
                                         PM100-PROMO-ITM-END-TMST.              
                                                                                
      *3100-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 4000-PROCESS-ADMIN-TIER-RECS.                                           
      *     FETCHES ANY ADMIN TIERED INFORMATION ASSOCIATED WITH                
      *     THE PROMO ID AND WRITES OUT ANY ADMIN RECORDS.                      
      *----------------------------------------------------------               
       4000-PROCESS-ADMIN-TIER-RECS.                                            
                                                                                
           PERFORM 4100-POPULATE-ADMIN-TIER.                                    
           PERFORM 9000-WRITE-PROMO-ITM-REC.                                    
                                                                                
           INITIALIZE PV-TIER-THRS-QTY                                          
                      PV-TIER-THRS-AMT                                          
                      PV-TIER-DISC-AMT                                          
                      PV-TIER-DISC-PCT                                          
                      PV-PRICG-TYP-CDE.                                         
                                                                                
           PERFORM 4020-FETCH-ADMIN-TIER-CSR.                                   
                                                                                
      *4000-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 4010-OPEN-ADMIN-TIER-CSR.                                               
      *     OPENS THE ADMIN TIER CURSOR.                                        
      *----------------------------------------------------------               
       4010-OPEN-ADMIN-TIER-CSR.                                                
                                                                                
           EXEC SQL                                                             
                 OPEN ADMINTIERCSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROES                                            
                    SET PS-NOT-END-OF-ADMIN-CSR TO TRUE                         
               WHEN OTHER                                                       
                    MOVE SQLCODE     TO PV-SQL-CODE                             
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE '4010-OPEN-ADMIN-TIER-CSR'                             
                                      TO PV-PARAGRAPH-NAME                      
                    MOVE 'OPEN ERROR ON ADMINTIERCSR'                           
                                      TO PV-OPERATION-MSG                       
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      *4010-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 4020-FETCH-ADMIN-TIER-CSR.                                              
      *     FETCHES THE ADMIN TIER CURSOR.                                      
      *----------------------------------------------------------               
       4020-FETCH-ADMIN-TIER-CSR.                                               
                                                                                
           EXEC SQL                                                             
               FETCH ADMINTIERCSR                                               
                INTO  :PV-TIER-PROMO-ID                                         
                     ,:PV-TIER-THRS-QTY :PV-THRS-QTY-NULL                       
                     ,:PV-TIER-THRS-AMT :PV-THRS-AMT-NULL                       
                     ,:PV-TIER-DISC-AMT :PV-TIER-AMT-NULL                       
                     ,:PV-TIER-DISC-PCT :PV-TIER-PCT-NULL                       
                     ,:PV-TIER-PRICG-ID                                         
                     ,:PV-RCPT-CMMT-TXT                                         
                     ,:PV-PRICG-TYP-CDE :PV-PRICG-TYP-NULL                      
                     ,:PV-TIER-SLS-RTL-AMT                                      
                     ,:PV-TIER-SLS-UNT-QTY                                      
                     ,:PV-TIER-MKDN-RTL-AMT                                     
                     ,:PV-TIER-SLS-COST-AMT                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROES                                            
                    ADD 1 TO PA-ADMIN-CNT                                       
               WHEN SQLCODE = 1403                                              
                    SET PS-END-OF-ADMIN-CSR TO TRUE                             
               WHEN OTHER                                                       
                    MOVE SQLCODE     TO PV-SQL-CODE                             
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE '4020-FETCH-ADMIN-TIER-CSR'                            
                                      TO PV-PARAGRAPH-NAME                      
                    MOVE 'FETCH ERROR ON ADMINTIERCSR'                          
                                     TO PV-OPERATION-MSG                        
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      *4020-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 4030-CLOSE-ADMIN-TIER-CSR.                                              
      *     CLOSE THE ADMIN TIER CURSOR.                                        
      *----------------------------------------------------------               
       4030-CLOSE-ADMIN-TIER-CSR.                                               
                                                                                
           EXEC SQL                                                             
                CLOSE ADMINTIERCSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE SQLCODE     TO PV-SQL-CODE                             
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE '4030-CLOSE-ADMIN-TIER-CSR'                            
                                     TO PV-PARAGRAPH-NAME                       
                    MOVE 'CLOSE ERROR ON ADMINTIERCSR'                          
                                     TO PV-OPERATION-MSG                        
                    PERFORM Z100-ABEND                                          
           END-EVALUATE.                                                        
                                                                                
      *4030-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 4100-POPULATE-ADMIN-TIER.                                               
      *     POPULATES THE COPYBOOK FOR AN ADMIN TIER RECORD.                    
      *----------------------------------------------------------               
       4100-POPULATE-ADMIN-TIER.                                                
                                                                                
           INITIALIZE PM100-SMPL-PRICG-REC                                      
                      PM100-AD-VERSION-ARRAY.                                   
                                                                                
           SET PM100-ADMN-TIER-PRICG TO TRUE.                                   
           PERFORM 5000-CALCULATE-TIER-SUM-VAR.                                 
           MOVE ZEROES                TO PM100-GMA-NBR                          
                                         PM100-DMA-NBR                          
                                         PM100-BYR-NBR                          
                                         PM100-DEPT-NBR                         
                                         PM100-PROMO-ITM-ID                     
                                         PM100-AD-PG-NBR.                       
           MOVE SPACES                TO PM100-GMA-FRST-NM                      
                                         PM100-GMA-LAST-NM                      
                                         PM100-DMA-FRST-NM                      
                                         PM100-DMA-LAST-NM                      
                                         PM100-BYR-FRST-NM                      
                                         PM100-BYR-LAST-NM                      
                                         PM100-DEPT-NM                          
                                         PM100-AD-SP-CDE                        
                                         PM100-PROMO-ITM-STRT-TMST              
                                         PM100-PROMO-ITM-END-TMST.              
           MOVE 'A'                   TO PM100-PROMO-ITM-ADV-IND.               
           MOVE PV-ROUND-SLS-RTL-AMT  TO PM100-ACTL-SLS-RTL-AMT.                
           MOVE PV-ROUND-MKDN-RTL-AMT TO PM100-ACTL-MKDN-RTL-AMT.               
           MOVE PV-ROUND-SLS-UNT-QTY  TO PM100-ACTL-SLS-UNT-QTY.                
           MOVE PV-ROUND-SLS-COST-AMT TO PM100-ACTL-SLS-COST-AMT.               
           MOVE PV-TIER-THRS-AMT      TO PM100-TIER-THRS-AMT.                   
           MOVE PV-TIER-DISC-PCT      TO PM100-TIER-DISC-PCT.                   
           MOVE PV-TIER-DISC-AMT      TO PM100-TIER-DISC-AMT.                   
           MOVE PV-TIER-THRS-QTY      TO PM100-TIER-THRS-QTY.                   
           MOVE PV-TIER-PRICG-ID      TO PM100-TIER-PRICG-ID.                   
           MOVE PV-RCPT-CMMT-TXT      TO PM100-TIER-RCPT-CMMT-TXT               
                                         PM100-PROMO-ITM-DESC.                  
           MOVE PV-PRICG-TYP-CDE      TO PM100-TIER-PRICG-TYP-CDE.              
                                                                                
      *4100-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *---------------------------------------------------------------          
      * 5000-CALCULATE-TIER-SUM-VAR.                                            
      *     DIVIDE THE SALES RTL AMT, SALES UNIT QTY, MARKDOWN RTL              
      *     AMT, AND THE SLS COST AMT BY 1000.                                  
      *---------------------------------------------------------------          
       5000-CALCULATE-TIER-SUM-VAR.                                             
                                                                                
           COMPUTE PV-ROUND-SLS-RTL-AMT ROUNDED =                               
                     PV-TIER-SLS-RTL-AMT / 1000                                 
                     ON SIZE ERROR                                              
                     MOVE 0 TO PV-ROUND-SLS-RTL-AMT                             
           END-COMPUTE.                                                         
                                                                                
           COMPUTE PV-ROUND-MKDN-RTL-AMT ROUNDED =                              
                     PV-TIER-MKDN-RTL-AMT / 1000                                
                     ON SIZE ERROR                                              
                     MOVE 0 TO PV-ROUND-MKDN-RTL-AMT                            
           END-COMPUTE.                                                         
                                                                                
           COMPUTE PV-ROUND-SLS-UNT-QTY ROUNDED =                               
                     PV-TIER-SLS-UNT-QTY / 1000                                 
                     ON SIZE ERROR                                              
                     MOVE 0 TO PV-ROUND-SLS-UNT-QTY                             
           END-COMPUTE.                                                         
                                                                                
           COMPUTE PV-ROUND-SLS-COST-AMT ROUNDED =                              
                     PV-TIER-SLS-COST-AMT / 1000                                
                     ON SIZE ERROR                                              
                     MOVE 0 TO PV-ROUND-SLS-COST-AMT                            
           END-COMPUTE.                                                         
                                                                                
      *5000-EXIT.                                                               
       EJECT.                                                                   
                                                                                
070941****************************************************************          
070941** READ THE ORACLE LOGON CONTROL CARD.                        **          
070941****************************************************************          
070941 7000-READ-ORACLE-LOGON.                                                  
070941                                                                          
070941     READ ORACLE-LOGON-FILE                                               
070941         INTO CC-ORACLE-CONNECTION                                        
070941         AT END                                                           
070941             SET PS-END-OF-LOGON-FILE TO TRUE                             
070941     END-READ.                                                            
                                                                                
      *----------------------------------------------------------               
      * 9000-WRITE-PROMO-ITM-REC.                                               
      *     WRITES THE PROMO ITEM RECORD TO THE SUMMARY FILE.                   
      *----------------------------------------------------------               
       9000-WRITE-PROMO-ITM-REC.                                                
                                                                                
           WRITE PROMO-ITM-SUMM-REC                                             
               FROM PM100-PROMO-ITM-SUMM.                                       
                                                                                
           ADD 1 TO PA-RECS-WRITTEN.                                            
                                                                                
      *9000-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT     *        
      * LOGONS TO ORACLE.                                              *        
      ******************************************************************        
070941*    COPY PMPD700.                                                        
                                                                                
      *----------------------------------------------------------               
      * 9800-ABEND.                                                             
      *     PERFORMS A NON-SQL-ABEND.                                           
      *----------------------------------------------------------               
       9800-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      *9800-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * Z100-ABEND.                                                             
      *     PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE.                    
      *----------------------------------------------------------               
       Z100-ABEND.                                                              
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' PV-SQL-CODE.                       
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      *9900-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 9999-WRAPUP.                                                            
      *     END OF PROGRAM ROUTINE.  CLOSES ALL FILES AND DISPLAYS              
      *     ANY DATA FROM THE PROGRAM.                                          
      *----------------------------------------------------------               
       9999-WRAPUP.                                                             
                                                                                
           CLOSE CC-RUN-DTE-FILE                                                
                 PROMO-ITM-SUMM-FILE.                                           
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*********************************************'.             
           DISPLAY 'PROGRAM NAME: ' PC-PROGRAM-NAME.                            
           DISPLAY '*********************************************'.             
           DISPLAY 'NUMBER OF SIMPLE PRICING RECS FETCHED: '                    
                                                     PA-ITEM-CNT.               
           DISPLAY 'NUMBER OF TIERED RECS FETCHED: ' PA-TIER-CNT.               
           DISPLAY 'NUMBER OF ADMIN TIERED RECS FETCHED: '                      
                                                     PA-ADMIN-CNT.              
           DISPLAY 'NUMBER OF RECS WRITTEN: ' PA-RECS-WRITTEN.                  
           DISPLAY '*********************************************'.             
                                                                                
      *9999-EXIT.                                                               
       EJECT.                                                                   
                                                                                
