000100 IDENTIFICATION DIVISION.                                                 
000200******************************************************************        
000300*  DELETE RECORDS FROM CARTON UPDATE BACKUP FILE.                         
000400******************************************************************        
000500                                                                          
000600 PROGRAM-ID.        SUKUT031.                                             
000700 AUTHOR.            PATRICK BURKE.                                        
000800 INSTALLATION.      KOHLS.                                                
000900 DATE-WRITTEN       MAY 2000.                                             
001000 DATE-COMPILED.                                                           
001400*****************************************************************         
001807* CHANGE HISTORY                                                *         
001300*     MADE BY: PRITHIVIRAJ SUBRAMANIAN (COGNIZANT)              *         
001300*     DATE:    11/14/2014                                       *         
001300*      RECOMPILE THE PROGRAM AS THE PO NUMBER FIELD IN THE      *         
001300*      COPYBOOK RCRD017 IS INCREASED TO 9 FROM 7.               *         
001400*****************************************************************         
001100                                                                          
001200 ENVIRONMENT DIVISION.                                                    
001300 CONFIGURATION SECTION.                                                   
001400 SOURCE-COMPUTER.   IBM-3090.                                             
001500 OBJECT-COMPUTER.   IBM-3090.                                             
001600 INPUT-OUTPUT SECTION.                                                    
001700                                                                          
001800 FILE-CONTROL.                                                            
001900     SELECT CARTON-UPDATE-FILE                                            
002000         ASSIGN TO INP01.                                                 
002100     SELECT EDITED-UPDATE-FILE                                            
002200         ASSIGN TO OUT01.                                                 
002300                                                                          
002400 DATA DIVISION.                                                           
002500 FILE SECTION.                                                            
002600 FD  CARTON-UPDATE-FILE                                                   
002700     RECORDING MODE IS F                                                  
002800     DATA RECORD IS CARTON-UPDATE-RECORD.                                 
002900                                                                          
003000 01  CARTON-UPDATE-RECORD              PIC X(93).                         
003100                                                                          
003200 FD  EDITED-UPDATE-FILE                                                   
003300     RECORDING MODE IS F                                                  
003400     DATA RECORD IS EDITED-UPDATE-RECORD.                                 
003500                                                                          
003600 01  EDITED-UPDATE-RECORD              PIC X(93).                         
003700/                                                                         
003800 WORKING-STORAGE SECTION.                                                 
003900                                                                          
004000 01  ABEND-CODE                       PIC S9(04)  COMP SYNC               
004100                                                    VALUE ZERO.           
004200 01  PS-PROGRAM-SWITCHES.                                                 
004300     05  PS-EOF-UPDFILE-SW             PIC X(01) VALUE 'N'.               
004400         88  PS-EOF-UPDFILE                        VALUE 'Y'.             
004500     05  PS-INPKSU-FOUND-SW            PIC X(01) VALUE 'N'.               
004600         88  PS-INPKSU-FOUND                       VALUE 'Y'.             
004700         88  PS-INPKSU-NOT-FOUND                   VALUE 'N'.             
004710     05  PS-PURCHS-FOUND-SW            PIC X(01) VALUE 'N'.               
004720         88  PS-PURCHS-FOUND                       VALUE 'Y'.             
004730         88  PS-PURCHS-NOT-FOUND                   VALUE 'N'.             
004731     05  PS-STATCD-FOUND-SW            PIC X(01) VALUE 'N'.               
004732         88  PS-STATCD-FOUND                       VALUE 'Y'.             
004733         88  PS-STATCD-NOT-FOUND                   VALUE 'N'.             
004734     05  PS-KRPRPM-FOUND-SW            PIC X(01) VALUE 'N'.               
004735         88  PS-KRPRPM-FOUND                       VALUE 'Y'.             
004736         88  PS-KRPRPM-NOT-FOUND                   VALUE 'N'.             
004740     05  PS-GOOD-RECORD-SW             PIC X(01) VALUE 'N'.               
004750         88  PS-GOOD-RECORD                        VALUE 'Y'.             
004760         88  PS-NOT-GOOD-RECORD                    VALUE 'N'.             
004800                                                                          
004900 01  PC-PROGRAM-CONSTANTS.                                                
005000     05  PC-DEFAULT-TMST              PIC  X(26)  VALUE                   
005100         '9999-09-09-09.09.09.999999'.                                    
005400                                                                          
005900 01  PV-PROGRAM-VARIABLES.                                                
006401     05  PV-PO-NBR                    PIC S9(9) COMP VALUE ZERO.          
006402     05  PV-PO-LNE-NBR                PIC S9(9) COMP VALUE ZERO.          
006403     05  PV-UPC-NBR                   PIC S9(15) COMP VALUE ZERO.         
006404     05  PV-CARTON-NBR             PIC S9(18)V COMP-3 VALUE ZERO.         
006405     05  PV-CURRENT-TMST              PIC X(26) VALUE SPACES.             
006410                                                                          
006500     05  PV-NUM-DTE                   PIC 9(08) VALUE ZERO.               
006510     05  PV-PURCHS-NUM-DTE            PIC 9(08) VALUE ZERO.               
006600                                                                          
006601     05  PV-FORMAT-TMST.                                                  
006602         10  PV-FORMAT-TMST-DTE.                                          
006603             15  PV-FORMAT-TMST-DTE-CY  PIC X(04)  VALUE SPACES.          
006604             15  PV-FORMAT-TMST-DTE-D1  PIC X(01)  VALUE '-'.             
006605             15  PV-FORMAT-TMST-DTE-MM  PIC X(02)  VALUE SPACES.          
006606             15  PV-FORMAT-TMST-DTE-D2  PIC X(01)  VALUE '-'.             
006607             15  PV-FORMAT-TMST-DTE-DD  PIC X(02)  VALUE SPACES.          
006608             15  PV-FORMAT-TMST-DTE-D3  PIC X(01)  VALUE '-'.             
006609         10  PV-FORMAT-TMST-TM.                                           
006610             15  PV-FORMAT-TMST-TM-HH   PIC X(02)  VALUE SPACES.          
006611             15  PV-FORMAT-TMST-TM-D1   PIC X(01)  VALUE '.'.             
006612             15  PV-FORMAT-TMST-TM-MM   PIC X(02)  VALUE SPACES.          
006613             15  PV-FORMAT-TMST-TM-D2   PIC X(10)  VALUE                  
006620                                                     '.00.000000'.        
006621     05  PV-REFORMAT-TMST.                                                
006622         10  PV-REFORMAT-TMST-DTE.                                        
006623             15  PV-REFORMAT-TMST-DTE-CY  PIC 9(04).                      
006624             15  PV-REFORMAT-TMST-DTE-MM  PIC 9(02).                      
006625             15  PV-REFORMAT-TMST-DTE-DD  PIC 9(02).                      
006626         10  PV-REFORMAT-TMST-TM.                                         
006627             15  PV-REFORMAT-TMST-TM-HH   PIC 9(02).                      
006628             15  PV-REFORMAT-TMST-TM-MM   PIC 9(02).                      
006630                                                                          
006700 01  PV-COUNTERS.                                                         
007000     05  PV-TOTAL-INPUT-READ          PIC S9(17) COMP-3                   
007100                                                VALUE ZERO.               
007200     05  PV-UPDATES-WRITTEN           PIC S9(17) COMP-3                   
007300                                                VALUE ZERO.               
007400     05  PV-RECORDS-DELETED           PIC S9(17) COMP-3                   
007500                                                VALUE ZERO.               
007800/                                                                         
007900*  CARTON-UPDATE-RECORD.                                                  
007910                                                                          
008000     COPY RCRD017.                                                        
008100/                                                                         
008900*  PACKSLIP / VENDOR SHIPMENT UNIT ASSOCIATION TABLE.                     
008910                                                                          
009000     EXEC SQL                                                             
009100         INCLUDE TINPKSU                                                  
009200     END-EXEC.                                                            
009300/                                                                         
009400*  SHIPPING UNIT TABLE.                                                   
009410                                                                          
009500     EXEC SQL                                                             
009600         INCLUDE TSHPUNT                                                  
009700     END-EXEC.                                                            
009800/                                                                         
009900*  PURCHASE ORDER TABLE.                                                  
009910                                                                          
010000     EXEC SQL                                                             
010100         INCLUDE TPURCHS                                                  
010200     END-EXEC.                                                            
010300/                                                                         
010320                                                                          
010330     EXEC SQL                                                             
010340         INCLUDE TSTATCD                                                  
010350     END-EXEC.                                                            
010360/                                                                         
010370                                                                          
010380     EXEC SQL                                                             
010390         INCLUDE TKRPRPM                                                  
010391     END-EXEC.                                                            
010392/                                                                         
010400     EXEC SQL                                                             
010500         INCLUDE SQLCA                                                    
010600     END-EXEC.                                                            
010610                                                                          
010620     COPY SQLCA2.                                                         
010700/                                                                         
011000 PROCEDURE DIVISION.                                                      
011100                                                                          
011200 0000-PROGRAM-MAINLINE.                                                   
011300                                                                          
011400     PERFORM 1000-INITIALIZE.                                             
011500                                                                          
011600     PERFORM 2000-PROCESS-UPDATE-FILE                                     
011700         UNTIL PS-EOF-UPDFILE                                             
011800                                                                          
011900     PERFORM 9000-EOJ-ROUTINE.                                            
012000                                                                          
012100     GOBACK.                                                              
012200/                                                                         
012300 1000-INITIALIZE.                                                         
012400                                                                          
012500     OPEN INPUT  CARTON-UPDATE-FILE                                       
012600          OUTPUT EDITED-UPDATE-FILE.                                      
012730                                                                          
012800     PERFORM 8000-READ-UPD-FILE                                           
012900     IF PS-EOF-UPDFILE                                                    
013300         DISPLAY 'UPDATE FILE INP01 IS EMPTY'                             
013400     END-IF.                                                              
013500/                                                                         
013600 2000-PROCESS-UPDATE-FILE.                                                
013700                                                                          
013710     IF  RC017-PO-NBR = ZERO                                              
013711         ADD 1 TO PV-RECORDS-DELETED                                      
013730     ELSE                                                                 
013800         PERFORM 7000-SELECT-TINPKSU                                      
013900         EVALUATE TRUE                                                    
014000           WHEN PS-INPKSU-FOUND                                           
015700              PERFORM 7100-SELECT-TPURCHS                                 
015704              PERFORM 3000-CHECK-TPURCHS                                  
015707                    IF PS-GOOD-RECORD                                     
015708                       PERFORM 8100-WRITE-UPDATE-RECORD                   
015709                    ELSE                                                  
015710                       ADD 1 TO PV-RECORDS-DELETED                        
015711                    END-IF                                                
015712           WHEN OTHER                                                     
015720              ADD 1 TO PV-RECORDS-DELETED                                 
015800         END-EVALUATE                                                     
015810     END-IF.                                                              
015900                                                                          
016000     PERFORM 8000-READ-UPD-FILE.                                          
016110                                                                          
016120/                                                                         
016121 3000-CHECK-TPURCHS.                                                      
016122                                                                          
016123         IF PS-PURCHS-FOUND                                               
016130           PERFORM 7200-SELECT-TSTATCD                                    
016140             IF PS-STATCD-FOUND                                           
016141               PERFORM 7300-SELECT-TKRPRPM                                
016144             ELSE                                                         
016145           SET PS-GOOD-RECORD       TO TRUE                               
016146           SET PS-KRPRPM-NOT-FOUND  TO TRUE                               
016147             END-IF                                                       
016148         END-IF.                                                          
016149                                                                          
016150         IF PS-KRPRPM-FOUND                                               
016151              PERFORM 3100-CHECK-TKRPRPM                                  
016152         END-IF.                                                          
016160                                                                          
016170/                                                                         
016180 3100-CHECK-TKRPRPM.                                                      
016181                                                                          
016182            PERFORM 3200-FORMAT-TMST.                                     
016196                                                                          
016200            IF (PV-NUM-DTE - PV-PURCHS-NUM-DTE)                           
016300                  <  KRPRPM-FUNC-QTY                                      
016400           SET PS-GOOD-RECORD TO TRUE.                                    
016500/                                                                         
016510 3200-FORMAT-TMST.                                                        
016520                                                                          
016600            PERFORM 7500-SET-CURRENT-TMST.                                
016610                                                                          
016700            MOVE PV-CURRENT-TMST        TO PV-FORMAT-TMST                 
016800            MOVE PV-FORMAT-TMST-DTE-CY  TO PV-REFORMAT-TMST-DTE-CY        
016900            MOVE PV-FORMAT-TMST-DTE-MM  TO PV-REFORMAT-TMST-DTE-MM        
017000            MOVE PV-FORMAT-TMST-DTE-DD  TO PV-REFORMAT-TMST-DTE-DD        
017100            MOVE PV-REFORMAT-TMST-DTE   TO PV-NUM-DTE                     
017200                                                                          
017300            MOVE PURCHS-STATUS-TMST     TO PV-FORMAT-TMST-DTE             
017400            MOVE PV-FORMAT-TMST-DTE-CY  TO PV-REFORMAT-TMST-DTE-CY        
017500            MOVE PV-FORMAT-TMST-DTE-MM  TO PV-REFORMAT-TMST-DTE-MM        
017600            MOVE PV-FORMAT-TMST-DTE-DD  TO PV-REFORMAT-TMST-DTE-DD        
017700            MOVE PV-REFORMAT-TMST-DTE   TO PV-PURCHS-NUM-DTE.             
017710                                                                          
017800/                                                                         
027703 7000-SELECT-TINPKSU.                                                     
027710                                                                          
027720     INITIALIZE DCLTINPKSU.                                               
027730                                                                          
027800     EXEC SQL                                                             
027900         SELECT                                                           
028000                A.SHIPT_UNT_ID                                            
028100             ,  A.PO_NBR                                                  
028500          INTO                                                            
028600                :INPKSU-SHIPT-UNT-ID                                      
028700               ,:INPKSU-PO-NBR                                            
029200          FROM                                                            
029300                TINPKSU A                                                 
029400         WHERE                                                            
029500                A.SHIPT_UNT_ID       = :PV-CARTON-NBR AND                 
029600                A.PO_NBR             = :PV-PO-NBR                         
029900     END-EXEC                                                             
030000                                                                          
030100     EVALUATE TRUE                                                        
030200         WHEN SQLCODE = ZERO                                              
030201             SET PS-INPKSU-FOUND  TO TRUE                                 
030400         WHEN SQLCODE = +100                                              
030500             SET PS-INPKSU-NOT-FOUND  TO TRUE                             
030600         WHEN SQLCODE NOT EQUAL ZERO                                      
031100         WHEN SQLWARN0 NOT = SPACES                                       
031200             DISPLAY '** ABEND - SUKUT031 **'                             
031300             DISPLAY '** 7000-SELECT-TINPKSU **'                          
031400             PERFORM 9300-SQL-WARNING                                     
031500     END-EVALUATE.                                                        
031600/                                                                         
031700 7100-SELECT-TPURCHS.                                                     
031800                                                                          
031810     INITIALIZE DCLTPURCHS.                                               
031820                                                                          
031900     EXEC SQL                                                             
032000         SELECT                                                           
032100             A.PO_NBR                                                     
032120           , A.STATUS_TMST                                                
032130           , A.STATUS_CDE                                                 
032200         INTO                                                             
032300             :PURCHS-PO-NBR                                               
032320           , :PURCHS-STATUS-TMST                                          
032330           , :PURCHS-STATUS-CDE                                           
032400         FROM  TPURCHS A                                                  
032500         WHERE                                                            
032600             A.PO_NBR                = :PV-PO-NBR                         
032700         AND A.VER_STATUS_CDE        = 'A'                                
032800                                                                          
032900     END-EXEC                                                             
033000                                                                          
033100     EVALUATE TRUE                                                        
033200         WHEN SQLCODE = ZERO                                              
033210             SET PS-PURCHS-FOUND  TO TRUE                                 
033400         WHEN SQLCODE = +100                                              
033410             SET PS-PURCHS-NOT-FOUND  TO TRUE                             
033600         WHEN SQLCODE < ZERO                                              
033700             DISPLAY '** ABEND - SUKUT031 **'                             
033800             DISPLAY '** 7100-SELECT-TPURCHS **'                          
033900             PERFORM 9200-SQL-ERROR                                       
034000         WHEN SQLCODE > ZERO                                              
034100         WHEN SQLWARN0 NOT = SPACES                                       
034200             DISPLAY '** ABEND - SUKUT031 **'                             
034300             DISPLAY '** 7100-SELECT-TPURCHS **'                          
034400             PERFORM 9300-SQL-WARNING                                     
034500     END-EVALUATE.                                                        
034600/                                                                         
034700 7200-SELECT-TSTATCD.                                                     
034800                                                                          
035010     INITIALIZE DCLTSTATCD.                                               
035020                                                                          
035100     EXEC SQL                                                             
035200         SELECT                                                           
035600               STATUS_CDE                                                 
035900         INTO                                                             
036300             :STATCD-STATUS-CDE                                           
036700         FROM  TSTATCD                                                    
036800         WHERE                                                            
036810              STATUS_CDE            = :PURCHS-STATUS-CDE                  
036820         AND  STATUS_TYP_CDE        = 'P'                                 
036830         AND  STATUS_CLS            = 'CMP'                               
037100                                                                          
037200     END-EXEC                                                             
037300                                                                          
037400     EVALUATE TRUE                                                        
037500         WHEN SQLCODE = ZERO                                              
037600             SET PS-STATCD-FOUND  TO TRUE                                 
037700         WHEN SQLCODE = +100                                              
037800             SET PS-STATCD-NOT-FOUND  TO TRUE                             
037810         WHEN SQLCODE < ZERO                                              
037820             DISPLAY '** ABEND - SUKUT031 **'                             
037830             DISPLAY '** 7200-SELECT-TSTATCD **'                          
037840             PERFORM 9200-SQL-ERROR                                       
037850         WHEN SQLCODE > ZERO                                              
037860         WHEN SQLWARN0 NOT = SPACES                                       
037870             DISPLAY '** ABEND - SUKUT031 **'                             
037880             DISPLAY '** 7200-SELECT-TSTATCD **'                          
037890             PERFORM 9300-SQL-WARNING                                     
037891     END-EVALUATE.                                                        
037892/                                                                         
037893 7300-SELECT-TKRPRPM.                                                     
037894                                                                          
037895     INITIALIZE DCLTKRPRPM.                                               
037896                                                                          
037897     EXEC SQL                                                             
037899        SELECT  FUNC_QTY                                                  
037901          INTO :KRPRPM-FUNC-QTY                                           
037903          FROM  TKRPRPM                                                   
037906         WHERE  LOC_ID                  =  90                             
037907           AND  SYS_PRC_FUNC_CDE        = 'BCTNPG'                        
037909                                                                          
037910     END-EXEC                                                             
037911                                                                          
037912     EVALUATE TRUE                                                        
037913         WHEN SQLCODE = ZERO                                              
037914             SET PS-KRPRPM-FOUND  TO TRUE                                 
037915         WHEN SQLCODE = +100                                              
037916             SET PS-KRPRPM-NOT-FOUND  TO TRUE                             
037917         WHEN SQLCODE < ZERO                                              
037918             DISPLAY '** ABEND - SUKUT031 **'                             
037919             DISPLAY '** 7300-SELECT-TKRPRPM **'                          
037920             PERFORM 9200-SQL-ERROR                                       
037921         WHEN SQLCODE > ZERO                                              
037922         WHEN SQLWARN0 NOT = SPACES                                       
037923             DISPLAY '** ABEND - SUKUT031 **'                             
037924             DISPLAY '** 7300-SELECT-TKRPRPM **'                          
037925             PERFORM 9300-SQL-WARNING                                     
037926     END-EVALUATE.                                                        
037927/                                                                         
037930 7500-SET-CURRENT-TMST.                                                   
037931                                                                          
037932          EXEC SQL                                                        
037933              SET :PV-CURRENT-TMST = CURRENT TIMESTAMP                    
037934          END-EXEC                                                        
037935                                                                          
037936         EVALUATE TRUE                                                    
037937             WHEN SQLCODE = ZERO                                          
037938                 CONTINUE                                                 
037939                                                                          
037940             WHEN SQLWARN0 NOT EQUAL SPACE                                
037941             WHEN SQLCODE NOT EQUAL ZERO                                  
037942                 DISPLAY '**************************************'         
037943                 DISPLAY '** ABEND                            **'         
037944                 DISPLAY '** PROGRAM = SUKUT031               **'         
037945                 DISPLAY '** PARAGRAPH = 7500-SET-CURRENT-TMST**'         
037946                 DISPLAY '** SET CURRENT TIMESTAMP            **'         
037947                 DISPLAY '** CURRENT TMST = ' PV-CURRENT-TMST             
037948                 DISPLAY '**************************************'         
037949                 PERFORM 9200-SQL-ERROR                                   
037950         END-EVALUATE.                                                    
037951                                                                          
037960 8000-READ-UPD-FILE.                                                      
038000     READ CARTON-UPDATE-FILE                                              
038100          INTO RC017-CARTON-UPD-RECORD                                    
038200     AT END                                                               
038300         SET PS-EOF-UPDFILE           TO TRUE                             
038400     NOT AT END                                                           
038500         MOVE RC017-PO-NBR            TO PV-PO-NBR                        
038510         MOVE RC017-PO-LNE-NBR        TO PV-PO-LNE-NBR                    
038600         MOVE RC017-UPC-NBR           TO PV-UPC-NBR                       
038700         MOVE RC017-CARTON-NBR        TO PV-CARTON-NBR                    
038800         ADD 1                        TO PV-TOTAL-INPUT-READ              
038900     END-READ.                                                            
039000                                                                          
039010/                                                                         
039100 8100-WRITE-UPDATE-RECORD.                                                
039200     WRITE EDITED-UPDATE-RECORD                                           
039300         FROM RC017-CARTON-UPD-RECORD                                     
039400     ADD 1                            TO PV-UPDATES-WRITTEN.              
042200                                                                          
045100/                                                                         
045200 9000-EOJ-ROUTINE.                                                        
045300                                                                          
045400     CLOSE CARTON-UPDATE-FILE                                             
045500           EDITED-UPDATE-FILE.                                            
045600                                                                          
045601     DISPLAY '**********************************'.                        
045602     DISPLAY '** SUKUT031 SUMMARY **'.                                    
045603     DISPLAY 'TOTAL INPUT RECS       = ' PV-TOTAL-INPUT-READ.             
045605     DISPLAY 'TOTAL RECS WRITTEN     = ' PV-UPDATES-WRITTEN.              
045606     DISPLAY 'TOTAL RECS DELETED     = ' PV-RECORDS-DELETED.              
045610     DISPLAY '**********************************'.                        
045700                                                                          
046610/                                                                         
046700 9200-SQL-ERROR.                                                          
046800                                                                          
046900     CLOSE CARTON-UPDATE-FILE                                             
047000           EDITED-UPDATE-FILE                                             
047100                                                                          
047200     MOVE SQLCODE                     TO SQLCODE2.                        
047300     MOVE SQLERRD(3)                  TO SQLERRD3.                        
047400                                                                          
047500     DISPLAY '** ABEND **         ** = SQLERROR'.                         
047600     DISPLAY '** PROGRAM          ** = SUKUT031'.                         
047700     DISPLAY '** SQLCODE          ** = ' SQLCODE2.                        
047800     DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                        
047900     DISPLAY '** SQLERRM          ** = ' SQLERRM.                         
048000     DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                        
048100                                                                          
048200                                                                          
048300     MOVE +4013                       TO ABEND-CODE.                      
048400     CALL 'ILBOABN0' USING ABEND-CODE.                                    
048500                                                                          
048600 9300-SQL-WARNING.                                                        
048700                                                                          
048800     CLOSE CARTON-UPDATE-FILE                                             
048900           EDITED-UPDATE-FILE                                             
049000                                                                          
049100     DISPLAY '9300-SQL-WARNING'.                                          
049200     MOVE SQLCODE                     TO SQLCODE2.                        
049300     MOVE SQLERRD(3)                  TO SQLERRD3.                        
049400     DISPLAY '** ABEND **       ** = SQLWARNING'.                         
049500     DISPLAY '** PROGRAM        ** = SUKUT031'.                           
049600     DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
049700     DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
049800     DISPLAY '** SQLWARN0       ** = ' SQLWARN0.                          
049900     DISPLAY '** SQLWARN1       ** = ' SQLWARN1.                          
050000     DISPLAY '** SQLWARN2       ** = ' SQLWARN2.                          
050100     DISPLAY '** SQLWARN3       ** = ' SQLWARN3.                          
050200     DISPLAY '** SQLWARN4       ** = ' SQLWARN4.                          
050300     DISPLAY '** SQLWARN5       ** = ' SQLWARN5.                          
050400     DISPLAY '** SQLWARN6       ** = ' SQLWARN6.                          
050500     DISPLAY '** SQLWARN7       ** = ' SQLWARN7.                          
050600                                                                          
050700     MOVE +4013                       TO ABEND-CODE.                      
050800     CALL 'ILBOABN0' USING ABEND-CODE.                                    
