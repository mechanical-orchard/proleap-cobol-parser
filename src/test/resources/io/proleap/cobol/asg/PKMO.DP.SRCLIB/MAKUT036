       IDENTIFICATION DIVISION.                                         12/18/95
       PROGRAM-ID.    MAKUT036.                                         MAKUT036
       AUTHOR.        LINDA GRIESBACH.                                     LV005
      ******************************************************************      32
      * THE PURPOSE OF THIS PROGRAM IS TO CREATE OH/OO RECORDS THAT    *      32
      * WILL BE LOADED INTO THE TSKUINRU ORACLE TABLE.  THE INVENTORY  *      32
      * LOAD FILE CONTAINS A RECORD FOR EACH VDN/STORE COMBINATION.    *      32
      * ALONG WITH THE VND/STORE, THE RECORD CONTAINS THE OH AND OO    *      32
      * QUANTITIES FOR THAT VND/STORE.                                 *      32
      *                                                                *      32
      * THIS PROGRAM DETERMINES THE CORRESPONDING ITEM CLUSTER AND     *      32
      * AND ORG UNIT ID'S FOR EACH VND/STORE RECORD.  IT THEN CREATES  *      32
      * AN OUTPUT RECORD CONTAINING THE ITEM/ORG CLUSTER ID'S ALONG    *      32
      * WITH THE OH/OO QUANTITIES.                                     *      32
      ******************************************************************      32
      *  WR25478 - LINDA GRIESBACH                                     *      32
      *            STORE NBR EXPANSIN                                  *      32
      ******************************************************************      32
221500*  04/08/2019 - CHG0221500 - JIM ARENDT                          *      32
221500*            MAINFRAME REFACTORING - INCREASING OCCURS OF        *      32
221500*            STORE TABLE (ORG-CLUSTR-ID) FROM 2000 TO 9999       *      32
221500*            TO ALLOW FOR 6666 (STOREFRONT) AND TEST LAB STORES. *      32
221500******************************************************************      32
                                                                        MAKUT036
       ENVIRONMENT DIVISION.                                            MAKUT036
       INPUT-OUTPUT SECTION.                                            MAKUT036
       FILE-CONTROL.                                                    MAKUT036
                                                                        MAKUT036
           SELECT VND-STORE-INVENTORY-FILE                              MAKUT036
               ASSIGN TO INP01.                                         MAKUT036
                                                                        MAKUT036
           SELECT ORG-CLUSTR-FILE                                       MAKUT036
               ASSIGN TO INP02.                                         MAKUT036
                                                                        MAKUT036
           SELECT ITEM-CLUSTR-FILE                                      MAKUT036
               ASSIGN TO INP03.                                         MAKUT036
                                                                        MAKUT036
           SELECT INVENTORY-LOAD-FILE                                   MAKUT036
               ASSIGN TO OUT01.                                         MAKUT036
                                                                        MAKUT036
       DATA DIVISION.                                                   MAKUT036
       FILE SECTION.                                                    MAKUT036
       FD  VND-STORE-INVENTORY-FILE                                     MAKUT036
           RECORDING MODE IS F                                          MAKUT036
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT036
       01  VND-STORE-INVENTORY-RECORD  PIC X(109).                         CL**5
                                                                        MAKUT036
       FD  ORG-CLUSTR-FILE                                              MAKUT036
           RECORDING MODE IS F                                          MAKUT036
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT036
       01  ORG-CLUSTR-RECORD           PIC X(13).                          CL**5
                                                                        MAKUT036
       FD  ITEM-CLUSTR-FILE                                             MAKUT036
           RECORDING MODE IS F                                          MAKUT036
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT036
       01  ITEM-CLUSTR-RECORD          PIC X(34).                          CL**5
                                                                        MAKUT036
       FD  INVENTORY-LOAD-FILE                                          MAKUT036
           RECORDING MODE IS F                                          MAKUT036
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT036
       01  INVENTORY-LOAD-RECORD       PIC X(41).                          CL**5
                                                                        MAKUT036
                                                                        MAKUT036
       WORKING-STORAGE SECTION.                                         MAKUT036
                                                                        MAKUT036
       01  PROGRAM-VARIABLES.                                           MAKUT036
           05  PV-INV-READ-COUNT       PIC S9(13) VALUE +0 COMP.        MAKUT036
           05  PV-INV-WRITTEN-COUNT    PIC S9(13) VALUE +0 COMP.        MAKUT036
           05  PV-NO-ITEM-COUNT        PIC S9(9)  VALUE +0 COMP.        MAKUT036
           05  PV-NO-ORG-COUNT         PIC S9(9)  VALUE +0 COMP.        MAKUT036
           05  PV-SUB                  PIC S9(4)  COMP-3.                       
           05  PV-ABEND-CODE           PIC S9(4)  VALUE +0 COMP.                
           05  PV-LAST-DEPT            PIC X(3)   VALUE SPACES.                 
           05  PV-PREV-CLUSTR-KEY.                                              
               10  PV-PREV-OWNER-DEPT  PIC X(3)   VALUE SPACES.                 
               10  FILLER              PIC X(1)   VALUE SPACE.                  
               10  PV-PREV-ITEM-CLUSTR-VND                                      
                                       PIC X(20)  VALUE SPACES.                 
           05  PV-VND-CLUSTR-KEY.                                               
               10  PV-VND-CLUSTR-DEPT  PIC X(3).                                
               10  FILLER              PIC X(1)   VALUE SPACE.                  
               10  PV-VND-CLUSTR-ITEM-NAME                                      
                                       PIC X(20).                               
                                                                                
       01  PROGRAM-SWITCHES.                                            MAKUT036
           05  PS-VND-STORE-INV-EOF-SW PIC X(1)   VALUE 'N'.            MAKUT036
               88 PS-VND-STORE-INV-EOF            VALUE 'Y'.            MAKUT036
               88 PS-VND-STORE-INV-NOT-EOF        VALUE 'N'.            MAKUT036
           05  PS-ORG-CLUSTR-EOF-SW    PIC X(1)   VALUE 'N'.            MAKUT036
               88 PS-ORG-CLUSTR-EOF               VALUE 'Y'.            MAKUT036
               88 PS-ORG-CLUSTR-NOT-EOF           VALUE 'N'.            MAKUT036
           05  PS-ITEM-CLUSTR-EOF-SW   PIC X(1)   VALUE 'N'.            MAKUT036
               88 PS-ITEM-CLUSTR-EOF              VALUE 'Y'.            MAKUT036
               88 PS-ITEM-CLUSTR-NOT-EOF          VALUE 'N'.            MAKUT036
           05  PS-FIRST-ORG-CLUSTR-SW  PIC X(1)   VALUE 'Y'.            MAKUT036
               88 PS-FIRST-ORG-CLUSTR             VALUE 'Y'.            MAKUT036
               88 PS-NOT-FIRST-ORG-CLUSTR         VALUE 'N'.            MAKUT036
           05  PS-FIRST-ITEM-CLUSTR-SW PIC X(1)   VALUE 'Y'.            MAKUT036
               88 PS-FIRST-ITEM-CLUSTR            VALUE 'Y'.            MAKUT036
               88 PS-NOT-FIRST-ITEM-CLUSTR        VALUE 'N'.            MAKUT036
           05  PS-ITEM-CLUSTR-MATCH-SW PIC X(1)   VALUE 'N'.            MAKUT036
               88 PS-ITEM-CLUSTR-MATCH            VALUE 'Y'.            MAKUT036
               88 PS-ITEM-CLUSTR-NO-MATCH         VALUE 'N'.            MAKUT036
           05  PS-ITEM-CLUSTR-FILE-SW  PIC X(1)   VALUE 'Y'.            MAKUT036
               88 PS-ITEM-CLUSTR-IN-FILE          VALUE 'Y'.            MAKUT036
               88 PS-ITEM-CLUSTR-NOT-IN-FILE      VALUE 'N'.            MAKUT036
                                                                        MAKUT036
       01  ORG-CLUSTR-TABLE.                                                    
221500*    05  ORG-CLUSTR-ID           OCCURS 2000 TIMES                        
221500     05  ORG-CLUSTR-ID           OCCURS 9999 TIMES                        
                                       PIC 9(9).                                
                                                                                
       01  ERROR-MESSAGE.                                                       
           05  FILLER                  PIC X(29)                                
                  VALUE '*** ERROR IN PROGRAM MAKUT036'.                        
           05  FILLER                  PIC X(14)                                
                  VALUE '; ERROR CODE ='.                                       
           05  EM-ERROR-CODE           PIC --------9                            
                                                  VALUE ZERO.                   
           05  FILLER                  PIC X(15)                                
                  VALUE '; ERROR DESC ='.                                       
           05  EM-ERROR-DESC           PIC X(65)  VALUE SPACES.                 
                                                                                
       01  LOG-MESSAGE.                                                 MAKUT036
           05  FILLER                  PIC X(23)                        MAKUT036
                   VALUE '*** PROGRAM MAKUT036 - '.                     MAKUT036
           05  LM-MESSAGE              PIC X(109) VALUE SPACES.         MAKUT036
                                                                                
       01  ITEM-NOT-FOUND-LOG-MSG.                                      MAKUT036
           05  FILLER                  PIC X(37)                        MAKUT036
                   VALUE 'ITEM CLUSTER ROW NOT FOUND FOR = '.           MAKUT036
           05  INF-LM-ITEM-NAME        PIC X(20)  VALUE SPACES.         MAKUT036
           05  FILLER                  PIC X(14)                                
                   VALUE ' - DEPT NBR = '.                                      
           05  INF-LM-DEPT             PIC X(3)  VALUE SPACES.          MAKUT036
                                                                                
       01  RUN-SUMMARY-LOG-MSG1.                                        MAKUT036
           05  FILLER                  PIC X(35)                        MAKUT036
                   VALUE 'VND STORE INVENTORY RECORDS READ = '.         MAKUT036
           05  RSLM1-VND-STOR-INV-RECS PIC Z(12)9 VALUE ZERO.           MAKUT036
                                                                        MAKUT036
       01  RUN-SUMMARY-LOG-MSG2.                                        MAKUT036
           05  FILLER                  PIC X(33)                        MAKUT036
                   VALUE 'INVENTORY LOAD RECORDS WRITTEN = '.           MAKUT036
           05  RSLM2-INV-LOAD-RECS     PIC Z(12)9 VALUE ZERO.           MAKUT036
                                                                        MAKUT036
       01  RUN-SUMMARY-LOG-MSG3.                                        MAKUT036
           05  FILLER                  PIC X(43)                        MAKUT036
                   VALUE 'NUMBER OF VND STORE INVENTORY RECORDS DROPP'. MAKUT036
           05  FILLER                  PIC X(39)                        MAKUT036
                   VALUE 'ED DUE TO NO MATCHING ITEM CLUSTER ID= '.     MAKUT036
           05  RSLM3-EXTRACT-NO-ITEM-RECS                               MAKUT036
                                       PIC Z(8)9   VALUE ZERO.                  
                                                                        MAKUT036
       01  RUN-SUMMARY-LOG-MSG4.                                        MAKUT036
           05  FILLER                  PIC X(38)                        MAKUT036
                   VALUE 'NUMBER OF VND STORE INVENTORY RECORDS '.      MAKUT036
           05  FILLER                  PIC X(38)                        MAKUT036
                   VALUE 'DROPPED DUE TO NO MATCHING ORG UNIT = '.      MAKUT036
           05  RSLM4-STORE-NO-ORG-RECS PIC Z(8)9   VALUE ZERO.          MAKUT036
                                                                        MAKUT036
       01  PV-ORG-CLUSTR-RECORD.                                                
           05  PV-ORG-CLUSTR-STORE     PIC X(4).                                
           05  PV-ORG-CLUSTR-ID        PIC 9(9).                                
                                                                        MAKUT036
       01  PV-ITEM-CLUSTR-RECORD.                                               
           05  PV-ITEM-CLUSTR-KEY.                                              
               10  PV-ITEM-CLUSTR-DEPT PIC X(3).                                
               10  FILLER              PIC X(1).                                
               10  PV-ITEM-CLUSTR-VND  PIC X(20).                               
           05  FILLER                  PIC X(1).                                
           05  PV-ITEM-CLUSTR-ID       PIC 9(9).                                
                                                                                
      * FIELDS FOR THE VND STORE INVENTORY EXTRACT FILE                 MAKUT036
           COPY MARD015.                                                MAKUT036
                                                                        MAKUT036
      * FIELDS FOR THE INVENTORY LOAD FILE                              MAKUT036
           COPY MARD016.                                                MAKUT036
                                                                        MAKUT036
                                                                        MAKUT036
       EJECT                                                            MAKUT036
       PROCEDURE DIVISION.                                              MAKUT036
                                                                        MAKUT036
       0000-MAINLINE.                                                   MAKUT036
                                                                        MAKUT036
           PERFORM 1000-INITIALIZE                                      MAKUT036
              THRU 1000-EXIT.                                                   
                                                                        MAKUT036
           PERFORM 2000-CREATE-OH-OO-FILE                               MAKUT036
              THRU 2000-EXIT                                                    
              UNTIL PS-VND-STORE-INV-EOF.                                       
                                                                        MAKUT036
           PERFORM 3000-CLOSE                                           MAKUT036
              THRU 3000-EXIT.                                                   
                                                                        MAKUT036
           DISPLAY 'MAKUT036 - SUCCESSFUL END'.                         MAKUT036
           MOVE ZERO TO RETURN-CODE.                                    MAKUT036
           GOBACK.                                                      MAKUT036
                                                                        MAKUT036
       0000-EXIT.                                                       MAKUT036
           EXIT.                                                                
                                                                        MAKUT036
                                                                        MAKUT036
       1000-INITIALIZE.                                                 MAKUT036
                                                                        MAKUT036
           OPEN INPUT  VND-STORE-INVENTORY-FILE                         MAKUT036
                       ORG-CLUSTR-FILE                                  MAKUT036
                       ITEM-CLUSTR-FILE                                 MAKUT036
                OUTPUT INVENTORY-LOAD-FILE.                                     
                                                                        MAKUT036
           INITIALIZE ORG-CLUSTR-TABLE.                                         
                                                                                
           PERFORM 1100-LOAD-ORG-CLUSTR-FILE                                    
              THRU 1100-EXIT                                                    
             UNTIL PS-ORG-CLUSTR-EOF.                                           
                                                                                
           PERFORM 9997-READ-ITEM-CLUSTR-FILE                                   
              THRU 9997-EXIT.                                                   
                                                                                
           PERFORM 9998-READ-VND-STORE-INV-FILE                                 
              THRU 9998-EXIT.                                                   
                                                                        MAKUT036
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       1100-LOAD-ORG-CLUSTR-FILE.                                               
                                                                        MAKUT036
           READ ORG-CLUSTR-FILE                                                 
               INTO PV-ORG-CLUSTR-RECORD                                        
                   AT END                                                       
                       IF PS-FIRST-ORG-CLUSTR                                   
                           MOVE -1003 TO EM-ERROR-CODE                          
                                         PV-ABEND-CODE                          
                           MOVE 'EMPTY ORG CLUSTER TABLE'                       
                                      TO EM-ERROR-DESC                          
                           PERFORM 9999-ABEND                                   
                              THRU 9999-EXIT                                    
                       ELSE                                                     
                           SET PS-ORG-CLUSTR-EOF TO TRUE                        
                           GO TO 1100-EXIT                                      
                       END-IF                                                   
           END-READ.                                                            
                                                                        MAKUT036
           IF PS-FIRST-ORG-CLUSTR                                               
               SET PS-NOT-FIRST-ORG-CLUSTR TO TRUE                              
           END-IF.                                                              
                                                                                
           IF PV-ORG-CLUSTR-STORE IS NUMERIC                                    
             AND PV-ORG-CLUSTR-STORE > ZERO                                     
               MOVE PV-ORG-CLUSTR-STORE TO PV-SUB                               
               MOVE PV-ORG-CLUSTR-ID    TO ORG-CLUSTR-ID (PV-SUB)               
           END-IF.                                                              
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                        MAKUT036
                                                                        MAKUT036
       2000-CREATE-OH-OO-FILE.                                          MAKUT036
                                                                        MAKUT036
      * THE INPUT FILE IS SORTED BY VND/STORE -                         MAKUT036
      * IT MAY CONTAIN THE SAME VND FOR MULITPLE STORES -               MAKUT036
      * SO MULTIPLE INPUT RECORDS COULD BE BYPASSED                     MAKUT036
      * IF THE VND IS NOT IN THE ITEM-CLUSTR-FILE                       MAKUT036
                                                                        MAKUT036
           IF PV-VND-CLUSTR-KEY = PV-ITEM-CLUSTR-KEY                    MAKUT036
             OR PV-VND-CLUSTR-KEY < PV-ITEM-CLUSTR-KEY                          
             OR PS-ITEM-CLUSTR-EOF                                              
               NEXT SENTENCE                                                    
           ELSE                                                                 
               SET PS-ITEM-CLUSTR-NO-MATCH TO TRUE                              
               SET PS-ITEM-CLUSTR-IN-FILE  TO TRUE                              
               PERFORM 9997-READ-ITEM-CLUSTR-FILE                               
                  THRU 9997-EXIT                                                
                 UNTIL PS-ITEM-CLUSTR-MATCH                                     
                    OR PS-ITEM-CLUSTR-NOT-IN-FILE                               
                    OR PS-ITEM-CLUSTR-EOF                                       
           END-IF.                                                              
                                                                                
                                                                        MAKUT036
           IF PV-VND-CLUSTR-KEY = PV-ITEM-CLUSTR-KEY                            
               MOVE MA015-STORE TO PV-SUB                                       
               MOVE ORG-CLUSTR-ID (PV-SUB)                                      
                                TO MA016-ORG-UNIT-CLUSTER-ID                    
               IF MA016-ORG-UNIT-CLUSTER-ID NOT = ZERO                          
                   MOVE PV-ITEM-CLUSTR-ID TO MA016-ITEM-CLUSTER-ID              
                   MOVE MA015-ONHAND-QTY  TO MA016-ONHAND-QTY                   
                   MOVE MA015-ONORD-QTY   TO MA016-ONORD-QTY                    
                                                                                
                   WRITE INVENTORY-LOAD-RECORD                                  
                       FROM MA016-INVENTORY-LOAD                                
                   ADD +1 TO PV-INV-WRITTEN-COUNT                               
               ELSE                                                             
                   ADD +1 TO PV-NO-ORG-COUNT                                    
               END-IF                                                           
           ELSE                                                                 
               IF PV-VND-CLUSTR-KEY NOT = PV-PREV-CLUSTR-KEY                    
                   MOVE MA015-OWNER-DEPT TO PV-PREV-OWNER-DEPT                  
                                            INF-LM-DEPT                         
                   MOVE MA015-ITEM-NAME TO PV-PREV-ITEM-CLUSTR-VND              
                                           INF-LM-ITEM-NAME                     
                   MOVE ITEM-NOT-FOUND-LOG-MSG TO LM-MESSAGE                    
                   DISPLAY LOG-MESSAGE UPON SYSOUT                              
               END-IF                                                           
               ADD +1 TO PV-NO-ITEM-COUNT                                       
           END-IF.                                                              
                                                                                
           PERFORM 9998-READ-VND-STORE-INV-FILE                                 
              THRU 9998-EXIT.                                                   
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3000-CLOSE.                                                              
                                                                                
           CLOSE VND-STORE-INVENTORY-FILE                                       
                 ORG-CLUSTR-FILE                                                
                 ITEM-CLUSTR-FILE                                               
                 INVENTORY-LOAD-FILE.                                           
                                                                                
           MOVE PV-INV-READ-COUNT       TO RSLM1-VND-STOR-INV-RECS.             
           MOVE RUN-SUMMARY-LOG-MSG1    TO LM-MESSAGE.                          
           DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
                                                                                
           MOVE PV-INV-WRITTEN-COUNT    TO RSLM2-INV-LOAD-RECS.                 
           MOVE RUN-SUMMARY-LOG-MSG2    TO LM-MESSAGE.                          
           DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
                                                                                
           MOVE PV-NO-ITEM-COUNT        TO RSLM3-EXTRACT-NO-ITEM-RECS.  MAKUT036
           MOVE RUN-SUMMARY-LOG-MSG3    TO LM-MESSAGE.                  MAKUT036
           DISPLAY LOG-MESSAGE UPON SYSOUT.                             MAKUT036
                                                                        MAKUT036
           MOVE PV-NO-ORG-COUNT         TO RSLM4-STORE-NO-ORG-RECS.     MAKUT036
           MOVE RUN-SUMMARY-LOG-MSG4    TO LM-MESSAGE.                  MAKUT036
           DISPLAY LOG-MESSAGE UPON SYSOUT.                             MAKUT036
                                                                        MAKUT036
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                        MAKUT036
                                                                        MAKUT036
       9997-READ-ITEM-CLUSTR-FILE.                                              
                                                                        MAKUT036
           READ ITEM-CLUSTR-FILE                                                
               INTO PV-ITEM-CLUSTR-RECORD                                       
                   AT END                                                       
                       IF PS-FIRST-ITEM-CLUSTR                                  
                           MOVE -1003 TO EM-ERROR-CODE                          
                                         PV-ABEND-CODE                          
                           MOVE 'EMPTY ITEM CLUSTER TABLE'                      
                                      TO EM-ERROR-DESC                          
                           PERFORM 9999-ABEND                                   
                              THRU 9999-EXIT                                    
                       ELSE                                                     
                           MOVE SPACES TO PV-ITEM-CLUSTR-VND                    
                           SET PS-ITEM-CLUSTR-EOF TO TRUE                       
                           GO TO 9997-EXIT                                      
                       END-IF                                                   
           END-READ.                                                            
                                                                        MAKUT036
           IF PS-FIRST-ITEM-CLUSTR                                              
               SET PS-NOT-FIRST-ITEM-CLUSTR TO TRUE                             
               GO TO 9997-EXIT                                                  
           END-IF.                                                              
                                                                        MAKUT036
           IF PV-VND-CLUSTR-KEY = PV-ITEM-CLUSTR-KEY                    MAKUT036
               SET PS-ITEM-CLUSTR-MATCH TO TRUE                                 
               GO TO 9997-EXIT                                                  
           END-IF.                                                              
                                                                        MAKUT036
           IF PV-VND-CLUSTR-KEY < PV-ITEM-CLUSTR-KEY                    MAKUT036
               SET PS-ITEM-CLUSTR-NOT-IN-FILE TO TRUE                           
           END-IF.                                                              
                                                                        MAKUT036
       9997-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9998-READ-VND-STORE-INV-FILE.                                            
                                                                        MAKUT036
           READ VND-STORE-INVENTORY-FILE                                MAKUT036
               INTO MA015-ROLLUP-INVENTORY                              MAKUT036
                   AT END                                               MAKUT036
                       SET PS-VND-STORE-INV-EOF TO TRUE                 MAKUT036
                       GO TO 9998-EXIT                                          
           END-READ.                                                            
                                                                                
           MOVE MA015-OWNER-DEPT TO PV-VND-CLUSTR-DEPT.                         
           MOVE MA015-ITEM-NAME  TO PV-VND-CLUSTR-ITEM-NAME.                    
           ADD +1 TO PV-INV-READ-COUNT.                                         
                                                                                
       9998-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9999-ABEND.                                                              
                                                                        MAKUT036
           MOVE ERROR-MESSAGE TO LOG-MESSAGE.                           MAKUT036
                                                                        MAKUT036
           DISPLAY LOG-MESSAGE UPON SYSOUT.                             MAKUT036
                                                                        MAKUT036
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                         MAKUT036
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
