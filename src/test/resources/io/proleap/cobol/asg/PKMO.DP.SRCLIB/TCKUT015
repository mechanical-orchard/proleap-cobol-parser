      ***************************************************************** 00010036
       IDENTIFICATION DIVISION.                                         00020036
      ***************************************************************** 00030036
                                                                        00040036
       PROGRAM-ID.             TCKUT015.                                00050036
       AUTHOR.                 BARB SCHULTZ.                            00060036
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070036
       DATE-WRITTEN            NOVEMBER 2002.                           00080036
       DATE-COMPILED.                                                   00090036
                                                                        00100036
      ***************************************************************** 00110036
      *                                                               * 00120036
      *  THIS PROGRAM READS THE MERCHANT ACTIVITY FILE AND CHANGES    * 00130036
      *  THE DATE ON ANY TRANSACTIONS THAT HAVE BEEN PROCESSED AFTER  * 00140036
      *  MIDNIGHT TO THE PROCESSING DATE.                             * 00150036
      ***************************************************************** 00160036
W3540C* MODIFICATION LOG                                               *00170036
W3540C* ------------ ---                                               *00180036
W3540C*                                                                *00190036
W3540C*   REQUIREMENT  : PAYMENT RELATED CHANGES FOR FSH PROJECT       *00200036
W3540C*                  DEBIT SETTLEMENT PROCESS                      *00210036
W3540C*   PROJECT ID   : W3540C                                        *00220036
W3540C*   AUTHOR       : COGNIZANT                                     *00230036
W3540C*   DATE CHANGED : 18-OCT-2012                                   *00240037
W3540C*   CHANGES MADE :                                               *00250036
W3540C*                  NEW CONTROL CARD SVCC000 IS INTRODUCED IN THIS*00260036
W3540C*                  PROGRAM. THIS INPUT WILL HAVE CURRENT PROC.   *00270036
W3540C*                  DATE AND PREVIOUS PROC. DATE. THESE CHANGES   *00280036
W3540C*                  ARE HANDLED IN THIS PROGRAM.                  *00290036
W3540C*                  ALSO THIS PROGRAM IS CHANGED TO HAVE DB2      *00300036
W3540C*                  STATEMENTS FOR SELECTING THE SALES DATE FROM  *00310036
W3540C*                  TEPTND, TEPPART AND TEPTRN TABLES.            *00320046
W3540C******************************************************************00330036
INC000* 2012-11-10 BDH ADDED OR CONDITION ON TO 2200- QUERY.  NEED TO  *00331046
INC000*                CARE FOR TRANSACTION STARTED BEFORE AND ENDED   *00331146
INC000*                MIDNIGHT.  COMMENTED WITH INC000                *00332046
INC000******************************************************************00333046
INC001* 2012-12-16 BDH MODIFIED LOGIC TO ALLOW UNMATCHED TRANSACTIONS  *00334065
INC001*                FROM THE BANK FILE TO PASS THROUGH WITH OUT     *00335065
INC001*                CHANGING THE SALES DATE.                        *00336065
INC001******************************************************************00337065
       ENVIRONMENT DIVISION.                                            00340036
      ***************************************************************** 00350036
                                                                        00360036
       CONFIGURATION SECTION.                                           00370036
                                                                        00380036
       SOURCE-COMPUTER.        IBM-3090.                                00390036
                                                                        00400036
       OBJECT-COMPUTER.        IBM-3090.                                00410036
                                                                        00420036
       INPUT-OUTPUT SECTION.                                            00430036
                                                                        00440036
       FILE-CONTROL.                                                    00450036
                                                                        00460036
           SELECT MERCHANT-ACTIVITY-IN-FILE                             00470036
               ASSIGN TO INP01.                                         00480036
                                                                        00490036
           SELECT CONTROL-DATE-FILE                                     00500036
               ASSIGN TO INP02.                                         00510036
                                                                        00520036
W3540C     SELECT CONTROL-PROC-DATE-FILE                                00530036
W3540C         ASSIGN TO INP03.                                         00540036
W3540C                                                                  00550036
           SELECT MERCHANT-ACTIVITY-OUT-FILE                            00560036
               ASSIGN TO OUT01.                                         00570036
                                                                        00580036
                                                                        00590036
      ******************************************************************00600036
       DATA DIVISION.                                                   00610036
      ******************************************************************00620036
                                                                        00630036
       FILE SECTION.                                                    00640036
                                                                        00650036
       FD  MERCHANT-ACTIVITY-IN-FILE                                    00660036
           RECORDING MODE IS F                                          00670036
           BLOCK CONTAINS 0 RECORDS                                     00680036
           LABEL RECORDS ARE STANDARD.                                  00690036
                                                                        00700036
       01  MERCHANT-ACTIVITY-IN-RECORD  PIC X(80).                      00710036
                                                                        00720036
       FD  CONTROL-DATE-FILE                                            00730036
           RECORDING MODE IS F                                          00740036
           BLOCK CONTAINS 0 RECORDS                                     00750036
           LABEL RECORDS ARE OMITTED.                                   00760036
                                                                        00770036
       01  CONTROL-DATE-RECORD          PIC X(80).                      00780036
                                                                        00790036
W3540C FD  CONTROL-PROC-DATE-FILE                                       00800036
W3540C     RECORDING MODE IS F                                          00810036
W3540C     BLOCK CONTAINS 0 RECORDS                                     00820036
W3540C     LABEL RECORDS ARE OMITTED.                                   00830036
W3540C                                                                  00840036
W3540C 01  CONTROL-PROC-DATE-RECORD     PIC X(80).                      00850036
W3540C                                                                  00860036
       FD  MERCHANT-ACTIVITY-OUT-FILE                                   00870036
           RECORDING MODE IS F                                          00880036
           BLOCK CONTAINS 0 RECORDS                                     00890036
           LABEL RECORDS ARE STANDARD.                                  00900036
                                                                        00910036
       01  MERCHANT-ACTIVITY-OUT-RECORD PIC X(80).                      00920036
                                                                        00930036
      ******************************************************************00940036
       WORKING-STORAGE SECTION.                                         00950036
      ******************************************************************00960036
       01  FILLER                            PIC X(50)  VALUE           00970036
           ' *** WORKING STORAGE STARTS HERE FOR TCKUT015 *** '.        00980036
                                                                        00990036
      ******************************************************************01000036
      * RECORD LAYOUT FOR INPUT DATE CARD (POS DATE)                   *01010036
      ******************************************************************01020036
       01  CC-CONTROL-CARD.                                             01030036
           05  CC-CONTROL-CARD-DISPLAY.                                 01040036
               10  CC-CONTROL-NAME     PIC X(14)    VALUE SPACE.        01050036
                   88  CC-VALID-CONTROL-NAME        VALUE               01060036
                      'P.O.S. DATE = '.                                 01070036
               10  CC-POS-DATE-MMDDYY  PIC 9(6)     VALUE ZERO.         01080036
          05  FILLER                  PIC X(60)    VALUE SPACE.         01090036
                                                                        01100036
W3540C******************************************************************01110036
W3540C* RECORD LAYOUT FOR INPUT PROCESSING DATE CARD                   *01120036
W3540C******************************************************************01130036
W3540C 01  CC-CONTROL-PROC-CARD.                                        01140036
W3540C     05  CC-CONTROL-PROC-DATE.                                    01150036
W3540C         10  CC-PROC-END-DTE.                                     01160036
W3540C             15  CC-PROC-END-CC       PIC  X(02)    VALUE SPACES. 01170036
W3540C             15  CC-PROC-END-YY       PIC  X(02)    VALUE SPACES. 01180036
W3540C             15  CC-PROC-END-MM       PIC  X(02)    VALUE SPACES. 01190036
W3540C             15  CC-PROC-END-DD       PIC  X(02)    VALUE SPACES. 01200036
W3540C         10  FILLER                   PIC  X(01)    VALUE SPACES. 01210036
W3540C         10  CC-PROC-STA-DTE.                                     01220036
W3540C             15  CC-PROC-STA-CC       PIC  X(02)    VALUE SPACES. 01230036
W3540C             15  CC-PROC-STA-YY       PIC  X(02)    VALUE SPACES. 01240036
W3540C             15  CC-PROC-STA-MM       PIC  X(02)    VALUE SPACES. 01250036
W3540C             15  CC-PROC-STA-DD       PIC  X(02)    VALUE SPACES. 01260036
W3540C     05  FILLER                       PIC  X(63)    VALUE SPACES. 01270036
W3540C*                                                                 01280036
      ******************************************************************01290036
      *   ABEND CODE - AND NUMBER FOR EACH POSSIBLE TYPE OF ABEND.     *01300036
      ******************************************************************01310036
       01  AC-ABEND-CODE                    PIC S9(04) VALUE ZERO       01320036
                                                       COMP SYNC.       01330036
           88  AC-CONTROL-CARD-ERROR                   VALUE +4003.     01340036
           88  AC-CALENDAR-ROUTINE-ERROR               VALUE +4019.     01350036
           88  AC-TABLE-OVERFLOW                       VALUE +4004.     01360036
                                                                        01370036
       01  ABEND-CODE                       PIC S9(04) VALUE ZERO       01380036
                                                       COMP SYNC.       01390036
                                                                        01400036
       01  SUBSCRIPTS.                                                  01410036
           05  SUB                          PIC S9(04) VALUE ZERO       01420036
                                                       COMP.            01430036
      ******************************************************************01440036
      *   PROGRAM SWITCHES                                             *01450036
      ******************************************************************01460036
       01  PS-PROGRAM-SWITCHES.                                         01470036
           05  PS-END-OF-ACT-FILE-SW         PIC X(01)  VALUE 'N'.      01480036
               88  PS-END-OF-ACT-FILE                   VALUE 'Y'.      01490036
           05  PS-END-OF-CONTROL-FILE-SW     PIC  X(01) VALUE 'N'.      01500036
               88  PS-END-OF-CONTROL-FILE               VALUE 'Y'.      01510036
W3540C     05  PS-END-OF-CNTL-PROC-FILE-SW   PIC  X(01) VALUE 'N'.      01520037
W3540C         88  PS-END-OF-CONTROL-PROC-FILE          VALUE 'Y'.      01530037
                                                                        01540036
      ******************************************************************01550036
      *   PROGRAM CONSTANTS                                            *01560036
      ******************************************************************01570036
       01  PC-CONSTANTS.                                                01580036
           05  PC-PROGRAM-NAME             PIC X(08)     VALUE          01590036
                                                         'TCKUT015'.    01600036
                                                                        01610036
      ******************************************************************01620036
      *   PROGRAM ACCUMULATERS.                                        *01630036
      ******************************************************************01640036
       01  PA-ACCUMULATORS                 COMP-3.                      01650036
           05  PA-ACT-REC-CHG              PIC S9(07)    VALUE ZEROES.  01660036
           05  PA-ACT-FILE-RECS-READ       PIC S9(07)    VALUE ZEROES.  01670036
                                                                        01680036
      ******************************************************************01690036
      *   PROGRAM VARIABLES.                                           *01700036
      ******************************************************************01710036
       01  PV-PROGRAM-VARIABLES.                                        01720036
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACE.   01730036
W3540C     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.  01740036
W3540C     05  PV-OPERATION-MSG-1          PIC  X(40)    VALUE SPACE.   01750036
W3540C     05  PV-OPERATION-MSG-2          PIC  X(40)    VALUE SPACE.   01760036
W3540C     05  PV-SLS-DTE-CHG-COUNT        PIC  9(10)    VALUE ZEROES.  01770037
INC001     05  PV-MISSING-TXN-PASSED       PIC  9(10)    VALUE ZEROES.  01771067
                                                                        01780036
      ******************************************************************01790036
      *   DATE AND TIME FIELDS.                                        *01800036
      ******************************************************************01810036
       01  PV-DATE-AND-TIME-FIELDS.                                     01820036
           05  PV-HOLD-CCYY.                                            01830036
               10  PV-HOLD-CC              PIC 9(02)     VALUE 20.      01840036
               10  PV-HOLD-YY              PIC 9(02)     VALUE ZEROES.  01850036
           05  PV-POS-MMDDYY.                                           01860036
               10  PV-POS-MM               PIC 9(02)     VALUE ZEROES.  01870036
               10  PV-POS-DD               PIC 9(02)     VALUE ZEROES.  01880036
               10  PV-POS-YY               PIC 9(02)     VALUE ZEROES.  01890036
           05  PV-MAF-DATE-CCYYMMDD.                                    01900036
               10  PV-MAF-DATE-CCYY        PIC 9(04)     VALUE ZEROES.  01910036
               10  PV-MAF-DATE-MM          PIC 9(02)     VALUE ZEROES.  01920036
               10  PV-MAF-DATE-DD          PIC 9(02)     VALUE ZEROES.  01930036
           05  PV-RE-DATE-CCYYMMDD REDEFINES PV-MAF-DATE-CCYYMMDD       01940036
                                           PIC 9(08).                   01950036
           05  PV-HOLD-TC003.                                           01960036
               10  PV-HOLD-STORE-NBR       PIC  9(04)  VALUE ZEROES.    01970036
               10  PV-HOLD-REG-NBR         PIC  9(04)  VALUE ZEROES.    01980036
               10  PV-HOLD-TRAN-NBR        PIC  9(04)  VALUE ZEROES.    01990036
               10  PV-HOLD-SLS-DTE         PIC  X(08)  VALUE SPACES.    02000036
               10  PV-HOLD-STRT-TM         PIC  X(6)   VALUE SPACES.    02010036
               10  PV-HOLD-TRAN-SEQ-NBR    PIC S9(04)  VALUE ZEROES.    02020036
               10  PV-HOLD-TRAN-TYPE       PIC  X(02)  VALUE SPACES.    02030036
               10  PV-HOLD-TRAN-DISP       PIC  X(01)  VALUE SPACE.     02040036
               10  PV-HOLD-CARD-NBR        PIC  X(19)  VALUE SPACES.    02050036
               10  PV-HOLD-TRAN-AMT        PIC  S9(7)V99 VALUE ZEROES.  02060036
               10  PV-HOLD-TYP-CDE         PIC  X(01)  VALUE SPACE.     02070036
               10  PV-HOLD-ADJ-DISP        PIC  X(03)  VALUE SPACES.    02080036
               10  PV-HOLD-RESPONSE-CDE    PIC  9(03)  VALUE ZEROES.    02090036
               10  PV-HOLD-STATE-CDE       PIC  X(02)  VALUE SPACES.    02100036
               10  PV-HOLD-STORE-NAME-ABBR PIC  X(10)  VALUE SPACES.    02110036
                                                                        02120036
W3540C     05  PV-SELECT-VARIABLES.                                     02130036
W3540C         10  PV-PREV-SLS-DTE.                                     02140036
W3540C             15 PV-PREV-SLS-DTE-CC   PIC  X(02) VALUE SPACES.     02150036
W3540C             15 PV-PREV-SLS-DTE-YY   PIC  X(02) VALUE SPACES.     02160036
W3540C             15 FILLER               PIC  X(01) VALUE '-'.        02170036
W3540C             15 PV-PREV-SLS-DTE-MM   PIC  X(02) VALUE SPACES.     02180036
W3540C             15 FILLER               PIC  X(01) VALUE '-'.        02190036
W3540C             15 PV-PREV-SLS-DTE-DD   PIC  X(02) VALUE SPACES.     02200036
W3540C         10  PV-PREV-SLS-DTE-SEL     REDEFINES                    02210036
W3540C                PV-PREV-SLS-DTE      PIC  X(10).                  02220037
W3540C         10  PV-CURR-SLS-DTE.                                     02230036
W3540C             15 PV-CURR-SLS-DTE-CC   PIC  X(02) VALUE SPACES.     02240036
W3540C             15 PV-CURR-SLS-DTE-YY   PIC  X(02) VALUE SPACES.     02250036
W3540C             15 FILLER               PIC  X(01) VALUE '-'.        02260036
W3540C             15 PV-CURR-SLS-DTE-MM   PIC  X(02) VALUE SPACES.     02270036
W3540C             15 FILLER               PIC  X(01) VALUE '-'.        02280036
W3540C             15 PV-CURR-SLS-DTE-DD   PIC  X(02) VALUE SPACES.     02290036
W3540C         10  PV-CURR-SLS-DTE-SEL     REDEFINES                    02300036
W3540C                PV-CURR-SLS-DTE      PIC  X(10).                  02310037
W3540C         10  PV-STR-NBR              PIC  S9(04) COMP.            02320037
W3540C         10  PV-RGST-ID              PIC   9(04) COMP.            02330037
W3540C         10  PV-RGST-ID-SEL          REDEFINES                    02340036
W3540C                PV-RGST-ID           PIC  S9(04) COMP.            02350037
W3540C         10  PV-TRN-NBR              PIC  S9(04) COMP.            02360037
W3540C         10  PV-TRN-DTE.                                          02370036
W3540C             15 PV-TRN-DTE-CCYY      PIC  X(04) VALUE SPACES.     02380036
W3540C             15 FILLER               PIC  X(01) VALUE '-'.        02390036
W3540C             15 PV-TRN-DTE-MM        PIC  X(02) VALUE SPACES.     02400036
W3540C             15 FILLER               PIC  X(01) VALUE '-'.        02410036
W3540C             15 PV-TRN-DTE-DD        PIC  X(02) VALUE SPACES.     02420036
W3540C         10  PV-TRN-DTE-SEL          REDEFINES                    02430036
W3540C                PV-TRN-DTE           PIC  X(10).                  02440037
INC000     05  PV-TRN-TOT-AMT      PIC S9(7)V9(2) COMP-3.               02440563
W3540C*                                                                 02450036
      ******************************************************************02460036
      *    STANDARD DB2 ERROR HANDLING AREA                            *02470036
      ******************************************************************02480036
           COPY DPWS004.                                                02490036
                                                                        02500036
      ******************************************************************02510036
      *  MERCHANT ACTIVITY LAYOUT BUILT IN TCKUT001                    *02520036
      ******************************************************************02530036
           COPY TCWS003.                                                02540036
                                                                        02550036
W3540C******************************************************************02560036
W3540C*  PARAMETER LIST FOR THE CALENDAR ROUTINE (DPKUT500)            *02570036
W3540C******************************************************************02580036
W3540C     COPY DPWS500.                                                02590036
W3540C*                                                                 02600036
W3540C******************************************************************02610036
W3540C*    STANDARD LAYOUT FOR THE SQL COMMUNCATIONS AREA              *02620036
W3540C******************************************************************02630036
W3540C     EXEC SQL                                                     02640036
W3540C         INCLUDE SQLCA                                            02650036
W3540C     END-EXEC.                                                    02660036
W3540C                                                                  02670036
W3540C     COPY SQLCA2.                                                 02680036
W3540C*                                                                 02690036
W3540C******************************************************************02700036
W3540C*  DB2 TABLE TEPTND - TENDER TABLE                               *02710036
W3540C******************************************************************02720036
W3540C     EXEC SQL                                                     02730036
W3540C          INCLUDE TEPTND                                          02740036
W3540C     END-EXEC.                                                    02750036
W3540C*                                                                 02760036
W3540C******************************************************************02770036
W3540C*  DB2 TABLE TEPPART - PARTITION CONTROL TABLE                   *02780036
W3540C******************************************************************02790036
W3540C     EXEC SQL                                                     02800036
W3540C          INCLUDE TEPPART                                         02810036
W3540C     END-EXEC.                                                    02820036
W3540C*                                                                 02830036
W3540C******************************************************************02840036
W3540C*  DB2 TABLE TEPTRN  - COSA TRANSACTION TABLE                    *02850036
W3540C******************************************************************02860036
W3540C     EXEC SQL                                                     02870036
W3540C         INCLUDE TEPTRN                                           02880036
W3540C     END-EXEC.                                                    02890036
W3540C                                                                  02900036
      ***************************************************************** 02910036
      * PROCEDURE DIVISION.                                           * 02920036
      ***************************************************************** 02930036
       PROCEDURE DIVISION.                                              02940036
                                                                        02950036
      ******************************************************************02960036
      * 0000-MAINLINE-PARAGRAPH                                        *02970036
      *      PRODUCE DAILY DEBIT EXCEPTION REPORT                      *02980036
      ******************************************************************02990036
       0000-MAINLINE-PARAGRAPH.                                         03000036
                                                                        03010036
           PERFORM 1000-INITIALIZE-PROGRAM.                             03020036
                                                                        03030036
           PERFORM 2000-PROCESS-ACT-DATA                                03040036
             UNTIL PS-END-OF-ACT-FILE.                                  03050036
                                                                        03060036
           PERFORM 8000-END-PROGRAM.                                    03070036
                                                                        03080036
           GOBACK.                                                      03090036
                                                                        03100036
      ******************************************************************03110036
      * 1000-INITIALIZE-PROGRAM                                         03120036
      *  -  OPEN FILES                                                  03130036
      *  -  READ CONTROL CARD                                           03140036
      *  -  READ 1ST RECORD ON ACT DATA FILE                            03150036
      ******************************************************************03160036
       1000-INITIALIZE-PROGRAM.                                         03170036
                                                                        03180036
           OPEN INPUT  MERCHANT-ACTIVITY-IN-FILE                        03190036
                       CONTROL-DATE-FILE                                03200036
W3540C                 CONTROL-PROC-DATE-FILE                           03210036
                OUTPUT MERCHANT-ACTIVITY-OUT-FILE.                      03220036
                                                                        03230036
           READ CONTROL-DATE-FILE INTO CC-CONTROL-CARD                  03240036
              AT END                                                    03250036
                 SET PS-END-OF-CONTROL-FILE TO TRUE.                    03260036
                                                                        03270036
           IF PS-END-OF-CONTROL-FILE                                    03280036
              DISPLAY 'DATE CARD MISSING '                              03290036
              MOVE '1000-INITIALIZE-PROGRAM ' TO PV-PARAGRAPH-NAME      03300036
              SET AC-CONTROL-CARD-ERROR TO TRUE                         03310036
              PERFORM 9999-ABEND                                        03320036
           ELSE                                                         03330036
              DISPLAY PC-PROGRAM-NAME ' CONTROL DATE:  '                03340036
                   CC-POS-DATE-MMDDYY                                   03350036
              MOVE CC-POS-DATE-MMDDYY TO PV-POS-MMDDYY                  03360036
              MOVE PV-POS-YY          TO PV-HOLD-YY                     03370036
              MOVE PV-HOLD-CCYY       TO PV-MAF-DATE-CCYY               03380036
              MOVE PV-POS-MM          TO PV-MAF-DATE-MM                 03390036
              MOVE PV-POS-DD          TO PV-MAF-DATE-DD                 03400036
           END-IF.                                                      03410036
                                                                        03420036
W3540C     READ CONTROL-PROC-DATE-FILE INTO CC-CONTROL-PROC-CARD        03430036
W3540C        AT END                                                    03440036
W3540C           SET PS-END-OF-CONTROL-PROC-FILE TO TRUE.               03450036
W3540C                                                                  03460036
W3540C     IF PS-END-OF-CONTROL-PROC-FILE                               03470036
W3540C        DISPLAY 'PROCESSING DATE CARD MISSING '                   03480036
W3540C        MOVE '1000-INITIALIZE-PROGRAM '    TO PV-PARAGRAPH-NAME   03490036
W3540C        SET AC-CONTROL-CARD-ERROR          TO TRUE                03500036
W3540C        PERFORM 9999-ABEND                                        03510036
W3540C     ELSE                                                         03520036
W3540C        DISPLAY PC-PROGRAM-NAME ' CONTROL PROCESSING END DATE  : '03530036
W3540C             CC-PROC-END-DTE                                      03540036
W3540C        DISPLAY PC-PROGRAM-NAME ' CONTROL PROCESSING START DATE: '03550036
W3540C             CC-PROC-STA-DTE                                      03560036
W3540C     END-IF.                                                      03570036
W3540C*                                                                 03580036
W3540C     PERFORM 1100-VALIDATE-CURR-DATE.                             03590036
W3540C     PERFORM 1150-VALIDATE-PREV-DATE.                             03600036
W3540C*                                                                 03610036
           PERFORM 1200-READ-ACT-DATA-FILE.                             03620036
                                                                        03630036
W3540C******************************************************************03640036
W3540C*    VALIDATE CURRENT PROCESSING DATE FROM THE NEW CC - SVCC000  *03650037
W3540C******************************************************************03660036
W3540C  1100-VALIDATE-CURR-DATE.                                        03670036
W3540C                                                                  03680036
W3540C     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              03690036
W3540C                                                                  03700036
W3540C     SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                   03710036
W3540C     SET  DPG51-DO-NOT-INCR-DECR-DATE  TO TRUE.                   03720036
W3540C     SET  DPG52-LK-DTE-ISO             TO TRUE.                   03730036
W3540C     MOVE CC-PROC-END-DTE              TO DPG52-LK-DATE-INPUT.    03740036
W3540C     CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    03750036
W3540C                                             DPG54 DPG55 DPG56.   03760036
W3540C     EVALUATE TRUE                                                03770036
W3540C         WHEN DPG54-SEVERE-ERROR                                  03780036
W3540C         WHEN DPG54-DATE-INVALID                                  03790036
W3540C             MOVE '1100-VALIDATE-CURR-DATE'                       03800036
W3540C                                           TO PV-PARAGRAPH-NAME   03810036
W3540C             MOVE 'ERROR VALIDATING CURRENT DATE'                 03820037
W3540C                                           TO PV-OPERATION-MSG-1  03830036
W3540C             MOVE DPG54-ERROR-MESSAGE      TO PV-OPERATION-MSG-2  03840036
W3540C             PERFORM 9999-ABEND                                   03850036
W3540C     END-EVALUATE.                                                03860036
W3540C*                                                                 03870036
W3540C******************************************************************03880036
W3540C*    VALIDATE PREVIOUS PROCESSING DATE FROM THE NEW CC - SVCC000 *03890037
W3540C******************************************************************03900036
W3540C  1150-VALIDATE-PREV-DATE.                                        03910036
W3540C                                                                  03920036
W3540C     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              03930036
W3540C                                                                  03940036
W3540C     SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                   03950036
W3540C     SET  DPG51-DO-NOT-INCR-DECR-DATE  TO TRUE.                   03960036
W3540C     SET  DPG52-LK-DTE-ISO             TO TRUE.                   03970036
W3540C     MOVE CC-PROC-STA-DTE              TO DPG52-LK-DATE-INPUT.    03980036
W3540C     CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    03990036
W3540C                                             DPG54 DPG55 DPG56.   04000036
W3540C     EVALUATE TRUE                                                04010036
W3540C         WHEN DPG54-SEVERE-ERROR                                  04020036
W3540C         WHEN DPG54-DATE-INVALID                                  04030036
W3540C             MOVE '1150-VALIDATE-PREV-DATE'                       04040036
W3540C                                           TO PV-PARAGRAPH-NAME   04050036
W3540C             MOVE 'ERROR VALIDATING PREVIOUS DATE'                04060037
W3540C                                           TO PV-OPERATION-MSG-1  04070036
W3540C             MOVE DPG54-ERROR-MESSAGE      TO PV-OPERATION-MSG-2  04080036
W3540C             PERFORM 9999-ABEND                                   04090036
W3540C     END-EVALUATE.                                                04100036
W3540C*                                                                 04110036
      ******************************************************************04120036
      *    READ MAF RECORD                                             *04130036
      ******************************************************************04140036
       1200-READ-ACT-DATA-FILE.                                         04150036
                                                                        04160036
           READ MERCHANT-ACTIVITY-IN-FILE INTO TC003-MAF-DATA           04170036
              AT END                                                    04180036
                 SET PS-END-OF-ACT-FILE TO TRUE                         04190036
           END-READ.                                                    04200036
                                                                        04210036
           IF PS-END-OF-ACT-FILE                                        04220036
              CONTINUE                                                  04230036
           ELSE                                                         04240036
              ADD +1 TO PA-ACT-FILE-RECS-READ                           04250036
           END-IF.                                                      04260036
                                                                        04270036
      ******************************************************************04280036
      * DETERMINE IF THE RECORD IS GREATER THAN THE POS DATE (AFTER    *04290036
      * MIDNIGHT).  IF IT IS, CHANGE THE DATE TO THE POS DATE.          04300036
      * WRITE ALL RECORDS BACK OUT TO A SEQUENTIAL OUTPUT FILE.         04310036
      ******************************************************************04320036
       2000-PROCESS-ACT-DATA.                                           04330036
      * PV-RE-DATE-CCYYMMDD IS THE POS DATE IN THE CORRECT FORMAT      *04340036
W3540C     IF PV-RE-DATE-CCYYMMDD = TC003-SLS-DTE                       04410036
W3540C         MOVE TC003-SLS-DTE  TO PV-HOLD-SLS-DTE                   04420036
W3540C     ELSE                                                         04430036
W3540C         ADD 1 TO PV-SLS-DTE-CHG-COUNT                            04440036
W3540C         PERFORM 2100-POPULATE-VARI-SEL                           04450036
W3540C         PERFORM 2200-SELE-CORR-SLS-DTE                           04460036
INC001         IF SQLCODE = +100                                        04461065
INC001            ADD 1               TO PV-MISSING-TXN-PASSED          04461167
INC001            MOVE TC003-SLS-DTE  TO PV-HOLD-SLS-DTE                04462065
INC001         ELSE                                                     04463065
W3540C            MOVE EPPART-SLS-DTE(1:4) TO PV-HOLD-SLS-DTE(1:4)      04470065
W3540C            MOVE EPPART-SLS-DTE(6:2) TO PV-HOLD-SLS-DTE(5:2)      04480065
W3540C            MOVE EPPART-SLS-DTE(9:2) TO PV-HOLD-SLS-DTE(7:2)      04490065
INC001         END-IF                                                   04491065
W3540C     END-IF.                                                      04500036
           MOVE TC003-STORE-NBR    TO PV-HOLD-STORE-NBR.                04510036
           MOVE TC003-REG-NBR      TO PV-HOLD-REG-NBR.                  04520036
           MOVE TC003-TRAN-NBR     TO PV-HOLD-TRAN-NBR.                 04530036
           MOVE TC003-STRT-TM      TO PV-HOLD-STRT-TM.                  04540036
           MOVE TC003-TRAN-SEQ-NBR TO PV-HOLD-TRAN-SEQ-NBR.             04550036
           MOVE TC003-TRAN-TYPE    TO PV-HOLD-TRAN-TYPE.                04560036
           MOVE TC003-TRAN-DISP    TO PV-HOLD-TRAN-DISP.                04570036
           MOVE TC003-CARD-NBR     TO PV-HOLD-CARD-NBR.                 04580036
           MOVE TC003-TRAN-AMT     TO PV-HOLD-TRAN-AMT.                 04590036
           MOVE TC003-TYP-CDE      TO PV-HOLD-TYP-CDE.                  04600036
           MOVE TC003-ADJ-DISP     TO PV-HOLD-ADJ-DISP.                 04610036
           MOVE TC003-RESPONSE-CDE TO PV-HOLD-RESPONSE-CDE.             04620036
           MOVE TC003-STATE-CDE    TO PV-HOLD-STATE-CDE.                04630036
           MOVE TC003-STORE-NAME-ABBR TO PV-HOLD-STORE-NAME-ABBR.       04640036
           MOVE TC003-TYP-CDE      TO PV-HOLD-TYP-CDE.                  04650036
           MOVE TC003-ADJ-DISP     TO PV-HOLD-ADJ-DISP.                 04660036
           MOVE TC003-RESPONSE-CDE TO PV-HOLD-RESPONSE-CDE.             04670036
           MOVE TC003-STATE-CDE    TO PV-HOLD-STATE-CDE.                04680036
           MOVE TC003-STORE-NAME-ABBR TO PV-HOLD-STORE-NAME-ABBR.       04690036
           WRITE MERCHANT-ACTIVITY-OUT-RECORD FROM                      04700036
                 PV-HOLD-TC003.                                         04710036
           PERFORM 1200-READ-ACT-DATA-FILE.                             04720036
                                                                        04730036
W3540C******************************************************************04740036
W3540C* 2100-POPULATE-VARI-SEL.                                        *04750036
W3540C*     POPULATING THE VARIABLES WHICH ARE REQUIRED FOR SELECT     *04760036
W3540C******************************************************************04770036
W3540C 2100-POPULATE-VARI-SEL.                                          04780036
W3540C                                                                  04790036
W3540C     MOVE CC-PROC-STA-CC     TO PV-PREV-SLS-DTE-CC.               04800036
W3540C     MOVE CC-PROC-STA-YY     TO PV-PREV-SLS-DTE-YY.               04810036
W3540C     MOVE CC-PROC-STA-MM     TO PV-PREV-SLS-DTE-MM.               04820036
W3540C     MOVE CC-PROC-STA-DD     TO PV-PREV-SLS-DTE-DD.               04830036
W3540C     MOVE CC-PROC-END-CC     TO PV-CURR-SLS-DTE-CC.               04840036
W3540C     MOVE CC-PROC-END-YY     TO PV-CURR-SLS-DTE-YY.               04850036
W3540C     MOVE CC-PROC-END-MM     TO PV-CURR-SLS-DTE-MM.               04860036
W3540C     MOVE CC-PROC-END-DD     TO PV-CURR-SLS-DTE-DD.               04870036
W3540C     MOVE TC003-STORE-NBR    TO PV-STR-NBR.                       04880036
W3540C     MOVE TC003-REG-NBR      TO PV-RGST-ID.                       04890036
W3540C     MOVE TC003-TRAN-NBR     TO PV-TRN-NBR.                       04900036
W3540C     MOVE TC003-SLS-DTE(1:4) TO PV-TRN-DTE-CCYY.                  04910036
W3540C     MOVE TC003-SLS-DTE(5:2) TO PV-TRN-DTE-MM.                    04920036
W3540C     MOVE TC003-SLS-DTE(7:2) TO PV-TRN-DTE-DD.                    04930036
INC000     MOVE TC003-TRAN-AMT     TO PV-TRN-TOT-AMT.                   04931058
W3540C*                                                                 04940036
W3540C******************************************************************04950036
W3540C* 2200-SELE-CORR-SLS-DTE.                                        *04960036
W3540C*     SELECT CORRECT SALES DATE                                  *04970036
W3540C******************************************************************04980036
W3540C 2200-SELE-CORR-SLS-DTE.                                          04990036
W3540C                                                                  05000036
W3540C     EXEC SQL                                                     05010036
W3540C        SELECT  B.SLS_DTE                                         05020036
W3540C          INTO :EPPART-SLS-DTE                                    05030036
W3540C          FROM  TEPTND        A                                   05040036
W3540C               ,TEPPART       B                                   05050036
W3540C               ,TEPTRN        C                                   05060036
W3540C         WHERE  B.SLS_DTE >= :PV-PREV-SLS-DTE-SEL                 05070036
W3540C           AND  B.SLS_DTE <= DATE(:PV-CURR-SLS-DTE-SEL) + 1 DAY   05080036
W3540C           AND  A.PARTN_NBR  = B.PARTN_NBR                        05090036
W3540C           AND   A.STR_NBR   = :PV-STR-NBR                        05100036
W3540C           AND   A.RGST_ID   = :PV-RGST-ID-SEL                    05110036
W3540C           AND   A.TRN_NBR   = :PV-TRN-NBR                        05120036
W3540C           AND   C.PARTN_NBR = A.PARTN_NBR                        05130036
W3540C           AND   C.STR_NBR   = A.STR_NBR                          05140036
W3540C           AND   C.RGST_ID   = A.RGST_ID                          05150036
W3540C           AND   C.TRN_NBR   = A.TRN_NBR                          05160036
W3540C           AND   C.STRT_TM   = A.STRT_TM                          05170036
W3540C           AND   C.SLS_CORR_SEQ_NBR = A.SLS_CORR_SEQ_NBR          05180036
W3540C           AND   (C.TRN_DTE   = :PV-TRN-DTE-SEL                   05190046
INC000            OR    A.TNDR_AMT = :PV-TRN-TOT-AMT)                   05191064
W3540C        WITH UR                                                   05200036
W3540C     END-EXEC.                                                    05210036
W3540C                                                                  05220036
W3540C     EVALUATE TRUE                                                05230036
W3540C         WHEN SQLCODE = 0                                         05240036
W3540C             CONTINUE                                             05250036
W3540C         WHEN SQLCODE = -811                                      05260036
W3540C             CONTINUE                                             05270036
W3540C         WHEN SQLCODE = +100                                      05280036
W3540C             MOVE '2200-SELE-CORR-SLS-DTE'                        05290036
W3540C                      TO PV-PARAGRAPH-NAME                        05300036
W3540C             MOVE 'ORIGINAL SALES DATE NOT FOUND'                 05310045
W3540C                      TO PV-DB2-OPERATION-ATTEMPTED               05320036
W3540C             PERFORM 2210-DISPLAY-ABEND-RECORD                    05321043
INC001*            PERFORM 9100-SQL-ABEND                               05330065
W3540C         WHEN OTHER                                               05340036
W3540C             MOVE '2200-SELE-CORR-SLS-DTE'                        05350036
W3540C                      TO PV-PARAGRAPH-NAME                        05360036
W3540C             MOVE 'PROBLEM READING TEPTND TABLE '                 05370036
W3540C                      TO PV-DB2-OPERATION-ATTEMPTED               05380036
W3540C             PERFORM 2210-DISPLAY-ABEND-RECORD                    05381043
W3540C            PERFORM 9100-SQL-ABEND                                05390036
W3540C     END-EVALUATE.                                                05400036
W3540C*                                                                 05410036
W3540C******************************************************************05420040
W3540C* 2210-DISPLAY-ABEND-RECORD                                      *05430040
W3540C*     DISPLAY THE RECORD DETAILS WHICH CAUSE THE ABEND           *05440040
W3540C******************************************************************05450040
W3540C 2210-DISPLAY-ABEND-RECORD.                                       05460040
W3540C                                                                  05470040
INC001     DISPLAY 'BANK RECORD MISSING IN COSA TABLE!'                 05471065
W3540C     DISPLAY 'PV-PREV-SLS-DTE-SEL : ' PV-PREV-SLS-DTE-SEL.        05480040
W3540C     DISPLAY 'PV-CURR-SLS-DTE-SEL : ' PV-CURR-SLS-DTE-SEL.        05481040
W3540C     DISPLAY 'PV-STR-NBR          : ' PV-STR-NBR.                 05482040
W3540C     DISPLAY 'PV-RGST-ID-SEL      : ' PV-RGST-ID-SEL.             05483040
W3540C     DISPLAY 'PV-TRN-NBR          : ' PV-TRN-NBR.                 05484040
W3540C     DISPLAY 'PV-TRN-DTE-SEL      : ' PV-TRN-DTE-SEL.             05485040
W3540C                                                                  05486040
W3540C     DISPLAY 'TC003-STORE-NBR     : ' TC003-STORE-NBR.            05487040
W3540C     DISPLAY 'TC003-REG-NBR       : ' TC003-REG-NBR.              05488040
W3540C     DISPLAY 'TC003-TRAN-NBR      : ' TC003-TRAN-NBR.             05489040
W3540C     DISPLAY 'TC003-SLS-DTE       : ' TC003-SLS-DTE.              05490040
W3540C     DISPLAY 'TC003-TRAN-AMT      : ' TC003-TRAN-AMT.             05490140
INC001     DISPLAY '**********************'.                            05490266
W3540C*                                                                 05490340
      ******************************************************************05491040
      * 8000-END-PROGRAM                                               *05492040
      *     CLOSE FILES                                                *05493040
      ******************************************************************05494040
       8000-END-PROGRAM.                                                05495040
                                                                        05496040
           DISPLAY 'TOTAL TRANSACTIONS SALES DATE CHANGED '             05497040
                        PV-SLS-DTE-CHG-COUNT.                           05498040
           DISPLAY 'TOTAL TRANSACTIONS PASSED W/O DATE CHANGE '         05499067
                        PV-MISSING-TXN-PASSED.                          05499167
           CLOSE MERCHANT-ACTIVITY-IN-FILE                              05500036
                 CONTROL-DATE-FILE                                      05510036
W3540C           CONTROL-PROC-DATE-FILE                                 05520036
                 MERCHANT-ACTIVITY-OUT-FILE.                            05530036
                                                                        05540036
W3540C******************************************************************05550036
W3540C*      PERFORMS ABEND PROCESSING FOR SQL ERRORS.                 *05560036
W3540C******************************************************************05570036
W3540C 9100-SQL-ABEND.                                                  05580036
W3540C                                                                  05590036
W3540C     DISPLAY '9100-SQL-ABEND'.                                    05600036
W3540C     DISPLAY '**  ABEND     **'.                                  05610036
W3540C     DISPLAY '**  PROGRAM   ** = ' PC-PROGRAM-NAME.               05620036
W3540C     DISPLAY '**  PARAGRAPH ** = ' PV-PARAGRAPH-NAME.             05630036
W3540C     DISPLAY '**  OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.    05640036
W3540C                                                                  05650036
W3540C******************************************************************05660036
W3540C* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *05670036
W3540C* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *05680036
W3540C* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *05690036
W3540C* FORMAT.                                                        *05700036
W3540C******************************************************************05710036
W3540C     COPY DPPD004.                                                05720036
W3540C                                                                  05730036
W3540C     MOVE +4013 TO ABEND-CODE.                                    05740036
W3540C     CALL 'ILBOABN0' USING ABEND-CODE.                            05750036
W3540C*                                                                 05760036
      ***************************************************************** 05770036
      * 9999-ABEND                                                      05780036
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                  05790036
      ***************************************************************** 05800036
       9999-ABEND.                                                      05810036
                                                                        05820036
           DISPLAY '** ABEND           **'.                             05830036
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          05840036
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        05850036
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         05860036
                                                                        05870036
