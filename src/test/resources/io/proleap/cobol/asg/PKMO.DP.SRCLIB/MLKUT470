       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    MLKUT470.                                         00020007
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  09-01-2000.                                       00050007
       DATE-COMPILED.                                                   00060000
                                                                        00061007
      ******************************************************************00070000
      *PURPOSE:                                                         00080007
      * THIS PROGRAM WILL CREATE THE EXTRACT FOR THE RECALC PROGRAM     00110000
      * ON TABLE TWKDPTC                                                00120007
      ******************************************************************00140000
      * WR/PITS#  DATE      DESCRIPTION                                *00141013
      * -------- --------   -----------------------------------------  *00142013
      * W31475  06/01/2009  COGNIZANT                                  *00143013
      *                     MODIFIED CURSOR DECLARATION FOR MULTI ROW  *00144013
      *                     FETCH AS PART OF ML BATCH PERF TUNE PRJ    *00145013
      ******************************************************************00146013
                                                                        00150007
       ENVIRONMENT DIVISION.                                            00160000
       CONFIGURATION SECTION.                                           00180000
       SOURCE-COMPUTER.        IBM-3090.                                00200000
       OBJECT-COMPUTER.        IBM-3090.                                00210000
       INPUT-OUTPUT SECTION.                                            00230000
       FILE-CONTROL.                                                    00240000
                                                                        00250000
           SELECT WKDPTC-EXTR-FILE                                      00260007
               ASSIGN TO OUT01.                                         00270000
                                                                        00280000
       DATA DIVISION.                                                   00300000
       FILE SECTION.                                                    00330000
                                                                        00340000
       FD  WKDPTC-EXTR-FILE                                             00350007
           RECORDING MODE IS F                                          00360000
           LABEL RECORDS ARE STANDARD                                   00370000
           BLOCK CONTAINS 0 RECORDS.                                    00380000
       01  WKDPTC-EXTR-REC             PIC X(04).                       00400008
                                                                        00410000
      ******************************************************************00430000
       WORKING-STORAGE SECTION.                                         00440000
      ******************************************************************00450000
                                                                        00460000
W31475 01  ROW-ID                      PIC S9(04) COMP VALUE 0.         00462011
  |    01  HOST-VARIABLES.                                              00463012
  |        05  HV-WKDPTC-DEPT-NBR      PIC X(03)   VALUE SPACES         00464012
  |                                    OCCURS 100  TIMES.               00465012
  |        05  HV-WKDPTC-CHNL-CDE      PIC X(01)   VALUE SPACES         00468012
W31475                                 OCCURS 100  TIMES.               00469011
       01  PS-PROGRAM-SWITCHES.                                         00470000
           05  PS-TWKDPTC-DONE         PIC X       VALUE 'N'.           00480007
               88  PS-TWKDPTC-IS-DONE              VALUE 'Y'.           00490007
               88  PS-TWKDPTC-IS-NOT-DONE          VALUE 'N'.           00500007
                                                                        00510000
       01  PA-PROGRAM-ACCUMULATORS                 COMP-3.              00520000
           05  PA-TWKDPTC-ROWS-FETCHED PIC S9(13)  VALUE ZERO.          00530007
           05  PA-OUTPUT-RECS-WRITTEN  PIC S9(13)  VALUE ZERO.          00540000
                                                                        00550000
       01  CD-COUNTER-DISPLAY.                                          00560000
           05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.           00570000
                                                                        00580000
       01 ABEND-AREAS.                                                  00590000
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO           00600000
                                                   COMP SYNC.           00610000
               88  AC-BAD-DATA                     VALUE +4003.         00620000
               88  AC-DB2-ERROR                    VALUE +4013.         00630000
           05  AA-ABEND-LIT            PIC X(40)   VALUE                00640000
                 '*****       ABEND'.                                   00650000
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                00660000
                   '*****   PROGRAM: MLKUT470'.                         00670007
           05  AA-PARAGRAPH-LIT.                                        00680000
               10  FILLER              PIC X(17)   VALUE                00690000
                   '***** PARAGRAPH: '.                                 00700000
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.        00710000
           05  AA-MESSAGE-LINE.                                         00720000
               10  FILLER              PIC X(06)   VALUE '*****'.       00730000
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.        00740000
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                00750000
                   '*****    DB2 ERROR'.                                00760000
           05  AA-DB2-OPERATION-LIT.                                    00770000
               10  FILLER              PIC X(17)   VALUE                00780000
                   '***** OPERATION: '.                                 00790000
               10  AA-DB2-OPERATION.                                    00800000
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.        00810000
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.        00820000
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.        00830000
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.        00840000
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.        00850000
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.        00860000
                                                                        00870000
      *** TWKDPTC EXTRACT FILE                                          00880007
       01  EXTR-RECORD.                                                 00890000
           05  EXTR-DEPT-NBR    PIC X(03).                              00900000
JS0200     05  EXTR-CHNL-CDE    PIC X(01).                              00911008
       EJECT                                                            00920000
                                                                        00930000
      ******************************************************************00940000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                00950000
      ******************************************************************00960000
           COPY DPWS004.                                                00970000
                                                                        00980000
      ******************************************************************00990000
      *  DB2 ERRORS                                                     01000000
      ******************************************************************01010000
           COPY SQLCA2.                                                 01020000
                                                                        01030000
      ******************************************************************01040000
      *  DB2 COMMUNICATION AREA                                         01050000
      ******************************************************************01060000
           EXEC SQL                                                     01070000
                INCLUDE SQLCA                                           01080000
           END-EXEC.                                                    01090000
                                                                        01100000
      ******************************************************************01110000
      *  DB2 TWKDPTC TABLE LAYOUT                                       01120007
      ******************************************************************01130000
           EXEC SQL                                                     01140000
                INCLUDE TWKDPTC                                         01150007
           END-EXEC.                                                    01160000
                                                                        01170000
      ******************************************************************01170108
      * M/L DATE TABLE AND CURRENT OPEN FISCAL DATE INFO                01170308
      ******************************************************************01170408
           EXEC SQL                                                     01170600
               INCLUDE TDTATTR                                          01170700
           END-EXEC.                                                    01171000
                                                                        01180000
      ******************************************************************01190008
      * M/L DATE CONTROL INFO                                           01240208
      ******************************************************************01240408
           EXEC SQL                                                     01240500
               INCLUDE TMLCNTL                                          01240600
           END-EXEC.                                                    01240700
                                                                        01240800
      ******************************************************************01250000
      *  DB2 TWKDPTC CURSOR                                             01260007
      ******************************************************************01270000
           EXEC SQL                                                     01280000
W31475         DECLARE TWKDPTC_CURSOR CURSOR                            01292011
W31475         WITH ROWSET POSITIONING FOR                              01293011
               SELECT DISTINCT DEPT_NBR                                 01300000
                              ,CHNL_CDE                                 01311009
               FROM TWKDPTC                                             01320007
                   ,TMLCNTL                                             01331000
               WHERE (FSCL_WK_END_DTE BETWEEN                           01340006
                                   OPN_FSCL_WK_DTE                      01350006
                               AND MXPSTG_FSCL_WK_DTE) OR               01360006
                    ((FSCL_WK_END_DTE BETWEEN                           01361006
                      :DTATTR-PFM-END-DTE AND                           01362000
                      MXPSTG_FSCL_MN_DTE) AND                           01363000
                       (END_INV_RTL_AMT  <> 0  OR                       01363100
                        END_INV_COST_AMT <> 0))                         01364000
           END-EXEC.                                                    01380000
                                                                        01390000
      /*****************************************************************01420008
       PROCEDURE DIVISION.                                              01430000
      ******************************************************************01440000
                                                                        01450000
      ******************************************************************01460000
       A000-MAIN-MODULE.                                                01470000
      ******************************************************************01480000
      *                                                                 01490000
           PERFORM B000-INITIALIZE.                                     01500000
                                                                        01510000
           PERFORM B100-PROCESS-DATA.                                   01520000
                                                                        01530000
           PERFORM B900-EOJ-ROUTINE.                                    01540000
                                                                        01550000
           STOP RUN.                                                    01560000
                                                                        01570000
      /*****************************************************************01600008
       B000-INITIALIZE.                                                 01610000
      ******************************************************************01620000
      *                                                                 01630000
           OPEN OUTPUT WKDPTC-EXTR-FILE.                                01640007
           PERFORM 7100-SELECT-DB2-DATES.                               01641000
                                                                        01650000
      /*****************************************************************01690008
       B100-PROCESS-DATA.                                               01700000
      ******************************************************************01710000
      *                                                                 01720000
           PERFORM Z210-OPEN-CURSOR-TWKDPTC.                            01730007
                                                                        01740000
           PERFORM Z220-FETCH-TWKDPTC.                                  01750007
                                                                        01760000
           PERFORM                                                      01770000
             UNTIL PS-TWKDPTC-IS-DONE                                   01780007
W31475         PERFORM VARYING ROW-ID FROM 1 BY 1                       01782011
  |            UNTIL ROW-ID > SQLERRD (3)                               01783011
  |                 PERFORM C100-WRITE-EXTRACT                          01784011
  |            END-PERFORM                                              01785014
W31475*        PERFORM C100-WRITE-EXTRACT                               01800014
               PERFORM Z220-FETCH-TWKDPTC                               01801014
           END-PERFORM.                                                 01810000
                                                                        01820000
           PERFORM Z230-CLOSE-CURSOR-TWKDPTC.                           01830007
                                                                        01840000
      /*****************************************************************01870008
       C100-WRITE-EXTRACT.                                              01880000
      ******************************************************************01890000
      *                                                                 01900000
W31475*    MOVE WKDPTC-DEPT-NBR        TO EXTR-DEPT-NBR.                01910014
  |   *    MOVE WKDPTC-CHNL-CDE        TO EXTR-CHNL-CDE.                01920014
  |        MOVE HV-WKDPTC-DEPT-NBR(ROW-ID)                              01921214
  |                                    TO EXTR-DEPT-NBR.                01921312
  |        MOVE HV-WKDPTC-CHNL-CDE(ROW-ID)                              01921612
W31475                                 TO EXTR-CHNL-CDE.                01921711
                                                                        01922008
           WRITE WKDPTC-EXTR-REC       FROM EXTR-RECORD.                01930008
                                                                        01940008
           ADD 1                       TO PA-OUTPUT-RECS-WRITTEN.       01950000
                                                                        01960000
      /*****************************************************************01990008
       B900-EOJ-ROUTINE.                                                02000000
      ******************************************************************02010000
      *                                                                 02020000
           DISPLAY ' '.                                                 02030000
           DISPLAY '  PROGRAM MLKUT470 PROCESSING SUMMARY'.             02040007
           DISPLAY '  -----------------------------------'.             02050000
           DISPLAY ' '.                                                 02060000
      *                                                                 02070000
           MOVE PA-TWKDPTC-ROWS-FETCHED TO CD-COUNT-HOLD1.              02080007
           DISPLAY 'TWKDPTC ROWS FETCHED =      ' CD-COUNT-HOLD1.       02090007
      *                                                                 02100000
           MOVE PA-OUTPUT-RECS-WRITTEN TO CD-COUNT-HOLD1.               02110000
           DISPLAY 'EXTRACT RECORDS WRITTEN =   ' CD-COUNT-HOLD1.       02120000
      *                                                                 02130000
           DISPLAY ' '.                                                 02140000
                                                                        02150000
           CLOSE WKDPTC-EXTR-FILE.                                      02160007
                                                                        02170000
      /*****************************************************************02220008
       Z210-OPEN-CURSOR-TWKDPTC.                                        02230007
      ******************************************************************02240000
      *                                                                 02250000
           EXEC SQL                                                     02260000
               OPEN TWKDPTC_CURSOR                                      02270007
           END-EXEC.                                                    02280000
                                                                        02290000
           EVALUATE TRUE                                                02300000
               WHEN SQLCODE < 0                                         02310000
                   MOVE 'Z210-OPEN-CURSOR-TWKDPTC'                      02320007
                                       TO AA-PARAGRAPH-NAME             02330000
                   MOVE 'TWKDPTC TABLE ERROR'                           02340007
                                       TO AA-MESSAGE                    02350000
                   MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION              02360000
                   PERFORM Z998-SQL-ERROR                               02370000
               WHEN SQLCODE > 0                                         02380000
               WHEN SQLWARN0 NOT = SPACES                               02390000
                   MOVE 'Z210-OPEN-CURSOR-TWKDPTC'                      02400007
                                       TO AA-PARAGRAPH-NAME             02410000
                   MOVE 'TWKDPTC TABLE WARNING'                         02420007
                                       TO AA-MESSAGE                    02430000
                   MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION              02440000
                   PERFORM Z998-SQL-ERROR                               02450000
           END-EVALUATE.                                                02460000
                                                                        02470000
      /*****************************************************************02500008
       Z220-FETCH-TWKDPTC.                                              02510007
      ******************************************************************02520000
      *                                                                 02530000
W31475*    EXEC SQL                                                     02540011
  |   *        FETCH TWKDPTC_CURSOR                                     02550011
  |   *         INTO :WKDPTC-DEPT-NBR                                   02560011
  |   *             ,:WKDPTC-CHNL-CDE                                   02580011
  |   *    END-EXEC.                                                    02590011
  |        INITIALIZE HOST-VARIABLES.                                   02590111
  |                                                                     02590211
  |        EXEC SQL                                                     02591011
  |            FETCH NEXT ROWSET FROM TWKDPTC_CURSOR FOR 100 ROWS       02592011
  |             INTO :HV-WKDPTC-DEPT-NBR                                02593011
  |                 ,:HV-WKDPTC-CHNL-CDE                                02594011
W31475     END-EXEC.                                                    02595011
                                                                        02600000
           EVALUATE TRUE                                                02610000
               WHEN SQLCODE = 0                                         02620000
W31475*            ADD +1              TO PA-TWKDPTC-ROWS-FETCHED       02630014
W31475             ADD +100            TO PA-TWKDPTC-ROWS-FETCHED       02631014
               WHEN SQLCODE = +100                                      02640000
                   MOVE 'Y'            TO PS-TWKDPTC-DONE               02650007
W31475             ADD SQLERRD (3)     TO PA-TWKDPTC-ROWS-FETCHED       02651011
  |                PERFORM VARYING ROW-ID FROM 1 BY 1                   02652011
  |                UNTIL ROW-ID > SQLERRD (3)                           02653011
  |                     PERFORM C100-WRITE-EXTRACT                      02654011
W31475             END-PERFORM                                          02655011
               WHEN SQLCODE < 0                                         02660000
                   MOVE 'Z220-FETCH-TWKDPTC'                            02670007
                                       TO AA-PARAGRAPH-NAME             02680000
                   MOVE 'TWKDPTC TABLE ERROR'                           02690007
                                       TO AA-MESSAGE                    02700000
                   MOVE 'FETCH'        TO AA-DB2-OPERATION              02710000
                   PERFORM Z998-SQL-ERROR                               02720000
               WHEN ((SQLCODE > 0) AND (SQLCODE NOT = +100))            02730000
               WHEN SQLWARN0 NOT = SPACES                               02740000
                   MOVE 'Z220-FETCH-TWKDPTC'                            02750007
                                       TO AA-PARAGRAPH-NAME             02760000
                   MOVE 'TWKDPTC TABLE WARNING'                         02770007
                                       TO AA-MESSAGE                    02780000
                   MOVE 'FETCH'        TO AA-DB2-OPERATION              02790000
                   PERFORM Z998-SQL-ERROR                               02800000
           END-EVALUATE.                                                02810000
                                                                        02820000
      /*****************************************************************02850008
       Z230-CLOSE-CURSOR-TWKDPTC.                                       02860007
      ******************************************************************02870000
      *                                                                 02880000
           EXEC SQL                                                     02890000
               CLOSE TWKDPTC_CURSOR                                     02900007
           END-EXEC.                                                    02910000
                                                                        02920000
           EVALUATE TRUE                                                02930000
               WHEN SQLCODE < 0                                         02940000
                   MOVE 'Z230-CLOSE-CURSOR-TWKDPTC'                     02950007
                                       TO AA-PARAGRAPH-NAME             02960000
                   MOVE 'TWKDPTC TABLE ERROR'                           02970007
                                       TO AA-MESSAGE                    02980000
                   MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION              02990000
                   PERFORM Z998-SQL-ERROR                               03000000
               WHEN SQLCODE > 0                                         03010000
               WHEN SQLWARN0 NOT = SPACES                               03020000
                   MOVE 'Z230-CLOSE-CURSOR-TWKDPTC'                     03030007
                                       TO AA-PARAGRAPH-NAME             03040000
                   MOVE 'TWKDPTC TABLE WARNING'                         03050007
                                       TO AA-MESSAGE                    03060000
                   MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION              03070000
                   PERFORM Z998-SQL-ERROR                               03080000
           END-EVALUATE.                                                03090000
                                                                        03100000
      /*****************************************************************03110008
       7100-SELECT-DB2-DATES.                                           03120100
      ******************************************************************03120208
      *                                                                 03120300
           EXEC SQL                                                     03120400
               SELECT  PFM_END_DTE                                      03120500
                INTO  :DTATTR-PFM-END-DTE                               03121100
                FROM TDTATTR                                            03121600
                    ,TMLCNTL                                            03121700
                WHERE KY_DTE  = OPN_FSCL_MN_DTE                         03122000
           END-EXEC.                                                    03122100
      *                                                                 03122200
           EVALUATE SQLCODE                                             03122300
               WHEN 0                                                   03122400
                   DISPLAY 'DTATTR-PFM-END-DTE: '                       03122500
                            DTATTR-PFM-END-DTE                          03122600
               WHEN OTHER                                               03124100
                  MOVE '7100-SELECT-DB2-DATES'                          03124200
                                       TO AA-PARAGRAPH-NAME             03124300
                   PERFORM Z998-SQL-ERROR                               03124500
           END-EVALUATE.                                                03124600
      *                                                                 03124700
      /*****************************************************************03130008
       Z998-SQL-ERROR.                                                  03140000
      ******************************************************************03150000
      *                                                                 03160000
                                                                        03170000
           CLOSE WKDPTC-EXTR-FILE.                                      03180007
                                                                        03190000
           DISPLAY AA-ABEND-LIT.                                        03200000
           DISPLAY AA-DB2-ERROR-LIT.                                    03210000
           DISPLAY AA-PROGRAM-LIT.                                      03220000
           DISPLAY AA-PARAGRAPH-LIT.                                    03230000
           DISPLAY AA-MESSAGE-LINE.                                     03240000
           DISPLAY AA-DB2-OPERATION-LIT.                                03250000
           SET AC-DB2-ERROR TO TRUE.                                    03260000
           MOVE +4013                  TO ABEND-CODE.                   03270000
                                                                        03280000
           COPY DPPD004.                                                03290000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03300000
                                                                        03310000
