       IDENTIFICATION DIVISION.                                         00010008
       PROGRAM-ID.     INKUP308.                                        00020003
       INSTALLATION.   KOHLS DEPARTMENT STORES.                         00030000
       DATE-WRITTEN.   12/17/2012.                                      00040002
       DATE-COMPILED.                                                   00050000
                                                                        00060000
      ******************************************************************00070000
      *PURPOSE: THE PROGRAM CREATES BOTH POSITIVE AND NEGATIVE          00080000
      *         ADJUSTMENTS TO THE TADJADD TABLE AND FASHION SALES.     00090000
      *                                                                 00100000
      *         THE RECORD FORMAT MIMICS THE MASS_ADD & MASS_DELETE     00110000
      *         FUNCTIONALITY FOUND IN THE INKUT300 MODULE              00120000
      *                                                                 00130000
      *                                                                 00140000
      * CHANGE HISTORY:                                                 00150000
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                     00160000
      * -------  ----------  ----------------------------------------   00170000
      * PKE12569 1-18-2013   INITIAL INSTALL                            00180000
      *                      (CREATION...)  - COGNIZANT                 00190000
P54554* PRB54554 7-10-2015   SKIP THE RECORD WITHOUT PRICE HISTORY IN   00191041
P54554*                      TPPCPRC TABLE.ADJUST THE TRAILER IN THE    00192040
P54554*                      ADJUSTMENT FILE ACCORDINGLY.WRITE THE      00193040
P54554*                      SKIPPED ERROR RECORD IN FILE FOR APPENDING 00194040
P54554*                      IN SUMMARY REPORT EMAIL STEP OF INK926 JOB.00195042
P54554* PRB54554 7-17-2015   Add sku_stat_cde <> '98' condition in      00196043
      *                      TSKXREF select                             00197043
      ******************************************************************00200000
                                                                        00210000
       ENVIRONMENT DIVISION.                                            00220000
       CONFIGURATION SECTION.                                           00230000
       SOURCE-COMPUTER. IBM-3090.                                       00240000
       OBJECT-COMPUTER. IBM-3090.                                       00250000
       INPUT-OUTPUT SECTION.                                            00260000
       FILE-CONTROL.                                                    00270000
                                                                        00280000
                                                                        00290000
           SELECT MASS-ADDDEL-HEADER                                    00300000
               ASSIGN TO INP01.                                         00310000
                                                                        00320000
           SELECT MASS-ADDDEL-INP                                       00330000
               ASSIGN TO INP02.                                         00340000
                                                                        00350000
           SELECT FASHION-ADJUSTMENTS                                   00360000
               ASSIGN TO OUT01.                                         00370000
                                                                        00380000
           SELECT REPORT-FILE                                           00390000
               ASSIGN TO OUT02.                                         00400000
                                                                        00400119
P54554     SELECT MASS-ADDDEL-TRAILER                                   00400240
P54554         ASSIGN TO OUT03.                                         00400340
                                                                        00400442
P54554     SELECT ERROR-REPORT-FILE                                     00401040
P54554         ASSIGN TO RPT01.                                         00402040
                                                                        00410000
       DATA DIVISION.                                                   00420000
       FILE SECTION.                                                    00430000
                                                                        00440000
       FD  MASS-ADDDEL-HEADER                                           00450000
           LABEL RECORDS ARE STANDARD                                   00460000
           RECORDING MODE IS F                                          00470000
           BLOCK CONTAINS 0 RECORDS.                                    00480000
       01  MASS-ADDDEL-HEADER-REC        PIC X(80).                     00490000
                                                                        00500000
       FD  MASS-ADDDEL-INP                                              00510000
           LABEL RECORDS ARE STANDARD                                   00520000
           RECORDING MODE IS F                                          00530000
           BLOCK CONTAINS 0 RECORDS.                                    00540000
       01  MASS-ADDDEL-INP-REC           PIC X(100).                    00550000
                                                                        00560000
       FD  FASHION-ADJUSTMENTS                                          00570000
           LABEL RECORDS ARE STANDARD                                   00580000
           RECORDING MODE IS F                                          00590000
           BLOCK CONTAINS 0 RECORDS.                                    00600000
       01  FASHION-ADJUSTMENTS-RECORD    PIC X(200).                    00610000
                                                                        00620000
       FD  REPORT-FILE                                                  00630000
           LABEL RECORDS ARE STANDARD                                   00640000
           RECORDING MODE IS F                                          00650000
           BLOCK CONTAINS 0 RECORDS.                                    00660000
       01  REPORT-RECORD                 PIC X(80).                     00670000
                                                                        00670126
P54554 FD  MASS-ADDDEL-TRAILER                                          00671040
P54554     LABEL RECORDS ARE STANDARD                                   00672040
P54554     RECORDING MODE IS F                                          00673040
P54554     BLOCK CONTAINS 0 RECORDS.                                    00674040
P54554 01  MASS-ADDDEL-TRAILER-REC       PIC X(80).                     00675040
                                                                        00680042
P54554 FD  ERROR-REPORT-FILE                                            00681040
P54554     LABEL RECORDS ARE STANDARD                                   00682040
P54554     RECORDING MODE IS F                                          00683040
P54554     BLOCK CONTAINS 0 RECORDS.                                    00684040
P54554 01  ERROR-REPORT-FILE-RECORD        PIC X(80).                   00685040
      ******************************************************************00690000
       WORKING-STORAGE SECTION.                                         00700000
      ******************************************************************00710000
                                                                        00720000
       01  PC-PROGRAM-CONSTANTS.                                        00730000
           05  PC-PROGRAM-NAME          PIC  X(08)  VALUE 'INKUP308'.   00740003
           05  PC-MAX-DEADLOCKS         PIC S9(03)  VALUE +3.           00750000
           05  PC-11                    PIC S9(04) COMP VALUE 11.       00760000
           05  PC-MAX-RETRIES           PIC S9(03) VALUE +9             00770000
                                                    COMP SYNC.          00780000
           05  PC-ONE-BILLION           PIC S9(11) COMP-3               00790000
                                                   VALUE +1000000000.   00800000
           05  PC-PRIME1                PIC S9(05) COMP-3 VALUE +38543. 00810000
           05  PC-PRIME2                PIC S9(05) COMP-3 VALUE +58889. 00820000
           05  PC-AREA-888888           PIC S9(09) COMP   VALUE 888888. 00830000
           05  PC-AREA-999999           PIC S9(09) COMP   VALUE 999999. 00840000
           05  PC-AREA-777777           PIC S9(09) COMP   VALUE 777777. 00850000
           05  PC-MIN-AREA-NBR-ADD      PIC S9(09)  COMP                00860000
                                                    VALUE 990000.       00870000
           05  PC-MAX-AREA-NBR-ADD      PIC S9(09)  COMP                00880000
                                                    VALUE 999998.       00890000
           05  PC-MIN-AREA-NBR-DEL      PIC S9(09)  COMP                00900000
                                                    VALUE 880000.       00910000
           05  PC-MAX-AREA-NBR-DEL      PIC S9(09)  COMP                00920000
                                                    VALUE 889999.       00930000
      *                                                                 00940000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00950000
           05  PE-MSG-01                PIC X(50)   VALUE               00960000
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED  '.     00970000
           05  PE-MSG-02                PIC X(50)   VALUE               00980000
                'MAX RETRIES EXCEEDED ON TADJADD TABLE           '.     00990000
           05  PE-MSG-03                PIC X(50)   VALUE               01000000
                'ATTEMPT TO INSERT ROW ON TABLE TADJADD FAILED   '.     01010000
           05  PE-MSG-04                PIC X(50)   VALUE               01020000
                'ERROR IN SELECT FROM TCKRSTCNTL                 '.     01030000
           05  PE-MSG-05                PIC X(50)   VALUE               01040000
                'UPDATE TO TCKRSTINF FAILED                      '.     01050000
           05  PE-MSG-06                PIC X(50)   VALUE               01060000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT'.     01070000
           05  PE-MSG-07                PIC X(50)   VALUE               01080000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     01090000
           05  PE-MSG-08                PIC X(50)   VALUE               01100000
                'ERROR WHILE VALIDATING CONTROL DATE'.                  01110000
      *                                                                 01111038
P54554 01  PC-REPORT-MESSAGE.                                           01112040
P54554     05  PC-MSG-01                PIC X(56)   VALUE               01113040
P54554       '*****ABOVE REPORTED SKU REQUIRES MANUAL ADJUSTMENT***** '.01114040
P54554     05  FILLER                   PIC X(24)       VALUE SPACES.   01115040
      *                                                                 01120042
       01  PS-PROGRAM-SWITCHES.                                         01130000
           05  PS-EOF-HEADER-FL-SW      PIC X(01)   VALUE 'N'.          01140000
               88  PS-EOF-HEADER-FL                 VALUE 'Y'.          01150000
               88  PS-NOT-EOF-HEADER-FL             VALUE 'N'.          01160000
           05  PS-EOF-DETAIL-FL-SW      PIC X(01)   VALUE 'N'.          01170000
               88  PS-EOF-DETAIL-FL                 VALUE 'Y'.          01180000
               88  PS-NOT-EOF-DETAIL-FL             VALUE 'N'.          01190000
           05  PS-EOF-RTV-FL-SW         PIC X(01)   VALUE 'N'.          01200000
               88  PS-EOF-RTV-FL                    VALUE 'Y'.          01210000
               88  PS-NOT-EOF-RTV-FL                VALUE 'N'.          01220000
26342      05  PS-CONTROL-SW            PIC X(01)   VALUE 'N'.          01230000
26342          88  PS-END-OF-CONTROL                VALUE 'Y'.          01240000
26342          88  PS-NOT-END-OF-CONTROL            VALUE 'N'.          01250000
           05  PS-INSERT-SW             PIC X(01)   VALUE 'N'.          01260000
               88  PS-INSERT-DONE                   VALUE 'Y'.          01270000
               88  PS-INSERT-NOT-DONE               VALUE 'N'.          01280000
28543      05  PS-VALID-LOC-SW          PIC X(01)   VALUE 'N'.          01290000
28543          88  PS-VALID-LOCATION                VALUE 'Y'.          01300000
28543          88  PS-INVALID-LOCATION              VALUE 'N'.          01310000
           05  PS-AREA-NBR-SW           PIC X(01)   VALUE 'N'.          01320000
               88  PS-AREA-NBR-END                  VALUE 'Y'.          01330000
               88  PS-AREA-NBR-NOT-END              VALUE 'N'.          01340000
           05  PS-AREA-NBR-FOUND-SW     PIC X(01)   VALUE 'N'.          01350000
               88  PS-AREA-NBR-FOUND                VALUE 'Y'.          01360000
               88  PS-AREA-NBR-NOT-FOUND            VALUE 'N'.          01370000
           05  PS-FIRST-COMMIT-SW       PIC X(01)   VALUE 'N'.          01380000
               88  PS-FIRST-COMMIT                  VALUE 'Y'.          01390000
           05  PS-DEADLOCK-RESTART-SW   PIC X(01)   VALUE 'N'.          01400000
               88  PS-PROGRAM-DEADLOCKED            VALUE 'Y'.          01410000
           05  PS-RUN-TYPE-SW           PIC X(01)   VALUE ' '.          01420000
               88  PS-NORMAL-RUN                    VALUE '0'.          01430000
               88  PS-RESTART-RUN                   VALUE '9'.          01440000
P54554     05  PS-CURSOR-FETCH          PIC X(01)   VALUE 'Y'.          01441040
P54554         88  PS-FETCH-SUCCESS                 VALUE 'Y'.          01442040
P54554         88  PS-FETCH-FAIL                    VALUE 'N'.          01450040
       01  PV-PROGRAM-VARIABLES.                                        01460000
           05  PV-DISPLAY               PIC  9(11)  VALUE ZERO.         01470000
           05  PV-ADD-NUM               PIC S9(04) COMP VALUE ZERO.     01480000
           05  PV-RANDOM-NUMBER         PIC S9(11) COMP-3 VALUE +0.     01490000
           05  PV-ADJ-QTY               PIC S9(09) COMP VALUE ZERO.     01500000
           05  PV-WRITE-POS-COUNT       PIC S9(09) COMP-3 VALUE ZERO.   01510000
           05  PV-TADJADD-POS-COUNT     PIC S9(09) COMP-3 VALUE ZERO.   01520000
           05  PV-READ-POS-COUNT        PIC S9(09) COMP-3 VALUE ZERO.   01530000
           05  PV-DEADLOCK-COUNT        PIC S9(09) COMP-3 VALUE ZERO.   01540000
           05  PV-COMMIT-COUNT          PIC S9(09) COMP-3 VALUE ZERO.   01550000
           05  PV-MA-READ-COUNT         PIC S9(09) COMP-3 VALUE ZERO.   01560000
           05  PV-READ-ADD-COUNT        PIC S9(09) COMP-3 VALUE ZERO.   01570000
           05  PV-READ-DEL-COUNT        PIC S9(09) COMP-3 VALUE ZERO.   01580000
           05  PV-WRITE-NEG-COUNT       PIC S9(09) COMP-3 VALUE ZERO.   01590000
           05  PV-WRITE-ADD-COUNT       PIC S9(09) COMP-3 VALUE ZERO.   01600000
           05  PV-WRITE-DEL-COUNT       PIC S9(09) COMP-3 VALUE ZERO.   01610000
P54554     05  PV-ERR-REC-COUNT         PIC S9(09) COMP-3 VALUE ZERO.   01611040
           05  PV-TADJADD-NEG-COUNT     PIC S9(09) COMP-3 VALUE ZERO.   01620000
           05  PV-TADJADD-ADD-COUNT     PIC S9(09) COMP-3 VALUE ZERO.   01630000
           05  PV-TADJADD-DEL-COUNT     PIC S9(09) COMP-3 VALUE ZERO.   01640000
           05  PV-READ-NEG-COUNT        PIC S9(09) COMP-3 VALUE ZERO.   01650000
           05  PV-MASSADD-COUNT         PIC S9(09) COMP-3 VALUE ZERO.   01660000
           05  PV-MASSDEL-COUNT         PIC S9(09) COMP-3 VALUE ZERO.   01670000
           05  PV-RETRY-COUNT           PIC S9(04) COMP   VALUE +0.     01680000
           05  PV-AREA-NBR-COUNT        PIC S9(04) COMP   VALUE +0.     01690000
           05  PV-PROGRAM-NAME          PIC  X(08) VALUE 'INKUP308'.    01700003
           05  PV-PARAGRAPH-NAME        PIC  X(30) VALUE SPACE.         01710000
           05  PV-OPERATION-ATTEMPTED   PIC  X(30) VALUE SPACE.         01720000
           05  PV-DB2-OPER-ATTEMPTED    PIC  X(30) VALUE SPACE.         01730000
28543      05  PV-SKU-NUMBER            PIC S9(08)V COMP-3.             01740000
           05  PV-SKU-NBR               PIC  9(08).                     01750000
           05  PV-SKU-NBR-ALPH REDEFINES                                01760000
               PV-SKU-NBR               PIC  X(08).                     01770000
           05  PV-SEED                  PIC S9(11) COMP-3 VALUE ZERO.   01780000
           05  PV-WORK-FIELD            PIC S9(17) COMP-3 VALUE ZERO.   01790000
           05  PV-WORK-FIELD2           PIC S9(17) COMP-3 VALUE ZERO.   01800000
           05  PV-TODAYS-DATE           PIC  9(06)  VALUE ZERO.         01810000
           05  PV-TODAYS-TIME           PIC  9(08)  VALUE ZERO.         01820000
           05  FILLER REDEFINES PV-TODAYS-TIME.                         01830000
               10  PV-HH                PIC  9(02).                     01840000
               10  PV-MIN               PIC  9(02).                     01850000
               10  PV-SEC               PIC  9(02).                     01860000
               10  PV-HUND-SEC          PIC  9(02).                     01870000
           05  PV-INITIAL-SEED          PIC  9(08)    VALUE ZERO.       01880000
           05  FILLER REDEFINES PV-INITIAL-SEED.                        01890000
               10  PV-SEED-BYTE-1-2     PIC  9(02).                     01900000
               10  PV-SEED-BYTE-3-4     PIC  9(02).                     01910000
               10  PV-SEED-BYTE-5-6     PIC  9(02).                     01920000
               10  PV-SEED-BYTE-7-8     PIC  9(02).                     01930000
28543      05  PV-INPUT-LOC             PIC  9(05) VALUE ZERO.          01940000
28543      05  PV-HOLD-LOC-NBR          PIC S9(04) VALUE ZERO COMP.     01950000
28543      05  PV-PREV-LOC-NBR          PIC S9(04) VALUE ZERO COMP.     01960000
28543      05  PV-HOLD-KEY-DATE         PIC  X(10) VALUE SPACES.        01970000
           05  PV-MAX-AREA-NBR          PIC S9(10) COMP-3               01980000
                                                   VALUE ZEROES.        01990000
           05  PV-MIN-AREA-NBR          PIC S9(10) COMP-3               02000000
                                                   VALUE ZEROES.        02010000
           05  PV-LOOKUP-AREA-NBR       PIC  9(08) VALUE ZEROES.        02020000
           05  PV-CURRENT-AREA-NBR      PIC  9(08) VALUE ZEROES.        02030000
           05  PV-CURRENT-TIMESTAMP     PIC  X(26) VALUE SPACES.        02040000
           05  PV-COMMIT-TIMESTAMP      PIC  X(26) VALUE SPACES.        02050000
           05  PV-DISPLAY-FIELD         PIC ZZZZZZZZ9  VALUE ZERO.      02060000
           05  PV-DISPLAY-FIELD2        PIC S9(7)V9(2) VALUE ZERO.      02070000
           05  PV-DISPLAY-FIELD3        PIC S9(9)      VALUE ZERO.      02080000
           05  PV-JOB-NAME              PIC  X(08) VALUE 'INK926'.      02090000
P54554     05  PV-SKUNUM                PIC  9(08).                     02091040
                                                                        02100000
       01  ABEND-CODE                   PIC S9(04)  COMP VALUE ZERO.    02110000
           88  MISSING-CONTROL-ABEND                   VALUE +4002.     02120000
           88  AC-OTHER-ERROR                          VALUE +4003.     02130000
           88  AC-SQL-ERROR                            VALUE +4013.     02140000
                                                                        02150000
       01  PD-PROGRAM-DISPLAYS.                                         02160000
           05  PD-DEADLOCK-COUNT        PIC  9(04) VALUE 0.             02170000
                                                                        02180000
26342                                                                   02190000
28543  01  IN-INTERNAL-UPC-ID.                                          02200000
           05  IN-INTR-UPC-ID           PIC S9(10) COMP VALUE ZEROES.   02210000
           05  FILLER REDEFINES IN-INTR-UPC-ID.                         02220000
               10  FILLER               PIC  X(04).                     02230000
               10  IN-INTR-UPC-ID-4     PIC  X(04).                     02240000
               10  IN-INTR-UPC-ID-SPC REDEFINES IN-INTR-UPC-ID-4        02250000
                                        PIC S9(9)  COMP.                02260000
       01  PV-MASS-ADDDEL-HEADER.                                       02270000
           05  PV-RECORD-ID             PIC  9(02).                     02280000
               88  PV-NORMAL-HEADER                     VALUE 10.       02290000
               88  PV-RTV-HEADER                        VALUE 15.       02300000
           05  FILLER                   PIC X(01)       VALUE SPACES.   02310000
           05  PV-PROCESS-TYPE          PIC X(01)       VALUE SPACES.   02320000
               88  PV-MASSADD                           VALUE 'A', 'a'. 02330000
               88  PV-MASSDEL                           VALUE 'D', 'd'. 02340000
           05  FILLER                   PIC X(01)       VALUE SPACES.   02350000
           05  PV-DESCRIPTION           PIC X(20)       VALUE SPACES.   02360000
           05  PV-EOD                   PIC X(01)       VALUE SPACES.   02370000
               88 PV-END-OF-DESCRIPTION                 VALUE 'E'.      02380000
           05  FILLER                   PIC X(54)       VALUE SPACES.   02390000
                                                                        02400000
       01  PV-RPT-RECORD.                                               02410000
           05  PV-RPT-PROCESS-TYPE      PIC X(03)       VALUE SPACES.   02420000
               88  PV-RPT-MASSADD                       VALUE 'ADD'.    02430000
               88  PV-RPT-MASSDEL                       VALUE 'DEL'.    02440000
           05  PV-RPT-AREA-NBR          PIC S9(09) COMP VALUE ZEROES.   02450000
           05  PV-RPT-USED              PIC S9(09) COMP VALUE ZEROES.   02460000
           05  PV-RPT-AVAILABLE         PIC S9(09) COMP VALUE ZEROES.   02470000
           05  PV-RPT-AVAILABLE-PER     PIC S9(07)V9(2) COMP            02480000
                                                        VALUE ZEROES.   02490000
           05  PV-RPT-TOTAL-INSERT      PIC S9(09) COMP-3               02500000
                                                           VALUE ZEROES.02510000
           05  PV-RPT-FASHION-WRITTEN   PIC S9(09) COMP-3               02520000
                                                           VALUE ZEROES.02530000
           05  FILLER                   PIC  X(51)       VALUE SPACES.  02540000
      *                                                                 02540142
P54554 01  PV-MASS-ADDDEL-TRAILER.                                      02541040
P54554     05  PV-RECORD-ID-TR          PIC 9(02)   VALUE ZEROES.       02542040
P54554         88  PV-TRAILER                       VALUE 99.           02543040
P54554     05  FILLER                   PIC X(01)   VALUE SPACES.       02544040
P54554     05  PV-INPUT-UNITS-SUM       PIC 9(09)   VALUE ZEROES.       02545040
P54554     05  FILLER                   PIC X(01)   VALUE SPACES.       02546040
P54554     05  PV-INPUT-REC-COUNT       PIC 9(09)   VALUE ZEROES.       02547040
P54554     05  FILLER                   PIC X(58)   VALUE SPACES.       02548040
      *                                                                 02549042
       01  PT-AREA-NBR-TABLE.                                           02550000
           05  PT-AREA OCCURS 10000 TIMES INDEXED BY                    02560000
                                                   PT-AREA-NBR-IDX.     02570000
               15  PT-AREA-NBR          PIC  9(09) VALUE ZEROES.        02580000
       01  CC-CONTROL-CARD-REC.                                         02590000
           05  CC-JOB-NAME              PIC  X(08)      VALUE SPACES.   02600000
           05  FILLER                   PIC  X(72)      VALUE SPACES.   02610000
                                                                        02611019
P54554 01 RPT-FILE-HD1.                                                 02612040
P54554    05 FILLER                      PIC X(80) VALUE SPACES.        02613040
P54554 01 RPT-FILE-HD2.                                                 02614040
P54554     05 FILLER                      PIC X(26) VALUE               02615040
P54554                            '*** ERROR RECORD FOUND ***'.         02616040
P54554 01 RPT-FILE-HD3.                                                 02617040
P54554     05 FILLER                      PIC X(80) VALUE SPACES.       02618040
P54554 01 RPT-FILE-HD4.                                                 02619040
P54554     05 FILLER                   PIC X(7)  VALUE 'STR NBR'.       02619140
P54554     05 FILLER                   PIC X(3)  VALUE SPACES.          02619240
P54554     05 FILLER                   PIC X(11)  VALUE 'SKU NUMBER '.  02619340
P54554     05 FILLER                   PIC X(3)  VALUE SPACES.          02619440
P54554     05 FILLER                   PIC X(8)  VALUE 'QUANTITY'.      02619540
P54554     05 FILLER                   PIC X(48)  VALUE SPACES.         02619640
P54554 01 RPT-FILE-HD5.                                                 02619740
P54554     05 FILLER                   PIC X(9)  VALUE '---------'.     02619840
P54554     05 FILLER                   PIC X(6)  VALUE '------'.        02619940
P54554     05 FILLER                   PIC X(3)  VALUE SPACES.          02620040
P54554     05 FILLER                   PIC X(11)  VALUE '-----------'.  02620140
P54554     05 FILLER                   PIC X(11)  VALUE '-----------'.  02620240
P54554     05 FILLER                   PIC X(3)  VALUE SPACES.          02620340
P54554     05 FILLER                   PIC X(8) VALUE '--------'.       02620440
P54554     05 FILLER                   PIC X(8) VALUE '--------'.       02620540
P54554     05 FILLER                   PIC X(21)  VALUE SPACES.         02620640
P54554 01 RPT-DTL-LINE.                                                 02620740
P54554     05 FILLER                   PIC X(80) VALUE SPACES.          02620840
P54554 01 ERROR-REC-LAYOUT.                                             02620940
P54554     05  PV-STORE-NO-ERR            PIC 9(05)       VALUE ZEROES. 02621040
P54554     05  FILLER                     PIC X(11)       VALUE SPACES. 02621140
P54554     05  PV-SKU-NBR-ERR             PIC 9(08)       VALUE ZEROES. 02621240
P54554     05  FILLER                     PIC X(10)       VALUE SPACES. 02621340
P54554     05  PV-QUANTITY-ERR            PIC 9(10)       VALUE ZEROES. 02621440
P54554     05  FILLER                     PIC X(36)       VALUE SPACES. 02621640
                                                                        02621742
P54554 01 PV-RETURN-RSN                   PIC X(40)       VALUE SPACES. 02621840
P54554       88  PV-CURSOR-FETCH-FAILED    VALUE                        02621940
P54554                      'CURSOR FETCH ISSUE                      '. 02622040
      *                                                                 02622136
      /*****************************************************************02622323
      *01  IN309-INVENTORY-INPUT-RECORD.                                02623023
           COPY INWS309.                                                02650000
                                                                        02660000
      /*****************************************************************02670000
      *01  FS035-ADJUSTMENT-RECORD                                      02680000
           COPY FSRD035.                                                02690000
                                                                        02700000
      /*****************************************************************02710000
      *01  PC010-PCT-INTRANSIT-REC                                      02720000
           COPY PCRD010.                                                02730000
                                                                        02740000
      ******************************************************************02750000
      *01  DP004-DB2-ERROR-FIELDS                                       02760000
           COPY DPWS004.                                                02770000
                                                                        02780000
      ******************************************************************02790000
      * DB2 TABLE DCLGEN - TADJADD                                      02800000
      ******************************************************************02810000
           EXEC SQL                                                     02820000
               INCLUDE TADJADD                                          02830000
           END-EXEC.                                                    02840000
                                                                        02850000
      ******************************************************************02860000
      * DB2 TABLE DCLGEN - TINVPAR                                      02870000
      ******************************************************************02880000
           EXEC SQL                                                     02890000
               INCLUDE TINVPAR                                          02900000
           END-EXEC.                                                    02910000
                                                                        02920000
      ******************************************************************02930000
      * DB2 TABLE DCLGEN - TINCNTL                                      02940000
      ******************************************************************02950000
           EXEC SQL                                                     02960000
               INCLUDE TINCNTL                                          02970000
           END-EXEC.                                                    02980000
                                                                        02990000
      *                                                                 03000000
           EXEC SQL                                                     03010000
               INCLUDE XMT00001                                         03020000
           END-EXEC.                                                    03030000
                                                                        03040000
           EXEC SQL                                                     03050000
               INCLUDE XMT00002                                         03060000
           END-EXEC.                                                    03070000
                                                                        03080000
      ******************************************************************03090000
           EXEC SQL                                                     03100000
               INCLUDE SQLCA                                            03110000
           END-EXEC.                                                    03120000
                                                                        03130000
      ************************************************************      03140000
      * MASS ADD/DELETE AREA CODE CURSOR                                03150000
      ************************************************************      03160000
           EXEC SQL                                                     03170000
               DECLARE AREA_NBR_CSR CURSOR FOR                          03180000
                 SELECT DISTINCT AREA_NBR                               03190000
                 FROM TADJADD                                           03200000
                 WHERE AREA_NBR BETWEEN :PV-MIN-AREA-NBR                03210000
                       AND :PV-MAX-AREA-NBR                             03220000
                 ORDER BY AREA_NBR                                      03230000
                 WITH UR                                                03240000
                 FOR FETCH ONLY                                         03250000
           END-EXEC.                                                    03260000
      /*****************************************************************03270000
      ***  COMMON PRICING MODULE                                        03280000
           COPY INWS400.                                                03290000
                                                                        03300000
      ***************************************************************** 03310000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     03320000
      ***************************************************************** 03330000
           EXEC SQL                                                     03340000
               INCLUDE TCKRSTCN                                         03350000
           END-EXEC.                                                    03360000
      *                                                                 03370000
           EXEC SQL                                                     03380000
               INCLUDE TCKRSTIN                                         03390000
           END-EXEC.                                                    03400000
      *                                                                 03410000
      ***************************************************************** 03420000
       PROCEDURE DIVISION.                                              03430000
      ***************************************************************** 03440000
       A000-MAINLINE-DRIVER-SECTION.                                    03450000
                                                                        03460000
           PERFORM B100-INITIALIZATION.                                 03470000
                                                                        03480000
           PERFORM B200-PROCES-MASS-ADDDEL-HDR.                         03490000
                                                                        03500000
           PERFORM B300-EOJ-TASKS.                                      03510000
                                                                        03511042
P54554       IF PV-ERR-REC-COUNT > ZEROES                               03512040
P54554           MOVE +4091          TO RETURN-CODE                     03513040
P54554       END-IF.                                                    03520040
                                                                        03521020
           STOP RUN.                                                    03530000
                                                                        03540000
      /**************************************************************** 03550000
      * B100-INITIALIZATION.                                            03560000
      * OPEN INPUT/OUTPUT FILES, INITIALIZE PROGRAM VARIABLES AND       03570000
P54554* COUNTERS,READ HEADER & TRAILER RECORD.                          03580040
      ***************************************************************** 03590000
       B100-INITIALIZATION.                                             03600000
                                                                        03610000
           OPEN INPUT  MASS-ADDDEL-HEADER                               03620000
                INPUT  MASS-ADDDEL-INP                                  03630000
                OUTPUT FASHION-ADJUSTMENTS                              03640000
P54554                 ERROR-REPORT-FILE                                03641040
P54554                 MASS-ADDDEL-TRAILER                              03642040
                       REPORT-FILE.                                     03650000
      *                                                                 03660000
           INITIALIZE IN309-INVENTORY-INPUT-RECORD                      03670000
28543                 PC010-PCT-INTRANSIT-REC                           03680000
                      PV-MIN-AREA-NBR                                   03690000
                      PV-MAX-AREA-NBR                                   03700000
                      PV-CURRENT-AREA-NBR                               03710000
                      PV-LOOKUP-AREA-NBR.                               03720000
                                                                        03730000
           PERFORM Z100-READ-MASS-ADDDEL-HEADER.                        03740000
P54554     PERFORM Z110-READ-MASS-ADDDEL-TRAILER.                       03741040
                                                                        03742024
           MOVE ZEROS                       TO PV-WRITE-POS-COUNT.      03760000
           MOVE ZEROS                       TO PV-TADJADD-POS-COUNT.    03770000
           MOVE ZEROS                       TO PV-READ-POS-COUNT.       03780000
           MOVE ZEROS                       TO PV-WRITE-NEG-COUNT.      03790000
           MOVE ZEROS                       TO PV-TADJADD-NEG-COUNT.    03800000
           MOVE ZEROS                       TO PV-READ-NEG-COUNT.       03810000
           MOVE ZEROS                       TO PV-MASSADD-COUNT.        03820000
           MOVE ZEROS                       TO PV-MASSDEL-COUNT.        03830000
           MOVE ZEROS                       TO PV-AREA-NBR-COUNT.       03840000
                                                                        03850000
           ACCEPT PV-TODAYS-DATE    FROM DATE.                          03860000
           ACCEPT PV-TODAYS-TIME    FROM TIME.                          03870000
      *                                                                 03880000
      ***************************************************************** 03890000
      * B200-PROCES-MASS-ADDDEL-HDR                                     03900000
      *   PROCESS MASS ADD/DELETE HEADER RECORD                         03910000
      ***************************************************************** 03920000
       B200-PROCES-MASS-ADDDEL-HDR.                                     03930000
      *                                                                 03940000
           EVALUATE TRUE                                                03950000
              WHEN PV-NORMAL-HEADER                                     03960000
                   DISPLAY 'MASS ADD/DELETE PROCESS WITHOUT RETAIL '    03970000
                   PERFORM C100-NORMAL-MASS-ADDDEL                      03980000
470           WHEN PV-RTV-HEADER                                        03990000
470                DISPLAY 'MASS ADD/DELETE PROCESS WITH RETAIL '       04000000
                   PERFORM C200-RTV-MASS-ADDDEL                         04010000
470           WHEN OTHER                                                04020000
470                DISPLAY 'INVALID HEADER RECORD ID          '         04030000
470                DISPLAY 'B200-PROCES-MASS-ADDDEL-HDR      '          04040000
470                PERFORM 9999-OTHER-ABEND                             04050000
470        END-EVALUATE.                                                04060000
      *                                                                 04070000
      ***************************************************************** 04080000
      * B300-EOJ-TASKS.                                                 04090000
      ***************************************************************** 04100000
       B300-EOJ-TASKS.                                                  04110000
                                                                        04120000
      *                                                                 04130000
           CLOSE MASS-ADDDEL-HEADER                                     04140000
                 MASS-ADDDEL-INP                                        04150000
                 FASHION-ADJUSTMENTS                                    04160000
P54554           ERROR-REPORT-FILE                                      04161040
P54554           MASS-ADDDEL-TRAILER                                    04162040
                 REPORT-FILE.                                           04170000
                                                                        04180000
           IF PV-NORMAL-HEADER                                          04190000
                  IF PV-MASSADD                                         04200000
                     MOVE PV-MA-READ-COUNT TO PV-READ-ADD-COUNT         04210000
                     MOVE ZEROS            TO PV-READ-DEL-COUNT         04220000
                  ELSE                                                  04230000
                     MOVE PV-MA-READ-COUNT TO PV-READ-DEL-COUNT         04240000
                     MOVE ZEROS            TO PV-READ-ADD-COUNT         04250000
                  END-IF                                                04260000
           ELSE                                                         04270000
                  IF PV-MASSADD                                         04280000
                     MOVE PV-MA-READ-COUNT TO PV-READ-POS-COUNT         04290000
                     MOVE ZEROS            TO PV-READ-NEG-COUNT         04300000
                  ELSE                                                  04310000
                     MOVE PV-MA-READ-COUNT TO PV-READ-NEG-COUNT         04320000
                     MOVE ZEROS            TO PV-READ-POS-COUNT         04330000
                  END-IF                                                04340000
           END-IF.                                                      04350000
                                                                        04360000
           DISPLAY ' '.                                                 04370000
           DISPLAY '* * * * * * * * * * * * * * * * * * *'.             04380000
           DISPLAY '*INKUP308 SUMMARY REPORT:           *'.             04390003
           DISPLAY '*                                   *'.             04400000
           DISPLAY '*MASS ADD ADJUSTMENTS FOR RTV       *'.             04410000
           DISPLAY '*  INVENTORY READ * ', PV-READ-POS-COUNT            04420000
           DISPLAY '*  FASHION WRITE  * ', PV-WRITE-POS-COUNT           04430000
           DISPLAY '*  TADJADD WRITTE * ', PV-TADJADD-POS-COUNT         04440000
           DISPLAY '*                                   *'.             04450000
           DISPLAY '*MASS DELETE ADJUSTMENTS FOR RTV    *'.             04460000
           DISPLAY '*  NEG INV READ   * ', PV-READ-NEG-COUNT            04470000
           DISPLAY '*  FASHION WRITE  * ', PV-WRITE-NEG-COUNT           04480000
           DISPLAY '*  TADJADD WRITTE * ', PV-TADJADD-NEG-COUNT         04490000
           DISPLAY '*                                   *'.             04500000
           DISPLAY '*MASS ADD ADJUSTMENTS FOR NORMAL    *'.             04510000
           DISPLAY '*  INVENTORY READ * ', PV-READ-ADD-COUNT            04520000
           DISPLAY '*  FASHION WRITE  * ', PV-WRITE-ADD-COUNT           04530000
P54554     DISPLAY '*  ERR REC SKIP   * ', PV-ERR-REC-COUNT             04531040
           DISPLAY '*  TADJADD WRITTE * ', PV-TADJADD-ADD-COUNT         04540000
           DISPLAY '*                                   *'.             04550000
           DISPLAY '*MASS DELETE ADJUSTMENTS FOR NORMAL  *'.            04560042
           DISPLAY '*  INVENTORY READ * ', PV-READ-DEL-COUNT            04570000
           DISPLAY '*  FASHION WRITE  * ', PV-WRITE-DEL-COUNT           04580000
           DISPLAY '*  TADJADD WRITTE * ', PV-TADJADD-DEL-COUNT         04590000
           DISPLAY '* * * * * * * * * * * * * * * * * * *'.             04600000
                                                                        04610000
      *                                                                 04620000
      ****************************************************************  04630000
      * C100-NORMAL-MASS-ADDDEL.                                        04640000
      *   PROCESS NORMAL MASS ADD/DELETE ADJUSTMENTS WITHOUT RETAIL     04650000
      ***************************************************************** 04660000
       C100-NORMAL-MASS-ADDDEL.                                         04670000
                                                                        04680000
470        EVALUATE TRUE                                                04690000
470           WHEN PV-MASSADD                                           04700000
                   IF PS-AREA-NBR-NOT-FOUND                             04710000
                      MOVE PC-MIN-AREA-NBR-ADD TO PV-MIN-AREA-NBR       04720000
                      MOVE PC-MAX-AREA-NBR-ADD TO PV-MAX-AREA-NBR       04730000
                      PERFORM D100-GET-AREA-NBR                         04740000
                   END-IF                                               04750000
470                PERFORM Z200-READ-MASS-ADDDEL-NOR                    04760000
470                PERFORM D200-PRCS-NOR-MASS-ADD                       04770000
470                        UNTIL PS-EOF-DETAIL-FL                       04780000
                                                                        04780142
P54554          IF PV-ERR-REC-COUNT > ZEROS                             04781040
P54554               SUBTRACT PV-ERR-REC-COUNT                          04782040
P54554                   FROM PV-INPUT-REC-COUNT                        04783040
P54554               PERFORM Z600-WRITE-TRAILER-REC                     04785040
P54554             WRITE ERROR-REPORT-FILE-RECORD FROM RPT-DTL-LINE     04785140
P54554             WRITE ERROR-REPORT-FILE-RECORD FROM PC-REPORT-MESSAGE04785240
P54554             WRITE ERROR-REPORT-FILE-RECORD FROM RPT-DTL-LINE     04785340
P54554          END-IF                                                  04786040
                                                                        04786142
                   PERFORM D600-WRITE-NOR-REP-REC                       04790000
              WHEN PV-MASSDEL                                           04800000
                   IF PS-AREA-NBR-NOT-FOUND                             04810000
                      MOVE PC-MIN-AREA-NBR-DEL TO PV-MIN-AREA-NBR       04820000
                      MOVE PC-MAX-AREA-NBR-DEL TO PV-MAX-AREA-NBR       04830000
                      PERFORM D100-GET-AREA-NBR                         04840000
                   END-IF                                               04850000
470                PERFORM Z200-READ-MASS-ADDDEL-NOR                    04860000
470                PERFORM D300-PRCS-NOR-MASS-DEL                       04870000
470                        UNTIL PS-EOF-DETAIL-FL                       04880000
                   PERFORM D600-WRITE-NOR-REP-REC                       04890000
470           WHEN OTHER                                                04900000
470                DISPLAY 'INVALID PROCESS TYPE FOUND         '        04910000
470                DISPLAY 'C100-NORMAL-MASS-ADDDEL            '        04920000
470                PERFORM 9999-OTHER-ABEND                             04930000
470         END-EVALUATE.                                               04940000
      ****************************************************************  04950000
      * C200-RTV-MASS-ADDDEL.                                           04960000
      *   PROCESS MASS ADD/DELETE ADJUSTMENTS WITH RETAIL               04970000
      ***************************************************************** 04980000
       C200-RTV-MASS-ADDDEL.                                            04990000
                                                                        05000000
2470        EVALUATE TRUE                                               05010000
2470           WHEN PV-MASSADD                                          05020000
                    IF PS-AREA-NBR-NOT-FOUND                            05030000
                       MOVE PC-MIN-AREA-NBR-ADD TO PV-MIN-AREA-NBR      05040000
                       MOVE PC-MAX-AREA-NBR-ADD TO PV-MAX-AREA-NBR      05050000
                       PERFORM D100-GET-AREA-NBR                        05060000
                    END-IF                                              05070000
2470                PERFORM Z300-READ-MASS-ADDDEL-RTV                   05080000
                    PERFORM D400-PRCS-RTV-MASS-ADD                      05090000
                            UNTIL PS-EOF-DETAIL-FL                      05100000
                    PERFORM D600-WRITE-NOR-REP-REC                      05110000
               WHEN PV-MASSDEL                                          05120000
                    IF PS-AREA-NBR-NOT-FOUND                            05130000
                       MOVE PC-MIN-AREA-NBR-DEL TO PV-MIN-AREA-NBR      05140000
                       MOVE PC-MAX-AREA-NBR-DEL TO PV-MAX-AREA-NBR      05150000
                       PERFORM D100-GET-AREA-NBR                        05160000
                    END-IF                                              05170000
2470                PERFORM Z300-READ-MASS-ADDDEL-RTV                   05180000
                    PERFORM D500-PRCS-RTV-MASS-DEL                      05190000
                            UNTIL PS-EOF-DETAIL-FL                      05200000
                    PERFORM D600-WRITE-NOR-REP-REC                      05210000
2470           WHEN OTHER                                               05220000
2470                DISPLAY 'INVALID PROCESS TYPE FOUND         '       05230000
2470                DISPLAY 'C200-RTV-MASS-ADDDEL               '       05240000
2470                PERFORM 9999-OTHER-ABEND                            05250000
2470         END-EVALUATE.                                              05260000
      *                                                                 05270000
      ***************************************************************** 05280000
      * D100-GET-AREA-NBR                                               05290000
      *  TO IDENTIFY THE AREA NUMBER TO USE FOR CURRENT PROCESS         05300000
      ***************************************************************** 05310000
       D100-GET-AREA-NBR.                                               05320000
            PERFORM E100-OPEN-AREA-NBR-CSR                              05330000
            PERFORM E200-FETCH-AREA-NBR-CSR                             05340000
                               UNTIL PS-AREA-NBR-END                    05350000
            PERFORM E300-CLOSE-AREA-NBR-CSR                             05360000
            SET  PT-AREA-NBR-IDX            TO 1                        05370000
            MOVE PV-MIN-AREA-NBR            TO PV-LOOKUP-AREA-NBR       05380000
            PERFORM E400-AREA-NBR-LOOKUP                                05390000
                               UNTIL PS-AREA-NBR-FOUND                  05400000
                               OR PV-LOOKUP-AREA-NBR > PV-MAX-AREA-NBR. 05410000
           IF PS-AREA-NBR-FOUND                                         05420000
              DISPLAY 'AREA NUMBER SELECTED:'                           05430000
                                   PV-CURRENT-AREA-NBR                  05440000
           ELSE                                                         05450000
              DISPLAY '*****MASS ADD/Delete ***********'                05460000
              DISPLAY 'D100-GET-AREA-NBR               '                05470000
              DISPLAY 'AREA NUMBER NOT AVAILABLE       '                05480000
              PERFORM 9999-OTHER-ABEND                                  05490000
           END-IF.                                                      05500000
      *                                                                 05510000
      ***************************************************************** 05520000
      * D200-PRCS-NOR-MASS-ADD.                                         05530000
      * PROCESS THE POSITIVE INVENTORY ADJUSTMENTS WITHOUT RETAIL       05540000
      ***************************************************************** 05550000
       D200-PRCS-NOR-MASS-ADD.                                          05560000
                                                                        05570000
           ADD 1                            TO  PV-MASSADD-COUNT.       05580000
           IF PC010-STR-NBR NOT = INVPAR-LOC-NBR                        05590000
               MOVE PC010-STR-NBR           TO INVPAR-LOC-NBR           05600000
               PERFORM E900-SELECT-ACT-INV-DT                           05610000
           END-IF.                                                      05620000
                                                                        05630000
           PERFORM E500-INSERT-PRCS-NOR-ADD.                            05640000
                                                                        05641006
P54554      IF PS-FETCH-SUCCESS                                         05650040
             IF INVPAR-ACTL-UNIT-BK-DTE NOT EQUAL '9999-09-09'          05660013
               MOVE SPACES              TO FS035-ADJUSTMENT-RECORD      05670000
               MOVE '+'                 TO FS035-ACTN-CDE               05680000
               PERFORM E110-REFRMT-NOR-REC-FS                           05690000
               ADD 1                    TO PV-WRITE-ADD-COUNT           05700000
             END-IF                                                     05701013
P54554      END-IF.                                                     05710040
           PERFORM Z200-READ-MASS-ADDDEL-NOR.                           05720000
      *                                                                 05730000
      ***************************************************************** 05740000
      * D300-PRCS-NOR-MASS-DEL                                          05750000
      * PROCESS THE NEGATIVE INVENTORY ADJUSTMENTS WITHOUT RETAIL       05760000
      ***************************************************************** 05770000
       D300-PRCS-NOR-MASS-DEL.                                          05780000
                                                                        05790000
           ADD 1                            TO  PV-MASSDEL-COUNT.       05800000
           IF PC010-STR-NBR NOT = INVPAR-LOC-NBR                        05810000
               MOVE PC010-STR-NBR           TO INVPAR-LOC-NBR           05820000
               PERFORM E900-SELECT-ACT-INV-DT                           05830000
           END-IF.                                                      05840000
                                                                        05850000
           PERFORM E600-INSERT-PRCS-NOR-DEL.                            05860000
                                                                        05870000
           IF INVPAR-ACTL-UNIT-BK-DTE NOT EQUAL '9999-09-09'            05880000
               MOVE SPACES                  TO FS035-ADJUSTMENT-RECORD  05890000
               MOVE '-'                     TO FS035-ACTN-CDE           05900000
               PERFORM E110-REFRMT-NOR-REC-FS                           05910000
               ADD 1                        TO PV-WRITE-DEL-COUNT       05920000
           END-IF.                                                      05930000
                                                                        05940000
           PERFORM Z200-READ-MASS-ADDDEL-NOR.                           05950000
      *                                                                 05960000
      ***************************************************************** 05970000
      * D400-PRCS-RTV-MASS-ADD.                                         05980000
      *    PROCESS THE POSITIVE INVENTORY ADJUSTMENTS WITH RETAIL       05990000
      ***************************************************************** 06000000
       D400-PRCS-RTV-MASS-ADD.                                          06010000
                                                                        06020000
           ADD 1                            TO  PV-MASSADD-COUNT.       06030000
           IF IN309-LOC-NBR NOT = INVPAR-LOC-NBR                        06040000
               MOVE IN309-LOC-NBR           TO INVPAR-LOC-NBR           06050000
               PERFORM E900-SELECT-ACT-INV-DT                           06060000
           END-IF.                                                      06070000
                                                                        06080000
           PERFORM E700-INSERT-PRCS-RTV-ADD.                            06090000
                                                                        06100000
           IF INVPAR-ACTL-UNIT-BK-DTE NOT EQUAL '9999-09-09'            06110000
               MOVE SPACES                  TO FS035-ADJUSTMENT-RECORD  06120000
               MOVE '+'                     TO FS035-ACTN-CDE           06130000
               PERFORM E120-REFRMT-RTV-FS-REC                           06140000
               ADD 1                        TO PV-WRITE-POS-COUNT       06150000
           END-IF.                                                      06160000
                                                                        06170000
           PERFORM Z300-READ-MASS-ADDDEL-RTV.                           06180000
      *                                                                 06190000
      ***************************************************************** 06200000
      * D500-PRCS-RTV-MASS-DEL.                                         06210000
      *    PROCESS THE NEGATIVE INVENTORY ADJUSTMENTS WITH RETAIL.      06220000
      ***************************************************************** 06230000
       D500-PRCS-RTV-MASS-DEL.                                          06240000
                                                                        06250000
           ADD 1                            TO  PV-READ-NEG-COUNT.      06260000
           IF IN309-LOC-NBR NOT = INVPAR-LOC-NBR                        06270000
               MOVE IN309-LOC-NBR           TO INVPAR-LOC-NBR           06280000
               PERFORM E900-SELECT-ACT-INV-DT                           06290000
           END-IF.                                                      06300000
                                                                        06310000
           PERFORM E800-INSERT-PRCS-RTV-DEL.                            06320000
                                                                        06330000
           IF INVPAR-ACTL-UNIT-BK-DTE NOT EQUAL '9999-09-09'            06340000
               MOVE SPACES                  TO FS035-ADJUSTMENT-RECORD  06350000
               MOVE '-'                     TO FS035-ACTN-CDE           06360000
               PERFORM E120-REFRMT-RTV-FS-REC                           06370000
               ADD 1                        TO PV-WRITE-NEG-COUNT       06380000
           END-IF.                                                      06390000
                                                                        06400000
           PERFORM Z300-READ-MASS-ADDDEL-RTV.                           06410000
      *                                                                 06420000
      ***************************************************************** 06430000
      * D600-WRITE-NOR-REP-REC.                                         06440000
      * WRITE THE PROCESSED ADJUSTMENT'S REPORT RECORD                  06450000
      ***************************************************************** 06460000
       D600-WRITE-NOR-REP-REC.                                          06470000
                                                                        06480000
              MOVE PV-CURRENT-AREA-NBR      TO PV-RPT-AREA-NBR          06490000
              COMPUTE PV-RPT-USED = PV-AREA-NBR-COUNT + 1               06500000
              COMPUTE PV-RPT-AVAILABLE = 10000 - PV-RPT-USED            06510000
              COMPUTE PV-RPT-AVAILABLE-PER = PV-RPT-AVAILABLE * 0.01    06520000
              IF PV-NORMAL-HEADER                                       06530000
                 IF PV-MASSADD                                          06540000
                     SET PV-RPT-MASSADD     TO TRUE                     06550000
                     MOVE PV-TADJADD-ADD-COUNT                          06560000
                                            TO PV-RPT-TOTAL-INSERT      06570000
                     MOVE PV-WRITE-ADD-COUNT                            06580000
                                            TO PV-RPT-FASHION-WRITTEN   06590000
                 ELSE                                                   06600000
                     SET PV-RPT-MASSDEL     TO TRUE                     06610000
                     MOVE PV-TADJADD-DEL-COUNT                          06620000
                                            TO PV-RPT-TOTAL-INSERT      06630000
                     MOVE PV-WRITE-DEL-COUNT                            06640000
                                            TO PV-RPT-FASHION-WRITTEN   06650000
                  END-IF                                                06660000
              ELSE                                                      06670000
                 IF PV-MASSADD                                          06680000
                     SET PV-RPT-MASSADD     TO TRUE                     06690000
                     MOVE PV-TADJADD-POS-COUNT                          06700000
                                            TO PV-RPT-TOTAL-INSERT      06710000
                     MOVE PV-WRITE-POS-COUNT                            06720000
                                            TO PV-RPT-FASHION-WRITTEN   06730000
                 ELSE                                                   06740000
                     SET PV-RPT-MASSDEL     TO TRUE                     06750000
                     MOVE PV-TADJADD-NEG-COUNT                          06760000
                                            TO PV-RPT-TOTAL-INSERT      06770000
                     MOVE PV-WRITE-NEG-COUNT                            06780000
                                            TO PV-RPT-FASHION-WRITTEN   06790000
                 END-IF                                                 06800000
           END-IF.                                                      06810000
                                                                        06820000
           PERFORM  Z400-WRITE-REPORT-FL.                               06830000
      *                                                                 06840000
      ******************************************************************06850000
      * E100-OPEN-AREA-NBR-CSR                                          06860000
      *  OPEN THE AREA NUMBER CURSOR                                    06870000
      ******************************************************************06880000
       E100-OPEN-AREA-NBR-CSR.                                          06890000
            EXEC SQL                                                    06900000
                OPEN AREA_NBR_CSR                                       06910000
            END-EXEC.                                                   06920000
                                                                        06930000
            EVALUATE TRUE                                               06940000
                WHEN SQLCODE = +0                                       06950000
                    CONTINUE                                            06960000
                WHEN SQLWARN NOT = SPACE                                06970000
                WHEN SQLCODE NOT = +0                                   06980000
                    DISPLAY '**************************************'    06990000
                    DISPLAY '** ABEND                            **'    07000000
                    DISPLAY '** PARA = E100-OPEN-AREA-NBR-CSR    **'    07010000
                    DISPLAY '**************************************'    07020000
                    PERFORM 9999-SQL-ABEND                              07030000
            END-EVALUATE.                                               07040000
      ******************************************************************07050000
      * E200-FETCH-AREA-NBR-CSR                                         07060000
      *  FETCH A ROW FROM THE AREA NUMBER CURSOR                        07070000
      ******************************************************************07080000
       E200-FETCH-AREA-NBR-CSR.                                         07090000
           EXEC SQL                                                     07100000
             FETCH AREA_NBR_CSR                                         07110000
               INTO :ADJADD-AREA-NBR                                    07120000
           END-EXEC.                                                    07130000
                                                                        07140000
           EVALUATE TRUE                                                07150000
               WHEN SQLCODE = +0                                        07160000
                    ADD 1                   TO PV-AREA-NBR-COUNT        07170000
                    MOVE ADJADD-AREA-NBR    TO                          07180000
                         PT-AREA-NBR(PT-AREA-NBR-IDX)                   07190000
                    SET PT-AREA-NBR-IDX     UP BY 1                     07200000
               WHEN SQLCODE = +100                                      07210000
                    SET PS-AREA-NBR-END     TO TRUE                     07220000
               WHEN SQLWARN NOT = SPACE                                 07230000
               WHEN SQLCODE NOT = +0                                    07240000
                    DISPLAY '**************************************'    07250000
                    DISPLAY '** ABEND                            **'    07260000
                    DISPLAY '** PARA = E200-FETCH-AREA-NBR-CSR   **'    07270000
                    DISPLAY '**************************************'    07280000
                    PERFORM 9999-SQL-ABEND                              07290000
           END-EVALUATE.                                                07300000
      ******************************************************************07310000
      * E300-CLOSE-AREA-NBR-CSR                                         07320000
      *  CLOSE THE AREA NUMBER CURSOR                                   07330000
      ******************************************************************07340000
       E300-CLOSE-AREA-NBR-CSR.                                         07350000
           EXEC SQL                                                     07360000
               CLOSE AREA_NBR_CSR                                       07370000
           END-EXEC.                                                    07380000
                                                                        07390000
           EVALUATE TRUE                                                07400000
               WHEN SQLCODE = +0                                        07410000
                   CONTINUE                                             07420000
               WHEN SQLWARN NOT = SPACE                                 07430000
               WHEN SQLCODE NOT = +0                                    07440000
                   DISPLAY '**************************************'     07450000
                   DISPLAY '** ABEND                            **'     07460000
                   DISPLAY '** PARA = E300-CLOSE-AREA-NBR-CSR   **'     07470000
                   DISPLAY '**************************************'     07480000
                   PERFORM 9999-SQL-ABEND                               07490000
           END-EVALUATE.                                                07500000
                                                                        07510000
      ******************************************************************07520000
      *  CHOOSE AN AVAILABLE AREA NUMBER FOR CURRENT ADJUSTMENT         07530000
      ******************************************************************07540000
       E400-AREA-NBR-LOOKUP.                                            07550000
           IF PV-LOOKUP-AREA-NBR = PT-AREA-NBR(PT-AREA-NBR-IDX)         07560000
              SET PT-AREA-NBR-IDX           UP BY 1                     07570000
              ADD 1 TO PV-LOOKUP-AREA-NBR                               07580000
           ELSE                                                         07590000
              IF PV-LOOKUP-AREA-NBR NOT EQUAL PC-AREA-888888            07600000
                 MOVE   'Y'                 TO PS-AREA-NBR-FOUND-SW     07610000
                 MOVE PV-LOOKUP-AREA-NBR    TO PV-CURRENT-AREA-NBR      07620000
              END-IF                                                    07630000
           END-IF.                                                      07640000
      *                                                                 07650000
      ***************************************************************** 07660000
      * E500-INSERT-PRCS-NOR-ADD.                                       07670000
      *   TO INSERT MASS ADD ADJUSTMENTS TO TABLE TADJADD               07680000
      ***************************************************************** 07690000
       E500-INSERT-PRCS-NOR-ADD.                                        07700000
           INITIALIZE IN400-PV-INPUT-AREA.                              07710000
                                                                        07720000
      *** GENERATE A RANDOM NUMBER FOR THE DKEY.....                    07730000
           ADD 11                           TO PV-ADD-NUM.              07740000
           ACCEPT PV-TODAYS-TIME            FROM TIME.                  07750000
           COMPUTE PV-TODAYS-TIME   = (PV-TODAYS-DATE                   07760000
                                    + PV-ADD-NUM)                       07770000
                                    * 11.                               07780000
           MOVE PV-HUND-SEC                 TO PV-SEED-BYTE-1-2.        07790000
           MOVE PV-SEC                      TO PV-SEED-BYTE-3-4.        07800000
           MOVE PV-MIN                      TO PV-SEED-BYTE-5-6.        07810000
           MOVE PV-HH                       TO PV-SEED-BYTE-7-8.        07820000
           MOVE PV-INITIAL-SEED             TO PV-SEED.                 07830000
           COMPUTE PV-WORK-FIELD = ((PV-SEED + PC-PRIME1)               07840000
                                 * PC-PRIME2).                          07850000
           DIVIDE PV-WORK-FIELD BY  PC-ONE-BILLION                      07860000
               GIVING PV-WORK-FIELD2                                    07870000
               REMAINDER PV-RANDOM-NUMBER.                              07880000
                                                                        07890000
           MOVE PV-RANDOM-NUMBER            TO ADJADD-DKEY-NBR.         07900000
                                                                        07910000
      *  DETERMINE RETAIL                                               07920000
           MOVE PC010-STR-NBR               TO PV-INPUT-LOC             07930000
                                               ADJADD-LOC-NBR           07940000
           MOVE PV-INPUT-LOC                TO PV-HOLD-LOC-NBR          07950000
                                               IN400-PV-LOC-IN          07960000
           MOVE INVPAR-PLND-INV-DTE         TO PV-HOLD-KEY-DATE         07970000
                                               IN400-PV-EFF-DTE-IN      07980000
                                                                        07990000
           IF PC010-INTR-UPC-ID > 0                                     08000000
               MOVE PC010-INTR-UPC-ID   TO IN-INTR-UPC-ID               08010000
               MOVE IN-INTR-UPC-ID-SPC  TO IN400-PV-UPC-ID-SPC          08020000
               SET IN400-PV-INTR-UPC-ID-LOOKUP TO TRUE                  08030000
           ELSE                                                         08040000
               IF PC010-UPC-NBR > 0                                     08050000
                  MOVE PC010-UPC-NBR    TO IN400-PV-UPC-NBR-IN          08060000
                  SET IN400-PV-UPC-NBR-LOOKUP                           08070000
                                        TO TRUE                         08080000
               END-IF                                                   08090000
           END-IF                                                       08100000
P54554     PERFORM H100-SKU-FETCH-PARA                                  08101040
                                                                        08110000
P54554     SET PS-FETCH-SUCCESS TO TRUE                                 08111040
           PERFORM IN400-LOOKUP-RETAIL                                  08120000
P54554     MOVE  IN400-PV-RETURN-RSN TO PV-RETURN-RSN                   08121040
                                                                        08130000
      *** MOVE THE VALID RECORD INFORMATION TO TADJADD...               08140000
           EVALUATE TRUE                                                08150000
      ***     PRICE FOUND - RENUMBER SKU/UPC.......                     08160000
               WHEN IN400-PS-RENUMBER-SKU                               08170000
                    MOVE PC010-STR-NBR         TO ADJADD-LOC-NBR        08180000
                    MOVE PV-CURRENT-AREA-NBR   TO ADJADD-AREA-NBR       08190000
                    MOVE IN400-PRC-SKU-OUT     TO ADJADD-SKU-NBR        08200000
                    MOVE IN400-PRC-UPC-OUT     TO ADJADD-UPC-NBR        08210000
                    MOVE SPACE                 TO ADJADD-MISSED-MD-IND  08220000
                    MOVE 'INKUP308'            TO ADJADD-CHG-ID-NBR     08230003
                    MOVE PV-DESCRIPTION        TO ADJADD-ADJ-RSN-TXT    08240000
      ***     PRICE FOUND - DUMMY SKU/UPC....                           08250000
               WHEN IN400-PS-DUMMY-SKU                                  08260000
                    MOVE PC010-STR-NBR         TO ADJADD-LOC-NBR        08270000
                    MOVE PV-CURRENT-AREA-NBR   TO ADJADD-AREA-NBR       08280000
                                                                        08290000
                    MOVE IN400-ATT-SKU-OUT     TO ADJADD-SKU-NBR        08300000
                    MOVE IN400-ATT-UPC-OUT     TO ADJADD-UPC-NBR        08310000
                    MOVE SPACE                 TO ADJADD-MISSED-MD-IND  08320000
                    MOVE 'INKUP308'            TO ADJADD-CHG-ID-NBR     08330003
                    MOVE PV-DESCRIPTION        TO ADJADD-ADJ-RSN-TXT    08340000
      ***     PRICE FOUND - NORMAL....                                  08350000
               WHEN IN400-PS-RTL-FOUND                                  08360000
                    MOVE PC010-STR-NBR       TO ADJADD-LOC-NBR          08370000
                    MOVE PV-CURRENT-AREA-NBR TO ADJADD-AREA-NBR         08380000
                    MOVE IN400-PRC-SKU-OUT   TO ADJADD-SKU-NBR          08390000
                    MOVE PC010-UPC-NBR       TO ADJADD-UPC-NBR          08400000
                    MOVE SPACE               TO ADJADD-MISSED-MD-IND    08410000
                    MOVE 'INKUP308'          TO ADJADD-CHG-ID-NBR       08420003
                    MOVE PV-DESCRIPTION      TO ADJADD-ADJ-RSN-TXT      08430000
      ***     INVALID CONDITION OCCURRED DURING THE PRICE LOOKUP....    08440000
P54554         WHEN PV-CURSOR-FETCH-FAILED                              08591040
P54554              SET PS-FETCH-FAIL TO TRUE                           08591540
P54554              ADD 1 TO PV-ERR-REC-COUNT                           08591640
                                                                        08591742
P54554                IF PV-ERR-REC-COUNT = 1                           08591840
P54554                   PERFORM Y100-WRITE-HEADINGS                    08591940
P54554                END-IF                                            08592040
                                                                        08592142
P54554              MOVE PC010-STR-NBR TO PV-STORE-NO-ERR               08592240
P54554              MOVE PV-SKUNUM TO PV-SKU-NBR-ERR                    08592340
P54554              MOVE PC010-IN-TRNST-QTY TO PV-QUANTITY-ERR          08592440
P54554              SUBTRACT PV-QUANTITY-ERR                            08592640
P54554                     FROM PV-INPUT-UNITS-SUM                      08592740
P54554              WRITE ERROR-REPORT-FILE-RECORD                      08592840
P54554                    FROM ERROR-REC-LAYOUT                         08592940
P54554              DISPLAY 'TPPCPRC CURSOR FETCH FAILED'               08593140
P54554              DISPLAY ' ERROR INTERNAL UPC :' PC010-INTR-UPC-ID   08594040
P54554              DISPLAY ' STORE              :' PC010-STR-NBR       08595040
P54554              DISPLAY ' SKU                :' PV-SKUNUM           08596040
P54554              DISPLAY ' AREA               :' ADJADD-AREA-NBR     08596140
               WHEN IN400-PV-INVALID-KEY-INPUT                          08597035
                    DISPLAY ' ERROR INTERNAL UPC :' PC010-INTR-UPC-ID   08598035
P54554              DISPLAY ' STORE  :' PC010-STR-NBR                   08599040
P54554                      ' SKU    :' PV-SKUNUM                       08599140
P54554                      ' DKEY   :' ADJADD-DKEY-NBR                 08599240
P54554                      ' AREA   :' ADJADD-AREA-NBR                 08599340
                    DISPLAY IN400-PV-PARAGRAPH                          08599435
                    MOVE 'E500-INSERT-PRCS-NOR-ADD'                     08599535
                                             TO PV-PARAGRAPH-NAME       08599635
                    MOVE IN400-PV-RETURN-RSN TO PV-OPERATION-ATTEMPTED  08599735
                    PERFORM 9999-SQL-ABEND                              08599835
           END-EVALUATE.                                                08600000
                                                                        08601042
P54554      IF  PS-FETCH-SUCCESS                                        08602040
             MOVE IN400-PRC-UNIT-RTL-OUT      TO ADJADD-ADJ-UNIT-PR-AMT 08610040
             MOVE PC010-IN-TRNST-QTY          TO ADJADD-ADJ-QTY         08620040
                                                                        08630000
             COMPUTE ADJADD-ADJ-EXTD-AMT = ADJADD-ADJ-QTY               08640013
                                       * ADJADD-ADJ-UNIT-PR-AMT         08650014
                                                                        08660000
      *** INSERT INTO TADJADD....                                       08670000
             MOVE ZERO                        TO PV-RETRY-COUNT         08680030
             SET PS-INSERT-NOT-DONE           TO TRUE                   08690030
                                                                        08700000
              PERFORM F100-INSERT-TADJADD-ROW                           08710030
                 UNTIL PS-INSERT-DONE                                   08720030
                  OR PV-RETRY-COUNT > PC-MAX-RETRIES                    08730027
                                                                        08740000
              IF  PV-RETRY-COUNT > PC-MAX-RETRIES                       08750030
                DISPLAY 'SKU  :  ' IN400-TEMP-UPC-SKU                   08760027
                DISPLAY 'UPC  :  ' PC010-UPC-NBR                        08770027
28543           DISPLAY ' STORE: ' ADJADD-LOC-NBR                       08780027
                        ' AREA: '  ADJADD-AREA-NBR                      08790027
                        ' SKU: '   ADJADD-SKU-NBR                       08800027
                        ' UPC: '   ADJADD-UPC-NBR                       08810027
                        ' RTL: '   ADJADD-ADJ-UNIT-PR-AMT               08820027
                        ' DKEY: '  ADJADD-DKEY-NBR                      08830027
                MOVE 'E500-INSERT-PRCS-NOR-ADD ' TO PV-PARAGRAPH-NAME   08840027
                MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED   08850027
                PERFORM 9999-SQL-ABEND                                  08860027
              END-IF                                                    08861030
P54554      END-IF.                                                     08862040
      *                                                                 08880000
      ***************************************************************** 08890000
      * E600-INSERT-PRCS-NOR-DEL.                                       08900000
      *  TO INSERT MASS DELETE ADJUSTMENTS TO TABLE TADJADD             08910000
      ***************************************************************** 08920000
       E600-INSERT-PRCS-NOR-DEL.                                        08930000
                                                                        08940000
           INITIALIZE IN400-PV-INPUT-AREA.                              08950000
                                                                        08960000
      *** GENERATE A RANDOM NUMBER FOR THE DKEY.....                    08970000
           ADD 11                           TO PV-ADD-NUM.              08980000
                                                                        08990000
           ACCEPT PV-TODAYS-TIME    FROM TIME.                          09000000
           COMPUTE PV-TODAYS-TIME   = (PV-TODAYS-DATE                   09010000
                                    + PV-ADD-NUM)                       09020000
                                    * 11.                               09030000
           MOVE PV-HUND-SEC                 TO PV-SEED-BYTE-1-2.        09040000
           MOVE PV-SEC                      TO PV-SEED-BYTE-3-4.        09050000
           MOVE PV-MIN                      TO PV-SEED-BYTE-5-6.        09060000
           MOVE PV-HH                       TO PV-SEED-BYTE-7-8.        09070000
           MOVE PV-INITIAL-SEED             TO PV-SEED.                 09080000
                                                                        09090000
           COMPUTE PV-WORK-FIELD = ((PV-SEED + PC-PRIME1)               09100000
                                 * PC-PRIME2).                          09110000
                                                                        09120000
           DIVIDE PV-WORK-FIELD BY  PC-ONE-BILLION                      09130000
               GIVING PV-WORK-FIELD2                                    09140000
               REMAINDER PV-RANDOM-NUMBER.                              09150000
                                                                        09160000
           MOVE PV-RANDOM-NUMBER            TO ADJADD-DKEY-NBR.         09170000
                                                                        09180000
      *  DETERMINE RETAIL                                               09190000
                                                                        09200000
           MOVE PC010-STR-NBR               TO PV-INPUT-LOC             09210000
                                               ADJADD-LOC-NBR           09220000
           MOVE PV-INPUT-LOC                TO PV-HOLD-LOC-NBR          09230000
                                               IN400-PV-LOC-IN          09240000
           MOVE INVPAR-PLND-INV-DTE         TO PV-HOLD-KEY-DATE         09250000
                                               IN400-PV-EFF-DTE-IN      09260000
           IF PC010-INTR-UPC-ID > 0                                     09270000
               MOVE PC010-INTR-UPC-ID   TO IN-INTR-UPC-ID               09280000
               MOVE IN-INTR-UPC-ID-SPC  TO IN400-PV-UPC-ID-SPC          09290000
               SET IN400-PV-INTR-UPC-ID-LOOKUP TO TRUE                  09300000
           ELSE                                                         09310000
               IF PC010-UPC-NBR > 0                                     09320000
                  MOVE PC010-UPC-NBR    TO IN400-PV-UPC-NBR-IN          09330000
                  SET IN400-PV-UPC-NBR-LOOKUP                           09340000
                                        TO TRUE                         09350000
               END-IF                                                   09360000
           END-IF                                                       09370000
                                                                        09380000
           PERFORM IN400-LOOKUP-RETAIL                                  09390000
                                                                        09400000
      *** MOVE THE VALID RECORD INFORMATION TO TADJADD...               09410000
           EVALUATE TRUE                                                09420000
      ***     PRICE FOUND - RENUMBER SKU/UPC.......                     09430000
               WHEN IN400-PS-RENUMBER-SKU                               09440000
                    MOVE PC010-STR-NBR         TO ADJADD-LOC-NBR        09450000
                    MOVE PV-CURRENT-AREA-NBR   TO ADJADD-AREA-NBR       09460000
                    MOVE IN400-PRC-SKU-OUT     TO ADJADD-SKU-NBR        09470000
                    MOVE IN400-PRC-UPC-OUT     TO ADJADD-UPC-NBR        09480000
                    MOVE SPACE                 TO ADJADD-MISSED-MD-IND  09490000
                    MOVE 'INKUP308'            TO ADJADD-CHG-ID-NBR     09500003
                    MOVE PV-DESCRIPTION        TO ADJADD-ADJ-RSN-TXT    09510000
      ***     PRICE FOUND - DUMMY SKU/UPC....                           09520000
               WHEN IN400-PS-DUMMY-SKU                                  09530000
                    MOVE PC010-STR-NBR         TO ADJADD-LOC-NBR        09540000
                    MOVE PV-CURRENT-AREA-NBR   TO ADJADD-AREA-NBR       09550000
                                                                        09560000
                    MOVE IN400-ATT-SKU-OUT     TO ADJADD-SKU-NBR        09570000
                    MOVE IN400-ATT-UPC-OUT     TO ADJADD-UPC-NBR        09580000
                    MOVE SPACE                 TO ADJADD-MISSED-MD-IND  09590000
                    MOVE 'INKUP308'            TO ADJADD-CHG-ID-NBR     09600003
                    MOVE PV-DESCRIPTION        TO ADJADD-ADJ-RSN-TXT    09610000
      ***     PRICE FOUND - NORMAL....                                  09620000
               WHEN IN400-PS-RTL-FOUND                                  09630000
                    MOVE PC010-STR-NBR       TO ADJADD-LOC-NBR          09640000
                    MOVE PV-CURRENT-AREA-NBR TO ADJADD-AREA-NBR         09650000
                    MOVE IN400-PRC-SKU-OUT   TO ADJADD-SKU-NBR          09660000
                    MOVE PC010-UPC-NBR       TO ADJADD-UPC-NBR          09670000
                    MOVE SPACE               TO ADJADD-MISSED-MD-IND    09680000
                    MOVE 'INKUP308'          TO ADJADD-CHG-ID-NBR       09690003
                    MOVE PV-DESCRIPTION      TO ADJADD-ADJ-RSN-TXT      09700000
      ***     INVALID CONDITION OCCURRED DURING THE PRICE LOOKUP....    09710000
               WHEN IN400-PV-INVALID-KEY-INPUT                          09720000
                    DISPLAY ' ERROR INTERNAL UPC :' PC010-INTR-UPC-ID   09730000
                    DISPLAY 'SKU  :  ' IN400-TEMP-UPC-SKU               09740000
                    DISPLAY 'UPC  :  ' PC010-UPC-NBR                    09750000
                    DISPLAY ' STORE: ' ADJADD-LOC-NBR                   09760000
                            ' AREA: '  ADJADD-AREA-NBR                  09770000
                            ' SKU: '   ADJADD-SKU-NBR                   09780000
                            ' UPC: '   ADJADD-UPC-NBR                   09790000
                            ' RTL: '   ADJADD-ADJ-UNIT-PR-AMT           09800000
                            ' DKEY: '  ADJADD-DKEY-NBR                  09810000
                    DISPLAY IN400-PV-PARAGRAPH                          09820000
                    MOVE 'E600-INSERT-PRCS-NOR-DEL'                     09830000
                                             TO PV-PARAGRAPH-NAME       09840000
                    MOVE IN400-PV-RETURN-RSN TO PV-OPERATION-ATTEMPTED  09850000
                    PERFORM 9999-SQL-ABEND                              09860000
           END-EVALUATE.                                                09870000
                                                                        09880000
           MOVE IN400-PRC-UNIT-RTL-OUT      TO ADJADD-ADJ-UNIT-PR-AMT.  09890000
           MOVE PC010-IN-TRNST-QTY          TO ADJADD-ADJ-QTY.          09900000
                                                                        09910000
           COMPUTE ADJADD-ADJ-QTY = ADJADD-ADJ-QTY * -1.                09920000
                                                                        09930000
           COMPUTE ADJADD-ADJ-EXTD-AMT = ADJADD-ADJ-QTY                 09940000
                                       * ADJADD-ADJ-UNIT-PR-AMT.        09950000
                                                                        09960000
      *** INSERT INTO TADJADD....                                       09970000
           MOVE ZERO                        TO PV-RETRY-COUNT.          09980027
           SET PS-INSERT-NOT-DONE           TO TRUE.                    09990027
                                                                        10000000
      *** CHECK RECORD COUNT WHILE RUNNING IN RESTART MODE              10010000
           PERFORM F100-INSERT-TADJADD-ROW                              10020027
               UNTIL PS-INSERT-DONE                                     10030027
                  OR PV-RETRY-COUNT > PC-MAX-RETRIES                    10040027
                                                                        10050000
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          10060027
                DISPLAY 'INTRNAL UPC :  ' PC010-INTR-UPC-ID             10070027
                DISPLAY 'SKU  :  ' IN400-TEMP-UPC-SKU                   10080027
                DISPLAY 'UPC  :  ' PC010-UPC-NBR                        10090027
                DISPLAY ' STORE: ' ADJADD-LOC-NBR                       10100027
                        ' AREA: '  ADJADD-AREA-NBR                      10110027
                        ' SKU: '   ADJADD-SKU-NBR                       10120027
                        ' UPC: '   ADJADD-UPC-NBR                       10130030
                        ' RTL: '   ADJADD-ADJ-UNIT-PR-AMT               10140027
                        ' DKEY: '  ADJADD-DKEY-NBR                      10150027
                MOVE 'E600-INSERT-PRCS-NOR-DEL ' TO PV-PARAGRAPH-NAME   10160027
555908          MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED   10170027
                PERFORM 9999-SQL-ABEND                                  10180027
           END-IF.                                                      10190027
      *                                                                 10200000
      ***************************************************************** 10210000
      * E700-INSERT-PRCS-RTV-ADD.                                       10220000
      * TO INSERT MASS ADD ADJUSTMENTS WITH RETAIL TO TABLE TADJADD     10230000
      ***************************************************************** 10240000
       E700-INSERT-PRCS-RTV-ADD.                                        10250000
                                                                        10260000
      *** GENERATE A RANDOM NUMBER FOR THE DKEY.....                    10270000
           ADD 11                           TO PV-ADD-NUM.              10280000
                                                                        10290000
           ACCEPT PV-TODAYS-TIME    FROM TIME.                          10300000
           COMPUTE PV-TODAYS-TIME   = (PV-TODAYS-DATE                   10310000
                                    + PV-ADD-NUM)                       10320000
                                    * 11.                               10330000
           MOVE PV-HUND-SEC                 TO PV-SEED-BYTE-1-2.        10340000
           MOVE PV-SEC                      TO PV-SEED-BYTE-3-4.        10350000
           MOVE PV-MIN                      TO PV-SEED-BYTE-5-6.        10360000
           MOVE PV-HH                       TO PV-SEED-BYTE-7-8.        10370000
           MOVE PV-INITIAL-SEED             TO PV-SEED.                 10380000
                                                                        10390000
           COMPUTE PV-WORK-FIELD = ((PV-SEED + PC-PRIME1)               10400000
                                 * PC-PRIME2).                          10410000
                                                                        10420000
           DIVIDE PV-WORK-FIELD BY  PC-ONE-BILLION                      10430000
               GIVING PV-WORK-FIELD2                                    10440000
               REMAINDER PV-RANDOM-NUMBER.                              10450000
                                                                        10460000
           MOVE PV-RANDOM-NUMBER            TO ADJADD-DKEY-NBR.         10470000
                                                                        10480000
28543      MOVE IN309-LOC-NBR               TO ADJADD-LOC-NBR.          10490000
26342      MOVE PV-CURRENT-AREA-NBR         TO ADJADD-AREA-NBR          10500000
           MOVE IN309-SKU-NBR               TO ADJADD-SKU-NBR.          10510000
25248      MOVE IN309-UPC-CODE              TO ADJADD-UPC-NBR.          10520000
           MOVE SPACE                       TO ADJADD-MISSED-MD-IND.    10530000
           MOVE 'INKUP308'                  TO ADJADD-CHG-ID-NBR.       10540003
           MOVE IN309-PRICE                 TO ADJADD-ADJ-UNIT-PR-AMT.  10550000
           MOVE IN309-QTY                   TO ADJADD-ADJ-QTY.          10560000
           MOVE PV-DESCRIPTION              TO ADJADD-ADJ-RSN-TXT.      10570000
                                                                        10580000
           COMPUTE ADJADD-ADJ-EXTD-AMT = ADJADD-ADJ-QTY                 10590000
                                       * ADJADD-ADJ-UNIT-PR-AMT.        10600000
                                                                        10610000
      *** INSERT INTO TADJADD....                                       10620000
           MOVE ZERO                        TO PV-RETRY-COUNT.          10630027
           SET PS-INSERT-NOT-DONE           TO TRUE.                    10640027
                                                                        10650000
           PERFORM F200-INSERT-MASSADD-RTV-ROW.                         10660027
      *                                                                 10670000
      ***************************************************************** 10680000
      * E800-INSERT-PRCS-RTV-DEL.                                       10690000
      * TO INSERT MASS DEL ADJUSTMENTS WITH RETAIL TO TABLE TADJADD     10700000
      ***************************************************************** 10710000
       E800-INSERT-PRCS-RTV-DEL.                                        10720000
                                                                        10730000
      *** GENERATE A RANDOM NUMBER FOR THE DKEY.....                    10740000
           ADD 11                           TO PV-ADD-NUM.              10750000
                                                                        10760000
           ACCEPT PV-TODAYS-TIME    FROM TIME.                          10770000
           COMPUTE PV-TODAYS-TIME   = (PV-TODAYS-DATE                   10780000
                                    + PV-ADD-NUM)                       10790000
                                    * 11.                               10800000
           MOVE PV-HUND-SEC                 TO PV-SEED-BYTE-1-2.        10810000
           MOVE PV-SEC                      TO PV-SEED-BYTE-3-4.        10820000
           MOVE PV-MIN                      TO PV-SEED-BYTE-5-6.        10830000
           MOVE PV-HH                       TO PV-SEED-BYTE-7-8.        10840000
           MOVE PV-INITIAL-SEED             TO PV-SEED.                 10850000
                                                                        10860000
           COMPUTE PV-WORK-FIELD = ((PV-SEED + PC-PRIME1)               10870000
                                 * PC-PRIME2).                          10880000
                                                                        10890000
           DIVIDE PV-WORK-FIELD BY  PC-ONE-BILLION                      10900000
               GIVING PV-WORK-FIELD2                                    10910000
               REMAINDER PV-RANDOM-NUMBER.                              10920000
                                                                        10930000
           MOVE PV-RANDOM-NUMBER            TO ADJADD-DKEY-NBR.         10940000
                                                                        10950000
      *** MOVE THE VALID RECORD INFORMATION TO TADJADD...               10960000
           MOVE IN309-LOC-NBR               TO ADJADD-LOC-NBR           10970000
           MOVE PV-CURRENT-AREA-NBR         TO ADJADD-AREA-NBR          10980000
           MOVE IN309-SKU-NBR               TO ADJADD-SKU-NBR.          10990000
           MOVE IN309-UPC-CODE              TO ADJADD-UPC-NBR.          11000000
           MOVE SPACE                       TO ADJADD-MISSED-MD-IND.    11010000
           MOVE 'INKUP308'                  TO ADJADD-CHG-ID-NBR.       11020003
           MOVE IN309-PRICE                 TO ADJADD-ADJ-UNIT-PR-AMT.  11030000
           MOVE PV-DESCRIPTION              TO ADJADD-ADJ-RSN-TXT.      11040000
                                                                        11050000
           COMPUTE PV-ADJ-QTY  = IN309-QTY * -1.                        11060000
           MOVE PV-ADJ-QTY                  TO ADJADD-ADJ-QTY.          11070000
                                                                        11080000
           COMPUTE ADJADD-ADJ-EXTD-AMT = ADJADD-ADJ-QTY                 11090000
                                       * ADJADD-ADJ-UNIT-PR-AMT.        11100000
                                                                        11110000
      *** INSERT INTO TADJADD....                                       11120000
           MOVE ZERO                        TO PV-RETRY-COUNT.          11130027
           SET PS-INSERT-NOT-DONE           TO TRUE.                    11140027
                                                                        11150000
           PERFORM F300-INSERT-MASSDEL-RTV-ROW.                         11160027
      *                                                                 11170000
      ***************************************************************** 11180000
      * E900-SELECT-ACT-INV-DT.                                         11190000
      * SELECT ACTUAL INVENTORY DATE FROM TINVPAR                       11200000
      ***************************************************************** 11210000
       E900-SELECT-ACT-INV-DT.                                          11220000
                                                                        11230000
      *    INITIALIZE DCLTINVPAR.                                       11240000
           EXEC SQL                                                     11250000
               SELECT ACTL_UNIT_BK_DTE                                  11260000
                     ,MAX(PLND_INV_DTE)                                 11270000
               INTO :INVPAR-ACTL-UNIT-BK-DTE                            11280000
                   ,:INVPAR-PLND-INV-DTE                                11290000
               FROM TINVPAR A,                                          11300000
                    TINCNTL B                                           11310000
               WHERE B.ACTV_IND = 'Y'                                   11320000
                 AND A.INV_ID = B.INV_ID                                11330000
                 AND LOC_NBR = :INVPAR-LOC-NBR                          11340000
                 AND LOC_INV_STAT_CDE = 'IN'                            11350000
                 AND ACTL_FIN_BK_DTE = '9999-09-09'                     11360000
                 GROUP BY LOC_NBR                                       11370000
                        , ACTL_UNIT_BK_DTE                              11380000
                        , PLND_INV_DTE                                  11390000
           END-EXEC.                                                    11400000
                                                                        11410000
           EVALUATE TRUE                                                11420000
               WHEN SQLCODE = ZERO                                      11430000
                  CONTINUE                                              11440000
               WHEN SQLCODE = +100                                      11450000
                  MOVE '9999-09-09'         TO INVPAR-ACTL-UNIT-BK-DTE  11460000
               WHEN SQLCODE = -904                                      11470000
               WHEN SQLCODE = -913                                      11480000
                  MOVE 'E900-SELECT-ACT-INV-DT'                         11490000
                                            TO PV-PARAGRAPH-NAME        11500000
                  MOVE 'SELECT TINVPAR'     TO PV-OPERATION-ATTEMPTED   11510000
                  PERFORM 9999-SQL-ABEND                                11520000
           END-EVALUATE.                                                11530000
                                                                        11540000
      *                                                                 11550000
      ***************************************************************** 11560000
      * E110-REFRMT-NOR-REC-FS.                                         11570000
      * HERE REFORMAT THE ADJUSTMENT RECORD FOR FASHION SALES .         11580000
      ***************************************************************** 11590000
       E110-REFRMT-NOR-REC-FS.                                          11600000
                                                                        11610000
           MOVE IN400-ATT-SKU-OUT           TO PV-SKU-NBR               11620000
                                               PV-SKU-NUMBER.           11630000
           IF (IN400-PS-DUMMY-SKU) OR                                   11640000
              (PV-SKU-NBR-ALPH (6:3) IS EQUAL TO '000' OR '999')        11650000
               MOVE ZEROES                  TO FS035-ITM-NBR            11660000
           ELSE                                                         11670000
               MOVE IN400-ATT-SKU-OUT       TO PV-SKU-NBR               11680000
                                               PV-SKU-NUMBER            11690000
           END-IF.                                                      11700000
                                                                        11710000
           IF IN400-ATT-ITM-NBR-OUT > 0                                 11720000
               MOVE IN400-ATT-ITM-NBR-OUT   TO FS035-ITM-NBR            11730000
           ELSE                                                         11740000
               MOVE ZEROES                  TO FS035-ITM-NBR            11750000
           END-IF.                                                      11760000
                                                                        11770000
           MOVE 'INKUP308'                  TO FS035-ADJ-SRC-PGM-NM.    11780003
           MOVE PC010-STR-NBR               TO FS035-LOC-NBR.           11790000
           MOVE '123'                       TO FS035-ADJ-TYP.           11800000
                                                                        11810000
           MOVE PC010-IN-TRNST-QTY          TO FS035-ADJ-QTY.           11820000
           MOVE 'INOL'                      TO FS035-BTCH-NBR.          11830000
                                                                        11840000
           IF INVPAR-ACTL-UNIT-BK-DTE IS EQUAL TO '9999-09-09'          11850000
               MOVE 'N'                     TO FS035-UNT-BKG-IND        11860000
           ELSE                                                         11870000
               MOVE 'Y'                     TO FS035-UNT-BKG-IND.       11880000
                                                                        11890000
           PERFORM Z500-WRITE-FSSALE-FL.                                11900000
      *                                                                 11910000
      ***************************************************************** 11920000
      * E120-REFRMT-RTV-FS-REC                                          11930000
      * HERE REFORMAT THE ADJUSTMENT RECORD WITH RETAIL FOR             11940000
      * FASHION SALES                                                   11950000
      ***************************************************************** 11960000
       E120-REFRMT-RTV-FS-REC.                                          11970000
                                                                        11980000
           MOVE IN309-SKU-NBR               TO PV-SKU-NBR               11990000
                                       PV-SKU-NUMBER.                   12000000
           IF PV-SKU-NBR-ALPH (6:3) IS EQUAL TO '000' OR '999'          12010000
               MOVE ZEROES                  TO FS035-ITM-NBR            12020000
           ELSE                                                         12030000
28543          MOVE IN309-SKU-NBR           TO PV-SKU-NBR               12040000
                                               PV-SKU-NUMBER            12050000
               MOVE IN309-LOC-NBR           TO PV-INPUT-LOC             12060000
               PERFORM F400-GET-ITEM-NBR-RTV                            12070000
           END-IF.                                                      12080000
                                                                        12090000
           MOVE 'INKUP308'                  TO FS035-ADJ-SRC-PGM-NM.    12100003
           MOVE IN309-LOC-NBR               TO FS035-LOC-NBR.           12110000
           MOVE '123'                       TO FS035-ADJ-TYP.           12120000
                                                                        12130000
           MOVE IN309-QTY                   TO FS035-ADJ-QTY.           12140000
           MOVE 'INOL'                      TO FS035-BTCH-NBR.          12150000
                                                                        12160000
           IF INVPAR-ACTL-UNIT-BK-DTE IS EQUAL TO '9999-09-09'          12170000
               MOVE 'N'                     TO FS035-UNT-BKG-IND        12180000
           ELSE                                                         12190000
               MOVE 'Y'                     TO FS035-UNT-BKG-IND        12200000
           END-IF.                                                      12210000
                                                                        12220000
           PERFORM Z500-WRITE-FSSALE-FL.                                12230000
      *                                                                 12240000
      ***************************************************************** 12250000
      * F100-INSERT-TADJADD-ROW                                         12260000
      * INSERT THE ADJUSTMENT RECORD W/O RETAIL INTO TADJADD TABLE      12270000
      ***************************************************************** 12280000
       F100-INSERT-TADJADD-ROW.                                         12290027
               EXEC SQL                                                 12300027
                    INSERT                                              12310027
                    INTO  TADJADD                                       12320027
                         (LOC_NBR                                       12330027
                        , AREA_NBR                                      12340027
                        , SKU_NBR                                       12350027
                        , UPC_NBR                                       12360027
                        , ADJ_UNIT_PR_AMT                               12370027
                        , ADJ_QTY                                       12380027
                        , MISSED_MD_IND                                 12390027
                        , DKEY_NBR                                      12400027
                        , CHG_TMST                                      12410027
                        , CHG_ID_NBR                                    12420027
                        , ADJ_RSN_TXT                                   12430027
                        , ADJ_EXTD_AMT)                                 12440027
                   VALUES                                               12450027
                         (:ADJADD-LOC-NBR                               12460027
                        , :ADJADD-AREA-NBR                              12470027
                        , :ADJADD-SKU-NBR                               12480027
                        , :ADJADD-UPC-NBR                               12490027
                        , :ADJADD-ADJ-UNIT-PR-AMT                       12500027
                        , :ADJADD-ADJ-QTY                               12510027
                        , :ADJADD-MISSED-MD-IND                         12520027
                        , :ADJADD-DKEY-NBR                              12530027
                        ,  CURRENT TIMESTAMP                            12540027
                        , :ADJADD-CHG-ID-NBR                            12550027
                        , :ADJADD-ADJ-RSN-TXT                           12560027
                        , :ADJADD-ADJ-EXTD-AMT)                         12570027
               END-EXEC                                                 12580027
                                                                        12590000
               EVALUATE TRUE                                            12600027
                   WHEN SQLCODE = ZERO                                  12610027
                       SET PS-INSERT-DONE   TO TRUE                     12620027
                       IF PV-MASSADD                                    12630027
                          ADD 1             TO PV-TADJADD-ADD-COUNT     12640027
                       ELSE                                             12650027
                          ADD 1             TO PV-TADJADD-DEL-COUNT     12660027
                       END-IF                                           12670027
                   WHEN SQLCODE = -803                                  12680027
                       ADD 1 TO PV-RETRY-COUNT                          12690027
                       COMPUTE PV-RANDOM-NUMBER =                       12700027
                                PV-RANDOM-NUMBER *                      12710027
                               (11 * PV-ADD-NUM)                        12720027
                       ADD 11                TO PV-ADD-NUM              12730027
                       MOVE PV-RANDOM-NUMBER TO ADJADD-DKEY-NBR         12740027
                   WHEN SQLCODE = -904                                  12750027
                   WHEN SQLCODE = -911                                  12760027
                   WHEN SQLCODE = -913                                  12770027
                        ADD 1 TO PV-RETRY-COUNT                         12780027
                   WHEN OTHER                                           12790027
                      DISPLAY 'SKU  :  ' ADJADD-SKU-NBR                 12800027
                      DISPLAY 'UPC  :  ' PC010-UPC-NBR                  12810027
                      MOVE PV-RANDOM-NUMBER  TO PV-DISPLAY              12820027
                      DISPLAY 'DKEY :  ' PV-DISPLAY                     12830027
                      MOVE 'F100-INSERT-TADJADD-ROW'                    12840027
                                             TO PV-PARAGRAPH-NAME       12850027
                      MOVE PE-MSG-03                                    12860027
                                             TO PV-OPERATION-ATTEMPTED  12870027
                      PERFORM 9999-SQL-ABEND                            12880027
               END-EVALUATE.                                            12890027
      *                                                                 12900000
      ***************************************************************** 12910000
      * F200-INSERT-MASSADD-RTV-ROW                                     12920000
      * INSERT A POSITVE ADJUSTMENT WITH RETAIL INTO TADJADD            12930000
      ***************************************************************** 12940000
       F200-INSERT-MASSADD-RTV-ROW.                                     12950027
                                                                        12960000
           PERFORM UNTIL PS-INSERT-DONE                                 12970027
                      OR PV-RETRY-COUNT > PC-MAX-RETRIES                12980027
               EXEC SQL                                                 12990027
                    INSERT                                              13000027
                    INTO  TADJADD                                       13010027
                         (LOC_NBR                                       13020027
                        , AREA_NBR                                      13030027
                        , SKU_NBR                                       13040027
                        , UPC_NBR                                       13050027
                        , ADJ_UNIT_PR_AMT                               13060027
                        , ADJ_QTY                                       13070027
                        , MISSED_MD_IND                                 13080027
                        , DKEY_NBR                                      13090027
                        , CHG_TMST                                      13100027
                        , CHG_ID_NBR                                    13110027
                        , ADJ_RSN_TXT                                   13120027
                        , ADJ_EXTD_AMT)                                 13130027
                   VALUES                                               13140027
                         (:ADJADD-LOC-NBR                               13150027
                        , :ADJADD-AREA-NBR                              13160027
                        , :ADJADD-SKU-NBR                               13170027
                        , :ADJADD-UPC-NBR                               13180027
                        , :ADJADD-ADJ-UNIT-PR-AMT                       13190027
                        , :ADJADD-ADJ-QTY                               13200027
                        , :ADJADD-MISSED-MD-IND                         13210027
                        , :ADJADD-DKEY-NBR                              13220027
                        ,  CURRENT TIMESTAMP                            13230027
                        , :ADJADD-CHG-ID-NBR                            13240027
                        , :ADJADD-ADJ-RSN-TXT                           13250027
                        , :ADJADD-ADJ-EXTD-AMT)                         13260027
               END-EXEC                                                 13270027
                                                                        13280000
               EVALUATE TRUE                                            13290027
                   WHEN SQLCODE = ZERO                                  13300027
                       SET PS-INSERT-DONE   TO TRUE                     13310027
                       ADD 1                TO PV-TADJADD-POS-COUNT     13320027
                   WHEN SQLCODE = -803                                  13330027
                       ADD 1 TO PV-RETRY-COUNT                          13340027
                       COMPUTE PV-RANDOM-NUMBER =                       13350027
                                PV-RANDOM-NUMBER *                      13360027
                               (11 * PV-ADD-NUM)                        13370027
                       ADD 11                TO PV-ADD-NUM              13380027
                       MOVE PV-RANDOM-NUMBER TO ADJADD-DKEY-NBR         13390027
                   WHEN SQLCODE = +100                                  13400027
                        SET PS-INSERT-DONE   TO TRUE                    13410027
                   WHEN SQLWARN0 NOT = SPACES                           13420027
                   WHEN SQLCODE < ZERO                                  13430027
                   WHEN SQLCODE > ZERO                                  13440027
                     DISPLAY 'SKU  :  ' IN309-SKU-NBR                   13450027
                     DISPLAY 'UPC  :  ' IN309-UPC-CODE                  13460027
                      MOVE 'F200-INSERT-MASSADD-RTV-ROW'                13470027
                                            TO PV-PARAGRAPH-NAME        13480027
                      MOVE 'INSERT INTO TADJADD'                        13490027
                                            TO PV-OPERATION-ATTEMPTED   13500027
                      PERFORM 9999-SQL-ABEND                            13510027
               END-EVALUATE                                             13520027
           END-PERFORM.                                                 13530027
                                                                        13540000
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          13550027
                DISPLAY 'SKU  :  ' IN309-SKU-NBR                        13560027
                DISPLAY 'UPC  :  ' IN309-UPC-CODE                       13570027
                DISPLAY ' STORE: ' ADJADD-LOC-NBR                       13580027
                        ' AREA: '  ADJADD-AREA-NBR                      13590027
                        ' SKU: '   ADJADD-SKU-NBR                       13600027
                        ' UPC: '   ADJADD-UPC-NBR                       13610027
                        ' RTL: '   ADJADD-ADJ-UNIT-PR-AMT               13620027
                        ' DKEY: '  ADJADD-DKEY-NBR                      13630027
                MOVE 'F200-INSERT-MASSADD-RTV-ROW'                      13640027
                                            TO PV-PARAGRAPH-NAME        13650027
                MOVE 'MAX RETRIES EXCEEDED ON TADJADD TABLE'            13660027
                                            TO PV-OPERATION-ATTEMPTED   13670027
                PERFORM 9999-SQL-ABEND                                  13680027
           END-IF.                                                      13690027
      *                                                                 13700000
      ***************************************************************** 13710000
      * F300-INSERT-MASSDEL-RTV-ROW                                     13720000
      * INSERT A NEGATIVE ADJUSTMENT WITH RETAIL INTO TADJADD           13730000
      ***************************************************************** 13740000
       F300-INSERT-MASSDEL-RTV-ROW.                                     13750027
                                                                        13760000
           PERFORM UNTIL PS-INSERT-DONE                                 13770027
                      OR PV-RETRY-COUNT > PC-MAX-RETRIES                13780027
               EXEC SQL                                                 13790027
                    INSERT                                              13800027
                    INTO  TADJADD                                       13810027
                         (LOC_NBR                                       13820027
                        , AREA_NBR                                      13830027
                        , SKU_NBR                                       13840027
                        , UPC_NBR                                       13850027
                        , ADJ_UNIT_PR_AMT                               13860027
                        , ADJ_QTY                                       13870027
                        , MISSED_MD_IND                                 13880027
                        , DKEY_NBR                                      13890027
                        , CHG_TMST                                      13900027
                        , CHG_ID_NBR                                    13910027
                        , ADJ_RSN_TXT                                   13920027
                        , ADJ_EXTD_AMT)                                 13930027
                   VALUES                                               13940027
                         (:ADJADD-LOC-NBR                               13950027
                        , :ADJADD-AREA-NBR                              13960027
                        , :ADJADD-SKU-NBR                               13970027
                        , :ADJADD-UPC-NBR                               13980027
                        , :ADJADD-ADJ-UNIT-PR-AMT                       13990027
                        , :ADJADD-ADJ-QTY                               14000027
                        , :ADJADD-MISSED-MD-IND                         14010027
                        , :ADJADD-DKEY-NBR                              14020027
                        ,  CURRENT TIMESTAMP                            14030027
                        , :ADJADD-CHG-ID-NBR                            14040027
                        , :ADJADD-ADJ-RSN-TXT                           14050027
                        , :ADJADD-ADJ-EXTD-AMT)                         14060027
               END-EXEC                                                 14070027
                                                                        14080000
               EVALUATE TRUE                                            14090027
                   WHEN SQLCODE = ZERO                                  14100027
                       SET PS-INSERT-DONE   TO TRUE                     14110027
                       ADD 1                TO PV-TADJADD-NEG-COUNT     14120027
                   WHEN SQLCODE = -803                                  14130027
                       ADD 1                TO PV-RETRY-COUNT           14140027
                       COMPUTE PV-RANDOM-NUMBER =                       14150027
                                PV-RANDOM-NUMBER *                      14160027
                               (11 * PV-ADD-NUM)                        14170027
                       ADD 11                TO PV-ADD-NUM              14180027
                       MOVE PV-RANDOM-NUMBER TO ADJADD-DKEY-NBR         14190027
                   WHEN SQLCODE = +100                                  14200027
                        SET PS-INSERT-DONE   TO TRUE                    14210027
                   WHEN SQLCODE = -904                                  14220027
                   WHEN SQLCODE = -911                                  14230027
                   WHEN SQLCODE = -913                                  14240027
                        ADD 1                TO PV-RETRY-COUNT          14250027
                   WHEN SQLWARN0 NOT = SPACES                           14260027
                   WHEN SQLCODE < ZERO                                  14270027
                   WHEN SQLCODE > ZERO                                  14280027
                     DISPLAY 'SKU  :  ' IN309-SKU-NBR                   14290027
                     DISPLAY 'UPC  :  ' IN309-UPC-CODE                  14300027
                     MOVE PV-RANDOM-NUMBER  TO PV-DISPLAY               14310027
                      DISPLAY 'DKEY :  ' PV-DISPLAY                     14320027
                      MOVE 'F300-INSERT-MASSDEL-RTV-ROW'                14330027
                                            TO PV-PARAGRAPH-NAME        14340027
                      MOVE 'INSERT INTO TADJADD'                        14350027
                                            TO PV-OPERATION-ATTEMPTED   14360027
                      PERFORM 9999-SQL-ABEND                            14370027
               END-EVALUATE                                             14380027
           END-PERFORM.                                                 14390027
                                                                        14400000
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          14410027
                DISPLAY 'SKU  :  ' IN309-SKU-NBR                        14420027
                DISPLAY 'UPC  :  ' IN309-UPC-CODE                       14430027
                DISPLAY ' STORE: ' ADJADD-LOC-NBR                       14440027
                        ' AREA: '  ADJADD-AREA-NBR                      14450027
                        ' SKU: '   ADJADD-SKU-NBR                       14460027
                        ' UPC: '   ADJADD-UPC-NBR                       14470027
                        ' RTL: '   ADJADD-ADJ-UNIT-PR-AMT               14480027
                        ' DKEY: '  ADJADD-DKEY-NBR                      14490027
                MOVE 'F300-INSERT-MASSDEL-RTV-ROW'                      14500027
                                            TO PV-PARAGRAPH-NAME        14510027
                MOVE 'MAX RETRIES EXCEEDED ON TADJADD TABLE'            14520027
                                            TO PV-OPERATION-ATTEMPTED   14530027
                PERFORM 9999-SQL-ABEND                                  14540027
           END-IF.                                                      14550027
      *                                                                 14560000
      ***************************************************************** 14570000
      * F400-GET-ITEM-NBR-RTV.                                          14580000
      * GET THE ITEM NUMBER                                             14590000
      ***************************************************************** 14600000
       F400-GET-ITEM-NBR-RTV.                                           14610000
                                                                        14620000
           INITIALIZE IN400-PV-INPUT-AREA.                              14630000
                                                                        14640000
           SET IN400-PV-SKU-ATTRIB-LOOK     TO TRUE.                    14650000
           MOVE PV-INPUT-LOC                TO IN400-CONVERT-LOC.       14660000
           MOVE IN400-CONVERT-LOC           TO IN400-PV-LOC-IN.         14670000
                                                                        14680000
           MOVE INVPAR-PLND-INV-DTE         TO IN400-PV-EFF-DTE-IN.     14690000
           MOVE PV-SKU-NBR                  TO IN400-PV-SKU-IN.         14700000
                                                                        14710000
           PERFORM IN400-LOOKUP-RETAIL.                                 14720000
                                                                        14730000
           IF IN400-PV-ATT-FOUND                                        14740000
               MOVE IN400-ATT-ITM-NBR-OUT   TO FS035-ITM-NBR            14750000
           ELSE                                                         14760000
           IF IN400-PV-ATT-NOT-FOUND                                    14770000
               MOVE ZEROS                   TO FS035-ITM-NBR            14780000
           ELSE                                                         14790000
               DISPLAY 'SKU  :  ' PV-SKU-NBR                            14800000
               MOVE 'F400-GET-ITEM-NBR-RTV' TO PV-PARAGRAPH-NAME        14810000
               MOVE IN400-PV-RETURN-RSN     TO PV-OPERATION-ATTEMPTED   14820000
               PERFORM 9999-SQL-ABEND                                   14830000
           END-IF.                                                      14840000
      *                                                                 14850000
      ***************************************************************** 14860000
      * Z100-READ-MASS-ADDDEL-HEADER.                                   14870000
      *    GET THE HEADER RECORD FOR MASS ADD DELETE PROCESS            14880000
      ***************************************************************** 14890000
       Z100-READ-MASS-ADDDEL-HEADER.                                    14900000
                                                                        14910000
           READ MASS-ADDDEL-HEADER                                      14920000
               INTO PV-MASS-ADDDEL-HEADER                               14930000
                   AT END  SET PS-EOF-HEADER-FL  TO TRUE                14940000
           END-READ.                                                    14950000
P54554******************************************************************14951040
P54554* Z110-READ-MASS-ADDDEL-TRAILER.                                  14951140
P54554* READS THE TRAILER FOR MASS ADD/DELETE PROCESS                   14952040
P54554******************************************************************14953040
P54554 Z110-READ-MASS-ADDDEL-TRAILER.                                   14954040
P54554                                                                  14954140
P54554     READ MASS-ADDDEL-HEADER INTO PV-MASS-ADDDEL-TRAILER          14955040
P54554        AT END  SET PS-EOF-HEADER-FL  TO TRUE                     14956040
P54554     END-READ.                                                    14957040
P54554                                                                  14958040
P54554*                                                                 14959640
      ***************************************************************** 14960000
      * Z200-READ-MASS-ADDDEL-NOR.                                      14970000
      * READ ADJUSTMENT FILE FOR MASS ADD DELETE PROCESS(w/O RETAIL)    14980000
      ***************************************************************** 14990000
       Z200-READ-MASS-ADDDEL-NOR.                                       15000000
                                                                        15010000
           READ MASS-ADDDEL-INP                                         15020000
               INTO PC010-PCT-INTRANSIT-REC                             15030000
                   AT END SET PS-EOF-DETAIL-FL   TO TRUE.               15040000
      *                                                                 15050000
           IF PS-NOT-EOF-DETAIL-FL                                      15060000
              ADD 1                              TO PV-MA-READ-COUNT    15070000
           END-IF.                                                      15080000
      ***************************************************************** 15090000
      * Z300-READ-MASS-ADDDEL-RTV.                                      15100000
      * READ RECORDS ADJUSTMENT FILE WITH RETAIL FOR MASS ADD           15110000
      * DELETE PROCESS                                                  15120000
      ***************************************************************** 15130000
       Z300-READ-MASS-ADDDEL-RTV.                                       15140000
                                                                        15150000
           READ MASS-ADDDEL-INP                                         15160000
               INTO IN309-INVENTORY-INPUT-RECORD                        15170000
                   AT END  SET PS-EOF-DETAIL-FL      TO TRUE.           15180000
           IF PS-NOT-EOF-DETAIL-FL                                      15190000
              ADD 1                                  TO PV-MA-READ-COUNT15200000
           END-IF.                                                      15210000
      *                                                                 15220000
      ***************************************************************** 15230000
      * Z400-WRITE-REPORT-FL.                                           15240000
      * WRITE THE INVENTORY ADJUSTMENTS REPORT RECORD                   15250000
      ***************************************************************** 15260000
       Z400-WRITE-REPORT-FL.                                            15270000
                                                                        15280000
              WRITE REPORT-RECORD FROM PV-RPT-RECORD.                   15290000
      *                                                                 15300000
      ***************************************************************** 15310000
      * Z500-WRITE-FSSALE-FL.                                           15320000
      * WRITE THE INVENTORY ADJUSTMENTS FOR FASHION SALES               15330000
      ***************************************************************** 15340000
       Z500-WRITE-FSSALE-FL.                                            15350000
                                                                        15360000
           WRITE FASHION-ADJUSTMENTS-RECORD                             15370000
                                    FROM FS035-ADJUSTMENT-RECORD.       15380000
      *                                                                 15390000
P54554***************************************************************** 15390140
P54554* H100-SKU-FETCH-PARA.                                            15390240
P54554* FETCH THE SKU NBR FROM TSKXREF TABLE                            15390340
P54554***************************************************************** 15390440
P54554 H100-SKU-FETCH-PARA.                                             15390540
P54554                                                                  15390640
P54554         EXEC SQL                                                 15390740
P54554             SELECT SKU                                           15390840
P54554             INTO :SKXREF-SKU                                     15390940
P54554             FROM TSKXREF                                         15391040
P54554             WHERE INTR_UPC_ID = :PC010-INTR-UPC-ID               15391140
P54554               AND SKU_STAT_CDE <> '98'                           15391243
P54554             WITH UR                                              15391340
P54554         END-EXEC.                                                15391440
P54554                                                                  15391540
P54554       EVALUATE TRUE                                              15391640
P54554         WHEN SQLCODE = ZERO                                      15391740
P54554            MOVE SKXREF-SKU TO PV-SKUNUM                          15391840
P54554         WHEN OTHER                                               15391940
P54554            MOVE 'H100-SKU-FETCH-PARA'                            15392040
P54554                                      TO PV-PARAGRAPH-NAME        15392140
P54554            MOVE 'SELECT TSKXREF'     TO PV-OPERATION-ATTEMPTED   15392240
P54554            PERFORM 9999-SQL-ABEND                                15392340
P54554       END-EVALUATE.                                              15392440
P54554*                                                                 15392540
P54554***************************************************************** 15392640
P54554* Z600-WRITE-TRAILER-REC.                                         15392740
P54554* WRITE THE MANIPULATED TRAILER RECORD IN CASE OF ERR REC FOUND   15392840
P54554***************************************************************** 15392940
P54554 Z600-WRITE-TRAILER-REC.                                          15393040
P54554                                                                  15393140
P54554     WRITE MASS-ADDDEL-TRAILER-REC                                15393240
P54554                              FROM PV-MASS-ADDDEL-HEADER.         15393340
P54554     WRITE MASS-ADDDEL-TRAILER-REC                                15393440
P54554                              FROM PV-MASS-ADDDEL-TRAILER.        15393540
P54554*                                                                 15393640
P54554***************************************************************** 15393740
P54554*  WRITE THE HEADER FOR ERROR REPORT FILE                         15393840
P54554***************************************************************** 15393940
P54554 Y100-WRITE-HEADINGS.                                             15394040
P54554     WRITE ERROR-REPORT-FILE-RECORD FROM RPT-FILE-HD1.            15395040
P54554     WRITE ERROR-REPORT-FILE-RECORD FROM RPT-FILE-HD2.            15396040
P54554     WRITE ERROR-REPORT-FILE-RECORD FROM RPT-FILE-HD3.            15397040
P54554     WRITE ERROR-REPORT-FILE-RECORD FROM RPT-FILE-HD4.            15398040
P54554     WRITE ERROR-REPORT-FILE-RECORD FROM RPT-FILE-HD5.            15399040
      ******************************************************************15400000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               15410000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             15420000
      ******************************************************************15430000
       9999-SQL-ABEND.                                                  15440000
                                                                        15450000
           MOVE 4013      TO ABEND-CODE.                                15460000
           DISPLAY '**  ABEND     **'.                                  15470000
           DISPLAY '**  PROGRAM   **  = ' PV-PROGRAM-NAME.              15480000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            15490000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       15500000
                                                                        15510000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         15520000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        15530000
                                                                        15540000
           COPY DPPD004.                                                15550000
                                                                        15560000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           15570000
                                                                        15580000
      ******************************************************************15590000
      * DO THE NECESSARY PROCESSING TO ABEND FOR MISCELLANEOUS REASONS.*15600000
      ******************************************************************15610000
       9999-OTHER-ABEND.                                                15620000
           SET AC-OTHER-ERROR TO TRUE.                                  15630000
           DISPLAY '**  ABEND     **'.                                  15640000
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.              15650000
           DISPLAY '************************************************'.  15660000
           CALL 'ILBOABN0' USING ABEND-CODE.                            15670000
      ******************************************************************15680000
      *  PRICING MODULE.                                                15690000
      ******************************************************************15700000
           COPY INPD400.                                                15710000
