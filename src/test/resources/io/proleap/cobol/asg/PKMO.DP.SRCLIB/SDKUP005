       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    SDKUP005.                                                 
       AUTHOR.        KOHLS DEPARTMENT STORES.                                  
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  2002-04-03.                                               
       DATE-COMPILED.                                                           
      ******************************************************************        
      **  LOAD SDT_POS_LBR_STD TABLE.  THIS TABLE HOLDS THE LABOR               
      **  STANDARD AMOUNT USED FOR POS BASED ON STORE LOCATION (FREE-           
      **  STANDING, MALL OR STRIP) FOR A PARTICULAR MONTH.                      
      ******************************************************************        
      **04/03/2002 CHANGED BY:JACKIE HELING     INITIAL WRITE                   
      ******************************************************************        
022405**02/24/2005 CHANGED BY:JACKIE HELING     NEW COLUMN FOR NEG LBR          
      ******************************************************************        
063005**06/30/2005 CHANGED BY:JACKIE HELING     EFFECTIVE DATE FILE             
      ******************************************************************        
071205**07/12/2005 CHANGED BY:JACKIE HELING     CHANGE DATE HEADER FMT          
      ******************************************************************        
5527  * 10/21/09  COGNIZANT        PBI5527   MODIFIED PROGRAM TO SKIP           
5527  *                                       SPACES FOR PU-LOCATION            
5527  *                                       BEFORE INSERTING TO               
5527  *                                       SDT_POS_LBR_STD TABLE.            
      ******************************************************************        
I65165**04/08/2013 CHANGED BY:COGNIZANT         ADDED A CONDITION IN            
I65165**                                        UNSTRING TO BYPASS BAD          
I65165**                                        DATA X'0D' IN INPUT FILE        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT POS-LBR-STD                                                   
                  ASSIGN TO INP01.                                              
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  POS-LBR-STD                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 132 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS POS-LBR-STD-RECORD.                                   
       01  POS-LBR-STD-RECORD                PIC X(132).                        
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      **PROGRAM SWITCHES                                                        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-POS-STD-FILE-SW     PIC X(01) VALUE 'N'.               
               88 PS-END-OF-POS-STD-FILE               VALUE 'Y'.               
               88 PS-NOT-END-OF-POS-STD-FILE           VALUE 'N'.               
      ******************************************************************        
      **PROGRAM UNSTRING VARIABLES - TRUCK FILE LAYOUT                          
      ******************************************************************        
       01  PU-PROGRAM-UNSTRING.                                                 
           05  PU-LOCATION                 PIC X(1)   VALUE SPACES.             
           05  PU-LBR-STD                  PIC S9(04) VALUE ZEROES.             
022405     05  PU-NEG-LBR-STD              PIC S9(04) VALUE ZEROES.             
      ******************************************************************        
      **OSA PSALES FILE HEADER CHECK LAYOUT                                     
      ******************************************************************        
       01  OSA-FILE-LAYOUT.                                                     
           05  OSA-DATA-CHECK              PIC X(01)  VALUE SPACES.             
063005     05  FILLER                      PIC X(131) VALUE SPACES.             
      ******************************************************************        
      **DATE HEADER LAYOUT                                                      
      ******************************************************************        
063005 01  DATE-FILE-LAYOUT.                                                    
063005     05  DATE-FILE-MONTH             PIC 9(02)  VALUE ZEROES.             
063005     05  FILLER                      PIC X(01)  VALUE SPACES.             
071205     05  DATE-FILE-DAY               PIC 9(02)  VALUE ZEROES.             
071205     05  FILLER                      PIC X(01)  VALUE SPACES.             
063005     05  DATE-FILE-YEAR              PIC 9(04)  VALUE ZEROES.             
071205     05  FILLER                      PIC X(122) VALUE SPACES.             
      ******************************************************************        
      **TIME FIELDS                                                             
      ******************************************************************        
       01  SD-TIME-STAMP                   PIC  X(26)  VALUE SPACES.            
       01  FILLER REDEFINES SD-TIME-STAMP.                                      
           05  SD-CURR-DTE                 PIC  X(10).                          
           05  FILLER                      PIC  X(01).                          
           05  SD-HOUR                     PIC  X(02).                          
           05  FILLER                      PIC  X(01).                          
           05  SD-MINUTE                   PIC  X(02).                          
           05  FILLER                      PIC  X(01).                          
           05  SD-SECOND                   PIC  X(02).                          
           05  FILLER                      PIC  X(01).                          
           05  SD-MICROSEC                 PIC  X(06).                          
       01  PD-PROGRAM-DATE-VARIABLES.                                           
           05  PD-MONTH-END                PIC  X(10) VALUE SPACES.             
      ******************************************************************        
      **PROGRAM ACCUMULATORS                                                    
      ******************************************************************        
       01  PA-PROGRAM-COUNT.                                                    
           05  PA-RECORDS-READ             PIC  9(02) VALUE ZEROS.              
           05  PA-ROWS-INSERTED            PIC  9(02) VALUE ZEROS.              
           05  PA-ROWS-UPDATED             PIC  9(02) VALUE ZEROS.              
      ******************************************************************        
      **ABEND INFORMATION                                                       
      ******************************************************************        
       01  ABEND-CODE                  PIC S9(04)  VALUE +0 COMP SYNC.          
           88  AC-OPEN-FILE-ERROR                  VALUE +4005.                 
           88  AC-DB2-ERROR                        VALUE +4013.                 
      ******************************************************************        
      **PROGRAM CONSTANTS                                                       
      ******************************************************************        
       01  PC-PROGRAM-VARIABLES.                                                
           05  PC-PROGRAM-NAME             PIC  X(08) VALUE 'SDKUP005'.         
           05  PC-LOC-STRIP                PIC S9(04) COMP VALUE 1000.          
           05  PC-LOC-FREE-STAND           PIC S9(04) COMP VALUE 1001.          
           05  PC-LOC-MALL                 PIC S9(04) COMP VALUE 1002.          
      ******************************************************************        
      **PROGRAM VARIABLES                                                       
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME           PIC X(30) VALUE SPACES.              
063005     05  PV-DATE.                                                         
063005         10  PV-YEAR                 PIC 9(04) VALUE ZEROES.              
063005         10  FILLER                  PIC X(01) VALUE '-'.                 
063005         10  PV-MONTH                PIC 9(02) VALUE ZEROES.              
063005         10  FILLER                  PIC X(01) VALUE '-'.                 
063005         10  PV-DAY                  PIC 9(02) VALUE 15.                  
      ******************************************************************        
      **COPYBOOKS                                                               
      ******************************************************************        
      ***CALENDAR ROUTINE COPYBOOK                                              
           COPY DPWS500.                                                        
      ***SQLCA ROUTINE                                                          
           COPY DPWS004.                                                        
      ***DB2 MESSAGE AREA                                                       
           COPY SQLCA2.                                                         
      ******************************************************************        
      **TABLE INCLUSIONS                                                        
      ******************************************************************        
      ***SQL COMM AREA                                                          
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      ***SDT_POS_LBR_STD TABLE                                                  
           EXEC SQL                                                             
               INCLUDE SDT00015                                                 
           END-EXEC.                                                            
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAIN-LINE.                                                          
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
063005     PERFORM 4000-READ-INPUT-FILE.                                        
           PERFORM 2000-PROCESS-INPUT-FILE                                      
                   UNTIL PS-END-OF-POS-STD-FILE.                                
           PERFORM 6000-WRAPUP.                                                 
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      **INITIALIZATION                                                          
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.                     
                                                                                
           OPEN INPUT POS-LBR-STD.                                              
                                                                                
           PERFORM 1100-GET-TIMESTAMP.                                          
                                                                                
063005*  READ DATE HEADER                                                       
           PERFORM 4000-READ-INPUT-FILE.                                        
063005     PERFORM 1200-RETRIEVE-EOM.                                           
                                                                                
063005* READ COLUMN HEADERS                                                     
063005     PERFORM 4000-READ-INPUT-FILE.                                        
                                                                                
      **********************************************************                
      **RETRIEVE DB2 TIMESTAMP                                                  
      **********************************************************                
       1100-GET-TIMESTAMP.                                                      
                                                                                
           MOVE '1100-GET-TIMESTAMP' TO PV-PARAGRAPH-NAME.                      
                                                                                
           EXEC SQL                                                             
                SET :SD-TIME-STAMP = CURRENT TIMESTAMP                          
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT EQUAL ZERO                                            
               DISPLAY 'UNSUCCESSFUL TIME RETRIEVAL'                            
               PERFORM 9000-ABEND                                               
           END-IF.                                                              
                                                                                
      **********************************************************                
      **RETRIEVE END OF MONTH                                                   
      **********************************************************                
       1200-RETRIEVE-EOM.                                                       
                                                                                
           MOVE '1200-RETRIEVE-EOM' TO PV-PARAGRAPH-NAME.                       
063005     MOVE OSA-FILE-LAYOUT TO DATE-FILE-LAYOUT.                            
063005     MOVE DATE-FILE-MONTH TO PV-MONTH.                                    
063005     MOVE DATE-FILE-YEAR  TO PV-YEAR.                                     
                                                                                
      ***RETRIEVE END OF FISCAL MONTH BEING PROCESSED                           
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-VALID-FISCAL-CALENDAR TO TRUE.                             
           SET DPG51-DO-NOT-INCR-DECR-DATE TO TRUE.                             
           SET DPG52-LK-DTE-ISO-FMT        TO TRUE.                             
063005     MOVE PV-DATE                    TO DPG52-LK-DATE-INPUT.              
           PERFORM 1250-CALL-DATE-ROUTINE.                                      
063005     MOVE DPG56-LAST-FP-DB2-ISO-FMT  TO PD-MONTH-END.                     
                                                                                
                                                                                
      ******************************************************************        
      **CALLS DATE ROUTINE                                                      
      ******************************************************************        
       1250-CALL-DATE-ROUTINE.                                                  
                                                                                
           MOVE '1250-CALL-DATE-ROUTINE' TO PV-PARAGRAPH-NAME                   
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                        
                                                   DPG52                        
                                                   DPG53                        
                                                   DPG54                        
                                                   DPG55                        
                                                   DPG56.                       
                                                                                
           EVALUATE TRUE                                                        
              WHEN DPG54-WARNING-ERROR                                          
                   DISPLAY 'DATE ROUTINE WARNING: ' DPG54-ERROR-MESSAGE         
                   DISPLAY DPG52-DB2-ISO-DATE                                   
                   PERFORM 9000-ABEND                                           
              WHEN DPG54-SEVERE-ERROR                                           
                   DISPLAY 'DATE ROUTINE ERROR: ' DPG54-ERROR-MESSAGE           
                   DISPLAY DPG52-DB2-ISO-DATE                                   
                   PERFORM 9000-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      **********************************************************                
      **PROCESS INPUT FILE                                                      
      **********************************************************                
       2000-PROCESS-INPUT-FILE.                                                 
                                                                                
           MOVE '2000-PROCESS-INPUT-FILE' TO PV-PARAGRAPH-NAME                  
           UNSTRING OSA-FILE-LAYOUT                                             
I65165        DELIMITED BY ',' OR ' ' OR X'0D' INTO  PU-LOCATION                
                                                  ,  PU-LBR-STD                 
022405                                            ,  PU-NEG-LBR-STD.            
           MOVE PD-MONTH-END          TO SDT00015-MN-END-DTE.                   
           MOVE PU-LBR-STD            TO SDT00015-SLS-LBR-FCTR-NBR.             
022405     MOVE PU-NEG-LBR-STD        TO SDT00015-NEG-SLSLBRFCTR-NBR.           
                                                                                
           EVALUATE PU-LOCATION                                                 
             WHEN 'F'                                                           
                MOVE PC-LOC-FREE-STAND TO SDT00015-STR-FMT-CDE                  
             WHEN 'M'                                                           
                MOVE PC-LOC-MALL       TO SDT00015-STR-FMT-CDE                  
             WHEN 'S'                                                           
                MOVE PC-LOC-STRIP      TO SDT00015-STR-FMT-CDE                  
           END-EVALUATE.                                                        
                                                                                
5527       IF PU-LOCATION NOT EQUAL TO SPACES                                   
           PERFORM 2100-INSERT-RECORD                                           
5527       END-IF.                                                              
           PERFORM 4000-READ-INPUT-FILE.                                        
                                                                                
      **********************************************************                
      **INSERT DB RECORD INTO TABLE SDT_CS_LBR_STD                              
      **********************************************************                
       2100-INSERT-RECORD.                                                      
                                                                                
           MOVE '2100-INSERT-RECORD' TO PV-PARAGRAPH-NAME                       
           EXEC SQL                                                             
                INSERT INTO SDT_POS_LBR_STD                                     
                       (MN_END_DTE                                              
                      , STR_FMT_CDE                                             
                      , SLS_LBR_FCTR_NBR                                        
022405                , NEG_SLSLBRFCTR_NBR)                                     
                VALUES(:SDT00015-MN-END-DTE                                     
                     , :SDT00015-STR-FMT-CDE                                    
                     , :SDT00015-SLS-LBR-FCTR-NBR                               
022405               , :SDT00015-NEG-SLSLBRFCTR-NBR)                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = 0                                                  
                   ADD +1             TO PA-ROWS-INSERTED                       
              WHEN SQLCODE = -803                                               
                   PERFORM 2200-UPDATE-RECORD                                   
              WHEN OTHER                                                        
                   PERFORM 9000-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      **UPDATE RECORD ON TABLE SDT_CS_LBR_STD                                   
      ******************************************************************        
       2200-UPDATE-RECORD.                                                      
                                                                                
           MOVE '2200-UPDATE-RECORD' TO PV-PARAGRAPH-NAME                       
           EXEC SQL                                                             
                UPDATE SDT_POS_LBR_STD                                          
                SET SLS_LBR_FCTR_NBR   = :SDT00015-SLS-LBR-FCTR-NBR             
022405             ,NEG_SLSLBRFCTR_NBR = :SDT00015-NEG-SLSLBRFCTR-NBR           
                WHERE MN_END_DTE       = :SDT00015-MN-END-DTE                   
                  AND STR_FMT_CDE      = :SDT00015-STR-FMT-CDE                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = ZERO                                               
                   ADD +1                   TO PA-ROWS-UPDATED                  
              WHEN OTHER                                                        
                   DISPLAY 'ERROR ON UPDATE OF DATA'                            
                   PERFORM 9000-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      **READ INPUT FILE                                                         
      ******************************************************************        
       4000-READ-INPUT-FILE.                                                    
                                                                                
           MOVE '4000-READ-INPUT-FILE' TO PV-PARAGRAPH-NAME.                    
                                                                                
           READ POS-LBR-STD INTO OSA-FILE-LAYOUT                                
                AT END                                                          
                   SET PS-END-OF-POS-STD-FILE TO TRUE                           
           END-READ.                                                            
                                                                                
           ADD +1 TO PA-RECORDS-READ.                                           
                                                                                
      ******************************************************************        
      **END OF JOB                                                              
      ******************************************************************        
       6000-WRAPUP.                                                             
                                                                                
           MOVE '6000-WRAPUP' TO PV-PARAGRAPH-NAME.                             
                                                                                
           EXEC SQL                                                             
                COMMIT                                                          
           END-EXEC.                                                            
                                                                                
           IF  SQLCODE NOT ZERO                                                 
                   DISPLAY 'ERROR UPON COMMIT OF DATA'                          
                   PERFORM 9000-ABEND                                           
           END-IF.                                                              
                                                                                
           CLOSE POS-LBR-STD.                                                   
                                                                                
           DISPLAY '*************************************************'.         
           DISPLAY '* PROGRAM.................: ' PC-PROGRAM-NAME.              
           DISPLAY '* RUN DATE/TIME...........: ' SD-TIME-STAMP.                
           DISPLAY '*************************************************'.         
           DISPLAY '* RECORDS READ.......: '                                    
                                          PA-RECORDS-READ.                      
           DISPLAY '* ROWS INSERTED......: '                                    
                                          PA-ROWS-INSERTED.                     
           DISPLAY '* ROWS UPDATED.......: '                                    
                                          PA-ROWS-UPDATED.                      
           DISPLAY '*************************************************'.         
                                                                                
      **********************************************************                
      **ABEND ROUTINE                                                           
      **********************************************************                
       9000-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                        
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
      ******************************************************************        
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED             
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.              
      ******************************************************************        
           COPY DPPD004.                                                        
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
