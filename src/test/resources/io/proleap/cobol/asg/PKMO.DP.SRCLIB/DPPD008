00001 *                                                                 03/06/91
00002 *      THIS PROCEDURE DIVISION COPY BOOK IS USED TO SEND THE      DPPD008 
00003 *  THE CURRENT SCREEN IMAGE, USING A WORKING-STORAGE SWITCH TO       LV001
00004 *  INDICATE IF THE MAP SHOULD BE SENT IN ITS ENTIRETY, OR DATA    DPPD008 
00005 *  ONLY.  THE SWITCH SHOULD BE DEFINED AS FOLLOWS:                DPPD008 
00006 *                                                                 DPPD008 
00007 *    01  PS-PROGRAM SWITCHES.                                     DPPD008 
00008 *        05  PS-MAP-SEND-TYPE    PIC X(1)     VALUE '1'.          DPPD008 
00009 *            88  PS-SEND-MAP-N-DATA-FRSET     VALUE '1'.          DPPD008 
00010 *            88  PS-SEND-MAP-N-DATA-FRSET-CPOSN                   DPPD008 
00011 *                                             VALUE '2'.          DPPD008 
00012 *            88  PS-SEND-DATA-ONLY-FRSET      VALUE '3'.          DPPD008 
00013 *            88  PS-SEND-DATA-ONLY            VALUE '4'.          DPPD008 
00014 *            88  PS-SEND-DATA-ONLY-ERASEAUP   VALUE '5'.          DPPD008 
00015 *            88  PS-SEND-MAP-ONLY-FRSET       VALUE '6'.          DPPD008 
00016 *                                                                 DPPD008 
00017 *       BEFORE PERFORMING THE PARAGRAPH, THE FOLLOWING SWITCHES   DPPD008 
00018 *  MUST BE SET, TO TAILOR ANY ABENDS TO THE NEEDS OF THE          DPPD008 
00019 *  APPLICATION:                                                   DPPD008 
00020 *                                                                 DPPD008 
00021 *            DP013-RESTART-TERMINAL-IND                           DPPD008 
00022 *            DP013-ROLLBACK-IND                                   DPPD008 
00023 *            DP013-DISPLAY-PANEL-IND                              DPPD008 
00024 *            DP013-TRANSFER-OF-CONTROL-TYPE                       DPPD008 
00025 *                                                                 DPPD008 
00026                                                                   DPPD008 
00027  DP008-0000-WRITE-PANEL.                                          DPPD008 
00028      EVALUATE TRUE                                                DPPD008 
00029          WHEN PS-SEND-DATA-ONLY-FRSET                             DPPD008 
00030              IF  DP020-NEXT-ACT-ERROR                             DPPD008 
00031                  MOVE -1 TO ANAVCMDL                              DPPD008 
00032              END-IF                                               DPPD008 
00033              EXEC CICS                                            DPPD008 
00034                  SEND                                             DPPD008 
00035                      MAP    (PC-CURRENT-MAP-NAME)                 DPPD008 
00036                      MAPSET (PC-CURRENT-MAPSET-NAME)              DPPD008 
00037                      FROM   (MD-MAP-DATA-AREA)                    DPPD008 
00038                      DATAONLY                                     DPPD008 
00039                      FRSET                                        DPPD008 
00040                      FREEKB                                       DPPD008 
00041                      CURSOR                                       DPPD008 
00042                      FORMFEED                                     DPPD008 
00043                      RESP   (PV-CICS-RESPONSE)                    DPPD008 
00044              END-EXEC                                             DPPD008 
00045              IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)            DPPD008 
00046                  SET DP013-CICS-ABEND TO TRUE                     DPPD008 
00047                  MOVE 'DP008-0000-WRITE-PANEL' TO DP013-PARAGRAPH DPPD008 
00048                  STRING 'ATTEMPTING TO SEND MAP '                 DPPD008 
00049                                                 DELIMITED BY SIZE DPPD008 
00050                         PC-CURRENT-MAP-NAME     DELIMITED BY SIZE DPPD008 
00051                         ' (MAPSET '             DELIMITED BY SIZE DPPD008 
00052                         PC-CURRENT-MAPSET-NAME  DELIMITED BY SIZE DPPD008 
00053                         ') - DATA ONLY - FRSET' DELIMITED BY SIZE DPPD008 
00054                      INTO DP013-MESSAGE-TEXT (1)                  DPPD008 
00055                  MOVE '     USING SYMBOLIC CURSOR POSITIONING' TO DPPD008 
00056                            DP013-MESSAGE-TEXT (2)                 DPPD008 
00057                  PERFORM DP013-0000-PROCESS-ABEND                 DPPD008 
00058              END-IF                                               DPPD008 
00059          WHEN PS-SEND-MAP-N-DATA-FRSET                            DPPD008 
00060              IF  DP020-NEXT-ACT-ERROR                             DPPD008 
00061                  MOVE -1 TO ANAVCMDL                              DPPD008 
00062              END-IF                                               DPPD008 
00063              EXEC CICS                                            DPPD008 
00064                  SEND                                             DPPD008 
00065                      MAP    (PC-CURRENT-MAP-NAME)                 DPPD008 
00066                      MAPSET (PC-CURRENT-MAPSET-NAME)              DPPD008 
00067                      FROM   (MD-MAP-DATA-AREA)                    DPPD008 
00068                      ERASE                                        DPPD008 
00069                      FREEKB                                       DPPD008 
00070                      FRSET                                        DPPD008 
00071                      CURSOR                                       DPPD008 
00072                      FORMFEED                                     DPPD008 
00073                      RESP   (PV-CICS-RESPONSE)                    DPPD008 
00074              END-EXEC                                             DPPD008 
00075              IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)            DPPD008 
00076                  SET DP013-CICS-ABEND TO TRUE                     DPPD008 
00077                  MOVE 'DP008-0000-WRITE-PANEL' TO DP013-PARAGRAPH DPPD008 
00078                  STRING 'ATTEMPTING TO SEND MAP '                 DPPD008 
00079                                                 DELIMITED BY SIZE DPPD008 
00080                         PC-CURRENT-MAP-NAME     DELIMITED BY SIZE DPPD008 
00081                         ' (MAPSET '             DELIMITED BY SIZE DPPD008 
00082                         PC-CURRENT-MAPSET-NAME  DELIMITED BY SIZE DPPD008 
00083                         ') - MAP AND DATA - FRSET'                DPPD008 
00084                                                 DELIMITED BY SIZE DPPD008 
00085                      INTO DP013-MESSAGE-TEXT (1)                  DPPD008 
00086                  MOVE '     USING SYMBOLIC CURSOR POSITIONING' TO DPPD008 
00087                            DP013-MESSAGE-TEXT (2)                 DPPD008 
00088                  PERFORM DP013-0000-PROCESS-ABEND                 DPPD008 
00089              END-IF                                               DPPD008 
00090          WHEN PS-SEND-MAP-N-DATA-FRSET-CPOSN                      DPPD008 
00091              EXEC CICS                                            DPPD008 
00092                  SEND                                             DPPD008 
00093                      MAP    (PC-CURRENT-MAP-NAME)                 DPPD008 
00094                      MAPSET (PC-CURRENT-MAPSET-NAME)              DPPD008 
00095                      FROM   (MD-MAP-DATA-AREA)                    DPPD008 
00096                      ERASE                                        DPPD008 
00097                      FREEKB                                       DPPD008 
00098                      FRSET                                        DPPD008 
00099                      CURSOR (DP020-APPL-CURSOR-POSITION)          DPPD008 
00100                      FORMFEED                                     DPPD008 
00101                      RESP   (PV-CICS-RESPONSE)                    DPPD008 
00102              END-EXEC                                             DPPD008 
00103              IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)            DPPD008 
00104                  SET DP013-CICS-ABEND TO TRUE                     DPPD008 
00105                  MOVE 'DP008-0000-WRITE-PANEL' TO DP013-PARAGRAPH DPPD008 
00106                  STRING 'ATTEMPTING TO SEND MAP '                 DPPD008 
00107                                                 DELIMITED BY SIZE DPPD008 
00108                         PC-CURRENT-MAP-NAME     DELIMITED BY SIZE DPPD008 
00109                         ' (MAPSET '             DELIMITED BY SIZE DPPD008 
00110                         PC-CURRENT-MAPSET-NAME  DELIMITED BY SIZE DPPD008 
00111                         ') - MAP AND DATA - FRSET'                DPPD008 
00112                                                 DELIMITED BY SIZE DPPD008 
00113                      INTO DP013-MESSAGE-TEXT (1)                  DPPD008 
00114                  MOVE '     USING RELATIVE CURSOR POSITIONING' TO DPPD008 
00115                            DP013-MESSAGE-TEXT (2)                 DPPD008 
00116                  PERFORM DP013-0000-PROCESS-ABEND                 DPPD008 
00117              END-IF                                               DPPD008 
00118          WHEN PS-SEND-DATA-ONLY                                   DPPD008 
00119              EXEC CICS                                            DPPD008 
00120                  SEND                                             DPPD008 
00121                      MAP    (PC-CURRENT-MAP-NAME)                 DPPD008 
00122                      MAPSET (PC-CURRENT-MAPSET-NAME)              DPPD008 
00123                      FROM   (MD-MAP-DATA-AREA)                    DPPD008 
00124                      DATAONLY                                     DPPD008 
00125                      FREEKB                                       DPPD008 
00126                      CURSOR                                       DPPD008 
00127                      FORMFEED                                     DPPD008 
00128                      RESP   (PV-CICS-RESPONSE)                    DPPD008 
00129              END-EXEC                                             DPPD008 
00130              IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)            DPPD008 
00131                  SET DP013-CICS-ABEND TO TRUE                     DPPD008 
00132                  MOVE 'DP008-0000-WRITE-PANEL' TO DP013-PARAGRAPH DPPD008 
00133                  STRING 'ATTEMPTING TO SEND MAP '                 DPPD008 
00134                                                 DELIMITED BY SIZE DPPD008 
00135                         PC-CURRENT-MAP-NAME     DELIMITED BY SIZE DPPD008 
00136                         ' (MAPSET '             DELIMITED BY SIZE DPPD008 
00137                         PC-CURRENT-MAPSET-NAME  DELIMITED BY SIZE DPPD008 
00138                         ') - DATA ONLY - NO FRSET'                DPPD008 
00139                                                 DELIMITED BY SIZE DPPD008 
00140                      INTO DP013-MESSAGE-TEXT (1)                  DPPD008 
00141                  MOVE '     USING RELATIVE CURSOR POSITIONING' TO DPPD008 
00142                            DP013-MESSAGE-TEXT (2)                 DPPD008 
00143                  PERFORM DP013-0000-PROCESS-ABEND                 DPPD008 
00144              END-IF                                               DPPD008 
00145          WHEN PS-SEND-DATA-ONLY-ERASEAUP                          DPPD008 
00146              IF  DP020-NEXT-ACT-ERROR                             DPPD008 
00147                  MOVE -1 TO ANAVCMDL                              DPPD008 
00148              END-IF                                               DPPD008 
00149              EXEC CICS                                            DPPD008 
00150                  SEND                                             DPPD008 
00151                      MAP    (PC-CURRENT-MAP-NAME)                 DPPD008 
00152                      MAPSET (PC-CURRENT-MAPSET-NAME)              DPPD008 
00153                      FROM   (MD-MAP-DATA-AREA)                    DPPD008 
00154                      DATAONLY                                     DPPD008 
00155                      FRSET                                        DPPD008 
00156                      FREEKB                                       DPPD008 
00157                      ERASEAUP                                     DPPD008 
00158                      CURSOR                                       DPPD008 
00159                      FORMFEED                                     DPPD008 
00160                      RESP   (PV-CICS-RESPONSE)                    DPPD008 
00161              END-EXEC                                             DPPD008 
00162              IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)            DPPD008 
00163                  SET DP013-CICS-ABEND TO TRUE                     DPPD008 
00164                  MOVE 'DP008-0000-WRITE-PANEL' TO DP013-PARAGRAPH DPPD008 
00165                  STRING 'ATTEMPTING TO SEND MAP '                 DPPD008 
00166                                                 DELIMITED BY SIZE DPPD008 
00167                         PC-CURRENT-MAP-NAME     DELIMITED BY SIZE DPPD008 
00168                         ' (MAPSET '             DELIMITED BY SIZE DPPD008 
00169                         PC-CURRENT-MAPSET-NAME  DELIMITED BY SIZE DPPD008 
00170                         ') - DATA ONLY - FRSET - '                DPPD008 
00171                                                 DELIMITED BY SIZE DPPD008 
00172                      INTO DP013-MESSAGE-TEXT (1)                  DPPD008 
00173                  MOVE '          ERASEAUP' TO                     DPPD008 
00174                            DP013-MESSAGE-TEXT (2)                 DPPD008 
00175                  MOVE '     USING SYMBOLIC CURSOR POSITIONING' TO DPPD008 
00176                            DP013-MESSAGE-TEXT (3)                 DPPD008 
00177                  PERFORM DP013-0000-PROCESS-ABEND                 DPPD008 
00178              END-IF                                               DPPD008 
00179          WHEN PS-SEND-MAP-ONLY-FRSET                              DPPD008 
00180              IF  DP020-NEXT-ACT-ERROR                             DPPD008 
00181                  MOVE -1 TO ANAVCMDL                              DPPD008 
00182              END-IF                                               DPPD008 
00183              EXEC CICS                                            DPPD008 
00184                  SEND                                             DPPD008 
00185                      MAP    (PC-CURRENT-MAP-NAME)                 DPPD008 
00186                      MAPSET (PC-CURRENT-MAPSET-NAME)              DPPD008 
00187                      MAPONLY                                      DPPD008 
00188                      FREEKB                                       DPPD008 
00189                      FRSET                                        DPPD008 
00190                      CURSOR                                       DPPD008 
00191                      FORMFEED                                     DPPD008 
00192                      RESP   (PV-CICS-RESPONSE)                    DPPD008 
00193              END-EXEC                                             DPPD008 
00194              IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)            DPPD008 
00195                  SET DP013-CICS-ABEND TO TRUE                     DPPD008 
00196                  MOVE 'DP008-0000-WRITE-PANEL' TO DP013-PARAGRAPH DPPD008 
00197                  STRING 'ATTEMPTING TO SEND MAP '                 DPPD008 
00198                                                 DELIMITED BY SIZE DPPD008 
00199                         PC-CURRENT-MAP-NAME     DELIMITED BY SIZE DPPD008 
00200                         ' (MAPSET '             DELIMITED BY SIZE DPPD008 
00201                         PC-CURRENT-MAPSET-NAME  DELIMITED BY SIZE DPPD008 
00202                         ') - MAP ONLY - FRSET'  DELIMITED BY SIZE DPPD008 
00203                      INTO DP013-MESSAGE-TEXT (1)                  DPPD008 
00204                  MOVE '     USING SYMBOLIC CURSOR POSITIONING' TO DPPD008 
00205                            DP013-MESSAGE-TEXT (2)                 DPPD008 
00206                  PERFORM DP013-0000-PROCESS-ABEND                 DPPD008 
00207              END-IF                                               DPPD008 
00208      END-EVALUATE.                                                DPPD008 
00209                                                                   DPPD008 
00210      SET DP020-NEXT-ACT-READ-MAP TO TRUE.                         DPPD008 
00211 *DP008-0000-EXIT.                                                 DPPD008 
