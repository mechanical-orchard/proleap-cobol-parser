00001 ******************************************************************05/17/95
00002  IDENTIFICATION DIVISION.                                         RCKUT023
00003 ******************************************************************   LV001
00004                                                                   RCKUT023
00005  PROGRAM-ID.             RCKUT023.                                RCKUT023
00006  AUTHOR.                 LARRY SCHLEICHER.                        RCKUT023
00007  INSTALLATION.           KOHLS DEPARTMENT STORES.                 RCKUT023
00008  DATE-WRITTEN.           03-17-95.                                RCKUT023
00009  DATE-COMPILED.                                                   RCKUT023
00010                                                                   RCKUT023
00011 ******************************************************************RCKUT023
00012 *     THIS SUBROUTINE WILL EDIT A FREIGHT BILL, BILL OF LADING   *RCKUT023
00013 *  AND VEHICLE NUMBER FIELDS.  FOR ALL THREE FIELDS, ALL SPACES  *RCKUT023
00014 *  AND SPECIAL CHARACTERS WILL BE REMOVED AND THE VALID          *RCKUT023
00015 *  CHARACTERS WILL BE LEFT JUSTIFIED.  ALSO LEADING ZEROES WILL  *RCKUT023
00016 *  ALSO BE REMOVED FROM ALL THREE FIELDS.                        *RCKUT023
00017 *                                                                *RCKUT023
00018 *       THE PARAMETER LIST TO USE WHEN CALLING THIS SUBROUTINE   *RCKUT023
00019 *  IS DEFINED AS FOLLOWS:                                        *RCKUT023
00020 *                                                                *RCKUT023
00021 *       FIELDS TO BE SENT TO THE SUBROUTINE:                     *RCKUT023
00022 *                                                                *RCKUT023
00023 *            THE FREIGHT BILL FIELD                 X(22)        *RCKUT023
00024 *            THE BILL OF LADING FIELD               X(30)        *RCKUT023
00025 *            THE VEHICLE NUMBER FIELD               X(10)        *RCKUT023
00026 *                                                                *RCKUT023
00027 *       RESPONSES FROM THE SUBROUTINE:                           *RCKUT023
00028 *                                                                *RCKUT023
00029 *            AN INDICATION OF THE SUCCESS OR        X(1)         *RCKUT023
00030 *                 FAILURE OF THE CALL, AND, IN THE               *RCKUT023
00031 *                 CASE OF AN ERROR, THE SPECIFIC                 *RCKUT023
00032 *                 TYPE OF ERROR DETECTED                         *RCKUT023
00033 *                                                                *RCKUT023
00034 *            AN INDICATION OF THE SUCCESS OR        X(1)         *RCKUT023
00035 *                 FAILURE OF THE CALL, AND, IN THE               *RCKUT023
00036 *                 CASE OF AN ERROR, THE SPECIFIC                 *RCKUT023
00037 *                 TYPE OF ERROR DETECTED                         *RCKUT023
00038 *                                                                *RCKUT023
00039 *            AN INDICATION OF THE SUCCESS OR        X(1)         *RCKUT023
00040 *                 FAILURE OF THE CALL, AND, IN THE               *RCKUT023
00041 *                 CASE OF AN ERROR, THE SPECIFIC                 *RCKUT023
00042 *                 TYPE OF ERROR DETECTED                         *RCKUT023
00043 *                                                                *RCKUT023
00044 ******************************************************************RCKUT023
00045                                                                   RCKUT023
00046 ******************************************************************RCKUT023
00047  ENVIRONMENT DIVISION.                                            RCKUT023
00048 ******************************************************************RCKUT023
00049                                                                   RCKUT023
00050  CONFIGURATION SECTION.                                           RCKUT023
00051                                                                   RCKUT023
00052  SOURCE-COMPUTER.        IBM-4381.                                RCKUT023
00053  OBJECT-COMPUTER.        IBM-4381.                                RCKUT023
00054                                                                   RCKUT023
00055  INPUT-OUTPUT SECTION.                                            RCKUT023
00056                                                                   RCKUT023
00057  FILE-CONTROL.                                                    RCKUT023
00058                                                                   RCKUT023
00059 ******************************************************************RCKUT023
00060  DATA DIVISION.                                                   RCKUT023
00061 ******************************************************************RCKUT023
00062                                                                   RCKUT023
00063  WORKING-STORAGE SECTION.                                         RCKUT023
00064                                                                   RCKUT023
00065 *                                                                 RCKUT023
00066 *  SENDING/RECEIVING AREA FOR THE NUMERIC EDIT ROUTINE (RCKUT023) RCKUT023
00067 *                                                                 RCKUT023
00068                                                                   RCKUT023
00069      COPY RCWS023.                                                RCKUT023
00070                                                                   RCKUT023
00071  01  PS-PROGRAM-SWITCHES.                                         RCKUT023
00072      05  PS-ZERO-FOUND-SW            PIC X(1)     VALUE 'Y'.      RCKUT023
00073          88  PS-ZERO-FOUND                        VALUE 'Y'.      RCKUT023
00074          88  PS-NON-ZERO-FOUND                    VALUE 'N'.      RCKUT023
00075                                                                   RCKUT023
00076  01  PC-PROGRAM-CONSTANTS.                                        RCKUT023
00077      05  PC-ZERO                     PIC X(01)    VALUE '0'.      RCKUT023
00078                                                                   RCKUT023
00079  01  PV-PROGRAM-VARIABLES.                                        RCKUT023
00080      05  PV-CURRENT-INDEX-LOCATION   PIC S9(4)    VALUE ZEROS     RCKUT023
00081                                                   COMP SYNC.      RCKUT023
00082      05  PV-30-DIGIT-BEFORE          PIC X(30).                   RCKUT023
00083      05  FILLER REDEFINES PV-30-DIGIT-BEFORE.                     RCKUT023
00084          10  PV-30-CHAR-BEFORE OCCURS 30 TIMES                    RCKUT023
00085                                INDEXED BY PV-BEFORE-INDEX.        RCKUT023
00086              15  PV-ONE-CHAR-BEFORE  PIC X(01).                   RCKUT023
00087                                                                   RCKUT023
00088      05  PV-30-DIGIT-AFTER           PIC X(30).                   RCKUT023
00089      05  FILLER REDEFINES PV-30-DIGIT-AFTER.                      RCKUT023
00090          10  PV-30-CHAR-AFTER OCCURS 30 TIMES                     RCKUT023
00091                                INDEXED BY PV-AFTER-INDEX.         RCKUT023
00092              15  PV-ONE-CHAR-AFTER   PIC X(01).                   RCKUT023
00093                                                                   RCKUT023
00094      05  PV-VERIFY-CHAR              PIC X(01).                   RCKUT023
00095          88  PV-VALID-CHARACTER      VALUE '0' THRU '9',          RCKUT023
00096                                            'A' THRU 'Z'.          RCKUT023
00097  LINKAGE SECTION.                                                 RCKUT023
00098                                                                   RCKUT023
00099  01  FORMAT-DATA-AREA                PIC X(80).                   RCKUT023
00100                                                                   RCKUT023
00101 ******************************************************************RCKUT023
00102  PROCEDURE DIVISION USING FORMAT-DATA-AREA.                       RCKUT023
00103 ******************************************************************RCKUT023
00104                                                                   RCKUT023
00105  0000-MAINLINE.                                                   RCKUT023
00106      PERFORM 1000-INITIALIZE.                                     RCKUT023
00107                                                                   RCKUT023
00108      IF RC023-FREIGHT-BILL NOT = SPACES                           RCKUT023
00109          PERFORM 2000-FORMAT-FREIGHT-BILL                         RCKUT023
00110          SET RC023-FOUND-DATA-1 TO TRUE                           RCKUT023
00111      END-IF.                                                      RCKUT023
00112                                                                   RCKUT023
00113      IF RC023-BILL-OF-LADING NOT = SPACES                         RCKUT023
00114          PERFORM 3000-FORMAT-BILL-OF-LADING                       RCKUT023
00115          SET RC023-FOUND-DATA-2 TO TRUE                           RCKUT023
00116      END-IF.                                                      RCKUT023
00117                                                                   RCKUT023
00118      IF RC023-VEHICLE NOT = SPACES                                RCKUT023
00119          PERFORM 4000-FORMAT-VEHICL                               RCKUT023
00120          SET RC023-FOUND-DATA-3 TO TRUE                           RCKUT023
00121      END-IF.                                                      RCKUT023
00122                                                                   RCKUT023
00123      MOVE RC023-FORMAT-DATA-AREA TO                               RCKUT023
00124                FORMAT-DATA-AREA.                                  RCKUT023
00125                                                                   RCKUT023
00126 ******************************************************************RCKUT023
00127 *                                                                *RCKUT023
00128 ******************************************************************RCKUT023
00129                                                                   RCKUT023
00130  1000-INITIALIZE.                                                 RCKUT023
00131      MOVE FORMAT-DATA-AREA TO                                     RCKUT023
00132                RC023-FORMAT-DATA-AREA.                            RCKUT023
00133                                                                   RCKUT023
00134                                                                   RCKUT023
00135 ******************************************************************RCKUT023
00136 *  REMOVE ALL INVALID CHARACTERS FROM THE FREIGHT BILL FIELD     *RCKUT023
00137 ******************************************************************RCKUT023
00138                                                                   RCKUT023
00139  2000-FORMAT-FREIGHT-BILL.                                        RCKUT023
00140      MOVE RC023-FREIGHT-BILL TO PV-30-DIGIT-BEFORE.               RCKUT023
00141      PERFORM 5000-REMOVE-SPECIAL-CHARACTERS.                      RCKUT023
00142      MOVE PV-30-DIGIT-AFTER TO PV-30-DIGIT-BEFORE.                RCKUT023
00143      PERFORM 7000-REMOVE-LEADING-ZEROES.                          RCKUT023
00144      MOVE PV-30-DIGIT-AFTER TO RC023-FREIGHT-BILL.                RCKUT023
00145                                                                   RCKUT023
00146 ******************************************************************RCKUT023
00147 *  REMOVE ALL INVALID CHARACTERS FROM THE BILL OF LADING FIELD.  *RCKUT023
00148 ******************************************************************RCKUT023
00149                                                                   RCKUT023
00150  3000-FORMAT-BILL-OF-LADING.                                      RCKUT023
00151      MOVE RC023-BILL-OF-LADING TO PV-30-DIGIT-BEFORE.             RCKUT023
00152      PERFORM 5000-REMOVE-SPECIAL-CHARACTERS.                      RCKUT023
00153      MOVE PV-30-DIGIT-AFTER TO PV-30-DIGIT-BEFORE.                RCKUT023
00154      PERFORM 7000-REMOVE-LEADING-ZEROES.                          RCKUT023
00155      MOVE PV-30-DIGIT-AFTER TO RC023-BILL-OF-LADING.              RCKUT023
00156                                                                   RCKUT023
00157                                                                   RCKUT023
00158 ******************************************************************RCKUT023
00159 *  REMOVE ALL INVALID CHARACTERS FROM THE VEHICLE FIELD AND      *RCKUT023
00160 *  REMOVE ALL LEADING ZEROES.                                    *RCKUT023
00161 ******************************************************************RCKUT023
00162                                                                   RCKUT023
00163  4000-FORMAT-VEHICL.                                              RCKUT023
00164      MOVE RC023-VEHICLE TO PV-30-DIGIT-BEFORE.                    RCKUT023
00165      PERFORM 5000-REMOVE-SPECIAL-CHARACTERS.                      RCKUT023
00166      MOVE PV-30-DIGIT-AFTER TO PV-30-DIGIT-BEFORE.                RCKUT023
00167      PERFORM 7000-REMOVE-LEADING-ZEROES.                          RCKUT023
00168      MOVE PV-30-DIGIT-AFTER TO RC023-VEHICLE.                     RCKUT023
00169                                                                   RCKUT023
00170 ******************************************************************RCKUT023
00171 *                                                                *RCKUT023
00172 ******************************************************************RCKUT023
00173                                                                   RCKUT023
00174  5000-REMOVE-SPECIAL-CHARACTERS.                                  RCKUT023
00175      SET PV-BEFORE-INDEX TO 1.                                    RCKUT023
00176      SET PV-AFTER-INDEX TO 1.                                     RCKUT023
00177      MOVE SPACES TO PV-30-DIGIT-AFTER.                            RCKUT023
00178      PERFORM 6000-MOVE-GOOD-DATA                                  RCKUT023
00179          VARYING PV-BEFORE-INDEX FROM 1 BY 1                      RCKUT023
00180          UNTIL PV-BEFORE-INDEX > 30.                              RCKUT023
00181                                                                   RCKUT023
00182 ******************************************************************RCKUT023
00183 *  MOVE ALL VALID CHARACTERS FROM THE BEFORE LAYOUT TO THE AFTER *RCKUT023
00184 *  LAYOUT, ONE CHARACTER AT A TIME.                              *RCKUT023
00185 ******************************************************************RCKUT023
00186                                                                   RCKUT023
00187  6000-MOVE-GOOD-DATA.                                             RCKUT023
00188      MOVE PV-ONE-CHAR-BEFORE (PV-BEFORE-INDEX) TO PV-VERIFY-CHAR. RCKUT023
00189      IF PV-VALID-CHARACTER                                        RCKUT023
00190          MOVE PV-VERIFY-CHAR TO PV-ONE-CHAR-AFTER (PV-AFTER-INDEX)RCKUT023
00191          SET PV-AFTER-INDEX UP BY 1                               RCKUT023
00192      END-IF.                                                      RCKUT023
00193                                                                   RCKUT023
00194 ******************************************************************RCKUT023
00195 *  MOVE ALL VALID CHARACTERS AFTER THE LEADING ZEROES FROM THE   *RCKUT023
00196 *  BEFORE LAYOUT TO THE AFTER LAYOUT, ONE CHARACTER AT A TIME.   *RCKUT023
00197 ******************************************************************RCKUT023
00198                                                                   RCKUT023
00199  7000-REMOVE-LEADING-ZEROES.                                      RCKUT023
00200      SET PV-BEFORE-INDEX TO 1.                                    RCKUT023
00201      SET PV-AFTER-INDEX TO 1.                                     RCKUT023
00202      SET PS-ZERO-FOUND TO TRUE.                                   RCKUT023
00203      MOVE SPACES TO PV-30-DIGIT-AFTER.                            RCKUT023
00204      PERFORM 8000-MOVE-DATA-AFTER-ZEROES                          RCKUT023
00205          VARYING PV-BEFORE-INDEX FROM 1 BY 1                      RCKUT023
00206          UNTIL PV-BEFORE-INDEX > 30.                              RCKUT023
00207                                                                   RCKUT023
00208 ******************************************************************RCKUT023
00209 *  MOVE ALL VALID CHARACTERS FROM THE BEFORE LAYOUT TO THE AFTER *RCKUT023
00210 *  LAYOUT, ONE CHARACTER AT A TIME.                              *RCKUT023
00211 ******************************************************************RCKUT023
00212                                                                   RCKUT023
00213  8000-MOVE-DATA-AFTER-ZEROES.                                     RCKUT023
00214      IF PS-ZERO-FOUND AND                                         RCKUT023
00215         PV-ONE-CHAR-BEFORE (PV-BEFORE-INDEX) = PC-ZERO            RCKUT023
00216          NEXT SENTENCE                                            RCKUT023
00217      ELSE                                                         RCKUT023
00218          SET PS-NON-ZERO-FOUND TO TRUE                            RCKUT023
00219          MOVE PV-ONE-CHAR-BEFORE (PV-BEFORE-INDEX) TO             RCKUT023
00220                                 PV-ONE-CHAR-AFTER (PV-AFTER-INDEX)RCKUT023
00221          SET PV-AFTER-INDEX UP BY 1                               RCKUT023
00222      END-IF.                                                      RCKUT023
