       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    IMKUP990.                                                 
       AUTHOR.        JOHN SELWITSCHKA.                                         
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  08/08/2014.                                               
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *  IMKUP990 - TUPCPLS UPDATE AND INSERT                                   
      *****************************************************************         
      *                                                                         
      *  THIS PROGRAM WILL UPDATE TUPCPLS                                       
      *                                                                         
      *  TABLES ACCESSED ARE:                                                   
      *      XMT_LOC - LOCATIONS                                                
      *      TUPCPLS - UPC PHYSICAL LOCATION STATUS                             
      *      TCKRSTCNTL - DB2 CHECKPOINT RESTART CONTROL                        
      *      TCKRSTINF  - DB2 CHECKPOINT RESTART INFORMATION                    
      *  TABLES UPDATED ARE:                                                    
      *      TUPCPLS - UPC PHYSICAL LOCATION STATUS                             
      *      TCKRSTCNTL - DB2 CHECKPOINT RESTART CONTROL                        
      *      TCKRSTINF  - DB2 CHECKPOINT RESTART INFORMATION                    
      *  FILES READ ARE:                                                        
      *      IMRDXXX SKU UPDATE FILE                                            
      *****************************************************************         
      * 08/08/14 - JOHN SELWITSCHKA                                             
      *    - NEW PROGRAM                                                        
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT SKU-UPDATE-FILE              ASSIGN TO INP01.                 
           SELECT CONTROL-CARD-FILE            ASSIGN TO INP02.                 
                                                                                
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
       FILE SECTION.                                                            
                                                                                
       FD  SKU-UPDATE-FILE                                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS PRICE-UPDATE-RECORD.                                  
       01  SKU-UPDATE-RECORD           PIC  X(80).                              
      *                                                                         
       FD  CONTROL-CARD-FILE                                            00590094
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                          00600094
           BLOCK CONTAINS 0 RECORDS.                                    00610094
       01  CONTROL-RECORD              PIC X(80).                       00620094
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      * PC PROGRAM CONSTANTS                                           *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME                PIC  X(08)   VALUE                
                                                           'IMKUP990'.          
           05  PC-OUT-MSGLENGTH               PIC S9(09)   VALUE +450           
                                                           COMP.                
           05  PC-MAXIMUM-RETRY-COUNT         PIC S9(04)   VALUE +2             
                                                           COMP SYNC.           
           05  PC-MAX-STORES                  PIC  9(02)   VALUE 40.            
           05  PC-CLEARANCE                   PIC  X(02)   VALUE '30'.          
           05  PC-MIXED                       PIC  X(02)   VALUE '25'.          
           05  PC-ORIGIN-CNTRY                PIC  X(03)   VALUE 'US '.         
           05  PC-DEFAULT-TIMESTAMP           PIC  X(26)   VALUE                
               '9999-09-09-23.59.00.000000'.                                    
           05  PC-TIMESTAMP-INACTIVE-TIME     PIC  X(16)   VALUE                
               '-23.59.59.999999'.                                              
           05  PC-TIMESTAMP-ACTIVE-TIME       PIC  X(16)   VALUE                
               '-00.00.00.000000'.                                              
                                                                                
      ******************************************************************        
      * PV PROGRAM VARIABLES                                           *        
      ******************************************************************        
       01  PV-SKU-RECORD.                                                       
           05  PV-SKU                         PIC S9(08)   VALUE +0.            
           05  FILLER                         PIC  X(72)   VALUE SPACES.        
                                                                                
       01  PV-WORK-VARIABLES.                                                   
           05  PV-UPC-COMP-10                 PIC S9(10)   VALUE +0             
                                                           COMP.                
           05  PV-COMPLETION-CODE             PIC S9(09)   VALUE +0             
                                                           COMP.                
           05  PV-REASON                      PIC S9(09)   VALUE +0             
                                                           COMP.                
           05  PV-REASON-TEXT                 PIC S9(09)   VALUE +0             
                                                           COMP.                
           05  PV-UNT-COST-NULL-IND           PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-ORIG-CLR-NULL-IND           PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-MKDN-CDE-NULL-IND           PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-ORIG-EFF-END-NULL-IND       PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-EFF-END-NULL-IND            PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-EFF-END-NULL-IND-UNP        PIC S9(04)   VALUE +0.            
           05  PV-EFF-END-NULL-IND-DIS        PIC ----9.                        
           05  PV-RETRY-COUNT                 PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-PREV-STR-NBR                PIC S9(04)   VALUE +0             
                                                           COMP.                
                                                                                
           05  PV-PARAGRAPH-NAME              PIC  X(30)   VALUE SPACES.        
           05  PV-OPERATION-MSG-1             PIC  X(40)   VALUE SPACES.        
           05  PV-DB2-OPERATION-ATTEMPTED     PIC  X(30)   VALUE SPACES.        
           05  PV-NEW-SKU-STATUS              PIC  X(02)   VALUE SPACES.        
               88  PV-NEW-SKU-STATUS-ACTIVE                VALUE '20'.          
               88  PV-NEW-SKU-STATUS-CLEARANCE             VALUE '30'.          
               88  PV-NEW-SKU-STATUS-MIXED                 VALUE '25'.          
           05  PV-HOLD-UIR-EFF-DATE.                                            
               10  PV-HOLD-UIR-EFF-CC         PIC  X(02)   VALUE SPACES.        
               10  PV-HOLD-UIR-EFF-YY         PIC  X(02)   VALUE SPACES.        
               10  PV-HOLD-UIR-EFF-DASH1      PIC  X(01)   VALUE '-'.           
               10  PV-HOLD-UIR-EFF-MM         PIC  X(02)   VALUE SPACES.        
               10  PV-HOLD-UIR-EFF-DASH2      PIC  X(01)   VALUE '-'.           
               10  PV-HOLD-UIR-EFF-DD         PIC  X(02)   VALUE SPACES.        
           05  PV-COMMIT-TIMESTAMP            PIC  X(26)   VALUE SPACES.        
           05  PV-CURRENT-TIMESTAMP           PIC  X(26)   VALUE SPACES.        
           05  PV-RUN-DATE                    PIC  X(10)   VALUE SPACES.        
           05  PV-SAVE-SKU                    PIC S9(08)   VALUE +0             
                                                           COMP-3.              
           05  PV-PREV-SKU                    PIC S9(08)   VALUE +0             
                                                           COMP-3.              
           05  PV-PREV-PRCHG-TYP-CDE          PIC  X(03)   VALUE SPACES.        
           05  PV-ORIG-UNT-RTL-AMT            PIC S9(7)V99 VALUE +0             
                                                           COMP-3.              
           05  PV-ORIG-UNT-COST-AMT           PIC S9(7)V999 VALUE +0            
                                                           COMP-3.              
           05  PV-CURR-UNT-RTL-AMT            PIC S9(7)V99 VALUE +0             
                                                           COMP-3.              
           05  PV-CURR-UNT-COST-AMT           PIC S9(7)V999 VALUE +0            
                                                           COMP-3.              
           05  PV-OLD-MEITGP-NBR              PIC S9(09)   VALUE +0             
                                                           COMP.                
           05  PV-SAVE-REGN-PRIC-IND          PIC  X(01)   VALUE SPACES.        
           05  PV-STORE0-UPC-STAT-CDE         PIC  X(02)   VALUE SPACES.        
           05  PV-STORE0-ORIG-CLR-DTE         PIC  X(10)   VALUE SPACES.        
           05  PV-RECS-READ-CNTR              PIC  9(09)   VALUE ZEROES.00830000
           05  PV-SQLCODE-DISPLAY             PIC +(8)9    VALUE ZEROES.00830000
           05  PV-UPCPLS-UPC-ID-NUM           PIC  9(10)   VALUE ZERO.          
           05  PV-DETAIL-KEY.                                                   
               10  PV-DK-SKU                  PIC  X(08)   VALUE SPACES.        
               10  PV-DK-STORE-NBR            PIC  9(04)   VALUE ZERO.          
           05  PV-DELAY-INTERVAL.                                               
               10  FILLER                   PIC X(02)   VALUE SPACES.           
               10  PV-DELAY-TIME            PIC 9(08)   VALUE 6000.             
           05  WS-SYSWAIT                   PIC X(08)   VALUE 'SYSWAIT'.        
           05  PV-UPCPLS-EFF-BEG-TMST       PIC X(26)   VALUE SPACES.           
           05  PV-UPCPLS-EFF-END-TMST       PIC X(26)   VALUE SPACES.           
           05  PV-DISPLAY-10                PIC 9(10)   VALUE ZERO.             
           05  PV-DISPLAY-4                 PIC 9(04)   VALUE ZERO.             
           05  PV-DISPLAY-8                 PIC 9(08)   VALUE ZERO.             
           05  PV-DISPLAY-15                PIC 9(15)   VALUE ZERO.             
                                                                                
      ******************************************************************        
      * CR CHECKPOINT RESTART VARIABLES                                *        
      ******************************************************************        
       01  CR-CHECKPOINT-RESTART-AREA.                                          
           05  CR-AREA-LEN                    PIC S9(04) VALUE +138             
                                                         COMP.                  
           05  CR-CHECKPOINT-RESTART-VAR.                                       
               10  CR-DETAIL-KEY              PIC  X(12) VALUE SPACES.          
               10  CR-RECS-PROCESSED-CNTR     PIC  9(09) VALUE ZERO.            
               10  CR-RECS-READ-CNTR          PIC  9(09) VALUE ZERO.            
               10  CR-SKUS-READ-CNTR          PIC  9(09) VALUE ZERO.            
               10  CR-SKUS-NOT-FOUND-CNTR     PIC  9(09) VALUE ZERO.            
               10  CR-TUPCPLS-ROW-UPD-CNTR    PIC  9(09) VALUE ZERO.            
               10  CR-TUPCPLS-ROW-INS-CNTR    PIC  9(09) VALUE ZERO.            
               10  CR-TUPCPLS-ROW-INACTV-CNTR PIC  9(09) VALUE ZERO.            
               10  CR-TUPCPLS-ROW-DEL-CNTR    PIC  9(09) VALUE ZERO.            
               10  CR-MSGS-TO-XSITEM-QUEUE    PIC  9(09) VALUE ZERO.            
               10  CR-TYPE-75-MSGS-CREATED    PIC  9(09) VALUE ZERO.            
               10  CR-TYPE-80-MSGS-CREATED    PIC  9(09) VALUE ZERO.            
               10  CR-IMT-EVNT-MSG-ROW-INS-CNTR                                 
                                              PIC  9(09) VALUE ZERO.            
               10  CR-COMMIT-CNTR             PIC  9(09) VALUE ZERO.            
               10  CR-SKU-STRS-BYPASSED-CNTR  PIC  9(09) VALUE ZERO.            
                                                                                
      ******************************************************************        
      * PS PROGRAM SWITCHES                                            *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW              PIC  X(01)   VALUE 'N'.           
               88  PS-END-OF-FILE                          VALUE 'Y'.           
           05  PS-LOC-CSR-SW                  PIC X(01)    VALUE 'N'.           
               88  PS-END-OF-LOC-CSR                       VALUE 'Y'.           
               88  PS-MORE-LOC-ROWS                        VALUE 'N'.           
           05  PS-FIRST-COMMIT-SW             PIC  X(01)   VALUE 'N'.           
               88  PS-FIRST-COMMIT                         VALUE 'Y'.           
           05  PS-FIRST-SKU-STORE-SW          PIC  X(01)   VALUE 'N'.           
               88  PS-FIRST-SKU-STORE                      VALUE 'Y'.           
               88  PS-NOT-FIRST-SKU-STORE                  VALUE 'N'.           
           05  PS-NO-DOCUMENTS-SW             PIC  X(01)   VALUE 'N'.           
               88  PS-NO-DOCUMENTS                         VALUE 'Y'.           
           05  PS-CURRENT-ROW-FOUND-SW        PIC  X(01)   VALUE 'N'.           
               88  PS-CURRENT-ROW-FOUND                    VALUE 'Y'.           
               88  PS-CURRENT-ROW-NOT-FOUND                VALUE 'N'.           
           05  PS-CURRENT-ROW-FOUND-SW        PIC  X(01)   VALUE 'N'.           
               88  PS-CURRENT-LOC-ROW-FOUND                VALUE 'Y'.           
               88  PS-CURRENT-LOC-ROW-NOT-FOUND            VALUE 'N'.           
           05  PS-HAVE-UPCPLS-COST-INFO-SW    PIC  X(01)   VALUE 'N'.           
               88  PS-HAVE-UPCPLS-COST-INFO                VALUE 'Y'.           
               88  PS-NOT-HAVE-UPCPLS-COST-INFO            VALUE 'N'.           
           05  PS-HAVE-STORE0-TUPCPLS-SW      PIC  X(01)   VALUE 'N'.           
               88  PS-HAVE-STORE0-TUPCPLS                  VALUE 'Y'.           
               88  PS-NOT-HAVE-STORE0-TUPCPLS              VALUE 'N'.           
           05  PS-IMT-EVNT-MSG-STARTED-SW     PIC  X(01)   VALUE 'N'.           
               88  PS-IMT-EVNT-MSG-STARTED                 VALUE 'Y'.           
               88  PS-IMT-EVNT-MSG-NOT-STARTED             VALUE 'N'.           
           05  PS-SKU-INFO-FOUND-SW           PIC  X(01)   VALUE 'N'.           
               88  PS-SKU-INFO-FOUND                       VALUE 'Y'.           
               88  PS-SKU-INFO-NOT-FOUND                   VALUE 'N'.           
           05  PS-NEW-CLR-RCP-SW              PIC  X(01)   VALUE 'N'.           
               88  PS-NEW-CLR-RCP                          VALUE 'Y'.           
               88  PS-NOT-NEW-CLR-RCP                      VALUE 'N'.           
           05  PS-CHECKPOINT-RESTART-SW       PIC  X(01)   VALUE 'N'.           
               88  PS-CHECKPOINT-RESTART                   VALUE 'Y'.           
               88  PS-NOT-CHECKPOINT-RESTART               VALUE 'N'.           
           05  PS-STRS-ALL-SAME-PRICE-SW      PIC  X(01)   VALUE 'N'.           
               88  PS-STRS-ALL-SAME-PRICE                  VALUE 'Y'.           
               88  PS-NOT-STRS-ALL-SAME-PRICE              VALUE 'N'.           
                                                                                
       01  ABEND-CODE                         PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           88  PV-ABEND-4001                   VALUE +4001.                     
      *        NO CONTROL CARD 1                                                
           88  PV-ABEND-4002                   VALUE +4002.                     
      *        INCORRECT CONTROL CARD DATA                                      
           88  PV-ABEND-4003                   VALUE +4003.                     
      *        EFFECTIVE DATE LESS THAN CURRENT DATE                            
           88  PV-ABEND-4004                   VALUE +4004.                     
      *        INVALID CONTROL CARD JOB NAME                                    
           88  PV-ABEND-4005                   VALUE +4005.                     
      *        INVALID CONTROL CARD XS ITEM QUEUE NAME                          
           88  PV-ABEND-4006                   VALUE +4006.                     
      *        NO QUEUE MANAGER CONTROL CARD                                    
           88  PV-ABEND-4007                   VALUE +4007.                     
      *        INVALID CONTROL CARD QUEUE MANAGER NAME                          
           88  PV-ABEND-4008                   VALUE +4008.                     
      *        NO TUPCPLS ROW FOR FINAL OR MARK2                                
           88  PV-ABEND-4013                   VALUE +4013.                     
      *        DB2 SQL ABEND                                                    
           88  PV-ABEND-4020                   VALUE +4020.                     
      *        MQSERIES ABEND                                                   
                                                                                
       01  DISPLAY-EDITS.                                                       
           05  DI-RECS-PROCESSED-CNTR       PIC  Z(08)9  VALUE ZEROS.           
           05  DI-RECS-READ-CNTR            PIC  Z(08)9  VALUE ZEROS.           
           05  DI-SKUS-READ-CNTR            PIC  Z(08)9  VALUE ZEROS.           
           05  DI-SKUS-NOT-FOUND-CNTR       PIC  Z(08)9  VALUE ZEROS.           
           05  DI-SKU-STRS-BYPASSED-CNTR    PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TUPCPLS-ROW-UPDATE-CNTR   PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TUPCPLS-ROW-INSERT-CNTR   PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TUPCPLS-ROW-INACTV-CNTR   PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TUPCPLS-ROW-DELETE-CNTR   PIC  Z(08)9  VALUE ZEROS.           
           05  DI-MSGS-TO-XSITEM-QUEUE      PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TYPE-75-MSGS-CREATED      PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TYPE-80-MSGS-CREATED      PIC  Z(08)9  VALUE ZEROS.           
           05  DI-IMT-EVNT-ROW-INSERT-CNTR  PIC  Z(08)9  VALUE ZEROS.           
           05  DI-COMMIT-CNTR               PIC  Z(08)9  VALUE ZEROS.           
           05  DI-INPUT-RECS-DISPLAY        PIC  9(06)   VALUE ZEROS.           
                                                                                
       01  PT-PROGRAM-TABLES.                                                   
           05  LOC-TBL-MAX               PIC 9(5)    VALUE 10000.               
           05  LOCATION-TABLE.                                                  
               10  LOC-TBL-SIZE          PIC 9(5)    VALUE ZEROES.              
               10  LOC-ENTRY  OCCURS 10000 TIMES INDEXED BY LOC-INDEX.          
                   15  LOC-NBR           PIC 9(05).                             
                                                                                
      ****************************************************************          
      * RECORD LAYOUT FOR CONTROL CARD FILE.                                    
      * CC PROCESS OLD DATE WILL DEFAULT TO N.  IT WILL BE Y ONLY IF    00840094
      * WE NEED TO PROCESS A PRICE CHANGE THAT IS IN THE PAST.          00840094
      ****************************************************************          
       01  CC-CONTROL-CARD.                                                     
           05  CC-BEG-DATE             PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  CC-END-DATE             PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  CC-UPDATE-IND           PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(57) VALUE SPACES.                  
                                                                                
      *****************************************************************         
      *    DB2 COMMUNICATION AREA                                               
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      *  XMT_LOC - LOCATION TABLE                                               
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE XMT00001                                                
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  TSKXREF - SKU CROSS REFERENCE TABLE                                    
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TSKXREF                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  TUPCPLS - UPC PHYSICAL LOCATION STATUS TABLE                           
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TUPCPLS                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    COBOL DECLARATIONS FOR THE CHECKPOINT RESTART CONTROL TABLE          
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TCKRSTCN                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    COBOL DECL FOR THE CHECKPOINT RESTART INFO TABLE                     
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TCKRSTIN                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
           EXEC SQL                                                             
             DECLARE LOC_CSR CURSOR FOR                                         
                   SELECT LOC_NBR                                               
                     FROM XMT_LOC                                               
                    WHERE LOC_NBR <> 0                                          
                      AND E_BUS_IND <> 'Y'                                      
                      AND LOC_TYP_CDE = 'DS'                                    
                    ORDER BY LOC_NBR                                            
                     WITH UR                                                    
           END-EXEC.                                                            
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      * 0000-MAIN-MODULE.                                              *        
      *  ==> PROCESSES:                                                *        
      *    - CONTROLS HIGHEST LEVEL FLOW OF PROGRAM                    *        
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
           MOVE '0000-MAIN-MODULE' TO PV-PARAGRAPH-NAME.                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-SKU-UPDATES                                     
             UNTIL PS-END-OF-FILE.                                              
                                                                                
           PERFORM 3000-EOJ-PROCESSING.                                         
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZATION.                                           *        
      *  ==> PROCESSES:                                                *        
      *      - CHECKS FOR RESTART PROCESSING.                          *        
      *      - INITIALIZES VARIABLES.                                  *        
      ******************************************************************        
       1000-INITIALIZATION.                                                     
           MOVE '1000-INITIALIZATION ' TO PV-PARAGRAPH-NAME.                    
                                                                                
           OPEN INPUT SKU-UPDATE-FILE                                           
                      CONTROL-CARD-FILE.                                        
                                                                                
           PERFORM 4100-READ-CONTROL-CARD-1.                                    
                                                                                
           MOVE CC-END-DATE TO PV-UPCPLS-EFF-BEG-TMST (1:10).                   
           MOVE PC-TIMESTAMP-ACTIVE-TIME TO                                     
                     PV-UPCPLS-EFF-BEG-TMST (11:16).                            
           MOVE CC-BEG-DATE TO PV-UPCPLS-EFF-END-TMST (1:10).                   
           MOVE PC-TIMESTAMP-INACTIVE-TIME TO                                   
                     PV-UPCPLS-EFF-END-TMST (11:16).                            
                                                                                
      *    PERFORM 7400-INIT-CHECKPOINT-SELECT.                                 
                                                                                
      ******************************************************************        
      *    IF PROGRAM IS IN A RESTART STATUS.  SELECT SAVED DATA FROM  *        
      *    THE RESTART INFO TABLE.                                     *        
      ******************************************************************        
      *    IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                                   
      *        PERFORM 7500-SELECT-RESTART-INFO                                 
      *        MOVE TCKRSTINF-WORK-STRG-DESC-TEXT                               
      *                            TO CR-CHECKPOINT-RESTART-VAR                 
      *        MOVE CR-DETAIL-KEY  TO PV-DETAIL-KEY                             
      *        SET PS-CHECKPOINT-RESTART TO TRUE                                
      *    ELSE                                                                 
      *        SET PS-FIRST-COMMIT TO TRUE                                      
      *        INITIALIZE CR-CHECKPOINT-RESTART-VAR                             
      *    END-IF.                                                              
                                                                                
           PERFORM 1100-BUILD-LOC-TBL.                                          
                                                                                
           PERFORM 4000-READ-RECORD.                                            
                                                                                
      *    IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                                   
      *        DISPLAY '** RESTART RUN **'                              06720800
      *        DISPLAY 'INPUT RECORD RESTARTED ON RECORD: '             02067000
      *                 PV-RECS-READ-CNTR                               02068000
      *        DISPLAY 'SKU NUMBER:   ' IM048-UIR-SKU                   06720900
      *        DISPLAY 'STORE NUMBER: ' IM048-UIR-STR-NBR               06720800
      *    END-IF.                                                              
                                                                                
      *    IF PS-END-OF-FILE                                                    
PK0708*      AND TCKRSTCNTL-CKPT-STAT-CODE = '0'                                
      *        SET PS-NO-DOCUMENTS TO TRUE                                      
      *        DISPLAY ' '                                                      
      *        DISPLAY '***********************************'                    
      *        DISPLAY ' NO SKUS TO PROCESS.              '                     
      *        DISPLAY '***********************************'                    
      *        DISPLAY ' '                                                      
      *    ELSE                                                                 
      *        PERFORM 7100-GET-COMMIT-TIMESTAMP                                
      *        PERFORM 7200-SET-CURRENT-TIMESTAMP                               
      *******  SET PV-UPCPLS-EFF-BEG-TMST = IM048-UIR-EFF-DTE MIDNIGHT          
      *        MOVE PC-TIMESTAMP-ACTIVE-TIME TO                                 
      *              PV-UPCPLS-EFF-BEG-TMST (11:16)                             
      *        PERFORM 7600-CALC-END-TMST                                       
      *    END-IF.                                                              
                                                                                
      ******************************************************************        
      * BUILD THE E-BUSINESS LOCATION INTERNAL TABLE                            
      ******************************************************************        
       1100-BUILD-LOC-TBL.                                                      
                                                                                
           MOVE 'N' TO PS-LOC-CSR-SW.                                           
           PERFORM 1110-OPEN-LOC-CSR.                                           
           PERFORM 1120-FETCH-LOC-CSR.                                          
                                                                                
           DISPLAY 'NON ECOM AND NON LOC ZERO LOCATIONS'                        
           PERFORM VARYING LOC-INDEX FROM 1 BY 1                                
                             UNTIL LOC-INDEX > LOC-TBL-MAX                      
                                OR PS-END-OF-LOC-CSR                            
             MOVE XMT00001-LOC-NBR      TO LOC-NBR(LOC-INDEX)                   
             DISPLAY LOC-NBR(LOC-INDEX)                                         
             ADD 1 TO LOC-TBL-SIZE                                              
             PERFORM 1120-FETCH-LOC-CSR                                         
           END-PERFORM.                                                         
                                                                                
           DISPLAY 'TOTAL LOCATIONS LOADED ' LOC-TBL-SIZE                       
                                                                                
           IF LOC-INDEX >  LOC-TBL-MAX                                          
             DISPLAY '*********** ABEND ************************'               
             DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME                     
             DISPLAY '**  PARAGRAPH ** => ' PV-PARAGRAPH-NAME                   
             DISPLAY ' TABLE OVERFLOW ON LOCATION-TABLE         '               
             DISPLAY ' INCREASE LOC-TBL-MAX LIMIT               '               
             DISPLAY ' CURRENT LIMIT = ' LOC-TBL-MAX                            
             DISPLAY '******************************************'               
             DISPLAY '  '                                                       
             MOVE +4004 TO ABEND-CODE                                           
             CALL 'ILBOABN0' USING ABEND-CODE                                   
           END-IF.                                                              
                                                                                
           PERFORM 1130-CLOSE-LOC-CSR.                                          
                                                                                
      ******************************************************************02170000
      *  OPEN THE CURSOR TO RETRIEVE E-BUSINESS LOCATIONS              *02180000
      ******************************************************************02190000
       1110-OPEN-LOC-CSR.                                               02200000
                                                                        02210000
           EXEC SQL                                                     02220000
               OPEN LOC_CSR                                             02230000
           END-EXEC.                                                    02240000
                                                                        02250000
           EVALUATE TRUE                                                02260000
               WHEN SQLCODE = ZERO                                      02270000
                   CONTINUE                                             02280000
               WHEN OTHER                                               02290000
                   MOVE '1110-OPEN-LOC-CSR'   TO PV-PARAGRAPH-NAME      02300000
                   MOVE 'OPEN LOC_CSR'    TO PV-DB2-OPERATION-ATTEMPTED 02310000
                   PERFORM 9999-SQL-ABEND                               02320000
           END-EVALUATE.                                                02330000
                                                                        02340000
      ******************************************************************02350000
      *  FETCH THE E-BUSINESS LOCATIONS CURSOR                         *02360000
      ******************************************************************02370000
       1120-FETCH-LOC-CSR.                                              02380000
                                                                        02390000
           EXEC SQL                                                     02400000
               FETCH LOC_CSR                                            02410000
               INTO   :XMT00001-LOC-NBR                                 02420000
           END-EXEC.                                                    02430000
                                                                        02440000
           EVALUATE TRUE                                                02450000
               WHEN SQLCODE = ZERO                                      02460000
                   CONTINUE                                             02470000
               WHEN SQLCODE = +100                                      02480000
                   SET PS-END-OF-LOC-CSR TO TRUE                        02490000
               WHEN OTHER                                               02500000
                   MOVE '1120-FETCH-LOC-CSR'   TO PV-PARAGRAPH-NAME     02510000
                   MOVE 'FETCH LOC_CSR' TO PV-DB2-OPERATION-ATTEMPTED   02520000
                   PERFORM 9999-SQL-ABEND                               02530000
           END-EVALUATE.                                                02540000
                                                                        02550000
      ******************************************************************02560000
      *  CLOSE THE E-BUSINESS LOCATIONS CURSOR                         *02570000
      ******************************************************************02580000
       1130-CLOSE-LOC-CSR.                                              02590000
                                                                        02600000
           EXEC SQL                                                     02610000
               CLOSE LOC_CSR                                            02620000
           END-EXEC.                                                    02630000
                                                                        02640000
           EVALUATE TRUE                                                02650000
               WHEN SQLCODE = ZERO                                      02660000
                   CONTINUE                                             02670000
               WHEN OTHER                                               02680000
                   MOVE '1130-CLOSE-LOC-CSR'   TO PV-PARAGRAPH-NAME     02690000
                   MOVE 'CLOSE LOC_CSR'    TO PV-DB2-OPERATION-ATTEMPTED02700000
                   PERFORM 9999-SQL-ABEND                               02710000
           END-EVALUATE.                                                02720000
                                                                                
      ******************************************************************        
      * 2000-PROCESS-SKU-UPDATES.                                      *        
      *  ==> PROCESSES:                                                *        
      *    - PROCESSES A SKU UPDATE RECORD                             *        
      *    - UPDATE TUPCPLS ROWS                                       *        
      ******************************************************************        
       2000-PROCESS-SKU-UPDATES.                                                
           MOVE '2000-PROCESS-SKU-UPDATES' TO PV-PARAGRAPH-NAME.                
                                                                                
           DISPLAY 'PV-SKU ' PV-SKU                                             
           MOVE PV-SKU                TO SKXREF-SKU.                            
           PERFORM 2010-SELECT-TSKXREF.                                         
           MOVE SKXREF-ITM-NBR     TO UPCPLS-ITM-NBR                            
                                      PV-DISPLAY-15.                            
           MOVE ZERO               TO UPCPLS-STR-NBR                            
                                      PV-DISPLAY-4.                             
           PERFORM 2015-SELECT-TUPCPLS.                                         
           IF PV-ORIG-EFF-END-NULL-IND > -1                                     
             PERFORM 2020-SELECT-TUPCPLS                                        
             IF SQLCODE = +100                                                  
                 DISPLAY 'TUPCPLS ORIG LOC ZERO NOT FOUND'                      
                 DISPLAY 'ITM NBR          '  PV-DISPLAY-15                     
                 DISPLAY 'LOC NBR          '  PV-DISPLAY-4                      
             ELSE                                                               
                 PERFORM 2025-SELECT-TUPCPLS                                    
                 IF SQLCODE = +100                                              
                     DISPLAY 'TUPCPLS CURR LOC ZERO NOT FOUND'                  
                     DISPLAY 'ITM NBR          '  PV-DISPLAY-15                 
                     DISPLAY 'LOC NBR          '  PV-DISPLAY-4                  
                 ELSE                                                           
                   MOVE PV-UPCPLS-EFF-END-TMST TO UPCPLS-EFF-END-TMST           
                   IF CC-UPDATE-IND = 'U'                                       
                       PERFORM 2030-UPDATE-TUPCPLS                              
                       IF SQLCODE = +100                                        
                           DISPLAY 'TUPCPLS LOC ZERO NOT FOUND'                 
                           DISPLAY 'ITM NBR          '  PV-DISPLAY-15           
                           DISPLAY 'LOC NBR          '  PV-DISPLAY-4            
                       ELSE                                                     
                           DISPLAY 'TUPCPLS LOC ZERO FOUND'                     
                           DISPLAY 'ITM NBR          '  PV-DISPLAY-15           
                           DISPLAY 'LOC NBR          '  PV-DISPLAY-4            
                           MOVE '25' TO UPCPLS-UPC-STAT-CDE                     
                      MOVE PV-UPCPLS-EFF-BEG-TMST TO UPCPLS-EFF-BEG-TMST        
                           PERFORM 2040-INSERT-TUPCPLS                          
                       END-IF                                                   
                   ELSE                                                         
                       PERFORM 2035-SELECT-TUPCPLS                              
                   END-IF                                                       
                 END-IF                                                         
             END-IF                                                             
           END-IF.                                                              
                                                                                
           IF PV-ORIG-EFF-END-NULL-IND > -1                                     
               PERFORM VARYING LOC-INDEX FROM 1 BY 1                            
                                 UNTIL LOC-INDEX > LOC-TBL-SIZE                 
                   MOVE LOC-NBR(LOC-INDEX) TO PV-DISPLAY-4                      
                   PERFORM 2100-LOC-PROCESSING                                  
               END-PERFORM                                                      
                                                                                
               IF CC-UPDATE-IND = 'U'                                           
                   PERFORM 7800-COMMIT                                          
               END-IF                                                           
                                                                                
               MOVE SKXREF-SKU         TO PV-DISPLAY-8                          
               IF CC-UPDATE-IND = 'U'                                           
                  DISPLAY 'SKU COMPLETED/COMMITTED ' PV-DISPLAY-8               
               END-IF                                                           
           END-IF.                                                              
                                                                                
           ADD +1 TO CR-RECS-PROCESSED-CNTR.                                    
      *    SET PS-NOT-CHECKPOINT-RESTART TO TRUE.                               
                                                                                
           PERFORM 4000-READ-RECORD.                                            
                                                                                
      ******************************************************************        
      * 2010-SELECT-TSKXREF.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - READS TSKXREF                                             *        
      ******************************************************************        
       2010-SELECT-TSKXREF.                                                     
           MOVE '2010-SELECT-TSKXREF' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                      
               EXEC SQL                                                         
                   SELECT  ITM_NBR                                              
                     INTO :SKXREF-ITM-NBR                                       
                     FROM  TSKXREF                                              
                    WHERE  SKU        = :SKXREF-SKU                             
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = +100                                          
                       MOVE 'READ TSKXREF TABLE'                                
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'READ TSKXREF TABLE'                                
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'READ TSKXREF TABLE'                                        
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2015-SELECT-TUPCPLS.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - SELECT THE ORIGINAL LOC ZERO ROW FROM THE TUPCPLS TABLE.  *        
      ******************************************************************        
       2015-SELECT-TUPCPLS.                                                     
           MOVE '2015-SELECT-TUPCPLS' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
               EXEC SQL                                                         
                   SELECT MAX(EFF_END_TMST)                                     
                    INTO :UPCPLS-EFF-END-TMST :PV-ORIG-EFF-END-NULL-IND         
                     FROM TUPCPLS                                               
                    WHERE ITM_NBR      = :UPCPLS-ITM-NBR                        
                      AND STR_NBR      = :UPCPLS-STR-NBR                        
      *               AND EFF_BEG_TMST < :PV-UPCPLS-EFF-BEG-TMST                
      *               AND (EFF_END_TMST IS NULL OR                              
      *                    EFF_END_TMST >= :PV-UPCPLS-EFF-BEG-TMST)             
      *               AND EFF_END_TMST > :PV-UPCPLS-EFF-BEG-TMST                
                      AND EFF_END_TMST IS NOT NULL                              
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                      DISPLAY 'UPCPLS-EFF-END-TMST '                            
                               UPCPLS-EFF-END-TMST                              
      *               MOVE PV-EFF-END-NULL-IND                                  
      *                                   TO PV-EFF-END-NULL-IND-UNP            
      *               MOVE PV-EFF-END-NULL-IND-UNP                              
      *                                   TO PV-EFF-END-NULL-IND-DIS            
      *           DISPLAY 'PV-EFF-END-NULL-IND ' PV-EFF-END-NULL-IND-DIS        
                      IF PV-ORIG-EFF-END-NULL-IND < 0                           
                          DISPLAY 'ITM NBR ' PV-DISPLAY-15                      
                          DISPLAY 'ORIG LOC ZERO NOT FOUND'                     
                          DISPLAY 'SKU BYPASSED           '                     
                      END-IF                                                    
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'READ TUPCPLS TABLE'                                
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'READ TUPCPLS TABLE'                                        
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2020-SELECT-TUPCPLS.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - SELECT THE ORIGINAL LOC ZERO ROW FROM THE TUPCPLS TABLE.  *        
      ******************************************************************        
       2020-SELECT-TUPCPLS.                                                     
           MOVE '2020-SELECT-TUPCPLS' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
               EXEC SQL                                                         
                   SELECT UPC_ID                                                
                        , OPERCO_NBR                                            
                        , STR_NBR                                               
                        , EFF_BEG_TMST                                          
                        , EFF_END_TMST                                          
                        , UPC_STAT_CDE                                          
                        , UNT_COST_AMT                                          
                        , UNT_RTL_AMT                                           
                        , MEITGP_NBR                                            
                        , CRE_BY_USR_ID                                         
                        , CRE_TMST                                              
                        , ITM_NBR                                               
                        , PLU_IND                                               
                        , UPC_STAT_CHG_DTE                                      
                        , UNT_RTL_CHG_DTE                                       
                        , ORIG_CLR_DTE                                          
                        , MKDN_CDE                                              
                    INTO :UPCPLS-UPC-ID                                         
                        ,:UPCPLS-OPERCO-NBR                                     
                        ,:UPCPLS-STR-NBR                                        
                        ,:UPCPLS-EFF-BEG-TMST                                   
                        ,:UPCPLS-EFF-END-TMST :PV-EFF-END-NULL-IND              
                        ,:UPCPLS-UPC-STAT-CDE                                   
                        ,:UPCPLS-UNT-COST-AMT :PV-UNT-COST-NULL-IND             
                        ,:UPCPLS-UNT-RTL-AMT                                    
                        ,:UPCPLS-MEITGP-NBR                                     
                        ,:UPCPLS-CRE-BY-USR-ID                                  
                        ,:UPCPLS-CRE-TMST                                       
                        ,:UPCPLS-ITM-NBR                                        
                        ,:UPCPLS-PLU-IND                                        
                        ,:UPCPLS-UPC-STAT-CHG-DTE                               
                        ,:UPCPLS-UNT-RTL-CHG-DTE                                
                        ,:UPCPLS-ORIG-CLR-DTE :PV-ORIG-CLR-NULL-IND             
                        ,:UPCPLS-MKDN-CDE     :PV-MKDN-CDE-NULL-IND             
                     FROM TUPCPLS                                               
                    WHERE ITM_NBR      = :UPCPLS-ITM-NBR                        
                      AND STR_NBR      = :UPCPLS-STR-NBR                        
      *               AND EFF_BEG_TMST < :PV-UPCPLS-EFF-BEG-TMST                
      *               AND (EFF_END_TMST IS NULL OR                              
      *                    EFF_END_TMST >= :PV-UPCPLS-EFF-BEG-TMST)             
      *               AND EFF_END_TMST > :PV-UPCPLS-EFF-BEG-TMST                
                      AND EFF_END_TMST = :UPCPLS-EFF-END-TMST                   
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       SET PS-CURRENT-ROW-FOUND       TO TRUE                   
                       MOVE UPCPLS-UNT-RTL-AMT        TO                        
                                         PV-ORIG-UNT-RTL-AMT                    
                       MOVE UPCPLS-UNT-COST-AMT       TO                        
                                         PV-ORIG-UNT-COST-AMT                   
      *                                                                         
      *  A 'NOT FOUND' IS VALID FOR A STORE SPECIFIC ROW, BUT SHOULD            
      *  NEVER HAPPEN FOR A '000' STORE ROW.                                    
      *                                                                         
                   WHEN SQLCODE = +100                                          
                       IF UPCPLS-STR-NBR  = ZEROES                              
                           DISPLAY 'ITM NBR ' PV-DISPLAY-15                     
                           MOVE 'READ TUPCPLS TABLE '                           
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                           PERFORM 9999-SQL-ABEND                               
                       ELSE                                                     
                           SET PS-CURRENT-ROW-NOT-FOUND TO TRUE                 
                       END-IF                                                   
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'READ TUPCPLS TABLE'                                
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'READ TUPCPLS TABLE'                                        
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2025-SELECT-TUPCPLS.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - SELECT THE CURRENT LOC ZERO ROW FROM THE TUPCPLS TABLE.   *        
      ******************************************************************        
       2025-SELECT-TUPCPLS.                                                     
           MOVE '2025-SELECT-TUPCPLS' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
               EXEC SQL                                                         
                   SELECT UPC_ID                                                
                        , OPERCO_NBR                                            
                        , STR_NBR                                               
                        , EFF_BEG_TMST                                          
                        , EFF_END_TMST                                          
                        , UPC_STAT_CDE                                          
                        , UNT_COST_AMT                                          
                        , UNT_RTL_AMT                                           
                        , MEITGP_NBR                                            
                        , CRE_BY_USR_ID                                         
                        , CRE_TMST                                              
                        , ITM_NBR                                               
                        , PLU_IND                                               
                        , UPC_STAT_CHG_DTE                                      
                        , UNT_RTL_CHG_DTE                                       
                        , ORIG_CLR_DTE                                          
                        , MKDN_CDE                                              
                    INTO :UPCPLS-UPC-ID                                         
                        ,:UPCPLS-OPERCO-NBR                                     
                        ,:UPCPLS-STR-NBR                                        
                        ,:UPCPLS-EFF-BEG-TMST                                   
                        ,:UPCPLS-EFF-END-TMST :PV-EFF-END-NULL-IND              
                        ,:UPCPLS-UPC-STAT-CDE                                   
                        ,:UPCPLS-UNT-COST-AMT :PV-UNT-COST-NULL-IND             
                        ,:UPCPLS-UNT-RTL-AMT                                    
                        ,:UPCPLS-MEITGP-NBR                                     
                        ,:UPCPLS-CRE-BY-USR-ID                                  
                        ,:UPCPLS-CRE-TMST                                       
                        ,:UPCPLS-ITM-NBR                                        
                        ,:UPCPLS-PLU-IND                                        
                        ,:UPCPLS-UPC-STAT-CHG-DTE                               
                        ,:UPCPLS-UNT-RTL-CHG-DTE                                
                        ,:UPCPLS-ORIG-CLR-DTE :PV-ORIG-CLR-NULL-IND             
                        ,:UPCPLS-MKDN-CDE     :PV-MKDN-CDE-NULL-IND             
                     FROM TUPCPLS                                               
                    WHERE ITM_NBR      = :UPCPLS-ITM-NBR                        
                      AND STR_NBR      = :UPCPLS-STR-NBR                        
      *               AND EFF_BEG_TMST < :PV-UPCPLS-EFF-BEG-TMST                
      *               AND (EFF_END_TMST IS NULL OR                              
      *                    EFF_END_TMST >= :PV-UPCPLS-EFF-BEG-TMST)             
      *               AND EFF_END_TMST > :PV-UPCPLS-EFF-BEG-TMST                
                      AND EFF_END_TMST IS NULL                                  
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       SET PS-CURRENT-ROW-FOUND       TO TRUE                   
                       MOVE UPCPLS-UNT-RTL-AMT        TO                        
                                         PV-CURR-UNT-RTL-AMT                    
                       MOVE UPCPLS-UNT-COST-AMT       TO                        
                                         PV-CURR-UNT-COST-AMT                   
      *                                                                         
      *  A 'NOT FOUND' IS VALID FOR A STORE SPECIFIC ROW, BUT SHOULD            
      *  NEVER HAPPEN FOR A '000' STORE ROW.                                    
      *                                                                         
                   WHEN SQLCODE = +100                                          
                       IF UPCPLS-STR-NBR  = ZEROES                              
                           MOVE 'READ TUPCPLS TABLE '                           
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                           PERFORM 9999-SQL-ABEND                               
                       ELSE                                                     
                           SET PS-CURRENT-ROW-NOT-FOUND TO TRUE                 
                       END-IF                                                   
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'READ TUPCPLS TABLE'                                
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'READ TUPCPLS TABLE'                                        
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2030-UPDATE-TUPCPLS.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - UPDATE THE CURRENT LOC ZERO ROW FROM THE TUPCPLS TABLE.   *        
      ******************************************************************        
       2030-UPDATE-TUPCPLS.                                                     
           MOVE '2030-UPDATE-TUPCPLS' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
                                                                                
                 EXEC SQL                                                       
                    UPDATE TUPCPLS                                              
                        SET EFF_END_TMST   = :PV-UPCPLS-EFF-END-TMST            
                        WHERE ITM_NBR      = :UPCPLS-ITM-NBR                    
                          AND STR_NBR      = :UPCPLS-STR-NBR                    
                          AND EFF_END_TMST IS NULL                              
                 END-EXEC                                                       
                                                                                
                 EVALUATE TRUE                                                  
                     WHEN SQLCODE = ZERO                                        
                         ADD SQLERRD(3) TO CR-TUPCPLS-ROW-UPD-CNTR              
                     WHEN SQLCODE = -913                                        
                         ADD +1      TO PV-RETRY-COUNT                          
                     WHEN OTHER                                                 
                         MOVE 'UPDATE TUPCPLS TABLE'                            
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                         PERFORM 9999-SQL-ABEND                                 
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'UPDATE TUPCPLS TABLE'                                      
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2035-SELECT-TUPCPLS.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - SELECT THE CURRENT LOC ROW FROM THE TUPCPLS TABLE.        *        
      ******************************************************************        
       2035-SELECT-TUPCPLS.                                                     
           MOVE '2035-SELECT-TUPCPLS' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
                                                                                
                 EXEC SQL                                                       
                    SELECT  UPC_STAT_CDE                                        
                      INTO :UPCPLS-UPC-STAT-CDE                                 
                      FROM TUPCPLS                                              
                    WHERE ITM_NBR      = :UPCPLS-ITM-NBR                        
                      AND STR_NBR      = :UPCPLS-STR-NBR                        
                      AND EFF_END_TMST IS NULL                                  
                    WITH UR                                                     
                 END-EXEC                                                       
                                                                                
                 EVALUATE TRUE                                                  
                     WHEN SQLCODE = ZERO                                        
                       DISPLAY 'TUPCPLS LOC ZERO FOUND'                         
                       DISPLAY 'ITM NBR          '  PV-DISPLAY-15               
                       DISPLAY 'LOC NBR          '  PV-DISPLAY-4                
                    DISPLAY 'LOC ZERO UPC STAT CDE ' UPCPLS-UPC-STAT-CDE        
                     WHEN SQLCODE = +100                                        
                       DISPLAY 'TUPCPLS LOC ZERO NOT FOUND'                     
                       DISPLAY 'ITM NBR          '  PV-DISPLAY-15               
                       DISPLAY 'LOC NBR          '  PV-DISPLAY-4                
                     WHEN SQLCODE = -913                                        
                         ADD +1      TO PV-RETRY-COUNT                          
                     WHEN OTHER                                                 
                         MOVE 'SELECT TUPCPLS TABLE'                            
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                         PERFORM 9999-SQL-ABEND                                 
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'SELECT TUPCPLS TABLE'                                      
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2040-INSERT-TUPCPLS.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - INSERT A NEW STORE ROW FOR A UPC ID ON THE TUPCPLS TABLE. *        
      ******************************************************************        
       2040-INSERT-TUPCPLS.                                                     
           MOVE '2040-INSERT-TUPCPLS' TO PV-PARAGRAPH-NAME.                     
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
           MOVE PV-ORIG-UNT-RTL-AMT  TO UPCPLS-UNT-RTL-AMT.                     
           MOVE PV-ORIG-UNT-COST-AMT TO UPCPLS-UNT-COST-AMT.                    
           MOVE PV-UPCPLS-EFF-BEG-TMST TO UPCPLS-EFF-BEG-TMST                   
           MOVE CC-END-DATE            TO UPCPLS-UPC-STAT-CHG-DTE               
                                          UPCPLS-UNT-RTL-CHG-DTE                
           MOVE SPACES TO UPCPLS-EFF-END-TMST                                   
                          UPCPLS-ORIG-CLR-DTE                                   
                          UPCPLS-MKDN-CDE                                       
           IF UPCPLS-EFF-END-TMST = SPACES                                      
               MOVE -1 TO PV-EFF-END-NULL-IND                                   
           ELSE                                                                 
               MOVE 0  TO PV-EFF-END-NULL-IND                                   
           END-IF.                                                              
           IF UPCPLS-ORIG-CLR-DTE = SPACES                                      
               MOVE -1 TO PV-ORIG-CLR-NULL-IND                                  
           ELSE                                                                 
               MOVE 0  TO PV-ORIG-CLR-NULL-IND                                  
           END-IF.                                                              
           IF UPCPLS-MKDN-CDE = SPACE                                           
               MOVE -1 TO PV-MKDN-CDE-NULL-IND                                  
           ELSE                                                                 
               MOVE 0  TO PV-MKDN-CDE-NULL-IND                                  
           END-IF.                                                              
                                                                                
           DISPLAY ':PV-UPCPLS-EFF-BEG-TMST ' PV-UPCPLS-EFF-BEG-TMST            
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
PK0608         OR SQLCODE = -803                                                
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                   INSERT INTO TUPCPLS                                          
                           (UPC_ID                                              
                           ,OPERCO_NBR                                          
                           ,STR_NBR                                             
                           ,EFF_BEG_TMST                                        
                           ,EFF_END_TMST                                        
                           ,UPC_STAT_CDE                                        
                           ,UNT_COST_AMT                                        
                           ,UNT_RTL_AMT                                         
                           ,MEITGP_NBR                                          
                           ,CRE_BY_USR_ID                                       
                           ,CRE_TMST                                            
                           ,ITM_NBR                                             
                           ,PLU_IND                                             
                           ,UPC_STAT_CHG_DTE                                    
                           ,UNT_RTL_CHG_DTE                                     
                           ,ORIG_CLR_DTE                                        
                           ,MKDN_CDE)                                           
                   VALUES (:UPCPLS-UPC-ID                                       
                          ,'03'                                                 
                          ,:UPCPLS-STR-NBR                                      
                          ,:UPCPLS-EFF-BEG-TMST                                 
                          ,:UPCPLS-EFF-END-TMST :PV-EFF-END-NULL-IND            
                          ,'25'                                                 
                          ,:UPCPLS-UNT-COST-AMT :PV-UNT-COST-NULL-IND           
                          ,:UPCPLS-UNT-RTL-AMT                                  
                          ,:UPCPLS-MEITGP-NBR                                   
                          ,:PC-PROGRAM-NAME                                     
                          ,CURRENT TIMESTAMP                                    
                          ,:UPCPLS-ITM-NBR                                      
                          ,'N'                                                  
                          ,:UPCPLS-UPC-STAT-CHG-DTE                             
                          ,:UPCPLS-UNT-RTL-CHG-DTE                              
                          ,:UPCPLS-ORIG-CLR-DTE :PV-ORIG-CLR-NULL-IND           
                          ,:UPCPLS-MKDN-CDE :PV-MKDN-CDE-NULL-IND )             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       ADD SQLERRD(3) TO CR-TUPCPLS-ROW-INS-CNTR                
                   WHEN SQLCODE = -803                                          
PK0608                 DISPLAY 'SKU NUMBER: ' PV-DISPLAY-8              0672    
PK0608                         ' STORE: ' PV-DISPLAY-4                  0672    
PK0608                 ADD +1 TO CR-SKU-STRS-BYPASSED-CNTR                      
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'INSERT TUPCPLS TABLE'                              
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'INSERT TUPCPLS TABLE'                                      
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2100-LOC-PROCESSING.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORM SKU/LOC TUPCPLS STORE ZERO ROW UPDATES            *        
      ******************************************************************        
       2100-LOC-PROCESSING.                                                     
           MOVE '2100-LOC-PROCESSING' TO PV-PARAGRAPH-NAME.                     
                                                                                
           MOVE SKXREF-ITM-NBR     TO UPCPLS-ITM-NBR                            
                                      PV-DISPLAY-15.                            
           MOVE LOC-NBR(LOC-INDEX) TO UPCPLS-STR-NBR                            
                                      PV-DISPLAY-4.                             
                                                                                
           PERFORM 2110-SELECT-TUPCPLS.                                         
                                                                                
           IF SQLCODE = +100                                                    
               DISPLAY '2110-SELECT-TUPCPLS'                                    
               DISPLAY 'TUPCPLS LOC STAT NOT FOUND'                             
               DISPLAY 'ITM NBR          '  PV-DISPLAY-15                       
               DISPLAY 'LOC NBR          '  PV-DISPLAY-4                        
               IF CC-UPDATE-IND = 'U'                                           
                  PERFORM 2120-INSERT-TUPCPLS                                   
               END-IF                                                           
           ELSE                                                                 
               DISPLAY '2110-SELECT-TUPCPLS'                                    
               DISPLAY 'TUPCPLS LOC STAT FOUND'                                 
               DISPLAY 'ITM NBR          '  PV-DISPLAY-15                       
               DISPLAY 'LOC NBR          '  PV-DISPLAY-4                        
               DISPLAY 'UPCPLS-UPC-STAT-CDE ' UPCPLS-UPC-STAT-CDE               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2110-SELECT-TUPCPLS.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - SELECT THE PREVIOUS LOC ROW FROM THE TUPCPLS TABLE.       *        
      ******************************************************************        
       2110-SELECT-TUPCPLS.                                                     
           MOVE '2110-SELECT-TUPCPLS' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
               EXEC SQL                                                         
                   SELECT UPC_ID                                                
                        , OPERCO_NBR                                            
                        , STR_NBR                                               
                        , EFF_BEG_TMST                                          
                        , EFF_END_TMST                                          
                        , UPC_STAT_CDE                                          
                        , UNT_COST_AMT                                          
                        , UNT_RTL_AMT                                           
                        , MEITGP_NBR                                            
                        , CRE_BY_USR_ID                                         
                        , CRE_TMST                                              
                        , ITM_NBR                                               
                        , PLU_IND                                               
                        , UPC_STAT_CHG_DTE                                      
                        , UNT_RTL_CHG_DTE                                       
                        , ORIG_CLR_DTE                                          
                        , MKDN_CDE                                              
                    INTO :UPCPLS-UPC-ID                                         
                        ,:UPCPLS-OPERCO-NBR                                     
                        ,:UPCPLS-STR-NBR                                        
                        ,:UPCPLS-EFF-BEG-TMST                                   
                        ,:UPCPLS-EFF-END-TMST :PV-EFF-END-NULL-IND              
                        ,:UPCPLS-UPC-STAT-CDE                                   
                        ,:UPCPLS-UNT-COST-AMT :PV-UNT-COST-NULL-IND             
                        ,:UPCPLS-UNT-RTL-AMT                                    
                        ,:UPCPLS-MEITGP-NBR                                     
                        ,:UPCPLS-CRE-BY-USR-ID                                  
                        ,:UPCPLS-CRE-TMST                                       
                        ,:UPCPLS-ITM-NBR                                        
                        ,:UPCPLS-PLU-IND                                        
                        ,:UPCPLS-UPC-STAT-CHG-DTE                               
                        ,:UPCPLS-UNT-RTL-CHG-DTE                                
                        ,:UPCPLS-ORIG-CLR-DTE :PV-ORIG-CLR-NULL-IND             
                        ,:UPCPLS-MKDN-CDE     :PV-MKDN-CDE-NULL-IND             
                     FROM TUPCPLS                                               
                    WHERE ITM_NBR      = :UPCPLS-ITM-NBR                        
                      AND STR_NBR      = :UPCPLS-STR-NBR                        
      *               AND EFF_BEG_TMST < :PV-UPCPLS-EFF-BEG-TMST                
      *               AND (EFF_END_TMST IS NULL OR                              
      *                    EFF_END_TMST >= :PV-UPCPLS-EFF-BEG-TMST)             
                      AND EFF_END_TMST IS NULL                                  
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       SET PS-CURRENT-LOC-ROW-FOUND       TO TRUE               
      *                                                                         
      *  A 'NOT FOUND' IS VALID FOR A STORE SPECIFIC ROW, BUT SHOULD            
      *  NEVER HAPPEN FOR A '000' STORE ROW.                                    
      *                                                                         
                   WHEN SQLCODE = +100                                          
                       IF UPCPLS-STR-NBR  = ZEROES                              
                           MOVE 'READ TUPCPLS TABLE '                           
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                           PERFORM 9999-SQL-ABEND                               
                       ELSE                                                     
                           SET PS-CURRENT-LOC-ROW-NOT-FOUND TO TRUE             
                       END-IF                                                   
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'READ TUPCPLS TABLE'                                
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'READ TUPCPLS TABLE'                                        
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2120-INSERT-TUPCPLS.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - INSERT A NEW STORE ROW FOR A UPC ID ON THE TUPCPLS TABLE. *        
      ******************************************************************        
       2120-INSERT-TUPCPLS.                                                     
           MOVE '2120-INSERT-TUPCPLS' TO PV-PARAGRAPH-NAME.                     
                                                                                
           MOVE PV-CURR-UNT-RTL-AMT  TO UPCPLS-UNT-RTL-AMT.                     
           MOVE PV-CURR-UNT-COST-AMT TO UPCPLS-UNT-COST-AMT.                    
           MOVE PV-UPCPLS-EFF-BEG-TMST TO UPCPLS-EFF-BEG-TMST                   
           MOVE CC-END-DATE            TO UPCPLS-UPC-STAT-CHG-DTE               
                                          UPCPLS-UNT-RTL-CHG-DTE                
                                          UPCPLS-ORIG-CLR-DTE                   
           MOVE SPACES TO UPCPLS-EFF-END-TMST                                   
           MOVE '1'                    TO UPCPLS-MKDN-CDE                       
           IF UPCPLS-EFF-END-TMST = SPACES                                      
               MOVE -1 TO PV-EFF-END-NULL-IND                                   
           ELSE                                                                 
               MOVE 0  TO PV-EFF-END-NULL-IND                                   
           END-IF.                                                              
           IF UPCPLS-ORIG-CLR-DTE = SPACES                                      
               MOVE -1 TO PV-ORIG-CLR-NULL-IND                                  
           ELSE                                                                 
               MOVE 0  TO PV-ORIG-CLR-NULL-IND                                  
           END-IF.                                                              
           IF UPCPLS-MKDN-CDE = SPACE                                           
               MOVE -1 TO PV-MKDN-CDE-NULL-IND                                  
           ELSE                                                                 
               MOVE 0  TO PV-MKDN-CDE-NULL-IND                                  
           END-IF.                                                              
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
PK0608         OR SQLCODE = -803                                                
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                   INSERT INTO TUPCPLS                                          
                           (UPC_ID                                              
                           ,OPERCO_NBR                                          
                           ,STR_NBR                                             
                           ,EFF_BEG_TMST                                        
                           ,EFF_END_TMST                                        
                           ,UPC_STAT_CDE                                        
                           ,UNT_COST_AMT                                        
                           ,UNT_RTL_AMT                                         
                           ,MEITGP_NBR                                          
                           ,CRE_BY_USR_ID                                       
                           ,CRE_TMST                                            
                           ,ITM_NBR                                             
                           ,PLU_IND                                             
                           ,UPC_STAT_CHG_DTE                                    
                           ,UNT_RTL_CHG_DTE                                     
                           ,ORIG_CLR_DTE                                        
                           ,MKDN_CDE)                                           
                   VALUES (:UPCPLS-UPC-ID                                       
                          ,'03'                                                 
                          ,:UPCPLS-STR-NBR                                      
                          ,:UPCPLS-EFF-BEG-TMST                                 
                          ,:UPCPLS-EFF-END-TMST :PV-EFF-END-NULL-IND            
                          ,'30'                                                 
                          ,:UPCPLS-UNT-COST-AMT :PV-UNT-COST-NULL-IND           
                          ,:UPCPLS-UNT-RTL-AMT                                  
                          ,:UPCPLS-MEITGP-NBR                                   
                          ,:PC-PROGRAM-NAME                                     
                          ,CURRENT TIMESTAMP                                    
                          ,:UPCPLS-ITM-NBR                                      
                          ,'N'                                                  
                          ,:UPCPLS-UPC-STAT-CHG-DTE                             
                          ,:UPCPLS-UNT-RTL-CHG-DTE                              
                          ,:UPCPLS-ORIG-CLR-DTE :PV-ORIG-CLR-NULL-IND           
                          ,:UPCPLS-MKDN-CDE :PV-MKDN-CDE-NULL-IND )             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       ADD SQLERRD(3) TO CR-TUPCPLS-ROW-INS-CNTR                
                   WHEN SQLCODE = -803                                          
PK0608                 DISPLAY 'SKU NUMBER: ' PV-DISPLAY-8              0672    
PK0608                         ' STORE: ' PV-DISPLAY-4                  0672    
PK0608                 ADD +1 TO CR-SKU-STRS-BYPASSED-CNTR                      
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'INSERT TUPCPLS TABLE'                              
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'INSERT TUPCPLS TABLE'                                      
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 3000-EOJ-PROCESSING.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - CHECK IF LAST SKU PROCESSED SHOULD BE COMPRESSED          *        
      *    - DELETE STYLE RESERVATION FOR LAST SKU PROCESSED           *        
      *    - CLOSE FILES                                               *        
      *    - DISPLAYS PROCESSING TOTALS                                *        
      *    - TAKES A FINAL COMMIT AND UPDATES THE CHECKPOINT STATUS    *        
      *      TO INDICATE SUCESSFULL COMPLETION                         *        
      ******************************************************************        
       3000-EOJ-PROCESSING.                                                     
           MOVE '3000-EOJ-PROCESSING' TO PV-PARAGRAPH-NAME.                     
                                                                                
           CLOSE SKU-UPDATE-FILE                                                
                 CONTROL-CARD-FILE.                                             
                                                                                
           PERFORM 9990-DISPLAY-CONTROL-INFO.                                   
                                                                                
      ******************************************************************        
      * 4000-READ-RECORD.                                              *        
      *  ==> PROCESSES:                                                *        
      *    - READS THE NEXT RECORD IN THE INPUT FILE.                  *        
      ******************************************************************        
       4000-READ-RECORD.                                                        
           MOVE '4000-READ-RECORD' TO PV-PARAGRAPH-NAME.                        
                                                                                
           READ SKU-UPDATE-FILE                                                 
               INTO PV-SKU-RECORD                                               
                   AT END                                                       
                       MOVE 'Y'     TO PS-END-OF-FILE-SW                        
                   NOT AT END                                                   
                       ADD +1       TO PV-RECS-READ-CNTR                        
                       ADD 1        TO DI-INPUT-RECS-DISPLAY                    
                       MOVE PV-SKU  TO PV-DISPLAY-8                             
           END-READ.                                                            
                                                                                
      ******************************************************************07160094
      * READ CONTROL FILE FOR JOB NAME, XS ITEM QUEUE NAME, AND        *07170094
      * PROCESS OLD DATE INDICATOR.                                    *07170094
      ******************************************************************07180094
       4100-READ-CONTROL-CARD-1.                                        07140094
           MOVE '4100-READ-CONTROL-CARD-1'  TO PV-PARAGRAPH-NAME.       07150094
                                                                        07210094
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                          
               AT END                                                           
                   DISPLAY '*** NO CONTROL CARD 1 ***'                          
                   SET PV-ABEND-4001 TO TRUE                                    
                   PERFORM 9999-ABEND.                                          
                                                                                
           DISPLAY CC-CONTROL-CARD.                                             
                                                                                
           IF CC-BEG-DATE = SPACES                                              
               DISPLAY '*** INVALID BEGIN DATE ***'                             
               SET PV-ABEND-4004 TO TRUE                                        
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
           IF CC-END-DATE = SPACES                                              
               DISPLAY '*** INVALID END DATE NAME ***'                          
               SET PV-ABEND-4005 TO TRUE                                        
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7200-SET-CURRENT-TIMESTAMP.                                    *        
      *  ==> PROCESSES:                                                *        
      *    - GET THE CURRENT TIMESTAMP FROM DB2.                       *        
      ******************************************************************        
       7200-SET-CURRENT-TIMESTAMP.                                              
           MOVE '7200-SET-CURRENT-TIMESTAMP' TO PV-PARAGRAPH-NAME.              
                                                                                
           EXEC SQL                                                             
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   MOVE PV-CURRENT-TIMESTAMP (1:10) TO                          
                          PV-RUN-DATE                                           
               WHEN OTHER                                                       
                   MOVE 'ERROR ON SET OF TIMESTAMP '                            
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 7800-COMMIT.                                                   *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.           *        
      *    - TAKE A COMMIT.                                            *        
      ******************************************************************        
       7800-COMMIT.                                                             
           MOVE '7800-COMMIT'      TO PV-PARAGRAPH-NAME.                        
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               ADD +1              TO CR-COMMIT-CNTR                            
           ELSE                                                                 
               MOVE 'ERROR ON COMMIT'                                           
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 9990-DISPLAY-CONTROL-INFO.                                     *        
      *  ==> PROCESSES:                                                *        
      *    - DISPLAYS CONTROL INFORMATION AT END OF JOB                *        
      ******************************************************************        
       9990-DISPLAY-CONTROL-INFO.                                               
                                                                                
           MOVE CR-RECS-PROCESSED-CNTR                                          
                                   TO DI-RECS-PROCESSED-CNTR.                   
           MOVE CR-RECS-READ-CNTR  TO DI-RECS-READ-CNTR.                        
           MOVE CR-SKUS-READ-CNTR  TO DI-SKUS-READ-CNTR.                        
           MOVE CR-SKUS-NOT-FOUND-CNTR                                          
                                   TO DI-SKUS-NOT-FOUND-CNTR.                   
           MOVE CR-SKU-STRS-BYPASSED-CNTR                                       
                                   TO DI-SKU-STRS-BYPASSED-CNTR.                
           MOVE CR-TUPCPLS-ROW-UPD-CNTR                                         
                                   TO DI-TUPCPLS-ROW-UPDATE-CNTR.               
           MOVE CR-TUPCPLS-ROW-INS-CNTR                                         
                                   TO DI-TUPCPLS-ROW-INSERT-CNTR.               
           MOVE CR-TUPCPLS-ROW-INACTV-CNTR                                      
                                   TO DI-TUPCPLS-ROW-INACTV-CNTR.               
           MOVE CR-TUPCPLS-ROW-DEL-CNTR                                         
                                   TO DI-TUPCPLS-ROW-DELETE-CNTR.               
           MOVE CR-COMMIT-CNTR     TO DI-COMMIT-CNTR.                           
                                                                                
           DISPLAY '********************************************'.              
           DISPLAY '**** RUN STATISTICS FOR PROGRAM IMKUP990 ***'.              
           DISPLAY '****'.                                                      
           DISPLAY 'INPUT RECS PROCESSED:  ' DI-RECS-PROCESSED-CNTR.            
           DISPLAY 'INPUT RECORDS READ:    ' DI-RECS-READ-CNTR.                 
           DISPLAY 'SKUS READ:             ' DI-SKUS-READ-CNTR.                 
           DISPLAY 'SKUS NOT FOUND:        ' DI-SKUS-NOT-FOUND-CNTR.            
           DISPLAY 'SKU/STORES BYPASSED:   ' DI-SKU-STRS-BYPASSED-CNTR.         
           DISPLAY 'TUPCPLS ROWS UPDATED:  ' DI-TUPCPLS-ROW-UPDATE-CNTR.        
           DISPLAY 'TUPCPLS ROWS INSERTED: ' DI-TUPCPLS-ROW-INSERT-CNTR.        
           DISPLAY 'TUPCPLS ROWS INACTIVD: ' DI-TUPCPLS-ROW-INACTV-CNTR.        
           DISPLAY 'TUPCPLS ROWS DELETED:  ' DI-TUPCPLS-ROW-DELETE-CNTR.        
           DISPLAY 'COMMITS TAKEN:         ' DI-COMMIT-CNTR.                    
           DISPLAY '****'.                                                      
           DISPLAY '**** RUN STATS END FOR PROGRAM IMKUP990  ***'.              
           DISPLAY '********************************************'.              
                                                                                
      ******************************************************************        
      * 9999-SQL-ABEND.                                                *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORMS ABENDS AFTER UNEXPECTED SQLCODE                  *        
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                                  
                                                                                
           DISPLAY '** ABEND **'.                                               
           DISPLAY 'IMKUP990 - ABEND'.                                          
           DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                            
           DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.                   
           DISPLAY 'SQL CODE  = ' PV-SQLCODE-DISPLAY.                           
           DISPLAY 'INPUT SKU = ' PV-DISPLAY-8.                                 
           DISPLAY 'STORE NBR = ' PV-DISPLAY-4.                                 
      *    DISPLAY 'EFFECTIVE DATE = ' IM048-UIR-EFF-DTE.                       
                                                                                
           PERFORM 9990-DISPLAY-CONTROL-INFO.                                   
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
       9999-ABEND.                                                      07730094
      ******************************************************************07740094
      * ABEND ROUTINE MISC ERRORS                                      *07750094
      ******************************************************************07760094
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           07770094
           DISPLAY '** PARAGRAPH **      = ' PV-PARAGRAPH-NAME.         07780094
           CALL 'ILBOABN0' USING ABEND-CODE.                            07800094
