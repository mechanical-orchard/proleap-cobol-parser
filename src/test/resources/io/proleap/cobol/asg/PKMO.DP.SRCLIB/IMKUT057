      *----------------------------------------------------------------*00010000
       IDENTIFICATION DIVISION.                                         00020000
      *----------------------------------------------------------------*00030000
       PROGRAM-ID.           IMKUT057.                                  00040000
       AUTHOR.               PAUL KEBER.                                00050000
       INSTALLATION.         KOHLS DEPARTMENT STORES.                   00060000
       DATE-WRITTEN          05/06/2004.                                00070000
       DATE-COMPILED.                                                   00080000
                                                                        00090000
      *----------------------------------------------------------------*00100000
      *   IMKUT057 - CREATE ITEM GROUP PRICING DOMAIN EXTRACT          *00110000
      *----------------------------------------------------------------*00130000
      *   THIS PROGRAM WILL INPUT AN ITEM GROUP PRICING DOMAIN UNLOAD  *00131000
      *   FILE AND REFORMAT IT INTO THE ITEM GROUP PRICING DOMAIN      *00132000
      *   EXTRACT FILE.                                                *00133000
      *                                                                *00134000
      *  CHANGED:                                                      *00140000
      *                                                                *00150000
      *    WR/PITS#     DATE        DESCRIPTION                        *00160000
      *    --------   ----------   ---------------------------------   *00170000
      *    WR23852    06-03-2002    INITIAL DEVELOPMENT.               *00180000
      *                                                                *00190000
      *----------------------------------------------------------------*00200000
                                                                        00210000
           EJECT                                                        00220000
      *----------------------------------------------------------------*00230000
       ENVIRONMENT DIVISION.                                            00240000
      *----------------------------------------------------------------*00250000
       CONFIGURATION SECTION.                                           00260000
                                                                        00270000
       SOURCE-COMPUTER. IBM-3090.                                       00280000
       OBJECT-COMPUTER. IBM-3090.                                       00290000
                                                                        00300000
       INPUT-OUTPUT SECTION.                                            00310000
       FILE-CONTROL.                                                    00320000
           SELECT GRP-PRIC-UNLOAD-FILE       ASSIGN TO INP01.           00330000
           SELECT GRP-PRIC-EXTRACT-FILE      ASSIGN TO OUT01.           00340000
                                                                        00350001
      *----------------------------------------------------------------*00360001
       DATA DIVISION.                                                   00370001
      *----------------------------------------------------------------*00380001
       FILE SECTION.                                                    00390001
      *                                                                 00400001
                                                                        00410000
       FD  GRP-PRIC-UNLOAD-FILE                                         00420000
           LABEL RECORDS ARE STANDARD                                   00430000
           RECORDING MODE IS F                                          00440000
           BLOCK CONTAINS 0 RECORDS.                                    00450000
       01  GRP-PRIC-UNLOAD-RECORD   PIC X(37).                          00460002
                                                                        00470000
       FD  GRP-PRIC-EXTRACT-FILE                                        00480000
           LABEL RECORDS ARE STANDARD                                   00490000
           RECORDING MODE IS F                                          00500000
           BLOCK CONTAINS 0 RECORDS.                                    00510000
       01  GRP-PRIC-EXTRACT-RECORD  PIC X(62).                          00520000
                                                                        00530000
                                                                        00540000
       WORKING-STORAGE SECTION.                                         00550000
                                                                        00560000
       01  PC-PROGRAM-CONSTANTS.                                        00570000
           05  PC-PROGRAM-NAME           PIC X(08)  VALUE 'IMKUT057'.   00580000
                                                                        00690000
        01 PV-PROGRAM-VARIABLES.                                        00700000
           05  PV-DEPT-NBR               PIC X(03)   VALUE SPACES.      00710000
           05  PV-DEPT-NBR-NUM REDEFINES PV-DEPT-NBR                    00711000
                                         PIC 9(03).                     00720000
           05  PV-BUYER-NBR              PIC X(03)   VALUE SPACES.      00730000
           05  PV-BUYER-NBR-NUM REDEFINES PV-BUYER-NBR                  00731000
                                         PIC 9(03).                     00732000
                                                                        00740000
       01  PS-PROGRAM-SWITCHES.                                         00750000
           05  PS-EOF-INPUT-FILE-SW      PIC X(01)   VALUE 'N'.         00760000
               88  PS-EOF-INPUT-FILE                 VALUE 'Y'.         00770000
                                                                        00780000
                                                                        00790000
       01  PA-PROGRAM-ACCUMUALTORS.                                     00800000
           05  PA-INPUT-RECS-READ        PIC 9(07)    VALUE 0.          00810000
           05  PA-OUTPUT-REC-WRITTEN     PIC 9(07)    VALUE 0.          00820000
                                                                        02570000
      ***************************************************************** 02651000
      *  ITEM GROUP PRICING DOMAIN UNLOAD FILE COPYBOOK                 02652000
      ******************************************************************02653000
           COPY IMRD038.                                                02654000
                                                                        02655000
      ***************************************************************** 02656000
      *  ITEM GROUP PRICING DOMAIN EXTRACT FILE COPYBOOK                02657000
      ******************************************************************02658000
           COPY IMRD039.                                                02659000
                                                                        02660000
      *----------------------------------------------------------------*02720000
       PROCEDURE DIVISION.                                              02730000
                                                                        02740000
      *----------------------------------------------------------------*02750000
      *    MAINLINE-LOGIC                                              *02760000
      *----------------------------------------------------------------*02770000
                                                                        02780000
       0000-IMKUT057.                                                   02790000
                                                                        02800000
           OPEN INPUT   GRP-PRIC-UNLOAD-FILE.                           02801000
           OPEN OUTPUT  GRP-PRIC-EXTRACT-FILE.                          02802000
           PERFORM 4000-READ-UNLOAD-FILE.                               02803000
                                                                        02820000
           PERFORM 2000-PROCESS-UNLOAD-DATA                             02830000
               UNTIL PS-EOF-INPUT-FILE.                                 02840000
                                                                        02850000
           PERFORM 9900-EOJ-LOGIC.                                      02860000
                                                                        02870000
           GOBACK.                                                      02880000
                                                                        02881000
           EJECT                                                        02890000
      *----------------------------------------------------------------*03010000
      *    2000-PROCESS-UNLOAD-DATA                                    *03020000
      *      PURPOSE: CREATE AN ITEM GROUP PRICING DOMAIN EXTRACT REC. *03040000
      *----------------------------------------------------------------*03050000
       2000-PROCESS-UNLOAD-DATA.                                        03060000
                                                                        03070000
           MOVE IM038-MEITGP-NBR      TO IM039-GP-PRIC-ID.              03080000
           MOVE IM038-MEITGP-NAME     TO IM039-GP-PRIC-DESC.            03081000
           MOVE IM038-MITGPL-QTY      TO IM039-GP-RTL-QTY.              03082000
           MOVE IM038-GP-AMT          TO IM039-GP-RTL-AMT.              03083000
           MOVE IM038-USG-RSTC-CDE    TO IM039-USG-RSTC-CDE.            03084000
           IF IM038-ORIG-MDSEDP-NBR = SPACES                            03084100
               MOVE ZERO              TO IM039-DEPT-NBR                 03085000
               MOVE -1                TO IM039-DEPT-NBR-NULL-IND        03085100
           ELSE                                                         03085200
               MOVE IM038-ORIG-MDSEDP-NBR                               03085300
                                      TO PV-DEPT-NBR                    03085400
               MOVE PV-DEPT-NBR-NUM   TO IM039-DEPT-NBR                 03085500
               MOVE ZERO              TO IM039-DEPT-NBR-NULL-IND        03085600
           END-IF.                                                      03085700
           IF IM038-ORIG-BUYER-NBR = SPACES                             03085800
               MOVE ZERO              TO IM039-BYR-NBR                  03085900
               MOVE -1                TO IM039-BYR-NBR-NULL-IND         03086000
           ELSE                                                         03086100
               MOVE IM038-ORIG-BUYER-NBR                                03086200
                                      TO PV-BUYER-NBR                   03086300
               MOVE PV-BUYER-NBR-NUM  TO IM039-BYR-NBR                  03086400
               MOVE ZERO              TO IM039-BYR-NBR-NULL-IND         03086500
           END-IF.                                                      03086600
           MOVE SPACES                TO IM039-FILLER.                  03086703
                                                                        03350000
           WRITE GRP-PRIC-EXTRACT-RECORD FROM                           03350100
                 IM039-GROUP-PRICE-EXTRACT-REC.                         03350200
           ADD 1 TO PA-OUTPUT-REC-WRITTEN.                              03350300
                                                                        03350400
           PERFORM 4000-READ-UNLOAD-FILE.                               03351000
                                                                        03352000
      *----------------------------------------------------------------*06396600
      *    READ GROUP PRICE UNLOAD FILE RECORD                          06396700
      *----------------------------------------------------------------*06396800
       4000-READ-UNLOAD-FILE.                                           06396900
                                                                        06397000
           READ GRP-PRIC-UNLOAD-FILE INTO IM038-GROUP-PRICE-UNLD-REC    06398000
               AT END                                                   06399000
                   SET PS-EOF-INPUT-FILE TO TRUE                        06399100
               NOT AT END                                               06399200
                   ADD 1 TO PA-INPUT-RECS-READ.                         06399300
                                                                        06400000
      *----------------------------------------------------------------*06520000
      * 9900-EOJ-LOGIC.                                                 06530000
      * PERFORMED AT END OF INPUT FILE TO CLOSE FILES, CURSOR AND       06540000
      * DISPLAY RECORD COUNTS.                                          06550000
      *----------------------------------------------------------------*06560000
       9900-EOJ-LOGIC.                                                  06570000
                                                                        06580000
           CLOSE  GRP-PRIC-UNLOAD-FILE                                  06590000
                  GRP-PRIC-EXTRACT-FILE.                                06600000
                                                                        06610000
           DISPLAY 'UNLOAD RECORDS READ: ' PA-INPUT-RECS-READ.          06620000
           DISPLAY 'EXTRACT RECORDS WRITTEN: ' PA-OUTPUT-REC-WRITTEN.   06630000
                                                                        06640000
                                                                        08140000
           DISPLAY '**------ END IMKUT057 ------**'.                    08150000
                                                                        08160000
      *---------------------------------------------------------------* 08170000
      *    END OF SOURCE CODE FOR IMKUT057                            * 08180000
      *---------------------------------------------------------------* 08190000
