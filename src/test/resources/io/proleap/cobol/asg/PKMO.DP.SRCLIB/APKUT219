       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    APKUT219.                                                 
       AUTHOR.        JILL JUEL.                                                
       DATE-WRITTEN.  AUGUST 2000.                                              
       DATE-COMPILED.                                                           
      ******************************************************************        
      *    REMOVE MEMORANDUM FROM PRINT FILE FOR EDI VENDORS           *        
      ******************************************************************        
      *    COBOL II WITH SQL                                           *        
      ******************************************************************        
      *  PURPOSE:  THIS PROGRAM USES THE AP MEMORANDUM PRINT FILE AS   *        
      *  INPUT AND STRIPS OUT THE PAGES FOR THE VENDORS THAT ARE SET   *        
      *  UP TO RECEIVE THEIR REMITTANCE INFORMATION VIA EDI. THE PRINT *        
      *  FILE HAS BEEN MODIFIED TO PUT THE REMIT VENDOR NUMBER IN THE  *        
      *  LAST 9 BYTES OF EVERY RECORD SO ALL LINES FOR A VENDOR CAN BE *        
      *  IDENTIFIED.  THIS FIELD WILL BE REMOVED FROM THE RECORDS AS   *        
      *  THEY ARE WRITTEN TO THE OUTPUT FILE.                          *        
      ******************************************************************        
      *                                                                *        
      *  DB2 TABLES:  TTRNCDE                                          *        
      *               TTRDPRT                                          *        
      *               TTRDTRN                                          *        
      *                                                                *        
      *   INPUT: AP MEMORANDUM IMAGE FILE                              *        
      *  OUTPUT: AP MEMORANDUM PRINT FILE                              *        
      *                                                                *        
      ******************************************************************        
      * MAINTENANCE LOG:                                               *        
      *                                                                *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-370.                                         
       OBJECT-COMPUTER.        IBM-370.                                         
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT MEMO-IMAGE-FILE  ASSIGN TO INP01.                             
           SELECT MEMO-PRINT-FILE  ASSIGN TO OUT01.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  MEMO-IMAGE-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 141 CHARACTERS                                       
           DATA RECORD IS MEMO-IMAGE-RECORD.                                    
                                                                                
       01  MEMO-IMAGE-RECORD              PIC X(141).                           
                                                                                
       FD  MEMO-PRINT-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS MEMO-PRINT-RECORD.                                    
                                                                                
       01  MEMO-PRINT-RECORD              PIC X(132).                           
                                                                                
       EJECT                                                                    
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  PS-MEMO-IMAGE-EOF-SW           PIC X  VALUE 'N'.                 
               88  NOT-MEMO-IMAGE-EOF                VALUE 'N'.                 
               88  MEMO-IMAGE-EOF                    VALUE 'Y'.                 
                                                                                
           05  PS-VENDOR-EDI-ELIGIBLE         PIC X  VALUE 'N'.                 
               88  VENDOR-NOT-EDI-ELIGIBLE           VALUE 'N'.                 
               88  VENDOR-EDI-ELIGIBLE               VALUE 'Y'.                 
                                                                                
       01  PROGRAM-VARIABLES.                                                   
           05  VENDOR-BEING-PROCESSED         PIC X(10)   VALUE SPACES.         
                                                                                
       01  PROGRAM-COUNTERS.                                                    
           05  RECORDS-READ                   PIC 9(09)       VALUE 0.          
           05  RECORDS-BYPASSED               PIC 9(09)       VALUE 0.          
           05  RECORDS-WRITTEN                PIC 9(09)       VALUE 0.          
                                                                                
       01  PV-MEMO-IMAGE-RECORD.                                                
           05  PV-MEMO-IMAGE                  PIC X(132)  VALUE SPACES.         
           05  PV-MEMO-REMIT-VENDOR           PIC X(009)  VALUE SPACES.         
                                                                                
                                                                                
       01  ABEND-CODE                         PIC S9(04) VALUE ZEROS            
                                                         COMP SYNC.             
           88  AC-BAD-WRITE                   VALUE  +4001.                     
           88  AC-CONTROL-CARD-ERROR          VALUE  +4003.                     
           88  AC-OPEN-ERROR                  VALUE  +4005.                     
           88  AC-CLOSE-ERROR                 VALUE  +4006.                     
           88  AC-BAD-DATA                    VALUE  +4008.                     
           88  AC-DB2-ERROR                   VALUE  +4013.                     
           88  AC-UNSUCCESSFUL-SELECT         VALUE  +4015.                     
           88  AC-MAX-RETRIES-EXCEEDED        VALUE  +4016.                     
           88  AC-DATE-ERROR                  VALUE  +4019.                     
                                                                                
       01  ABEND-AREAS.                                                         
           05  AA-ABEND-LIT                   PIC  X(40)  VALUE                 
                   '*****       ABEND'.                                         
           05  AA-PROGRAM-LIT                 PIC  X(40)  VALUE                 
                   '*****   PROGRAM: APKUT219'.                                 
           05  AA-PARAGRAPH-LIT.                                                
               10  FILLER                     PIC  X(17)  VALUE                 
                   '***** PARAGRAPH: '.                                         
               10  AA-PARAGRAPH-NAME          PIC  X(35)  VALUE SPACES.         
           05  AA-MESSAGE-LINE.                                                 
               10  FILLER                     PIC  X(06)  VALUE                 
                   '*****'.                                                     
               10  AA-MESSAGE                 PIC  X(127) VALUE SPACES.         
               10  FILLER REDEFINES AA-MESSAGE.                                 
                   15 AA-MESSAGE-1     PIC  X(45).                              
                   15 AA-MESSAGE-2     PIC  X(45).                              
                   15 AA-MESSAGE-3     PIC  X(37).                              
           05  AA-DB2-ERROR-LIT               PIC  X(40)  VALUE                 
                   '*****    DB2 ERROR'.                                        
           05  AA-DB2-OPERATION-LIT.                                            
               10  FILLER                     PIC  X(17)  VALUE                 
                   '***** OPERATION: '.                                         
               10  AA-DB2-OPERATION.                                            
                   15  AA-DB2-OP-1            PIC  X(25)  VALUE SPACES.         
                   15  AA-DB2-OP-2            PIC  X(25)  VALUE SPACES.         
           05  AA-DB2-TABLE-1                 PIC  X(18)  VALUE SPACES.         
           05  AA-DB2-TABLE-2                 PIC  X(18)  VALUE SPACES.         
           05  AA-DB2-TABLE-3                 PIC  X(18)  VALUE SPACES.         
       EJECT                                                                    
                                                                                
      *--------------------------------------------------------------*          
      *  DPWS004 CONTAINS THE FIELDS NECESSARY TO CALL THE SQLCA     *          
      *  MESSAGE MODULE 'DSNTIAR' AND DISPLAY THE FORMATTED RESULTS. *          
      *--------------------------------------------------------------*          
           COPY DPWS004.                                                        
       EJECT                                                                    
                                                                                
      *--------------------------------------------------------------*          
      *  TEXTENT TABLE                                               *          
      *--------------------------------------------------------------*          
           EXEC SQL                                                             
                INCLUDE TEXTENT                                                 
           END-EXEC.                                                            
       EJECT                                                                    
                                                                                
      *--------------------------------------------------------------*          
      *  TTRDPRT TABLE                                               *          
      *--------------------------------------------------------------*          
           EXEC SQL                                                             
                INCLUDE TTRDPRT                                                 
           END-EXEC.                                                            
       EJECT                                                                    
                                                                                
      *--------------------------------------------------------------*          
      *  TTRDTRN TABLE                                               *          
      *--------------------------------------------------------------*          
           EXEC SQL                                                             
                INCLUDE TTRDTRN                                                 
           END-EXEC.                                                            
       EJECT                                                                    
                                                                                
      *--------------------------------------------------------------*          
      *  COMMUNICATIONS AREA2 FOR DB2                                *          
      *--------------------------------------------------------------*          
           COPY SQLCA2.                                                         
      *--------------------------------------------------------------*          
      *     SQL COMMUNICATIONS AREA DCLGEN                           *          
      *--------------------------------------------------------------*          
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
       EJECT                                                                    
                                                                                
       LINKAGE SECTION.                                                         
      ************************                                                  
       PROCEDURE DIVISION.                                                      
      ************************                                                  
       A100-MAINLINE-ROUTINE.                                                   
                                                                                
           PERFORM B100-INITIALIZATION.                                         
                                                                                
           PERFORM B200-PROCESS-MEMOS                                           
             UNTIL MEMO-IMAGE-EOF.                                              
                                                                                
           PERFORM B300-TERMINATION.                                            
           GOBACK.                                                              
                                                                                
      *--------------------------------------------------------------*          
      *  INITIALIZATION PROCESSING                                   *          
      *--------------------------------------------------------------*          
       B100-INITIALIZATION.                                                     
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '**--------------- APKUT219 --------------------**'.         
           DISPLAY 'DATE ' FUNCTION CURRENT-DATE(1:8)                           
                 '  TIME ' FUNCTION CURRENT-DATE(9:6)                           
                                                                                
           OPEN  INPUT MEMO-IMAGE-FILE                                          
                OUTPUT MEMO-PRINT-FILE.                                         
                                                                                
           PERFORM R100-READ-MEMO-IMAGE.                                        
                                                                                
       EJECT.                                                                   
                                                                                
      *--------------------------------------------------------------*          
      *                                                              *          
      *--------------------------------------------------------------*          
       B200-PROCESS-MEMOS.                                                      
           IF PV-MEMO-REMIT-VENDOR NOT = VENDOR-BEING-PROCESSED                 
              PERFORM S100-SELECT-VENDOR-ELIGIBLITY                             
              MOVE PV-MEMO-REMIT-VENDOR TO VENDOR-BEING-PROCESSED               
           END-IF.                                                              
           IF VENDOR-EDI-ELIGIBLE                                               
              ADD 1 TO RECORDS-BYPASSED                                         
           ELSE                                                                 
              MOVE PV-MEMO-IMAGE TO MEMO-PRINT-RECORD                           
              PERFORM W100-WRITE-MEMO-PRINT                                     
           END-IF.                                                              
           PERFORM R100-READ-MEMO-IMAGE.                                        
                                                                                
       EJECT.                                                                   
                                                                                
      *--------------------------------------------------------------*          
      *  END OF PROGRAM PROCESSING                                   *          
      *--------------------------------------------------------------*          
       B300-TERMINATION.                                                        
           DISPLAY '**************************************'.                    
           DISPLAY '****  RECORD COUNTS FOR APKUT219  ****'.                    
           DISPLAY '**************************************'.                    
           DISPLAY 'CHECK RECORDS READ     ' RECORDS-READ.                      
           DISPLAY 'CHECK RECORDS BYPASSED ' RECORDS-BYPASSED.                  
           DISPLAY 'CHECK RECORDS WRITTEN  ' RECORDS-WRITTEN.                   
           DISPLAY '**************************************'.                    
           DISPLAY '****   END OF PROGRAM APKUT219    ****'.                    
           DISPLAY '**************************************'.                    
                                                                                
           CLOSE MEMO-IMAGE-FILE                                                
                 MEMO-PRINT-FILE.                                               
       EJECT.                                                                   
                                                                                
      *--------------------------------------------------------------*          
      *  READS THE CHECK INPUT FILE                                  *          
      *--------------------------------------------------------------*          
       R100-READ-MEMO-IMAGE.                                                    
           READ MEMO-IMAGE-FILE INTO PV-MEMO-IMAGE-RECORD                       
             AT END                                                             
                SET MEMO-IMAGE-EOF TO TRUE                                      
             NOT AT END                                                         
                ADD 1 TO RECORDS-READ                                           
           END-READ.                                                            
       EJECT.                                                                   
                                                                                
      *--------------------------------------------------------------*          
      *  THIS SQL CAN RETURN MORE THAN ONE ROW, BECAUSE THE SYSTEM   *          
      *  DOES NOT FORCE THE USER TO INACTIVATE THE 'TEST' ROW WHEN   *          
      *  THEY ARE ACTIVATING THE 'PROD' ROW, SO BOTH CAN BE ACTIVE   *          
      *  FOR A PERIOD OF TIME.                                       *          
      *--------------------------------------------------------------*          
       S100-SELECT-VENDOR-ELIGIBLITY.                                           
           EXEC SQL                                                             
               SELECT A.ENT_ID                                                  
                    , A.VND_CLSN_CDE                                            
                    , B.PROD_TEST_IND                                           
                 INTO :EXTENT-ENT-ID                                            
                    , :EXTENT-VND-CLSN-CDE                                      
                    , :TRDTRN-PROD-TEST-IND                                     
                 FROM TEXTENT A                                                 
                    , TTRDTRN B                                                 
                    , TTRDPRT C                                                 
                WHERE A.ENT_ID        = C.ENT_ID                                
                  AND A.DUN_NBR       = :PV-MEMO-REMIT-VENDOR                   
                  AND B.TP_DKEY       = C.TP_DKEY                               
                  AND B.TRAN_SET_CDE  = '820'                                   
                  AND B.PROD_TEST_IND = 'P'                                     
                  AND B.ACTV_DTE     <= CURRENT DATE                            
                  AND B.INACTV_DTE    = '9999-09-09'                            
                  AND C.ACTV_DTE     <= CURRENT DATE                            
                  AND C.INACTV_DTE    = '9999-09-09'                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   SET VENDOR-EDI-ELIGIBLE TO TRUE                              
               WHEN SQLCODE = -811                                              
                   SET VENDOR-EDI-ELIGIBLE TO TRUE                              
               WHEN SQLCODE = +100                                              
                   SET VENDOR-NOT-EDI-ELIGIBLE TO TRUE                          
               WHEN OTHER                                                       
                   DISPLAY 'MEMO-REMIT-VENDOR ' PV-MEMO-REMIT-VENDOR            
                   SET AC-UNSUCCESSFUL-SELECT    TO TRUE                        
                   MOVE 'S100-SELECT-VENDOR-ELIGIBLITY'                         
                                                 TO AA-PARAGRAPH-NAME           
                   MOVE 'UNSUCCESSFUL SELECT'    TO AA-DB2-OPERATION            
                   MOVE 'TEXTENT'                TO AA-DB2-TABLE-1              
                   MOVE 'TTRDTRN'                TO AA-DB2-TABLE-2              
                   MOVE 'TTRDPRT'                TO AA-DB2-TABLE-3              
                   PERFORM Z998-DB2-ABEND                                       
           END-EVALUATE.                                                        
       EJECT.                                                                   
                                                                                
      *--------------------------------------------------------------*          
      *  THIS ROUTINE WRITES ALL THE CHECK AND REMITTANCE LINES      *          
      *--------------------------------------------------------------*          
       W100-WRITE-MEMO-PRINT.                                                   
           WRITE MEMO-PRINT-RECORD.                                             
           ADD 1 TO RECORDS-WRITTEN.                                            
       EJECT.                                                                   
                                                                                
      *--------------------------------------------------------------*          
      *  DB2 ABEND ROUTINE                                           *          
      *--------------------------------------------------------------*          
       Z998-DB2-ABEND.                                                          
                                                                                
           DISPLAY AA-ABEND-LIT.                                                
           DISPLAY AA-DB2-ERROR-LIT.                                            
           DISPLAY AA-PROGRAM-LIT.                                              
           DISPLAY AA-PARAGRAPH-LIT.                                            
           DISPLAY AA-DB2-OPERATION-LIT.                                        
           DISPLAY AA-DB2-TABLE-1.                                              
           DISPLAY AA-DB2-TABLE-2.                                              
           DISPLAY AA-DB2-TABLE-3.                                              
           SET AC-DB2-ERROR TO TRUE.                                            
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
