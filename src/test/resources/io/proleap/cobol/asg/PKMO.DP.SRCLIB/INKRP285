       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         INKRP285.                                            
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       04/01/2003.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *     INKRP285 - CUMMULATIVE ACTIVE SKU NOT INVENTORIED REPORT TOP        
      *                EXCEPTION REPORT.                                        
      *                                                                         
      *     (CHANGES MADE TO THIS PROGRAM MAY NEED TO BE MADE TO                
      *      INKRP278, INKRP279, INKRP280, INKRP281)                            
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                             
      * -------  ----------  ----------------------------------------           
21732 * 21732    04-01-2003  CUMM ACTIVE SKU NOT INVENTORIED REPORT.            
21732 *                      (CREATION) -ROD JANSSEN                            
25248 * 25248    11-21-2003  UPC CAPABILITIES                                   
25248 *                      -ROD JANSSEN                                       
26600 * 26600    09-13-2004  STORE NUMBER EXPANSION. RE-WRITE REPORT TO         
26600 *                      ELIMINATE COPYBOOK INRD288 AND RELATED CODE        
26600 *                      -SCOTT JACKSON                                     
W33304* 33304    11-19-2008  HILO CHANGES                                       
W33304*                      -PATTY JAEGER  -  CODEWORKS                        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT STORE-MERGE-FILE                                              
               ASSIGN TO INP01.                                                 
      *                                                                         
           SELECT EXCEPT-CONTROL-FILE                                           
               ASSIGN TO INP02.                                                 
      *                                                                         
           SELECT STORE-INV-EXCEPTION-RPT                                       
               ASSIGN TO RPT01.                                                 
      *                                                                         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *                                                                         
       FD  STORE-MERGE-FILE                                                     
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
W33304 01  STORE-MERGE-RECORD          PIC X(150).                              
      *                                                                         
       FD  EXCEPT-CONTROL-FILE                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  EXCEPT-CONTROL-RECORD.                                               
           05  ECR-EXCEPT-CONTROL      PIC 9(05).                               
           05  ECR-REST-OF-RECORD      PIC X(75).                               
      *                                                                         
       FD  STORE-INV-EXCEPTION-RPT                                              
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING  MODE IS F                                                 
           DATA RECORD IS REPORT-LINE.                                          
       01  REPORT-LINE         PIC X(132).                                      
      *                                                                         
                                                                                
      /*****************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.        
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME       PIC X(30)    VALUE SPACES.               
           05  PV-DB2-OPER-ATTEMPTED   PIC X(30)    VALUE SPACES.               
           05  PV-ITEM-NMBR            PIC S9(15)V  VALUE +0  COMP-3.           
           05  PV-PHY-INV-OH-VAR       PIC S9(09) COMP VALUE ZERO.              
           05  PV-STORE-COUNT          PIC S9(09) COMP-3 VALUE 0.               
           05  PV-DEFAULT-CURRENT-DATE.                                         
               10  PV-DF-CC            PIC XX       VALUE '20'.                 
               10  PV-DF-YY            PIC XX.                                  
               10  FILLER              PIC X        VALUE '-'.                  
               10  PV-DF-MM            PIC XX.                                  
               10  FILLER              PIC X        VALUE '-'.                  
               10  PV-DF-DD            PIC XX.                                  
           05  PV-CURRENT-DATE.                                                 
               10  PV-CD-YY            PIC XX.                                  
               10  PV-CD-MM            PIC XX.                                  
               10  PV-CD-DD            PIC XX.                                  
           05  PV-TOTAL-INV-QTY        PIC S9(5)    COMP-3 VALUE 0.             
           05  PV-VARIANCE-QTY         PIC S9(5)    COMP-3 VALUE 0.             
           05  PV-STORE-EXCEPT-COUNT   PIC 9(05)    VALUE ZEROS.                
           05  PV-PLND-INV-TMST.                                                
               10  PV-DB2-PLND-INV-DTE     PIC X(10) VALUE SPACES.              
               10  FILLER                  PIC X(16)                            
                   VALUE '-23.59.59.999999'.                                    
           05  FILLER REDEFINES PV-PLND-INV-TMST.                               
               10  PV-PLND-INV-TMST-REDEF  PIC X(26).                           
           05  PV-CUTOFF-OH-QTY        PIC S9(09)    COMP-3 VALUE 0.            
           05  PV-MFSTNG                   PIC X(01) VALUE SPACES.              
           05  PV-PAGE-COUNT           PIC S9(05) COMP-3 VALUE 0.               
           05  PV-LINE-COUNT           PIC S9(05) COMP-3 VALUE 0.               
           05  PV-CURRENT-TIME.                                                 
               10  PV-CT-HH            PIC XX.                                  
               10  PV-CT-MM            PIC XX.                                  
           05  PV-LOW-RANGE            PIC S9(09) COMP-3 VALUE 0.               
           05  PV-HIGH-RANGE           PIC S9(09) COMP-3 VALUE 0.               
W33304     05  PV-RECS-REPORTED        PIC S9(09) COMP-3 VALUE 0.               
           05  PV-EDIT-SKU             PIC X(08) VALUE ZEROS.                   
W33304     05  PV-PREV-CYCLE-ID        PIC S9(9)  COMP   VALUE +0.              
                                                                                
       01  SA-SAVE-AREA.                                                        
           05  SA-STORE-NUMBER         PIC 9(04)    VALUE ZEROES.               
           05  SA-SKU                  PIC 9(08)    VALUE ZEROS.                
           05  SA-SKU-ATTRIBUTES.                                               
               10  SA-DEPT-CLASS.                                               
                   15  SA-DEPT         PIC 9(04)    VALUE ZEROS.                
                   15  SA-CLASS        PIC 9(04)    VALUE ZEROS.                
               10  SA-SUB-CLASS        PIC 9(04)    VALUE ZEROS.                
           05  SA-STORE-FOR-HEADER     PIC 9(04)    VALUE ZEROES.               
           05  SA-SKU-SAVE             PIC X(08)    VALUE SPACES.               
           05  SA-SKU-DESCRIP-SAVE     PIC X(25)    VALUE SPACES.               
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-INPUT-SW          PIC X(01) VALUE 'N'.                 
               88  PS-END-OF-INPUT                   VALUE 'Y'.                 
               88  PS-NOT-END-OF-INPUT               VALUE 'N'.                 
           05  PS-INP02-DONE-SW            PIC X(01) VALUE 'N'.                 
               88  ABSOLUTE-DIFF-END                 VALUE 'Y'.                 
           05  END-OF-CURSOR-SW            PIC X(01) VALUE 'N'.                 
               88  END-OF-CURSOR                     VALUE 'Y'.                 
           05  PS-VARIANCE-EXISTS-SW       PIC X(01) VALUE 'N'.                 
               88  PS-VARIANCE-EXISTS                VALUE 'Y'.                 
               88  PS-NO-VARIANCE-EXISTS             VALUE 'N'.                 
W33304     05  PS-STORE-FIRST-TIME-SW      PIC X(01) VALUE 'Y'.                 
W33304         88  STORE-FIRST-TIME-YES              VALUE 'Y'.                 
W33304         88  STORE-FIRST-TIME-NO               VALUE 'N'.                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES          PIC S9(4)    COMP   VALUE +3.            
           05  PC-MAX-RECS             PIC S9(4)    COMP   VALUE 5000.          
           05  PC-PROGRAM-NAME         PIC X(08)    VALUE 'INKRP285'.           
           05  PC-OPERCO-NBR           PIC X(02)    VALUE '03'.                 
           05  PC-BYR                  PIC X(03)    VALUE 'BYR'.                
           05  PC-DPT                  PIC X(03)    VALUE 'DPT'.                
           05  PC-MCL                  PIC X(03)    VALUE 'MCL'.                
           05  PC-CO                   PIC X(03)    VALUE 'CO'.                 
           05  PC-ZERO-STORE           PIC S9(4)    VALUE +0 COMP.              
           05  PC-CLEARANCE-CODE       PIC 9(02)    VALUE 30.                   
           05  PC-NULL-DATE            PIC X(10)   VALUE '9999-09-09'.          
                                                                                
       01  HL1-HEADING-LINE-1.                                                  
           05  FILLER                  PIC X(36) VALUE SPACES.                  
           05  FILLER                  PIC X(45) VALUE                          
               'CUMM ACTIVE SKU NOT INVENTORIED REPORT - TOP '.                 
           05  HL1-EXCEPT-CONTROL      PIC Z,ZZZ9.                              
           05  FILLER                  PIC X(11) VALUE                          
               ' EXCEPTIONS'.                                                   
W33304                                                                          
W33304 01  HL1-HEADING-LINE-2.                                                  
W33304     05  FILLER                  PIC X(2)  VALUE SPACES.                  
W33304     05  FILLER                  PIC X(07) VALUE 'CYCLE: '.               
W33304     05  HL2-CYCLE-NM            PIC ZZZZ99999.                           
W33304     05  FILLER                  PIC X(34) VALUE SPACES.                  
W33304     05  HL2-INV-NM              PIC X(20) VALUE SPACES.                  
W33304     05  FILLER                  PIC X(9)  VALUE SPACES.                  
W33304     05  FILLER                  PIC X(17) VALUE SPACES.                  
W33304                                                                          
       01  HL3-HEADING-LINE-3.                                                  
           05  FILLER                  PIC X(62) VALUE SPACES.                  
           05  FILLER                  PIC X(17) VALUE                          
               '***INVENTORIED***'.                                             
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(23) VALUE                          
               '****FASHION CUTOFF*****'.                                       
                                                                                
       01  HL4-HEADING-LINE-4.                                                  
25248      05  FILLER                  PIC X(17) VALUE SPACES.                  
25248      05  FILLER                  PIC X(04) VALUE 'DMS '.                  
25248      05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE '   SKU'.                
           05  FILLER                  PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(11) VALUE 'DESCRIPTION'.           
           05  FILLER                  PIC X(17) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE '  QTY '.                
           05  FILLER                  PIC X(16) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE 'ONHAND'.                
           05  FILLER                  PIC X(17) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE 'VARIANCE'.              
                                                                                
       01  HL5-HEADING-LINE-5.                                                  
25248      05  FILLER                  PIC X(14) VALUE SPACES.                  
25248      05  FILLER                  PIC X(09) VALUE '---------'.             
25248      05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE '--------'.              
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(25)                                
                                 VALUE '-------------------------'.             
           05  FILLER                  PIC X(07) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE '-------'.               
           05  FILLER                  PIC X(11) VALUE SPACES.                  
           05  FILLER                  PIC X(14)                                
                                             VALUE '--------------'.            
           05  FILLER                  PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE '----------'.            
                                                                                
       01  DL-DETAIL-LINE.                                                      
25248      05  FILLER                      PIC X(14) VALUE SPACES.              
25248      05  DL1-DMS                     PIC X(09).                           
25248      05  FILLER                      PIC X(01) VALUE SPACES.              
           05  DL1-SKU                     PIC X(08).                           
           05  FILLER                      PIC X(02) VALUE SPACES.              
           05  DL1-SKU-DESCRIP             PIC X(25).                           
           05  FILLER                      PIC X(05) VALUE SPACES.              
           05  DL1-INV-ONHND               PIC ZZZZZZZ9-.                       
           05  FILLER                      PIC X(16) VALUE SPACES.              
           05  DL1-FS-ONHND                PIC ZZZZZZZZ-.                       
           05  FILLER                      PIC X(09) VALUE SPACES.              
           05  DL1-VARIANCE                PIC ZZ,ZZZ,ZZZ-.                     
                                                                                
       01  DL-NO-EXCEPTION-LINE.                                                
           05  FILLER                  PIC X(49)    VALUE                       
               '************* NO EXCEPTIONS EXIST ***************'.             
           05  FILLER                  PIC X(83)    VALUE SPACES.               
                                                                                
       01  DL-EXCEPTION-SUMMARY-LINE.                                           
           05  FILLER                  PIC X(32)    VALUE                       
               ' EXCEPTIONS RANGE FROM A LOW OF '.                              
           05  DLESL-LOW               PIC Z,ZZZ,ZZ9-.                          
           05  FILLER                  PIC X(14)    VALUE                       
               ' TO A HIGH OF '.                                                
           05  DLESL-HIGH              PIC Z,ZZZ,ZZ9-.                          
           05  FILLER                  PIC X(03)    VALUE ' **'.                
                                                                                
      ******************************************************************        
      * COPYBOOK FOR REMAINING REPORT HEADING LINES, DETAIL LINE                
      ******************************************************************        
           COPY DPWS132O.                                                       
                                                                                
      ******************************************************************        
      *  INVENTORY EXCEPTIONS BY STORE EXTRACT - INPUT RECORD LAYOUT            
      ******************************************************************        
W33304     COPY INRD032.                                                        
                                                                                
      ******************************************************************        
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      * DB2 TABLE DCLGEN - TUPCPLS                                              
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TUPCPLS                                                  
           END-EXEC.                                                            
                                                                                
W33304******************************************************************        
W33304* DB2 TABLE DCLGEN - TINCNTL                                              
W33304******************************************************************        
W33304     EXEC SQL                                                             
W33304         INCLUDE TINCNTL                                                  
W33304     END-EXEC.                                                            
W33304                                                                          
      ******************************************************************        
      * DB2 TABLE DCLGEN - TSKXREF                                              
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 TABLE DCLGEN - IMV_SKU                                              
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE IMV00001                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 TABLE ERROR HANDLING                                                
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
           COPY SQLCA2.                                                         
                                                                                
      /****************************************************************         
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
                                                                                
       1000-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-INPUT                                           
               UNTIL PS-END-OF-INPUT.                                           
                                                                                
           PERFORM 9000-END-OF-JOB-TASKS.                                       
                                                                                
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      /****************************************************************         
      * 1000-INITIALIZATION.                                                    
      *****************************************************************         
       1000-INITIALIZATION.                                                     
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '* START OF INKRP285               '.                        
           DISPLAY '**********************************'.                        
      *                                                                         
           OPEN INPUT  STORE-MERGE-FILE                                         
                INPUT  EXCEPT-CONTROL-FILE                                      
                OUTPUT STORE-INV-EXCEPTION-RPT.                                 
                                                                                
           ACCEPT PV-CURRENT-DATE    FROM DATE.                                 
           ACCEPT PV-CURRENT-TIME    FROM TIME.                                 
                                                                                
           MOVE PV-CD-MM            TO DP132O-RUN-MONTH.                        
           MOVE PV-CD-DD            TO DP132O-RUN-DAY.                          
           MOVE PV-CD-YY            TO DP132O-RUN-YEAR.                         
           MOVE PV-CT-HH            TO DP132O-RUN-HOUR.                         
           MOVE PV-CT-MM            TO DP132O-RUN-MINUTE.                       
           MOVE PC-PROGRAM-NAME     TO DP132O-PROGRAM-NAME.                     
           MOVE 1                   TO DP132O-REPORT-NUMBER.                    
           MOVE ZEROS               TO PV-TOTAL-INV-QTY.                        
W33304     MOVE ZEROS               TO PV-RECS-REPORTED.                        
           MOVE ZEROS               TO PV-PAGE-COUNT.                           
                                                                                
      ** GET THE TOP XXX CONTROL RECORD FOR THE REPORT.                         
           PERFORM 5100-READ-EXCEPT-CONTROL-FILE.                               
                                                                                
      ** READ OF THE EXCEPTION MERGE FILE.                                      
           PERFORM 5000-READ-INV-FILE.                                          
W33304     PERFORM 5050-CHECK-REC-READ.                                         
W33304                                                                          
           IF PS-END-OF-INPUT                                                   
               DISPLAY '*************************************'                  
               DISPLAY '**  PROGRAM = INKRP285'                                 
               DISPLAY '**  PARAGRAPH = 1000-INITIALIZTION'                     
               DISPLAY '**'                                                     
               DISPLAY '**  NO EXCEPT RECORD TO PROCESS'                        
               DISPLAY '**  INP01'                                              
               DISPLAY '*************************************'                  
           END-IF.                                                              
                                                                                
      ** DETERMINE IF THE FIRST SKU SHOULD BE PROCESSED.                        
           IF (NOT PS-END-OF-INPUT)                                             
W33304         MOVE IN032-SKU-ATTRIBUTES  TO SA-SKU-ATTRIBUTES                  
W33304         MOVE IN032-SKU             TO SA-SKU                             
W33304         MOVE IN032-VARIANCE-QTY    TO PV-LOW-RANGE                       
                                             PV-HIGH-RANGE                      
           END-IF.                                                              
                                                                                
      ** PRINT HEADING LINES...                                                 
           IF (NOT PS-END-OF-INPUT)                                             
W33304         PERFORM 3300-GET-CYCLE-NAME                                      
               PERFORM 7100-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
      /****************************************************************         
      * 2000-PROCESS-INPUT.                                                     
      *    RECORDS ARE PRE-SORTED TO 'NOT' INCLUDE A STORE NUMBER OR AN         
      *    AREA NUMBER (IE. ZEROES). RECORDS COME IN WITH HIGHEST               
      *    VARIANCE DISCREPANCY TO LOWEST                                       
      *****************************************************************         
       2000-PROCESS-INPUT.                                                      
                                                                                
           PERFORM 2200-PROCESS-DETAIL-RECS                                     
                                                                                
      ** READ OF THE EXCEPTION MERGE FILE.                                      
           PERFORM 5000-READ-INV-FILE.                                          
W33304     PERFORM 5050-CHECK-REC-READ.                                         
                                                                                
      ******************************************************************        
      * 2200-PROCESS-DETAIL-RECS.                                               
      ******************************************************************        
       2200-PROCESS-DETAIL-RECS.                                                
                                                                                
           MOVE ZERO                      TO PV-VARIANCE-QTY.                   
                                                                                
W33304     MOVE IN032-QUANTITY            TO DL1-INV-ONHND.                     
W33304     MOVE IN032-VARIANCE-QTY        TO DL1-VARIANCE                       
                                             PV-VARIANCE-QTY.                   
                                                                                
           PERFORM 3600-CALC-FS-ONHAND.                                         
           MOVE  PV-CUTOFF-OH-QTY         TO DL1-FS-ONHND.                      
                                                                                
           IF PV-VARIANCE-QTY IS EQUAL TO ZERO                                  
               MOVE 'N' TO PS-VARIANCE-EXISTS-SW                                
           ELSE                                                                 
W33304         ADD 1            TO PV-RECS-REPORTED                             
               MOVE 'Y'         TO PS-VARIANCE-EXISTS-SW                        
           END-IF.                                                              
      *                                                                         
           IF PS-VARIANCE-EXISTS                                                
               ADD 1                 TO PV-STORE-COUNT                          
                                                                                
               IF PV-LINE-COUNT IS GREATER THAN 61                              
W33304             PERFORM 3300-GET-CYCLE-NAME                                  
                   PERFORM 7100-PRINT-HEADINGS                                  
               END-IF                                                           
                                                                                
               MOVE SA-SKU             TO DL1-SKU, PV-EDIT-SKU                  
                                                                                
               STRING SA-SKU-ATTRIBUTES (2:3) DELIMITED BY SIZE                 
                      '-' DELIMITED BY SIZE                                     
                      SA-SKU-ATTRIBUTES (7:2) DELIMITED BY SIZE                 
                      '-' DELIMITED BY SIZE                                     
                      SA-SKU-ATTRIBUTES (11:2) DELIMITED BY SIZE                
                                     INTO DL1-DMS                               
               PERFORM 8600-SELECT-IMV-SKU                                      
      *        MOVE PV-TOTAL-INV-QTY   TO DL1-INV-ONHND                         
      *        MOVE PV-CUTOFF-OH-QTY   TO DL1-FS-ONHND                          
                                                                                
      *        IF SA-SKU (6:3) IS EQUAL TO '000' OR '999'                       
      *            MOVE ZEROS          TO DL1-VARIANCE                          
      *        END-IF                                                           
                                                                                
               PERFORM 8400-PRT-DETAIL                                          
               MOVE 'N'                 TO PS-VARIANCE-EXISTS-SW                
                                                                                
           END-IF.                                                              
                                                                                
                                                                                
W33304 3300-GET-CYCLE-NAME.                                                     
W33304                                                                          
W33304     MOVE IN032-INV-ID       TO INCNTL-INV-ID                             
W33304                                HL2-CYCLE-NM.                             
W33304                                                                          
W33304     EXEC SQL                                                             
W33304        SELECT A.INV_NM                                                   
W33304           INTO : INCNTL-INV-NM                                           
W33304           FROM TINCNTL A                                                 
W33304        WHERE A.ACTV_IND         = 'Y'                                    
W33304          AND A.INV_ID           =: INCNTL-INV-ID                         
W33304        WITH UR                                                           
W33304     END-EXEC.                                                            
W33304                                                                          
W33304                                                                          
W33304     EVALUATE SQLCODE                                                     
W33304         WHEN +0                                                          
W33304            MOVE SPACES TO HL2-INV-NM                                     
W33304            MOVE INCNTL-INV-NM-TEXT(1:INCNTL-INV-NM-LEN)                  
W33304                                   TO HL2-INV-NM                          
W33304         WHEN OTHER                                                       
W33304             MOVE 'INV DESC N/AVAIL' TO HL2-INV-NM                        
W33304     END-EVALUATE.                                                        
W33304                                                                          
      ******************************************************************        
      * 3600-CALC-FS-ONHAND.                                                    
      ******************************************************************        
       3600-CALC-FS-ONHAND.                                                     
                                                                                
W33304     IF IN032-SKU (6:3) = '000' OR '999'                                  
              MOVE +0 TO PV-CUTOFF-OH-QTY                                       
           ELSE                                                                 
W33304        IF IN032-MFSTNG-IND IS EQUAL TO 'Y'                               
                  COMPUTE PV-CUTOFF-OH-QTY                                      
W33304                                 = IN032-INV-CUTOFF-OH-QTY -              
W33304                                   (IN032-CUTOFF-STR-EXPECT               
W33304                                   - IN032-CUTOFF-INTRANSIT)              
              ELSE                                                              
                  COMPUTE PV-CUTOFF-OH-QTY                                      
W33304                                 = IN032-INV-CUTOFF-OH-QTY                
W33304                                 - IN032-POST-FL-AFT-REC                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
      /*****************************************************************        
      * 5000-READ-INV-FILE.                                                     
      *    READ THE MERGED INPUT EXCEPTION SKUS.                                
      ******************************************************************        
       5000-READ-INV-FILE.                                                      
                                                                                
           READ STORE-MERGE-FILE                                                
W33304       INTO IN032-INV-EXCEPTS-STR-REC                                     
                AT END                                                          
                    SET PS-END-OF-INPUT TO TRUE                                 
                NOT AT END                                                      
W33304              MOVE IN032-SKU-ATTRIBUTES  TO SA-SKU-ATTRIBUTES             
W33304              MOVE IN032-SKU             TO SA-SKU                        
           END-READ.                                                            
                                                                                
W33304 5050-CHECK-REC-READ.                                                     
W33304     EVALUATE TRUE                                                        
W33304         WHEN PS-END-OF-INPUT                                             
W33304              CONTINUE                                                    
W33304         WHEN STORE-FIRST-TIME-YES                                        
W33304              SET STORE-FIRST-TIME-NO TO TRUE                             
W33304              MOVE IN032-INV-ID       TO PV-PREV-CYCLE-ID                 
W33304         WHEN IN032-INV-ID         NOT = PV-PREV-CYCLE-ID                 
W33304              MOVE ZERO               TO PV-RECS-REPORTED                 
W33304              MOVE IN032-INV-ID       TO PV-PREV-CYCLE-ID                 
W33304              MOVE 62                 TO PV-LINE-COUNT                    
W33304         WHEN PV-RECS-REPORTED >= ECR-EXCEPT-CONTROL                      
W33304              PERFORM 5060-LOOP-THRU-READS                                
W33304     END-EVALUATE.                                                        
W33304                                                                          
W33304 5060-LOOP-THRU-READS.                                                    
W33304                                                                          
W33304     PERFORM 5000-READ-INV-FILE                                           
W33304       UNTIL PS-END-OF-INPUT                                              
W33304          OR (IN032-INV-ID NOT = PV-PREV-CYCLE-ID)                        
W33304                                                                          
W33304     EVALUATE TRUE                                                        
W33304         WHEN PS-END-OF-INPUT                                             
W33304              CONTINUE                                                    
W33304         WHEN IN032-INV-ID NOT = PV-PREV-CYCLE-ID                         
W33304              MOVE ZERO            TO PV-RECS-REPORTED                    
W33304              MOVE IN032-INV-ID    TO PV-PREV-CYCLE-ID                    
W33304              MOVE 62              TO PV-LINE-COUNT                       
W33304     END-EVALUATE.                                                        
W33304                                                                          
      ******************************************************************        
      * 5100-READ THE EXCEPTION CONTROL FILE.                                   
      *    READ THE TOP EXCEPTION CONTROL FILE.                                 
      ******************************************************************        
       5100-READ-EXCEPT-CONTROL-FILE.                                           
                                                                                
           READ EXCEPT-CONTROL-FILE                                             
             AT END                                                             
               SET PS-END-OF-INPUT TO TRUE                                      
               DISPLAY '*************************************'                  
               DISPLAY '**  ABEND PROGRAM = INKRP285'                           
               DISPLAY '**  PARAGRAPH     = 1000-INITIALIZTION'                 
               DISPLAY '**'                                                     
               DISPLAY '**  MISSING EXCEPTION CONTROL RECORD'                   
               DISPLAY '**  INP03'                                              
               DISPLAY '*************************************'                  
               MOVE +4008          TO ABEND-CODE                                
               PERFORM 9990-ABEND                                               
           END-READ.                                                            
                                                                                
      ******************************************************************        
      * 7100-PRINT-HEADINGS.                                                    
      ******************************************************************        
       7100-PRINT-HEADINGS.                                                     
                                                                                
           ADD 1                    TO PV-PAGE-COUNT.                           
           MOVE PV-PAGE-COUNT       TO DP132O-PAGE-NUMBER.                      
                                                                                
           WRITE REPORT-LINE                                                    
             FROM DP132O-STANDRD-HEADER-132-COLS AFTER ADVANCING PAGE.          
                                                                                
           MOVE ECR-EXCEPT-CONTROL  TO HL1-EXCEPT-CONTROL.                      
                                                                                
           WRITE REPORT-LINE                                                    
             FROM HL1-HEADING-LINE-1 AFTER ADVANCING 1 LINE.                    
                                                                                
                                                                                
W33304     WRITE REPORT-LINE                                                    
W33304       FROM HL1-HEADING-LINE-2 AFTER ADVANCING 1 LINE.                    
W33304                                                                          
           WRITE REPORT-LINE                                                    
             FROM HL3-HEADING-LINE-3 AFTER ADVANCING 1 LINE.                    
                                                                                
           WRITE REPORT-LINE                                                    
             FROM HL4-HEADING-LINE-4 AFTER ADVANCING 1 LINE.                    
                                                                                
           WRITE REPORT-LINE                                                    
             FROM HL5-HEADING-LINE-5 AFTER ADVANCING 1 LINE.                    
                                                                                
           MOVE 6                   TO PV-LINE-COUNT.                           
                                                                                
      ******************************************************************        
      * 7700-PRINT THE STORE EXCEPTION SUMMARY LINE.                            
      ******************************************************************        
       7700-PRINT-EXCEPTION-RANGE.                                              
                                                                                
           IF PV-LINE-COUNT IS GREATER THAN 61                                  
W33304         PERFORM 3300-GET-CYCLE-NAME                                      
               PERFORM 7100-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           MOVE PV-LOW-RANGE    TO DLESL-LOW.                                   
           MOVE PV-HIGH-RANGE   TO DLESL-HIGH.                                  
                                                                                
           WRITE REPORT-LINE FROM DL-EXCEPTION-SUMMARY-LINE                     
                                AFTER ADVANCING 3 LINES.                        
                                                                                
           MOVE ZEROS           TO PV-HIGH-RANGE.                               
           MOVE ZEROS           TO PV-LOW-RANGE.                                
           ADD 3                TO PV-LINE-COUNT.                               
                                                                                
      *****************************************************************         
      * 8400-PRT-DETAIL.                                                        
      *****************************************************************         
       8400-PRT-DETAIL.                                                         
                                                                                
           WRITE REPORT-LINE   FROM DL-DETAIL-LINE.                             
           ADD 1               TO PV-LINE-COUNT.                                
W33304     MOVE IN032-VARIANCE-QTY TO PV-LOW-RANGE.                             
                                                                                
      ******************************************************************        
      * 8600-SELECT-IMV-SKU.                                                    
      ******************************************************************        
       8600-SELECT-IMV-SKU.                                                     
                                                                                
           INITIALIZE DCLIMV00001.                                              
                                                                                
           MOVE PV-EDIT-SKU       TO IMV00001-SKU-NBR.                          
                                                                                
           EXEC SQL                                                             
             SELECT                                                             
                   SKU_DESC                                                     
             INTO                                                               
                  :IMV00001-SKU-DESC                                            
             FROM IMV_SKU                                                       
             WHERE SKU_NBR  = :IMV00001-SKU-NBR                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   MOVE IMV00001-SKU-DESC (1:25) TO DL1-SKU-DESCRIP             
               WHEN OTHER                                                       
                   MOVE '****'        TO DL1-SKU-DESCRIP                        
           END-EVALUATE.                                                        
                                                                                
      *                                                                         
      /****************************************************************         
      * 9000-END-OF-JOB-TASKS.                                                  
      *****************************************************************         
       9000-END-OF-JOB-TASKS.                                                   
                                                                                
      *** DO WE HAVE VARIANCE TO PROCESS????                                    
W33304     IF PV-RECS-REPORTED   IS GREATER THAN 0                              
               IF PS-VARIANCE-EXISTS                                            
                   ADD 1                 TO PV-STORE-COUNT                      
                                                                                
                   IF PV-LINE-COUNT IS GREATER THAN 61                          
W33304                 PERFORM 3300-GET-CYCLE-NAME                              
                       PERFORM 7100-PRINT-HEADINGS                              
                   END-IF                                                       
                                                                                
                   PERFORM 2200-PROCESS-DETAIL-RECS                             
                                                                                
      *            MOVE SA-SKU             TO DL1-SKU, PV-EDIT-SKU              
                                                                                
      *            STRING SA-SKU-ATTRIBUTES (2:3) DELIMITED BY SIZE             
      *                '-' DELIMITED BY SIZE                                    
      *                   SA-SKU-ATTRIBUTES (7:2) DELIMITED BY SIZE             
      *                '-' DELIMITED BY SIZE                                    
      *                   SA-SKU-ATTRIBUTES (11:2) DELIMITED BY SIZE            
      *                              INTO DL1-DMS                               
      *                                                                         
      *            PERFORM 8600-SELECT-IMV-SKU                                  
      *            MOVE PV-TOTAL-INV-QTY   TO DL1-INV-ONHND                     
      *            MOVE PV-CUTOFF-OH-QTY   TO DL1-FS-ONHND                      
      *                                                                         
      *        IF SA-SKU (6:3) IS EQUAL TO '000' OR '999'                       
      *          MOVE ZEROS          TO DL1-VARIANCE                            
      *        END-IF                                                           
      *                                                                         
      *        PERFORM 8400-PRT-DETAIL                                          
                                                                                
      *** PRINT THE EXCEPTION RANGE LINE......                                  
                 PERFORM 7700-PRINT-EXCEPTION-RANGE                             
               END-IF                                                           
                                                                                
           ELSE                                                                 
      *** PRINT THE NO EXCEPTIONS LINE......                                    
             IF PV-LINE-COUNT IS GREATER THAN 61                                
W33304         PERFORM 3300-GET-CYCLE-NAME                                      
               PERFORM 7100-PRINT-HEADINGS                                      
             END-IF                                                             
             WRITE REPORT-LINE                                                  
                  FROM DL-NO-EXCEPTION-LINE AFTER ADVANCING 2 LINES             
           END-IF.                                                              
                                                                                
      *** CLOSE FILES..                                                         
           CLOSE STORE-MERGE-FILE                                               
                 EXCEPT-CONTROL-FILE                                            
                 STORE-INV-EXCEPTION-RPT.                                       
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '* END OF INKRP285                 '.                        
           DISPLAY '**********************************'.                        
                                                                                
      /****************************************************************         
      * 9900-SQL-ABEND.                                                         
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      *****************************************************************         
       9900-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKRP285'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           PERFORM 9990-ABEND.                                                  
                                                                                
       9990-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
