       IDENTIFICATION DIVISION.                                         00010001
       PROGRAM-ID.         WSKUT060.                                    00020001
       AUTHOR.             JIM HUNTER.                                  00030001
       INSTALLATION.       KOHLS DEPARTMENT STORES.                     00040001
       DATE-WRITTEN.       APRIL  2017                                  00050001
      ******************************************************************00060001
      *When compiling check 'MQSERIES PROGRAM(Y/N)' with 'Y' on second *00070001
      *compile screen.                                                 *00080001
      ******************************************************************00090001
      * KICK OFF INVENTORY SYNC FOR A WAMAS EFC                         00100011
      * This program will send the current date and time and a sequence 00120001
      * number to a WAMAS application in order to kick of the inv sync. 00130011
      ******************************************************************00140001
      *  CHANGE LOG                                                     00150001
      *  DATE       CHANGED BY      DESC                                00160001
      *  ---------- -------------   ------------------------------------00170001
      *  09/14/2021 MARJ HOEFS      USE THE STORE NUMBER FROM THE INPUT 00180011
      *                             FILE TO QUERY FOR THE MQ QUEUE SO   00190011
      *                             PGM CAN BE USED FOR ALL WAMAS EFCS. 00191011
      ******************************************************************00200001
       ENVIRONMENT DIVISION.                                            00210001
       CONFIGURATION SECTION.                                           00220001
       SOURCE-COMPUTER.    IBM-3090.                                    00230001
       OBJECT-COMPUTER.    IBM-3090.                                    00240001
                                                                        00250001
       INPUT-OUTPUT SECTION.                                            00260001
       FILE-CONTROL.                                                    00270001
           SELECT SEQUENCE-NBR-FILE-IN     ASSIGN TO IN01.              00280001
           SELECT SEQUENCE-NBR-FILE-OUT    ASSIGN TO OUT01.             00290005
                                                                        00300001
      ******************************************************************00310001
       DATA DIVISION.                                                   00320001
       FILE SECTION.                                                    00330001
                                                                        00340001
       FD  SEQUENCE-NBR-FILE-IN                                         00350001
           RECORDING MODE IS F                                          00360001
           LABEL RECORDS ARE STANDARD                                   00370001
           BLOCK CONTAINS 0 RECORDS                                     00380001
           RECORD CONTAINS 80 CHARACTERS.                               00390001
                                                                        00400001
       01  SEQUENCE-NBR-RECORD-IN            PIC  X(80).                00410001
                                                                        00420001
                                                                        00430001
       FD  SEQUENCE-NBR-FILE-OUT                                        00440001
           RECORDING MODE IS F                                          00450001
           LABEL RECORDS ARE STANDARD                                   00460001
           BLOCK CONTAINS 0 RECORDS                                     00470001
           RECORD CONTAINS 80 CHARACTERS.                               00480001
                                                                        00490001
       01  SEQUENCE-NBR-RECORD-OUT           PIC  X(80).                00500001
                                                                        00510001
      ******************************************************************00520001
       WORKING-STORAGE SECTION.                                         00530001
      ******************************************************************00540001
       01  PS-PROGRAM-SWITCHES.                                         00550001
           05  PS-MQPUT-SUCCESSFUL-IND     PIC X(01) VALUE SPACES.      00560001
               88  PS-MQPUT-SUCCESSFUL               VALUE 'Y'.         00570001
           05  PS-END-OF-MQ-CSR            PIC X(01) VALUE 'N'.         00580004
               88  PS-END-OF-MQ-CSR-Y                VALUE 'Y'.         00590004
               88  PS-END-OF-MQ-CSR-N                VALUE 'N'.         00600004
           05  PS-END-OF-SEQ-NBR-FILE-SW   PIC X(01) VALUE 'N'.         00610001
               88  PS-END-OF-SEQ-NBR-FILE            VALUE 'Y'.         00620001
                                                                        00630001
       01  PA-PROGRAM-ACCUMULATORS.                                     00640001
           05  PA-TOTAL-MQ-MSGS-SENT       PIC S9(9) VALUE +0 COMP-3.   00650001
                                                                        00660001
       01  PV-PROGRAM-VARIABLES.                                        00670001
           05  PV-INTRANSIT-SENT-TO-QUE    PIC 9(05) VALUE ZEROES.      00680001
           05  PV-MQ-CNT                   PIC 9(05) VALUE ZEROES.      00690001
           05  PV-MQ-TBL-CNT               PIC 9(05) VALUE ZEROES.      00700001
           05  PV-MQ-HANDLE-CNT            PIC 9(05) VALUE ZEROES.      00710001
           05  PV-CNT                      PIC 9(05) VALUE ZEROES.      00720001
           05  PV-RETRY-COUNTER            PIC S9(4) VALUE +0 COMP SYNC.00730001
           05  PV-HOLD-COMPLETION-CODE     PIC S9(9) BINARY.            00740001
           05  PV-HOLD-REASON              PIC S9(9) BINARY.            00750001
           05  PV-NEXT-SEQ-NBR             PIC 9(08) VALUE ZEROES.      00760003
           05  PV-CURRENT-TIMESTAMP        PIC X(26) VALUE SPACES.      00770004
           05  PV-CURRENT-TMST REDEFINES PV-CURRENT-TIMESTAMP.          00780004
               10  PV-CURR-YYYY            PIC X(04).                   00790004
               10  FILLER                  PIC X(01).                   00800004
               10  PV-CURR-MM              PIC X(02).                   00810004
               10  FILLER                  PIC X(01).                   00820004
               10  PV-CURR-DD              PIC X(02).                   00830004
               10  FILLER                  PIC X(01).                   00840004
               10  PV-CURR-HH              PIC X(02).                   00850004
               10  FILLER                  PIC X(01).                   00860004
               10  PV-CURR-MI              PIC X(02).                   00870004
               10  FILLER                  PIC X(01).                   00880004
               10  PV-CURR-SS              PIC X(02).                   00890004
               10  FILLER                  PIC X(07).                   00900004
           05  PV-XML-DATE-TIME.                                        00910004
               10  PV-XML-YYYY             PIC X(04) VALUE SPACES.      00920004
               10  PV-XML-MM               PIC X(02) VALUE SPACES.      00930004
               10  PV-XML-DD               PIC X(02) VALUE SPACES.      00940004
               10  PV-XML-HH               PIC X(02) VALUE SPACES.      00950004
               10  PV-XML-MI               PIC X(02) VALUE SPACES.      00960004
               10  PV-XML-SS               PIC X(02) VALUE SPACES.      00970004
                                                                        00980001
      ******************************************************************00990001
      * ABEND INFORMATION                                               01000001
      ******************************************************************01010001
       01  ABEND-CODE                      PIC S9(4) VALUE +0 COMP SYNC.01020001
           88  ABEND-4001-WRITE-ERROR                     VALUE +4001.  01030001
           88  ABEND-4002-READ-ERROR                      VALUE +4002.  01040001
           88  ABEND-4003-CTRL-CARD-ERROR                 VALUE +4003.  01050001
           88  ABEND-4004-TABLE-ERROR                     VALUE +4004.  01060001
           88  ABEND-4005-OPEN-ERROR                      VALUE +4005.  01070001
           88  ABEND-4006-CLOSE-ERROR                     VALUE +4006.  01080001
           88  ABEND-4007-SEQUENCE-ERROR                  VALUE +4007.  01090001
           88  ABEND-4008-DATA-ERROR                      VALUE +4008.  01100001
           88  ABEND-4009-KEY-DATA-ERROR                  VALUE +4009.  01110001
           88  ABEND-4013-DB2-ERROR                       VALUE +4013.  01120001
           88  ABEND-4019-CAL-SUB-ERROR                   VALUE +4019.  01130001
           88  ABEND-4020-MQ-ERROR                        VALUE +4020.  01140001
           88  ABEND-4444-FORCED-ABEND                    VALUE +4444.  01150001
                                                                        01160004
       01  PV-ABEND-VARIABLES.                                          01170001
           05  PV-SQLCODE                  PIC S9(9).                   01180001
           05  PV-DISPLAY-SQLCODE          PIC -------999.              01190001
           05  PV-ABEND-PROGRAM            PIC X(08) VALUE 'WSKUT060'.  01200001
           05  PV-ABEND-PARAGRAPH          PIC X(25) VALUE SPACES.      01210001
           05  PV-ABEND-OPERATION          PIC X(20) VALUE SPACES.      01220001
           05  PV-ABEND-STRUCTURE-1        PIC X(08) VALUE SPACES.      01230001
           05  PV-DB2-OPERATION-MESSAGE-1  PIC X(79) VALUE SPACES.      01240001
           05  PV-ABEND-CODE               PIC S9(4) VALUE +0 COMP SYNC.01250001
                                                                        01260001
                                                                        01270001
       01  PV-HOLD-MSGBUFFER.                                           01280001
           05  PV-MSGBUFFER-CHAR OCCURS   500000 TIMES                  01290001
                                              PIC X(01).                01300004
                                                                        01310001
      ***************************************************************** 01320001
      *  EFC SEQ NBR CONTROL LAYOUT (INPUT AND OUTPUT)                  01330009
      ***************************************************************** 01340001
       01  WS-SEQ-NBR-RECORD.                                           01350001
           05  WS-WHSE-NBR.                                             01360012
               10 WS-WHSE-ZERO                PIC  9(01)  VALUE ZERO.   01361012
               10 WS-STORE-NBR                PIC  9(03)  VALUE ZERO.   01362012
           05  FILLER                         PIC  X      VALUE SPACE.  01370008
           05  WS-NEXT-SEQ-NBR                PIC  9(08)  VALUE ZERO.   01380008
           05  FILLER                         PIC  X(67)  VALUE         01390008
               ' NEXT EFC FILE SEQUENCE NUMBER'.                        01400009
                                                                        01410001
                                                                        01420001
      ***************************************************************** 01430001
      *    RECORD PURPOSE: INVENTORY SYNC (for XML conversion)          01440002
      * The lines in the next 01 level need to start with upper case    01450003
      * and end with lower case for the XML to work.                    01460003
      ***************************************************************** 01470001
                                                                        01480001
       01  PC-XML-BANNER                     PIC X(39) VALUE            01490003
           '<?xml version="1.0" encoding="UTF-8" ?>'.                   01500002
                                                                        01510002
       01  PC-TELEGRAMINFO                   PIC X(13)                  01520004
               VALUE '<DI_TELEGRAM>'.                                   01530002
       01  PC-TELEGRAMINFOSL                 PIC X(14)                  01540003
               VALUE '</DI_TELEGRAM>'.                                  01550002
                                                                        01560002
       01 DI_TELEGRAM.                                                  01570004
          05 header.                                                    01580008
             10 FULL.                                                   01590008
                15 HEADER_SOURCE             PIC X(04) VALUE 'HOST'.    01600008
                15 HEADER_DESTINATION        PIC X(05) VALUE 'WAMAS'.   01610008
                15 HEADER_SEQUENCE           PIC x(14) VALUE SPACES.    01620008
                15 HEADER_CREATIONTIME       PIC X(14) VALUE SPACES.    01630008
                15 HEADER_RECORDTYPENAME     PIC X(17)                  01640008
                                             VALUE 'STOCKREQCTRL00001'. 01650004
          05 body.                                                      01660005
             10 STOCKREQCTRL00001.                                      01670005
                15 requestId                 PIC 9(07) value ZERO.      01680008
                15 systemPartner             PIC X(04) VALUE 'HOST'.    01690004
                15 whLoc                     PIC 9(04) VALUE ZERO.      01700008
                15 ctrlCmd                   PIC X(05) VALUE 'START'.   01710004
                                                                        01720001
       01  PV-XML                      PIC X(500000)   VALUE SPACES.    01730001
       01  XML-LENGTH                  PIC 9(9)    COMP.                01740001
                                                                        01750001
           COPY ISWS007.                                                01760001
           COPY ISWS008.                                                01770001
           COPY ISWS009.                                                01780001
                                                                        01790001
      ***************************************************************** 01800001
      * FILE OPEN STATUS'S ON THE CONTROL CARDS USED FOR GETTING        01810001
      * MQ-SERIES QUEUE MANAGER AND QUEUE NAME.                         01820001
      ***************************************************************** 01830001
                                                                        01840001
       01  PE-MQ-ABEND-VARIABLES.                                       01850001
           05  PE-MQ-QMANAGER              PIC X(05) VALUE SPACES.      01860001
           05  PE-MQ-QMANAGER-REMOTE       PIC X(30) VALUE SPACES.      01870001
           05  PE-MQ-QNAME                 PIC X(48) VALUE SPACES.      01880001
           05  PE-MQ-COMPLETION-CODE       PIC 9(04) VALUE ZEROES.      01890001
           05  PE-MQ-REASON-CODE           PIC 9(04) VALUE ZEROES.      01900001
                                                                        01910001
      ******************************************************************01920001
      *  DB2 FORMATTED MESSAGE AREA                                     01930001
      ******************************************************************01940001
           COPY DPWS004.                                                01950001
                                                                        01960001
      ******************************************************************01970001
      *  COMMUNICATIONS AREA FOR DB2                                    01980001
      ******************************************************************01990001
           EXEC SQL                                                     02000001
                INCLUDE SQLCA                                           02010001
           END-EXEC.                                                    02020001
                                                                        02030001
      ******************************************************************02040001
      *  DB2 DECLARATION - WMOS - Provider Queue information (Put/Write)02050001
      *      TABLE (WSV_PRVDR_QUE)                                      02060001
      ******************************************************************02070001
           EXEC SQL                                                     02080001
               INCLUDE WSV00005                                         02090001
           END-EXEC.                                                    02100001
                                                                        02110001
      ******************************************************************02120001
      * COBOL CURSOR DELCARATION TO READ MQ INFORMATION                *02130001
      ******************************************************************02140001
           EXEC SQL                                                     02150001
              DECLARE MQ-CSR CURSOR WITH HOLD FOR                       02160001
              SELECT A.INTGRTN_TYP_DESC                                 02170001
                   , A.LCL_QUE_MGR                                      02180001
                   , A.RMT_QUE_MGR                                      02190001
                   , A.QUE_NM                                           02200001
                FROM WSV_PRVDR_QUE A                                    02210001
               WHERE A.APPL_FUNC_DESC    = 'QUEUE'                      02220001
                 AND A.INTGRTN_TYP_CDE   = 'INVSYNCQ'                   02230005
                 AND A.DC_NBR            = :WSV00005-DC-NBR             02231014
           END-EXEC.                                                    02240001
                                                                        02250001
      ****************************************************************  02260001
      **     MQ SERIES WORKING STORAGE                                  02270001
      **         MQ COPYBOOKS                                           02280001
      ****************************************************************  02290001
       01  MQM-OBJECT-DESCRIPTOR.                                       02300001
           COPY CMQODV.                                                 02310001
                                                                        02320001
       01  MQM-MESSAGE-DESCRIPTOR.                                      02330001
           COPY CMQMDV.                                                 02340001
                                                                        02350001
       01  MQM-PUT-MESSAGE-OPTIONS.                                     02360001
           COPY CMQPMOV.                                                02370001
                                                                        02380001
       01  MQM-GET-MESSAGE-OPTIONS.                                     02390001
           COPY CMQGMOV.                                                02400001
                                                                        02410001
       01  MQM-CONSTANTS.                                               02420001
           COPY CMQV.                                                   02430001
                                                                        02440001
      ****************************************************************  02450001
      ** CONTROL CARDS FOR QUEUE MANAGER AND QUEUE NAME                 02460001
      ****************************************************************  02470001
       01  CC-CONTROL-CARD-1.                                           02480001
           05  CC-QMGR-NAME-REMOTE         PIC X(50).                   02490001
                                                                        02500001
       01  CC-CONTROL-CARD-2.                                           02510001
           05  CC-TRANS-QUEUE-NAME         PIC X(50).                   02520001
                                                                        02530001
       01  CC-CONTROL-CARD-4.                                           02540001
           05  CC-QMGR-NAME                PIC X(50).                   02550001
                                                                        02560001
      ****************************************************************  02570001
      *  MQ SERIES CONSTANTS FOR CALLING MODULES SUCH AS GET AND    **  02580001
      *  OPEN, CONNECT, DISCONNECT.                                     02590001
      ****************************************************************  02600001
       01  MQ-WORKING-STORAGE.                                          02610001
           05  PC-MQCONN                   PIC X(08) VALUE 'CSQBCONN'.  02620001
           05  PC-MQOPEN                   PIC X(08) VALUE 'CSQBOPEN'.  02630001
           05  PC-MQINQ                    PIC X(08) VALUE 'CSQBINQ'.   02640001
           05  PC-MQPUT                    PIC X(08) VALUE 'CSQBPUT'.   02650001
           05  PC-MQGET                    PIC X(08) VALUE 'CSQBGET'.   02660001
           05  PC-MQCMIT                   PIC X(08) VALUE 'CSQBCOMM'.  02670001
           05  PC-MQBACK                   PIC X(08) VALUE 'CSQBBACK'.  02680001
           05  PC-MQCLOSE                  PIC X(08) VALUE 'CSQBCLOS'.  02690001
           05  PC-MQDISC                   PIC X(08) VALUE 'CSQBDISC'.  02700001
           05  PC-TRANS-QNAME              PIC X(48) VALUE SPACES.      02710001
           05  PC-QMGR-NAME                PIC X(04) VALUE SPACES.      02720001
               88  PC-PROD-QMGR-NAME                 VALUE 'QKSP'.      02730001
               88  PC-ACCEPT-QMGR-NAME               VALUE 'QKSQ'.      02740001
               88  PC-TEST-QMGR-NAME                 VALUE 'QKST'.      02750001
                                                                        02760001
      ****************************************************************  02770001
      ** QUEUE MANAGER HANDLE                                           02780001
      *  QUEUE HANDLE                                                   02790001
      ****************************************************************  02800001
       01 PV-MQ-PROGRAM-HANDLES.                                        02810001
           05  PV-QM-NAME                  PIC X(48).                   02820001
           05  PV-QM-NAME-REMOTE           PIC X(48).                   02830001
           05  PV-HCONN                    PIC S9(9) BINARY VALUE 0.    02840001
           05  PV-PQ-HANDLE                PIC S9(9) BINARY VALUE 0.    02850001
                                                                        02860001
      ****************************************************************  02870001
      * ERROR PROCESSING VARIABLES                                      02880001
      * CODES FROM CALLS ARE STORED HERE                                02890001
      ****************************************************************  02900001
       01 PV-MQ-PROGRAM-VARIABLES.                                      02910001
           05  PV-COMPLETION-CODE          PIC S9(9) BINARY.            02920001
           05  PV-REASON                   PIC S9(9) BINARY.            02930001
           05  PV-CON-REASON               PIC S9(9) BINARY.            02940001
           05  PV-MQCHECK                  PIC X(08) VALUE 'MQR2C'.     02950001
           05  PV-ERROR-MESSAGE            PIC X(48) VALUE SPACES.      02960001
           05  PV-OPEN-OPTIONS             PIC S9(9) BINARY.            02970001
           05  PV-MQ-REASON-TEXT           PIC X(30) VALUE SPACES.      02980001
           05  PV-RETRY-COUNTER            PIC S9(4) VALUE +0 COMP SYNC.02990001
           05  PV-PARAGRAPH                PIC X(48) VALUE SPACES.      03000001
           05  PV-MSG-LENGTH               PIC S9(9) BINARY.            03010001
           05  PV-DC-NBR                   PIC 9(04).                   03020001
           05  PV-MSG-LENGTH-DISPLAY       PIC S9(9).                   03030001
           05  PV-MSGBUFFER.                                            03040001
               10  PV-MSGBUFFER-banner     PIC X(39) VALUE SPACES.      03050001
               10  PV-MSGBUFFER-data       PIC X(999999) VALUE SPACES.  03060001
                                                                        03070001
       01  PC-PROGRAM-CONSTANTS.                                        03080001
           05  PC-CURRENT-PROGRAM-NAME     PIC X(08) VALUE 'WSKUT060'.  03090002
           05  PC-WAIT-15-SECS             PIC S9(9) VALUE 1500.        03100001
           05  PC-MQPUT-RETRY-COUNTER      PIC 9(3)  VALUE ZERO.        03110001
           05  PC-DELAY-INTERVAL.                                       03120001
               10  FILLER                  PIC X(2)  VALUE SPACES.      03130001
               10  PC-DELAY-TIME           PIC 9(8)  VALUE 3000.        03140001
                                                                        03150003
      ****************************************************************  03160001
      * RETURN CODES ARE EVALUATED AND THIS SWITCH IS USED TO           03170001
      * INDICATE ANY ERRORS (THIS SWITCH MAY NOT BE USED SINCE THE      03180001
      * PROGRAM ABENDS BEFORE IT IS USED ANYWHERE)                      03190001
      ****************************************************************  03200001
       01 PV-MQ-PROGRAM-SWITCHES.                                       03210001
           05  PS-MQ-ERROR-SW              PIC X(01) VALUE 'N'.         03220001
               88  PS-NO-MQ-ERROR                    VALUE 'N'.         03230001
               88  PS-MQ-ERROR                       VALUE 'Y'.         03240001
       01  PV-MQ-INFO-FOR-DIFF-DCS.                                     03250001
           05  MQ-INFO OCCURS 1 TO 20 TIMES                             03260001
                            DEPENDING ON PV-MQ-CNT.                     03270001
              10 MQ-INTGRTN-TYP-DESC        PIC X(50).                  03280001
              10 MQ-QMGR-NAME               PIC X(50).                  03290001
              10 MQ-QMGR-NAME-REMOTE        PIC X(50).                  03300001
              10 MQ-TRANS-QUEUE-NAME        PIC X(50).                  03310001
       01  PV-MQ-HANDLE-FOR-DIFF-DCS.                                   03320001
           05  PV-MQ-HANDLE-INFO OCCURS 1 TO 20 TIMES                   03330001
                            DEPENDING ON PV-MQ-HANDLE-CNT.              03340001
              10 PV-MQ-HANDLE-HOLD  PIC S9(9) BINARY VALUE 0.           03350001
                                                                        03360001
      ****************************************************************  03370001
       PROCEDURE DIVISION.                                              03380001
      *                                                                 03390001
       0000-MAINLINE.                                                   03400001
           DISPLAY '************ START OF WSKUT060 ***************'     03410002
                                                                        03420001
           PERFORM 1000-INITIALIZE.                                     03430001
                                                                        03440001
           PERFORM 2000-BUILD-XML.                                      03450003
                                                                        03460003
           PERFORM 4000-CREATE-MSGS.                                    03470003
                                                                        03480003
           PERFORM 1500-MQ-QUEUE-CLOSE.                                 03490001
                                                                        03500001
           PERFORM 1600-DISCONNECT-QMGR.                                03510001
                                                                        03520001
           PERFORM 9000-EOJ-ROUTINE.                                    03530001
                                                                        03540001
           DISPLAY '************  END OF WSKUT060  ***************'     03550002
           STOP RUN.                                                    03560001
                                                                        03570001
      ******************************************************************03580001
      *           INITIALIZATION                                        03590001
      ******************************************************************03600001
       1000-INITIALIZE.                                                 03610001
                                                                        03620001
           OPEN  INPUT SEQUENCE-NBR-FILE-IN.                            03620111
           OPEN OUTPUT SEQUENCE-NBR-FILE-OUT.                           03620211
                                                                        03620311
           PERFORM 1100-READ-SEQ-NBR-FILE.                              03620411
                                                                        03620511
      *READ MQ CONTROL CARDS.                                           03630001
                                                                        03640001
           PERFORM 1200-GET-MQ-QUEUE-INFO.                              03650001
           MOVE WSV00005-INTGRTN-TYP-DESC                               03660001
                  TO MQ-INTGRTN-TYP-DESC(PV-MQ-CNT).                    03670001
           MOVE CC-QMGR-NAME                                            03680001
                  TO MQ-QMGR-NAME(PV-MQ-CNT).                           03690001
           MOVE CC-QMGR-NAME-REMOTE                                     03700001
                  TO MQ-QMGR-NAME-REMOTE(PV-MQ-CNT).                    03710001
           MOVE CC-TRANS-QUEUE-NAME                                     03720001
                  TO MQ-TRANS-QUEUE-NAME(PV-MQ-CNT).                    03730001
                                                                        03740001
      *CONNECT TO QUEUE MANAGER ONLY ONCE.                              03750001
                                                                        03760001
           PERFORM 1300-MQ-CONNECT-TO-QMGR.                             03770001
                                                                        03780001
      *OPEN THE MULTIPLE QUEUES FOR PUTS                                03790001
                                                                        03800001
           PERFORM 1400-MQ-QUEUE-OPEN.                                  03810001
                                                                        03820001
           MOVE ZEROES TO PV-INTRANSIT-SENT-TO-QUE                      03830003
                          PV-MQ-CNT.                                    03840003
                                                                        03850003
                                                                        03910001
      ******************************************************************03920001
      * READ THE NEXT SEQUENCE NUMBER CONTROL FILE.                     03930001
      * ABEND IF THE FILE IS EMPTY OR THE NEXT SEQ NBR IS NOT NUMERIC.  03940001
      ******************************************************************03950001
       1100-READ-SEQ-NBR-FILE.                                          03960001
                                                                        03970001
           READ SEQUENCE-NBR-FILE-IN    INTO WS-SEQ-NBR-RECORD          03980001
              AT END                                                    03990001
                 SET PS-END-OF-SEQ-NBR-FILE    TO TRUE.                 04000001
                                                                        04010001
           IF PS-END-OF-SEQ-NBR-FILE                                    04020001
               MOVE '1100-READ-SEQ-NBR-FILE'   TO PV-ABEND-PARAGRAPH    04030001
               MOVE 'INPUT SEQ NBR RECORD IS EMPTY'                     04040001
                                               TO PV-ABEND-OPERATION    04050001
               SET ABEND-4003-CTRL-CARD-ERROR  TO TRUE                  04060001
               PERFORM 9800-ABEND                                       04070001
           ELSE                                                         04080001
               MOVE WS-NEXT-SEQ-NBR            TO PV-NEXT-SEQ-NBR       04090001
               MOVE WS-STORE-NBR               TO WSV00005-DC-NBR       04091014
               DISPLAY 'WS-STORE-NBR:' WS-STORE-NBR                     04092015
           END-IF.                                                      04100001
                                                                        04110001
                                                                        04120001
           IF PV-NEXT-SEQ-NBR         IS NUMERIC                        04130001
               DISPLAY 'NEXT FILE SEQ NBR = '  PV-NEXT-SEQ-NBR          04140001
           ELSE                                                         04150001
               MOVE '1100-READ-SEQ-NBR-FILE' TO PV-ABEND-PARAGRAPH      04160001
               STRING 'NON-NUM SEQ NBR IN CTL FILE = '                  04170001
                       PV-NEXT-SEQ-NBR                                  04180001
                   DELIMITED BY SIZE                                    04190001
                   INTO PV-ABEND-OPERATION                              04200001
               SET ABEND-4008-DATA-ERROR     TO TRUE                    04210001
               PERFORM 9800-ABEND                                       04220001
           END-IF.                                                      04230001
                                                                        04240001
                                                                        04250001
      ****************************************************************  04260001
      * GET MQ INFORMATION FROM DB2 TABLES.                             04270001
      ****************************************************************  04280001
       1200-GET-MQ-QUEUE-INFO.                                          04290001
                                                                        04300001
           PERFORM 1210-OPEN-MQ-CSR.                                    04310001
                                                                        04320001
           PERFORM 1220-FETCH-MQ-CURSOR UNTIL                           04330001
                 PS-END-OF-MQ-CSR-Y.                                    04340001
                                                                        04350001
           PERFORM 1225-CLOSE-MQ-CURSOR.                                04360001
                                                                        04370001
      ****************************************************************  04380001
      * OPEN MQ CURSOR.                                                 04390001
      ****************************************************************  04400001
       1210-OPEN-MQ-CSR.                                                04410001
                                                                        04420001
           EXEC SQL                                                     04430001
               OPEN MQ-CSR                                              04440001
           END-EXEC.                                                    04450001
                                                                        04460001
           EVALUATE TRUE                                                04470001
             WHEN SQLCODE = ZERO                                        04480001
                 SET PS-END-OF-MQ-CSR-N TO TRUE                         04490001
                                                                        04500001
             WHEN OTHER                                                 04510001
                 MOVE '1210-OPEN-MQ-CURSOR' TO PV-ABEND-PARAGRAPH       04520001
                 MOVE 'OPEN MQ-CSR'         TO PV-ABEND-OPERATION       04530001
                 PERFORM 9900-DB2-ABEND                                 04540001
           END-EVALUATE.                                                04550001
                                                                        04560001
      ****************************************************************  04570001
      * FETCH MQ INFORMATION FOR PUTS.                                  04580001
      ****************************************************************  04590001
       1220-FETCH-MQ-CURSOR.                                            04600001
                                                                        04610001
           EXEC SQL                                                     04620001
               FETCH MQ-CSR                                             04630001
                INTO                                                    04640001
                    :WSV00005-INTGRTN-TYP-DESC                          04650001
                   ,:CC-QMGR-NAME                                       04660001
                   ,:CC-QMGR-NAME-REMOTE                                04670001
                   ,:CC-TRANS-QUEUE-NAME                                04680001
           END-EXEC.                                                    04690001
                                                                        04700001
           EVALUATE TRUE                                                04710001
             WHEN SQLCODE = ZERO                                        04720001
                                                                        04730001
                 DISPLAY 'QMGR-NAME-REMOTE:' CC-QMGR-NAME-REMOTE        04740001
                 DISPLAY 'QMGR-NAME:'        CC-QMGR-NAME               04751015
                 DISPLAY 'TRANS-QUEUE-NAME:' CC-TRANS-QUEUE-NAME        04752015
                                                                        04760001
                 ADD 1 TO PV-MQ-CNT                                     04770001
                                                                        04780001
                                                                        04790001
                 MOVE WSV00005-INTGRTN-TYP-DESC                         04800001
                        TO MQ-INTGRTN-TYP-DESC(PV-MQ-CNT)               04810001
                 MOVE CC-QMGR-NAME                                      04820001
                        TO MQ-QMGR-NAME(PV-MQ-CNT)                      04830001
                 MOVE CC-QMGR-NAME-REMOTE                               04840001
                        TO MQ-QMGR-NAME-REMOTE(PV-MQ-CNT)               04850001
                 MOVE CC-TRANS-QUEUE-NAME                               04860001
                        TO MQ-TRANS-QUEUE-NAME(PV-MQ-CNT)               04870001
                                                                        04880001
             WHEN SQLCODE = +100                                        04890001
                 SET PS-END-OF-MQ-CSR-Y TO TRUE                         04900001
                                                                        04910001
             WHEN OTHER                                                 04920001
                 MOVE '1220-FETCH-MQ-CURSOR' TO PV-ABEND-PARAGRAPH      04930001
                 MOVE 'FETCH MQ-CSR'         TO PV-ABEND-OPERATION      04940001
                 PERFORM 9900-DB2-ABEND                                 04950001
           END-EVALUATE.                                                04960001
                                                                        04970001
      ******************************************************************04980001
       1225-CLOSE-MQ-CURSOR.                                            04990001
                                                                        05000001
           EXEC SQL                                                     05010001
               CLOSE MQ-CSR                                             05020001
           END-EXEC.                                                    05030001
                                                                        05040001
           EVALUATE TRUE                                                05050001
             WHEN SQLCODE = ZERO                                        05060001
             DISPLAY ' TOTAL MQ PUTS:' PV-MQ-CNT                        05070001
                 CONTINUE                                               05080001
                                                                        05090001
             WHEN OTHER                                                 05100001
                 MOVE '1225-CLOSE-MQ-CURSOR' TO PV-ABEND-PARAGRAPH      05110001
                 MOVE 'CLOSE MQ-CSR'         TO PV-ABEND-OPERATION      05120001
                 PERFORM 9900-DB2-ABEND                                 05130001
           END-EVALUATE.                                                05140001
                                                                        05150001
      ****************************************************************  05160001
      ** CONNECT TO THE MULTIPLE QUEUE MANAGERS.                        05170001
      ** GET THE HANDLE TO THE QUEUE MANAGER - PV-HCONN                 05180001
      ****************************************************************  05190001
       1300-MQ-CONNECT-TO-QMGR.                                         05200001
                                                                        05210001
           MOVE '1300-MQ-CONNECT-TO-QMGR' TO PV-PARAGRAPH.              05220001
           MOVE 1 TO PV-MQ-TBL-CNT.                                     05230001
                                                                        05240001
           MOVE MQ-QMGR-NAME(PV-MQ-TBL-CNT) TO PV-QM-NAME.              05250001
                                                                        05260001
           CALL PC-MQCONN USING PV-QM-NAME                              05270001
                                PV-HCONN                                05280001
                                PV-COMPLETION-CODE                      05290001
                                PV-REASON.                              05300001
                                                                        05310001
           MOVE PV-REASON TO PV-CON-REASON.                             05320001
           IF PV-COMPLETION-CODE = MQCC-OK                              05330001
              ADD 1 TO PV-MQ-TBL-CNT                                    05340001
              DISPLAY 'CONNECTED TO QM -' MQ-QMGR-NAME(PV-MQ-TBL-CNT)   05350001
               CONTINUE                                                 05360001
           ELSE                                                         05370001
               CALL PV-MQCHECK USING PV-REASON                          05380001
                                     PV-MQ-REASON-TEXT                  05390001
               MOVE PV-QM-NAME           TO PE-MQ-QMANAGER              05400001
               MOVE PV-COMPLETION-CODE   TO PE-MQ-COMPLETION-CODE       05410001
               MOVE PV-REASON            TO PE-MQ-REASON-CODE           05420001
               MOVE '1300-MQ-CONNECT-TO-QMGR'                           05430001
                                         TO PV-ERROR-MESSAGE            05440001
               PERFORM 9200-MQ-ABEND                                    05450001
           END-IF.                                                      05460001
                                                                        05470001
      ****************************************************************  05480001
      ** OPEN MULTIPLE QUEUES FOR 'PUT'                                 05490001
      ****************************************************************  05500001
       1400-MQ-QUEUE-OPEN.                                              05510001
                                                                        05520001
           MOVE 1 TO PV-MQ-TBL-CNT.                                     05530001
           PERFORM 1410-MULTI-MQ-OPEN PV-MQ-CNT TIMES.                  05540001
                                                                        05550001
      ****************************************************************  05560001
      ** OPEN MULTIPLE QUEUES FOR 'PUT'                                 05570001
      ****************************************************************  05580001
       1410-MULTI-MQ-OPEN.                                              05590001
                                                                        05600001
           MOVE '1410-MUTI-MQ-OPEN' TO PV-PARAGRAPH.                    05610001
                                                                        05620001
      * CONTROL CARD 2 - QUEUE NAME                                     05630001
           MOVE MQ-TRANS-QUEUE-NAME(PV-MQ-TBL-CNT)  TO PC-TRANS-QNAME.  05640001
           COMPUTE PV-OPEN-OPTIONS = MQOO-OUTPUT                        05650001
                                   + MQOO-SET-IDENTITY-CONTEXT          05660001
                                   + MQOO-FAIL-IF-QUIESCING.            05670001
           MOVE PC-TRANS-QNAME       TO MQOD-OBJECTNAME.                05680001
                                                                        05690001
           DISPLAY 'PC-TRANS-QNAME:' PC-TRANS-QNAME                     05700001
           DISPLAY 'MQOD-OBJECTNAME:' MQOD-OBJECTNAME                   05710001
                                                                        05720001
      * CONTROL CARD 3 - REMOTE QMGR NAME                               05730001
           MOVE MQ-QMGR-NAME-REMOTE(PV-MQ-TBL-CNT)                      05740001
                                TO MQOD-OBJECTQMGRNAME.                 05750001
                                                                        05760001
      ******************************************************************05770001
      *REPLACE REMOTE QMGR NAME WITH LOCAL QMGR NAME TO WRITE TO LOCAL Q05780001
      ******************************************************************05790001
                                                                        05800001
           DISPLAY 'MQOD-OBJECTQMGRNAME:' MQOD-OBJECTQMGRNAME           05810001
           DISPLAY 'MQOD-OBJECTNAME    :' MQOD-OBJECTNAME               05820001
           DISPLAY 'PV-HCONN:' PV-HCONN                                 05830001
           DISPLAY 'MQM-OBJECT-DESCRIPTOR:' MQM-OBJECT-DESCRIPTOR       05840001
           DISPLAY 'PV-OPEN-OPTIONS:' PV-OPEN-OPTIONS                   05850001
           DISPLAY 'PV-PQ-HANDLE:' PV-PQ-HANDLE                         05860001
                                                                        05870001
           CALL PC-MQOPEN USING PV-HCONN                                05880001
                                MQM-OBJECT-DESCRIPTOR                   05890001
                                PV-OPEN-OPTIONS                         05900001
                                PV-PQ-HANDLE                            05910001
                                PV-COMPLETION-CODE                      05920001
                                PV-REASON.                              05930001
                                                                        05940001
           IF PV-COMPLETION-CODE = MQCC-OK THEN                         05950001
               ADD 1 TO PV-MQ-TBL-CNT                                   05960001
                        PV-MQ-HANDLE-CNT                                05970001
               MOVE  PV-PQ-HANDLE TO PV-MQ-HANDLE-HOLD(PV-MQ-HANDLE-CNT)05980001
               DISPLAY 'OPEN COMPLETE'                                  05990001
               CONTINUE                                                 06000001
           ELSE                                                         06010001
               CALL PV-MQCHECK USING PV-REASON                          06020001
                                     PV-MQ-REASON-TEXT                  06030001
               MOVE PV-QM-NAME-REMOTE    TO PE-MQ-QMANAGER-REMOTE       06040001
               MOVE PC-TRANS-QNAME       TO PE-MQ-QNAME                 06050001
               MOVE PV-COMPLETION-CODE   TO PE-MQ-COMPLETION-CODE       06060001
               MOVE PV-REASON            TO PE-MQ-REASON-CODE           06070001
               MOVE '1410-MULTI-MQ-OPEN' TO PV-ERROR-MESSAGE            06080001
               PERFORM 9200-MQ-ABEND                                    06090001
           END-IF.                                                      06100001
                                                                        06110001
      ****************************************************************  06120001
      ** CLOSES THE MQ QUEUE.                                           06130001
      ****************************************************************  06140001
       1500-MQ-QUEUE-CLOSE.                                             06150001
                                                                        06160001
           MOVE 1 TO PV-MQ-HANDLE-CNT.                                  06170001
           PERFORM 1510-MQ-MULTI-QUEUE-CLOSE PV-MQ-CNT TIMES.           06180001
                                                                        06190001
       1510-MQ-MULTI-QUEUE-CLOSE.                                       06200001
                                                                        06210001
           MOVE '1510-MQ-MULTI-QUEUE-CLOSE' TO PV-PARAGRAPH.            06220001
                                                                        06230001
           CALL PC-MQCLOSE USING PV-HCONN                               06240001
                                 PV-MQ-HANDLE-HOLD(PV-MQ-HANDLE-CNT)    06250001
                                 MQCO-NONE                              06260001
                                 PV-COMPLETION-CODE                     06270001
                                 PV-REASON.                             06280001
                                                                        06290001
           IF PV-COMPLETION-CODE = MQCC-OK                              06300001
               CONTINUE                                                 06310001
           ELSE                                                         06320001
               CALL PV-MQCHECK USING PV-REASON                          06330001
                                     PV-MQ-REASON-TEXT                  06340001
               MOVE PV-QM-NAME           TO PE-MQ-QMANAGER              06350001
               MOVE PC-TRANS-QNAME       TO PE-MQ-QNAME                 06360001
               MOVE PV-COMPLETION-CODE   TO PE-MQ-COMPLETION-CODE       06370001
               MOVE PV-REASON            TO PE-MQ-REASON-CODE           06380001
               MOVE 'MQ-CLOSE'           TO PV-ERROR-MESSAGE            06390001
               PERFORM 9200-MQ-ABEND                                    06400001
           END-IF.                                                      06410001
                                                                        06420001
      ****************************************************************  06430001
      ** DISCONNECT FROM THE QUEUE MANAGER                              06440001
      ****************************************************************  06450001
       1600-DISCONNECT-QMGR.                                            06460001
           MOVE '1600-DISCONNECT-QMGR' TO PV-PARAGRAPH.                 06470001
                                                                        06480001
           CALL PC-MQDISC USING PV-HCONN                                06490001
                                PV-COMPLETION-CODE                      06500001
                                PV-REASON                               06510001
                                                                        06520001
           IF PV-COMPLETION-CODE = MQCC-OK                              06530001
              CONTINUE                                                  06540001
           ELSE                                                         06550001
              CALL PV-MQCHECK USING PV-REASON                           06560001
                                    PV-MQ-REASON-TEXT                   06570001
              MOVE PV-QM-NAME             TO PE-MQ-QMANAGER             06580001
              MOVE PC-TRANS-QNAME         TO PE-MQ-QNAME                06590001
              MOVE PV-COMPLETION-CODE     TO PE-MQ-COMPLETION-CODE      06600001
              MOVE PV-REASON              TO PE-MQ-REASON-CODE          06610001
              MOVE '1600-DISCONNECT-QMGR' TO PV-ERROR-MESSAGE           06620001
              PERFORM 9200-MQ-ABEND                                     06630001
           END-IF.                                                      06640001
                                                                        06650001
                                                                        06660001
      ******************************************************************06670001
      * BUILD XML VALUES                                                06680003
      ******************************************************************06690001
       2000-BUILD-XML.                                                  06700003
                                                                        06710001
           PERFORM 2100-GET-CURRENT-TIMESTAMP.                          06720004
                                                                        06730004
           MOVE PV-CURR-YYYY            TO  PV-XML-YYYY.                06740004
           MOVE PV-CURR-MM              TO  PV-XML-MM.                  06750004
           MOVE PV-CURR-DD              TO  PV-XML-DD.                  06760004
           MOVE PV-CURR-HH              TO  PV-XML-HH.                  06770004
           MOVE PV-CURR-MI              TO  PV-XML-MI.                  06780004
           MOVE PV-CURR-SS              TO  PV-XML-SS.                  06790004
                                                                        06800004
           MOVE PV-XML-DATE-TIME        TO  HEADER_CREATIONTIME         06810007
                                            HEADER_SEQUENCE.            06820007
                                                                        06830002
           move WS-WHSE-NBR             to  whLoc.                      06840008
           MOVE PV-NEXT-SEQ-NBR         TO  requestId.                  06850003
                                                                        06860004
                                                                        06870008
      ******************************************************************06880004
      *  GET THE CURRENT TIMESTAMP FROM DB2                             06890004
      ******************************************************************06900004
       2100-GET-CURRENT-TIMESTAMP.                                      06910004
                                                                        06920004
           EXEC SQL                                                     06930004
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           06940004
           END-EXEC.                                                    06950004
                                                                        06960004
           IF SQLCODE = 0                                               06970004
               CONTINUE                                                 06980004
           ELSE                                                         06990004
               MOVE '2100-GET-CURRENT-TIMESTAMP' TO PV-ABEND-PARAGRAPH  07000004
               MOVE 'ERROR ON GET OF TIMESTAMP'  TO PV-ABEND-OPERATION  07010004
               PERFORM 9900-DB2-ABEND                                   07020004
           END-IF.                                                      07030004
                                                                        07040004
                                                                        07050001
      ****************************************************************  07060001
      * CREATE XML FOR RDC AND OUTPUT TO MQ                             07070001
      ****************************************************************  07080001
       4000-CREATE-MSGS.                                                07090001
                                                                        07100001
           PERFORM 4100-GENERATE-XML.                                   07110001
           ADD +1 TO PV-INTRANSIT-SENT-TO-QUE.                          07120001
                                                                        07130001
           MOVE PC-XML-BANNER              TO PV-MSGBUFFER-banner.      07140002
           MOVE PV-XML(1:XML-LENGTH)       TO PV-MSGBUFFER-data.        07150001
           DISPLAY pv-msgbuffer-banner.                                 07160001
           DISPLAY pv-msgbuffer-data(1:500)                             07170005
           DISPLAY 'XML-LENGTH'  XML-LENGTH.                            07180001
           COMPUTE PV-MSG-LENGTH = XML-LENGTH                           07190001
                                 + LENGTH OF PC-XML-BANNER.             07200002
           DISPLAY ' PV-MSG-LENGTH = ' PV-MSG-LENGTH.                   07210001
           MOVE PV-MSG-LENGTH TO PV-MSG-LENGTH-DISPLAY.                 07220001
           INITIALIZE PS-MQPUT-SUCCESSFUL-IND                           07230001
                      PC-MQPUT-RETRY-COUNTER.                           07240001
                                                                        07250001
           PERFORM 5000-MQ-PUT-TO-QUEUE                                 07260008
             UNTIL PS-MQPUT-SUCCESSFUL.                                 07270008
                                                                        07280001
           IF PV-INTRANSIT-SENT-TO-QUE = 5000                           07290001
               PERFORM 5100-MQ-COMMIT                                   07300001
               MOVE ZEROS TO PV-INTRANSIT-SENT-TO-QUE                   07310001
           END-IF.                                                      07320001
                                                                        07330001
                                                                        07340001
                                                                        07350001
      ***********************AssumptiveReceive***********************   07360001
       4100-GENERATE-XML.                                               07370001
      ***  WITH ATTRIBUTES parm below is commented since using it       07380008
      ***  causes the detail fields to not have labels and brackets     07390008
      ***  before and after them.                                       07400008
                                                                        07410001
           MOVE SPACES TO PV-XML.                                       07420001
           XML GENERATE PV-XML                                          07430001
               FROM DI_TELEGRAM                                         07440004
               COUNT IN XML-LENGTH                                      07450001
      ***      WITH ATTRIBUTES                                          07460008
               ON EXCEPTION                                             07470001
                   DISPLAY DI_TELEGRAM ' PV=' PV-XML                    07480004
                   PERFORM 4110-XML-ERROR                               07490001
               NOT ON EXCEPTION                                         07500001
                   CONTINUE                                             07510001
           END-XML.                                                     07520001
                                                                        07530001
      ***************************************************************   07540001
       4110-XML-ERROR.                                                  07550001
                                                                        07560001
           PERFORM 9000-EXPLAIN-XML-RESPONSE                            07570001
           PERFORM  VARYING XML-LINE-IDX FROM 1 BY 1                    07580001
                      UNTIL XML-LINE-IDX > XML-RESPONSE-LINES           07590001
           END-PERFORM.                                                 07600001
           MOVE +4018         TO ABEND-CODE.                            07610001
           CALL 'ILBOABN0' USING ABEND-CODE.                            07620001
                                                                        07630001
           COPY ISPD007.                                                07640001
           COPY ISPD008.                                                07650001
           COPY ISPD009.                                                07660001
                                                                        07670001
      ******************************************************************07680001
      *      PUT MQ MESSAGE                                             07690001
      ******************************************************************07700001
       5000-MQ-PUT-TO-QUEUE.                                            07710001
                                                                        07720001
           MOVE ZEROES TO PV-CNT.                                       07730001
           PERFORM 5100-MQ-MULTI-PUTS PV-MQ-HANDLE-CNT TIMES.           07740001
                                                                        07750001
       5100-MQ-MULTI-PUTS.                                              07760001
                                                                        07770001
           ADD 1 TO PV-CNT.                                             07780001
           MOVE MQMI-NONE    TO MQMD-MSGID.                             07790001
           MOVE MQCI-NONE    TO MQMD-CORRELID.                          07800001
           MOVE MQFMT-STRING TO MQMD-FORMAT.                            07810001
                                                                        07820001
           COMPUTE MQPMO-OPTIONS  = MQPMO-SYNCPOINT                     07830001
                                  + MQPMO-SET-IDENTITY-CONTEXT.         07840001
           PERFORM 5010-LOW-VALUES.                                     07850001
                                                                        07860001
           CALL PC-MQPUT USING PV-HCONN                                 07870001
                               PV-MQ-HANDLE-HOLD(PV-CNT)                07880001
                               MQM-MESSAGE-DESCRIPTOR                   07890001
                               MQM-PUT-MESSAGE-OPTIONS                  07900001
                               PV-MSG-LENGTH                            07910001
                               PV-MSGBUFFER                             07920001
                               PV-COMPLETION-CODE                       07930001
                               PV-REASON.                               07940001
                                                                        07950001
      *IF PUT FAILED THEN DISPLAY ERROR MESSAGE                         07960001
           IF (PV-COMPLETION-CODE NOT = MQCC-OK)                        07970001
               IF (PV-REASON NOT = MQRC-PAGESET-FULL                    07980001
                 AND PV-REASON NOT = MQRC-Q-FULL                        07990001
                 AND PV-REASON NOT = MQRC-Q-SPACE-NOT-AVAILABLE )       08000001
                   CALL PV-MQCHECK USING PV-REASON                      08010001
                                         PV-MQ-REASON-TEXT              08020001
                   DISPLAY 'MQ MESSAGE = ' PV-MSGBUFFER                 08030001
                   MOVE '5000-MQ-PUT-TO-QUEUE'  TO PV-PARAGRAPH         08040001
                   MOVE 'MQPUT FAILED'          TO PV-ERROR-MESSAGE     08050001
                   MOVE PV-COMPLETION-CODE  TO PV-HOLD-COMPLETION-CODE  08060001
                                               PE-MQ-COMPLETION-CODE    08070001
                   MOVE PV-REASON               TO PV-HOLD-REASON       08080001
                                                   PE-MQ-REASON-CODE    08090001
                   PERFORM 5200-BACK-OUT-MQ                             08100001
                   MOVE PV-HOLD-COMPLETION-CODE TO PV-COMPLETION-CODE   08110001
                   MOVE PV-HOLD-REASON          TO PV-REASON            08120001
                   PERFORM 9200-MQ-ABEND                                08130001
               ELSE                                                     08140001
                   ADD 1 TO PC-MQPUT-RETRY-COUNTER                      08150001
                   IF PC-MQPUT-RETRY-COUNTER >= 60                      08160001
                       CALL PV-MQCHECK USING PV-REASON                  08170001
                                             PV-MQ-REASON-TEXT          08180001
                       DISPLAY 'MQ MESSAGE = ' PV-MSGBUFFER             08190001
                       MOVE '5000-MQ-PUT-TO-QUEUE' TO PV-PARAGRAPH      08200001
                       MOVE 'MQPUT ERROR- RETRY > 60 '                  08210001
                            TO PV-ERROR-MESSAGE                         08220001
                       MOVE PV-COMPLETION-CODE                          08230001
                            TO PV-HOLD-COMPLETION-CODE                  08240001
                               PE-MQ-COMPLETION-CODE                    08250001
                       MOVE PV-REASON TO PV-HOLD-REASON                 08260001
                                         PE-MQ-REASON-CODE              08270001
                       PERFORM 5200-BACK-OUT-MQ                         08280001
                       MOVE PV-HOLD-COMPLETION-CODE                     08290001
                            TO PV-COMPLETION-CODE                       08300001
                       MOVE PV-HOLD-REASON TO PV-REASON                 08310001
                       PERFORM 9200-MQ-ABEND                            08320001
                   ELSE                                                 08330001
      *WAIT FOR 30 SECONDS AND TRY AGAIN                                08340001
                       CALL 'SYSWAIT' USING PC-DELAY-INTERVAL           08350001
                   END-IF                                               08360001
               END-IF                                                   08370001
           ELSE                                                         08380001
               ADD 1 TO PA-TOTAL-MQ-MSGS-SENT                           08390001
               SET PS-MQPUT-SUCCESSFUL TO TRUE                          08400001
           END-IF.                                                      08410001
                                                                        08420001
      ******************************************************************08430001
       5010-LOW-VALUES.                                                 08440001
                                                                        08450001
           MOVE PV-MSGBUFFER TO PV-HOLD-MSGBUFFER.                      08460001
           MOVE PV-HOLD-MSGBUFFER TO PV-MSGBUFFER.                      08470001
                                                                        08480001
      ******************************************************************08490001
      *  COMMIT MQ PROCESSING SINCE THE POINT OF THE LAST COMMIT        08500001
      ******************************************************************08510001
       5100-MQ-COMMIT.                                                  08520001
                                                                        08530001
           CALL PC-MQCMIT USING PV-HCONN                                08540001
                                PV-COMPLETION-CODE                      08550001
                                PV-REASON.                              08560001
                                                                        08570001
           IF (PV-COMPLETION-CODE NOT = MQCC-OK)                        08580001
              CALL PV-MQCHECK USING PV-REASON                           08590001
                                    PV-MQ-REASON-TEXT                   08600001
              MOVE '5100-COMMIT-MQ'   TO PV-PARAGRAPH                   08610001
              MOVE 'MQCMIT ERROR'     TO PV-ERROR-MESSAGE               08620001
              MOVE PV-COMPLETION-CODE TO PV-HOLD-COMPLETION-CODE        08630001
                                         PE-MQ-COMPLETION-CODE          08640001
              MOVE PV-REASON          TO PV-HOLD-REASON                 08650001
                                         PE-MQ-REASON-CODE              08660001
              PERFORM 5200-BACK-OUT-MQ                                  08670001
              MOVE PV-HOLD-COMPLETION-CODE TO PV-COMPLETION-CODE        08680001
              MOVE PV-HOLD-REASON          TO PV-REASON                 08690001
              PERFORM 9200-MQ-ABEND                                     08700001
           END-IF.                                                      08710001
                                                                        08720001
      ******************************************************************08730001
      * BACK OUT MQ PROCESSING SINCE THE POINT OF THE LAST COMMIT       08740001
      ******************************************************************08750001
       5200-BACK-OUT-MQ.                                                08760001
                                                                        08770001
           CALL PC-MQBACK USING PV-HCONN                                08780001
                                PV-COMPLETION-CODE                      08790001
                                PV-REASON.                              08800001
                                                                        08810001
           IF (PV-COMPLETION-CODE NOT = MQCC-OK)                        08820001
              CALL PV-MQCHECK USING PV-REASON                           08830001
                                    PV-MQ-REASON-TEXT                   08840001
              MOVE '5200-BACK-OUT-MQ'  TO PV-PARAGRAPH                  08850001
              MOVE 'MQBACK ERROR'    TO PV-ERROR-MESSAGE                08860001
              PERFORM 9200-MQ-ABEND                                     08870001
           END-IF.                                                      08880001
                                                                        08890001
      ******************************************************************08900001
      * DISPLAY STATISTICS FOR THE PROGRAM                              08910001
      ******************************************************************08920001
       9000-EOJ-ROUTINE.                                                08930001
                                                                        08940001
           PERFORM 9100-WRITE-SEQ-NBR-FILE.                             08950001
                                                                        08960001
           CLOSE SEQUENCE-NBR-FILE-IN                                   08970001
                 SEQUENCE-NBR-FILE-OUT.                                 08980001
                                                                        08990001
           DISPLAY ' '                                                  09000001
           DISPLAY ' ********************************************'      09010001
           DISPLAY '     * WSKUT060 SUMMARY STATISTICS *         '      09020002
           DISPLAY '       ---------------------------           '      09030001
           DISPLAY '   TOTAL MQ MESSAGES SENT:   '                      09040001
                                          PA-TOTAL-MQ-MSGS-SENT         09050001
           DISPLAY ' ********************************************'.     09060001
                                                                        09070001
                                                                        09080001
      ******************************************************************09090001
      * WRITE THE NEXT SEQUENCE NUMBER TO THE OUTPUT FILE.              09100001
      ******************************************************************09110001
       9100-WRITE-SEQ-NBR-FILE.                                         09120001
                                                                        09130001
           ADD +1   TO  PV-NEXT-SEQ-NBR.                                09140005
                                                                        09150001
           MOVE  PV-NEXT-SEQ-NBR          TO   WS-NEXT-SEQ-NBR.         09160001
                                                                        09170001
           WRITE SEQUENCE-NBR-RECORD-OUT  FROM WS-SEQ-NBR-RECORD.       09180005
                                                                        09190001
           DISPLAY 'OUTPUT NEXT SEQ NBR = '    WS-NEXT-SEQ-NBR.         09200001
                                                                        09210001
                                                                        09220001
      ****************************************************************  09230001
      ** PERFORM MQ-SERIES ABEND PROCESSING                             09240001
      ****************************************************************  09250001
       9200-MQ-ABEND.                                                   09260001
                                                                        09270001
           DISPLAY '****************** A B E N D ******************'.   09280001
           DISPLAY '** MQSERIES ERROR **'.                              09290001
           DISPLAY '** PROGRAM:    ', PC-CURRENT-PROGRAM-NAME.          09300001
           DISPLAY '** ABEND CODE: ', PV-ABEND-CODE.                    09310001
           DISPLAY '** PARAGRAPH:  ', PV-PARAGRAPH.                     09320001
           DISPLAY '** MESSAGE:    ', PV-ERROR-MESSAGE.                 09330001
           DISPLAY '** COMP CODE:  ', PE-MQ-COMPLETION-CODE.            09340001
           DISPLAY '** RSN CODE:   ', PE-MQ-REASON-CODE.                09350001
           DISPLAY '** RSN TEXT:   ', PV-MQ-REASON-TEXT.                09360001
           DISPLAY '***********************************************'.   09370001
           DISPLAY '   TOTAL MQ MESSAGES SENT:   '                      09380001
                                          PA-TOTAL-MQ-MSGS-SENT         09390001
           MOVE +4013 TO ABEND-CODE.                                    09400001
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                         09410001
                                                                        09420001
                                                                        09430001
      ******************************************************************09440001
      * ABEND PROGRAM WHEN A NON-DB2 AND NON-MQ ERROR OCCURS.           09450001
      ******************************************************************09460001
       9800-ABEND.                                                      09470001
                                                                        09480001
           DISPLAY '                     '.                             09490001
           DISPLAY '** ABEND           **'.                             09500001
           DISPLAY '** PROGRAM         ** = ' PV-ABEND-PROGRAM.         09510001
           DISPLAY '** PARAGRAPH       ** = ' PV-ABEND-PARAGRAPH.       09520001
           DISPLAY '** OPERATION       ** = ' PV-ABEND-OPERATION.       09530001
           DISPLAY '** ABEND CODE      ** = ' ABEND-CODE.               09540001
           DISPLAY '                     '.                             09550001
                                                                        09560001
           CALL 'ILBOABN0' USING ABEND-CODE.                            09570001
                                                                        09580001
                                                                        09590001
      ******************************************************************09600001
      * 9900-DB2-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE      09610001
      * EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                 09620001
      ******************************************************************09630001
       9900-DB2-ABEND.                                                  09640001
                                                                        09650001
           MOVE SQLCODE    TO PV-SQLCODE.                               09660001
           MOVE PV-SQLCODE TO PV-DISPLAY-SQLCODE.                       09670001
           DISPLAY '*** DB2 ERROR ***'.                                 09680001
           DISPLAY '*** SQLCODE   = ' PV-DISPLAY-SQLCODE.               09690001
           DISPLAY '*** PROGRAM   = ' PV-ABEND-PROGRAM.                 09700001
           DISPLAY '*** PARAGRAPH = ' PV-ABEND-PARAGRAPH.               09710001
           DISPLAY '*** OPERATION = ' PV-ABEND-OPERATION.               09720001
           DISPLAY '*** MESSAGE   = ' PV-DB2-OPERATION-MESSAGE-1.       09730001
           DISPLAY '*** TABLE 1   = ' PV-ABEND-STRUCTURE-1.             09740001
           DISPLAY '   TOTAL MQ MESSAGES SENT:   '                      09750001
                                          PA-TOTAL-MQ-MSGS-SENT         09760001
                                                                        09770001
      ******************************************************************09780001
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    09790001
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         09800001
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     09810001
      * FORMAT.                                                         09820001
      ******************************************************************09830001
           COPY DPPD004.                                                09840001
                                                                        09850001
           IF SQLCODE NOT = -911                                        09860001
               EXEC SQL                                                 09870001
                   ROLLBACK                                             09880001
               END-EXEC                                                 09890001
           END-IF.                                                      09900001
                                                                        09910001
           MOVE +4013         TO PV-ABEND-CODE.                         09920001
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                         09930001
      ******************************************************************09940001
