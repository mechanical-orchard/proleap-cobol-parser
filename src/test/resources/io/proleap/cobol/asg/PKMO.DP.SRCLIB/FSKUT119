      ***************************************************************** 00010000
       IDENTIFICATION DIVISION.                                         00020000
      ***************************************************************** 00030000
       PROGRAM-ID.    FSKUT119.                                         00040000
       AUTHOR.        MARIA KLAMIK.                                     00050000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00060000
       DATE-WRITTEN.  07-07-03.                                         00070000
       DATE-COMPILED.                                                   00080000
                                                                        00090000
      ***************************************************************** 00100000
      *   FASHION SALES ON-HAND ADJUSTMENT QUEUE SHUTDOWN MESSAGE.    * 00110000
                                                                        00120000
      *                   MQ PROGRAM                                  * 00121000
      *                                                               * 00122000
      *   THIS PROGRAM WILL PUT A SHUTDOWN MESSAGE ON THE             * 00123000
      *    FS.MERCHANDISE.ADJ.Q.                                      * 00125000
      *                                                               * 00126000
      *    XX/XX/XX - XXXXX XXXXXXX - XXXXXX                          * 00170000
      *    XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX  * 00180000
      ***************************************************************** 00190000
       ENVIRONMENT DIVISION.                                            00200000
      ***************************************************************** 00210000
                                                                        00220000
       CONFIGURATION SECTION.                                           00230000
                                                                        00240000
       SOURCE-COMPUTER.       IBM-3090.                                 00250000
       OBJECT-COMPUTER.       IBM-3090.                                 00260000
                                                                        00270000
       INPUT-OUTPUT SECTION.                                            00280000
                                                                        00290000
       FILE-CONTROL.                                                    00300000
           SELECT CONTROL-CARD     ASSIGN TO INP01.                     00310000
           SELECT CONTROL-CARD-2   ASSIGN TO INP02.                     00311000
                                                                        00312000
      ***************************************************************** 00312100
       DATA DIVISION.                                                   00312200
      ***************************************************************** 00312300
                                                                        00312400
       FILE SECTION.                                                    00312500
                                                                        00312600
       FD  CONTROL-CARD                                                 00312700
           RECORDING MODE F                                             00312800
           BLOCK CONTAINS 0 RECORDS                                     00312900
           LABEL RECORDS ARE OMITTED.                                   00313000
       01  CONTROL-CARD-LINE                PIC  X(80).                 00313100
                                                                        00313200
       FD  CONTROL-CARD-2                                               00313300
           RECORDING MODE F                                             00313400
           BLOCK CONTAINS 0 RECORDS                                     00313500
           LABEL RECORDS ARE OMITTED.                                   00313600
       01  CONTROL-CARD-LINE-2              PIC  X(80).                 00313700
                                                                        00316000
       WORKING-STORAGE SECTION.                                         00329300
                                                                        00329400
      ***************************************************************** 00329500
      * PC-PROGRAM CONSTANTS.                                           00329600
      ***************************************************************** 00329700
       01  PC-PROGRAM-CONSTANTS.                                        00329800
           05  PC-OPERATING-COMPANY           PIC X(02)   VALUE '03'.   00329900
           05  PC-PGM-NAME                    PIC X(08)   VALUE         00330000
                                                          'FSKUT119'.   00340000
           05  PC-FIFTY-STARS                 PIC X(50)  VALUE ALL '*'. 00400000
           05  PC-END-RUN-MSG                 PIC X(18)  VALUE          00410000
                                                  'END RUN OF PROGRAM'. 00420000
      *** MQ SERIES CONSTANTS                                           00430000
           05  PC-MQCONN                      PIC X(008)                00440000
                                                    VALUE 'CSQBCONN'.   00450003
           05  PC-MQOPEN                      PIC X(008)                00460000
                                                    VALUE 'CSQBOPEN'.   00470003
           05  PC-MQGET                       PIC X(008)                00480000
                                                    VALUE 'CSQBGET'.    00490003
           05  PC-MQPUT1                      PIC X(008)                00500000
                                                    VALUE 'CSQBPUT1'.   00510003
           05  PC-MQCLOSE                     PIC X(008)                00520000
                                                    VALUE 'CSQBCLOS'.   00530003
           05  PC-MQDISC                      PIC X(008)                00540000
                                                    VALUE 'CSQBDISC'.   00550003
           05  PC-MQCMIT                      PIC X(008)                00560000
                                                   VALUE 'CSQBCOMM'.    00570000
      ***************************************************************** 00580000
      *    CONTROL CARD                                                 00590000
      ***************************************************************** 00600000
       01  CC-CONTROL-CARD.                                             00610000
           05  CC-QMGR-NAME              PIC  X(04).                    00620000
           05  FILLER                    PIC  X(76).                    00630000
                                                                        00640000
      ***************************************************************** 00650000
      *    CONTROL CARD-2                                               00660000
      ***************************************************************** 00670000
       01  CC-CONTROL-CARD-2.                                           00680000
           05  CC-FS-MERCH-ADJ-Q-NAME    PIC  X(20).                    00700000
           05  FILLER                    PIC  X(60).                    00710000
                                                                        00750000
      ***************************************************************** 00760000
      * PV-PROGRAM VARIABLES.                                           00770000
      ***************************************************************** 00780000
       01  PV-PROGRAM-VARIABLES.                                        00790000
      *** MQ SERIES VARIABLES.                                          00940000
           05  PV-PUT-MESSAGE-BUFFER          PIC  X(200)               01030002
                                                 VALUE SPACES.          01040000
           05  PV-PUT-MSG-BUFFER-LENGTH       PIC S9(009)               01050000
                                                    BINARY              01060000
                                                    VALUE 200.          01070002
           05  PV-MESSAGE-LENGTH-IND-X        PIC  X(002).              01080000
           05  PV-MESSAGE-LENGTH-IND                                    01090000
            REDEFINES PV-MESSAGE-LENGTH-IND-X PIC S9(004)               01100000
                                                    COMP.               01110000
           05  PV-CONNECT-HANDLE              PIC S9(009)               01120000
                                                    BINARY              01130000
                                                    VALUE 0.            01140000
           05  PV-COMPLETION-CODE             PIC S9(009)               01210000
                                                    BINARY              01220000
                                                    VALUE 0.            01230000
           05  PV-REASON                      PIC S9(009)               01240000
                                                    BINARY              01250000
                                                    VALUE 0.            01260000
           05  PV-CONNECT-REASON              PIC S9(009)               01270000
                                                    BINARY              01280000
                                                    VALUE 0.            01290000
           05  PV-DISPLAY-REASON              PIC S9(009)               01300000
                                                    VALUE 0.            01310000
           05  PV-DISPLAY-COMP-CODE           PIC S9(009)               01320000
                                                    VALUE 0.            01330000
      ***************************************************************** 01340000
      * PE-ERROR MESSAGE VARIABLES                                      01350000
      ***************************************************************** 01360000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   01370000
      *** MQ CALL ERROR MESSAGES - LENGTH 68 BYTES                      01380000
      ***************************************************************** 01390000
           05  PE-MQ-ERROR-MESSAGE.                                     01400000
             10  FILLER                       PIC  X(021)               01410000
                 VALUE '******* ABEND *******'.                         01420000
             10  FILLER                       PIC  X(010)               01430000
                 VALUE 'MQ ERROR: '.                                    01440000
             10  PE-MQ-CALL-TYPE              PIC  X(007) VALUE SPACES. 01450000
             10  FILLER                       PIC  X(002) VALUE SPACES. 01460000
             10  FILLER                       PIC  X(009)               01470000
                 VALUE 'COMP CDE='.                                     01480000
             10  PE-MQ-COMPLETION-CODE        PIC  9(004) VALUE ZEROS.  01490000
             10  FILLER                       PIC  X(002) VALUE SPACES. 01500000
             10  FILLER                       PIC  X(008)               01510000
                 VALUE 'RSN CDE='.                                      01520000
             10  PE-MQ-REASON-CODE            PIC  9(004) VALUE ZEROS.  01530000
             10  FILLER                       PIC  X(001) VALUE SPACES. 01540000
      ***************************************************************** 01890000
      * PS-PROGRAM SWITCHES.                                            01900000
      ***************************************************************** 01910000
       01  PS-PROGRAM-SWITCHES.                                         01920000
           05  PS-CONTROL-CARD-SW              PIC X(01)   VALUE 'N'.   01930000
               88  PS-CONTROL-CARD-END                     VALUE 'Y'.   01940000
               88  PS-CONTROL-CARD-START                   VALUE 'N'.   01950000
           05  PS-CONTROL-CARD-2-SW            PIC X(01)   VALUE 'N'.   01960000
               88  PS-CONTROL-CARD-2-END                   VALUE 'Y'.   01970000
               88  PS-CONTROL-CARD-2-START                 VALUE 'N'.   01980000
                                                                        02070000
      ***************************************************************** 02080000
      * MQ SERIES API CONTROL BLOCKS                                    02090000
      ***************************************************************** 02100000
       01  MQM-OBJECT-DESCRIPTOR.                                       02110000
           COPY CMQODV.                                                 02120000
                                                                        02130000
       01  MQM-MESSAGE-DESCRIPTOR.                                      02140000
           COPY CMQMDV.                                                 02150000
                                                                        02160000
       01  MQM-GET-MESSAGE-OPTIONS.                                     02170000
           COPY CMQGMOV.                                                02180000
                                                                        02190000
       01  MQM-PUT-MESSAGE-OPTIONS.                                     02200000
           COPY CMQPMOV.                                                02210000
                                                                        02220000
      ***************************************************************** 02230000
      * MQV CONTAINS CONSTANTS(FOR FILLING IN THE CONTROL BLOCKS)       02240000
      * AND RETURN CODES(FOR TESTING THE RESULT OF A CALL)              02250000
      ***************************************************************** 02260000
       01  MQM-CONSTANTS.                                               02270000
           COPY CMQV.                                                   02280000
                                                                        02290000
      ***************************************************************** 02600000
      *    ABEND CODES THE ARE APPLICATION DEFINED                      02610000
      ***************************************************************** 02620000
       01  ABEND-CODE                  PIC S9(4)    VALUE ZEROS         02630000
                                                    COMP SYNC.          02640000
           88  AC-USER-ERROR                        VALUE +4003.        02650000
           88  AC-MQ-ERROR                          VALUE +4020.        02660000
                                                                        02670000
       LINKAGE SECTION.                                                 02680000
                                                                        02690000
      ******************************************************************02700000
       PROCEDURE DIVISION.                                              02710000
      ******************************************************************02720000
      * MAINLINE PARAGRAPH                                              02730000
      ******************************************************************02740000
       0000-MAIN-MODULE.                                                02750000
                                                                        02760000
           PERFORM 1000-HOUSEKEEPING.                                   02790000
                                                                        02791000
           PERFORM 1500-CONNECT-TO-Q-MGR.                               02792000
           PERFORM 2000-PUT-END-RUN-MSG.                                02800000
                                                                        02810000
           DISPLAY PC-FIFTY-STARS.                                      02820000
           DISPLAY '******* END OF PROGRAM FSKUT119 *****'.             02830000
           DISPLAY PC-FIFTY-STARS.                                      02830100
                                                                        02831000
           STOP RUN.                                                    02840000
                                                                        02850000
      ******************************************************************02851000
      * HOUSEKEEPING ROUTINE.                                          *02851100
      ******************************************************************02852000
       1000-HOUSEKEEPING.                                               02853000
                                                                        02854000
           DISPLAY PC-FIFTY-STARS.                                      02855000
           DISPLAY '***** START OF PROGRAM FSKUT119 *****'.             02856000
                                                                        02856100
           SET PS-CONTROL-CARD-START TO TRUE.                           02856200
           SET PS-CONTROL-CARD-2-START TO TRUE.                         02856300
                                                                        02856400
           OPEN INPUT CONTROL-CARD                                      02856500
                      CONTROL-CARD-2.                                   02856600
                                                                        02856700
      *  READ THE FIRST CONTROL CARD FOR THE QUEUE MGR NAME.            02856800
                                                                        02856900
           PERFORM 8000-READ-CONTROL-CARD.                              02857000
           IF PS-CONTROL-CARD-END                                       02857100
               DISPLAY PC-FIFTY-STARS                                   02857200
               DISPLAY PC-PGM-NAME                                      02857300
                    'CONTROL CARD MISSING - FOR QMGR NAME'              02857400
           ELSE                                                         02857500
                IF CC-QMGR-NAME > SPACES                                02857600
                    DISPLAY PC-FIFTY-STARS                              02857700
                    DISPLAY PC-PGM-NAME                                 02857800
                            '- CONTROL CARD FOR QMGR: '                 02857900
                            CC-CONTROL-CARD                             02858000
                ELSE                                                    02858100
                    SET PS-CONTROL-CARD-END TO TRUE                     02858200
                    DISPLAY PC-PGM-NAME                                 02858300
                         'INVALID QMGR NAME '                           02858400
                END-IF                                                  02858500
           END-IF.                                                      02858600
                                                                        02858700
           CLOSE CONTROL-CARD.                                          02858800
                                                                        02858900
      *  READ THE SECOND CONTROL CARD FOR THE QUEUE NAME                02859000
                                                                        02859100
            PERFORM 8010-READ-CONTROL-CARD-2.                           02859200
            IF PS-CONTROL-CARD-2-END                                    02859300
                DISPLAY PC-PGM-NAME                                     02859400
                   'CONTROL CARD MISSING - SALES QUEUE NAME '           02859500
            ELSE                                                        02859600
                IF CC-FS-MERCH-ADJ-Q-NAME > SPACES                      02859700
                    DISPLAY PC-FIFTY-STARS                              02859800
                    DISPLAY PC-PGM-NAME                                 02859900
                            '- CONTROL CARD FOR Q-NAME: '               02860000
                            CC-FS-MERCH-ADJ-Q-NAME                      02860100
                    DISPLAY PC-FIFTY-STARS                              02860200
                ELSE                                                    02860300
                    SET PS-CONTROL-CARD-2-END TO TRUE                   02860400
                    DISPLAY PC-PGM-NAME                                 02860500
                       'INVALID MERCH ADJUST QUEUE NAME'                02860600
                END-IF                                                  02860700
           END-IF.                                                      02861900
                                                                        02862000
           CLOSE CONTROL-CARD-2.                                        02862100
                                                                        02862200
           IF PS-CONTROL-CARD-END                                       02862300
              OR PS-CONTROL-CARD-2-END                                  02862400
              SET AC-USER-ERROR TO TRUE                                 02862500
              CALL 'ILBOABN0' USING ABEND-CODE                          02862600
           END-IF.                                                      02862700
                                                                        02863000
      ******************************************************************02865000
      *    CONNECT TO THE QUEUE MANAGER.                               *02866000
      ******************************************************************02867000
       1500-CONNECT-TO-Q-MGR.                                           02868000
                                                                        02869000
           CALL PC-MQCONN USING CC-QMGR-NAME                            02869100
                                PV-CONNECT-HANDLE                       02869200
                                PV-COMPLETION-CODE                      02869300
                                PV-REASON.                              02869400
                                                                        02870100
           MOVE PV-REASON TO PV-CONNECT-REASON.                         02870200
           IF (PV-COMPLETION-CODE = MQCC-OK)                            02870300
              CONTINUE                                                  02870400
           ELSE                                                         02870500
              MOVE 'MQCONN' TO PE-MQ-CALL-TYPE                          02870600
              MOVE PV-COMPLETION-CODE TO PV-DISPLAY-COMP-CODE           02870700
              MOVE PV-DISPLAY-COMP-CODE TO PE-MQ-COMPLETION-CODE        02870800
              MOVE PV-REASON TO PV-DISPLAY-REASON                       02870900
              MOVE PV-DISPLAY-REASON TO PE-MQ-REASON-CODE               02871000
              PERFORM 9000-MQ-ERROR                                     02871100
           END-IF.                                                      02871200
                                                                        02871300
      ******************************************************************02872000
      ******************************************************************02880000
       2000-PUT-END-RUN-MSG.                                            02890000
                                                                        02900000
           COMPUTE MQPMO-OPTIONS       = MQPMO-NO-SYNCPOINT             08820004
                                       + MQPMO-FAIL-IF-QUIESCING.       08830004
                                                                        08840000
           MOVE MQOT-Q                 TO  MQOD-OBJECTTYPE.             08850004
           MOVE CC-FS-MERCH-ADJ-Q-NAME TO MQOD-OBJECTNAME.              08860000
           MOVE MQPER-PERSISTENT       TO MQMD-PERSISTENCE.             08870004
           MOVE +9                     TO MQMD-PRIORITY.                08871004
           MOVE LOW-VALUES             TO  PV-PUT-MESSAGE-BUFFER.       08880004
           MOVE PC-END-RUN-MSG         TO PV-PUT-MESSAGE-BUFFER.        08900004
                                                                        08910000
           CALL PC-MQPUT1 USING PV-CONNECT-HANDLE                       08920000
                                MQM-OBJECT-DESCRIPTOR                   08930000
                                MQM-MESSAGE-DESCRIPTOR                  08940000
                                MQM-PUT-MESSAGE-OPTIONS                 08950000
                                PV-PUT-MSG-BUFFER-LENGTH                08960000
                                PV-PUT-MESSAGE-BUFFER                   08970000
                                PV-COMPLETION-CODE                      08980000
                                PV-REASON.                              08990000
                                                                        09000000
           IF (PV-COMPLETION-CODE = MQCC-OK)                            09040000
              CONTINUE                                                  09050000
           ELSE                                                         09060000
              MOVE 'MQPUT1' TO PE-MQ-CALL-TYPE                          09070000
              MOVE PV-COMPLETION-CODE TO PV-DISPLAY-COMP-CODE           09080000
              MOVE PV-DISPLAY-COMP-CODE TO PE-MQ-COMPLETION-CODE        09090000
              MOVE PV-REASON TO PV-DISPLAY-REASON                       09100000
              MOVE PV-DISPLAY-REASON TO PE-MQ-REASON-CODE               09110000
              PERFORM 9000-MQ-ERROR                                     09140000
           END-IF.                                                      09150000
                                                                        09160000
                                                                        09370000
      ***************************************************************** 10290000
      * READ CONTROL CARD TO OBTAIN THE INPUT QUEUE MGR NAME.           10300000
      ***************************************************************** 10310000
       8000-READ-CONTROL-CARD.                                          10320000
                                                                        10330000
            READ CONTROL-CARD INTO CC-CONTROL-CARD                      10340000
                AT END                                                  10350000
                    SET PS-CONTROL-CARD-END TO TRUE.                    10360000
                                                                        10370000
      ***************************************************************** 10380000
      * READ SECOND CONTROL CARD TO OBTAIN THE QUEUE NAME               10390001
      ***************************************************************** 10400000
       8010-READ-CONTROL-CARD-2.                                        10410000
                                                                        10420000
            READ CONTROL-CARD-2 INTO CC-CONTROL-CARD-2                  10430000
                AT END                                                  10440000
                    SET PS-CONTROL-CARD-2-END   TO TRUE.                10450000
                                                                        10460000
      ***************************************************************** 10470000
      * DISPLAY THE VALUES OF A FAILED MQ API COMMAND                   10480000
      ***************************************************************** 10490000
       9000-MQ-ERROR.                                                   10500000
                                                                        10510000
           DISPLAY '************************************************'.  10520000
           DISPLAY '**** ABEND *************************************'.  10530000
           DISPLAY '************************************************'.  10540000
           DISPLAY PE-MQ-ERROR-MESSAGE.                                 10550000
           SET AC-MQ-ERROR TO TRUE.                                     10660000
           CALL 'ILBOABN0' USING ABEND-CODE.                            10670000
