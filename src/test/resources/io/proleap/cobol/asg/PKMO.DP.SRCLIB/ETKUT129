       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.           ETKUT129.                                  00020003
       AUTHOR.               JON FIEDLER.                               00021000
       INSTALLATION.         KOHLS DEPARTMENT STORES.                   00022000
       DATE-WRITTEN          10/18/2000.                                00023038
       DATE-COMPILED.                                                   00024000
                                                                        00025000
      ******************************************************************00026038
      * THIS PROGRAM CHECKS FOR DOCUMENTS WHO ARE IN RECEIVE STATUS AND 00085038
      * DISPLAYS VENDOR INFORMATION WITH REGARD TO THE PARTICULAR       00085138
      * DOCUMENT. IF NO DOCUMENTS ARE FOUND IN RECEIVE STATUS THE       00085238
      * PROGRAM STOPS PROCESSING.                                       00085338
      ******************************************************************00085438
JF0603*  DATE  06/25/2003                                               00085544
JF0603*  CHANGE MADE:                                                   00085639
JF0603*   COMMENTED OUT LOGIC IN RECEIVED_INFO CURSOR THAT WON'T EXTRACT00085739
JF0603*   DOCUMENTS IN RECEIVE STATUS IF THE DOCUMENTS TIMESTAMP IS     00085839
JF0603*   WITHIN THE LAST TWO HOURS OF THE CURRENT DATE AND TIME.       00085939
JF0603*  MADE BY: JON FIEDLER             WR# : P000560597              00086043
JF0506*  DATE  05/02/2006                                               00086145
JF0506*  CHANGE MADE:                                                   00086245
JF0506* UNCOMMENTED OUT LOGIC IN RECEIVED_INFO CURSOR THAT WON'T EXTRACT00086345
JF0506* DOCUMENTS IN RECEIVE STATUS IF THE DOCUMENTS TIMESTAMP IS       00086445
JF0506* WITHIN THE LAST TWO HOURS OF THE CURRENT DATE AND TIME.         00086545
JF0506*  MADE BY: JON FIEDLER             WR# : WR26938                 00086645
************************************************************************00086745
       ENVIRONMENT DIVISION.                                            00086845
       CONFIGURATION SECTION.                                           00087000
       SOURCE-COMPUTER. IBM-3090.                                       00088000
       OBJECT-COMPUTER. IBM-3090.                                       00089000
                                                                        00090000
       INPUT-OUTPUT SECTION.                                            00100000
       FILE-CONTROL.                                                    00110000
                                                                        00120000
                                                                        00140000
       DATA DIVISION.                                                   00150000
       FILE SECTION.                                                    00160000
                                                                        00170000
                                                                        00230000
       WORKING-STORAGE SECTION.                                         00240000
                                                                        00250000
       01  PC-PROGRAM-CONSTANTS.                                        00260000
           05  PC-PROGRAM-NAME          PIC X(08)      VALUE            00290038
                                                          'ETKUT129'.   00410026
       01 PV-PROGRAM-VARIABLES.                                         00420000
           05  PV-ABEND-PARAGRAPH       PIC X(40)      VALUE SPACE.     00430038
                                                                        00597000
       01  ABEND-CODE                   PIC S9(04)  VALUE +0 COMP SYNC. 00597138
           88 ABEND-4013                               VALUE +4013.     00597238
           88 ABEND-4095                               VALUE +4095.     00597338
                                                                        00597434
       01  PS-PROGRAM-SWITCHES.                                         00598000
           05  PS-REC-TRANS-LEFT-SW     PIC X(01)       VALUE 'Y'.      00599038
               88  PS-NO-MORE-REC-TRANS-LEFT            VALUE 'N'.      00600038
               88  PS-MORE-REC-TRANS-LEFT               VALUE 'Y'.      00610038
           05  PS-ROW-FOUND-SW          PIC X(01)       VALUE 'N'.      00611038
               88  PS-ROW-FOUND                         VALUE 'Y'.      00612038
                                                                        00620000
       01  PS-PROGRAM-ACCUMUALTORS.                                     00630000
           05  PA-ROWS-FETCHED          PIC 9(07)       VALUE 0.        00640038
                                                                        00710000
      ***************************************************************** 00720004
      * DCLGEN FOR TABLE TINTTRN                                        00730004
      ***************************************************************** 00740004
           EXEC SQL                                                     00750004
               INCLUDE TINTTRN                                          00760004
           END-EXEC.                                                    00770004
                                                                        00780004
      ***************************************************************** 00790004
      * DCLGEN FOR TABLE TFGTRST                                        00800004
      ***************************************************************** 00810004
           EXEC SQL                                                     00820004
               INCLUDE TFGTRST                                          00830004
           END-EXEC.                                                    00840004
                                                                        00841004
      ***************************************************************** 00850004
      * DCLGEN FOR TABLE TENTNME                                        00860004
      ***************************************************************** 00870004
           EXEC SQL                                                     00880004
               INCLUDE TENTNME                                          00890004
           END-EXEC.                                                    00900004
                                                                        00910004
      **************************************************************    01341010
      *  COMMUNICATION AREAS FOR DB2                                    01342010
      **************************************************************    01343010
           EXEC SQL                                                     01344010
               INCLUDE SQLCA                                            01345010
           END-EXEC.                                                    01346010
                                                                        01347010
      ****************************************************              01348010
      *  ERROR MESSAGE AREA FOR DB2                      *              01349010
      ****************************************************              01349110
           COPY DPWS004.                                                01349210
                                                                        01349310
      ******************************************************************01349435
      * SQL ERROR HANDLING ARGUMENTS.......                             01349535
      ******************************************************************01349635
           COPY SQLCA2.                                                 01349735
                                                                        01349835
                                                                        01350000
       PROCEDURE DIVISION.                                              01360000
      ******************************************************************01370000
      * MAIN PROCEDURE                                                  01380000
      ******************************************************************01390000
       0000-MAINLINE-LOGIC.                                             01400000
                                                                        01410000
           PERFORM 1100-OPEN-RECEIVED-CURSOR.                           01440002
                                                                        01450000
           PERFORM 9900-EOJ-LOGIC.                                      01490000
                                                                        01510000
           GOBACK.                                                      01520000
           EJECT                                                        01530000
                                                                        01540002
      ***************************************************************** 02030000
      *    RECEIVED CURSOR -                                            02040001
      ***************************************************************** 02050000
                                                                        02060000
           EXEC SQL                                                     02070000
               DECLARE RECEIVED_INFO CURSOR FOR                         02080024
                 SELECT D.ENT_NAME_DESC,                                02090023
                        DATE (A.TP_DKEY_TMST),                          02100023
                        E.INCHG_CNTL_NBR,                               02110023
                        A.FUNC_CDE_CNTL_NBR,                            02120023
                        A.TRN_SET_CNTL_NBR,                             02130023
                        A.TRN_DTL_TXT                                   02140023
                 FROM TFGTRST A,                                        02150023
                      TTRDPRT C,                                        02160023
                      TENTNME D,                                        02170023
                      TINTTRN E                                         02171023
                WHERE A.TP_DKEY_TMST > CURRENT TIMESTAMP - 3 DAYS       02172038
JF0506               AND A.TP_DKEY_TMST < CURRENT TIMESTAMP - 1 HOUR    02173045
                     AND A.TRN_SET_CDE <> '997'                         02174023
                     AND A.TRN_RLSE_TMST IS NULL                        02175023
                     AND A.TP_DKEY = C.TP_DKEY                          02176023
                     AND C.ENT_ID = D.ENT_ID                            02177023
                     AND D.TYPE_CDE = '01'                              02178023
                     AND A.TP_DKEY = E.TP_DKEY                          02179023
                     AND A.TP_DKEY_TMST = E.TP_DKEY_TMST                02179123
                     AND E.DIR_IND = 'R'                                02179223
                     AND NOT EXISTS                                     02179323
                        (SELECT 1                                       02179423
                         FROM   TTNSTLC B                               02179523
                         WHERE  A.TP_DKEY = B.TP_DKEY                   02179623
                         AND    A.TP_DKEY_TMST = B.TP_DKEY_TMST         02179723
                         AND    A.FUNC_CDE_SEQ_NBR = B.FUNC_CDE_SEQ_NBR 02179823
                         AND    A.TRN_SET_SEQ_NBR = B.TRN_SET_SEQ_NBR)  02179923
                   END-EXEC.                                            02180001
                                                                        02190000
                                                                        02200000
       EJECT                                                            02210000
      ******************************************************************02220000
      *    OPEN-RECEIVED-CURSOR                                         02230001
      ******************************************************************02240000
       1100-OPEN-RECEIVED-CURSOR.                                       02250001
                                                                        02260000
           MOVE '1100-OPEN-RECEIVED-CURSOR' TO PV-ABEND-PARAGRAPH.      02270001
                                                                        02280000
           EXEC SQL                                                     02290000
               OPEN RECEIVED_INFO                                       02300001
           END-EXEC.                                                    02310000
                                                                        02320000
           EVALUATE TRUE                                                02330000
             WHEN SQLCODE = ZERO                                        02340005
               PERFORM 1200-FETCH-RECEIVED-INFO                         02350005
                 UNTIL PS-NO-MORE-REC-TRANS-LEFT                        02350105
               CONTINUE                                                 02351005
             WHEN SQLCODE = +100                                        02360005
               SET PS-NO-MORE-REC-TRANS-LEFT TO TRUE                    02370001
               CONTINUE                                                 02380000
             WHEN OTHER                                                 02390000
               DISPLAY PV-ABEND-PARAGRAPH                               02400000
               PERFORM 9999-SQL-ERROR                                   02410000
           END-EVALUATE.                                                02420000
                                                                        02430000
       EJECT                                                            02440000
      ******************************************************************02450000
      *    FETCH-RECEIVED-INFO                                          02460001
      *      FETCH A ROW FROM THE CURSOR                                02470000
      ******************************************************************02480000
       1200-FETCH-RECEIVED-INFO.                                        02490001
                                                                        02500000
           MOVE '1200-FETCH-RECEIVED-INFO' TO PV-ABEND-PARAGRAPH.       02510001
                                                                        02520000
           EXEC SQL                                                     02530000
               FETCH RECEIVED_INFO                                      02540002
               INTO  :ENTNME-ENT-NAME-DESC                              02550004
                   , :FGTRST-TP-DKEY-TMST                               02560004
                   , :INTTRN-INCHG-CNTL-NBR                             02561031
                   , :FGTRST-FUNC-CDE-CNTL-NBR                          02562031
                   , :FGTRST-TRN-SET-CNTL-NBR                           02570004
                   , :FGTRST-TRN-DTL-TXT                                02580004
           END-EXEC.                                                    02610000
                                                                        02620000
           EVALUATE TRUE                                                02630000
             WHEN SQLCODE = ZERO                                        02640005
               ADD 1 TO PA-ROWS-FETCHED                                 02650000
               MOVE 'Y' TO PS-ROW-FOUND-SW                              02650134
               DISPLAY 'ENT-NAME-DESC= ' ENTNME-ENT-NAME-DESC           02651031
               DISPLAY 'TP-DKEY-TMST= ' FGTRST-TP-DKEY-TMST             02652031
               DISPLAY 'TRN-SET-CNTL-NBR= ' FGTRST-TRN-SET-CNTL-NBR     02653031
               DISPLAY 'TRN-DTL-TXT= ' FGTRST-TRN-DTL-TXT               02654031
               DISPLAY 'INCHG-CNTL-NBR= ' INTTRN-INCHG-CNTL-NBR         02655031
               DISPLAY 'FUNC-CDE-CNTL-NBR= ' FGTRST-FUNC-CDE-CNTL-NBR   02656031
               DISPLAY '                   '                            02657031
             WHEN SQLCODE = 100                                         02660005
               SET PS-NO-MORE-REC-TRANS-LEFT TO TRUE                    02670001
             WHEN OTHER                                                 02680000
               DISPLAY PV-ABEND-PARAGRAPH                               02690000
               PERFORM 9999-SQL-ERROR                                   02700000
           END-EVALUATE.                                                02710000
                                                                        02720000
       EJECT                                                            02730000
      ******************************************************************02740000
      *    CLOSE-RECEIVED-CURSOR                                        02750002
      ******************************************************************02760000
       1300-CLOSE-RECEIVED-CURSOR.                                      02770008
                                                                        02780000
           MOVE '1300-CLOSE-RECEIVED-CURSOR' TO PV-ABEND-PARAGRAPH.     02790009
                                                                        02800000
           EXEC SQL                                                     02810000
               CLOSE RECEIVED_INFO                                      02820001
           END-EXEC.                                                    02830000
                                                                        02840000
           EVALUATE TRUE                                                02850000
             WHEN SQLCODE = ZEROS                                       02860000
               CONTINUE                                                 02870000
             WHEN OTHER                                                 02880000
               DISPLAY PV-ABEND-PARAGRAPH                               02890000
               PERFORM 9999-SQL-ERROR                                   02900000
           END-EVALUATE.                                                02910000
                                                                        02920000
       EJECT                                                            02930000
      ******************************************************************03470000
      *  THIS PROCEDURE PROCESSES THE END OF JOB LOGIC.                 03480000
      ******************************************************************03490000
       9900-EOJ-LOGIC.                                                  03500000
           PERFORM 1300-CLOSE-RECEIVED-CURSOR.                          03510009
           DISPLAY 'ROWS FETCHED    ' PA-ROWS-FETCHED.                  03530000
                                                                        03550000
           IF PS-ROW-FOUND                                              03551037
               SET ABEND-4095 TO TRUE                                   03553036
               MOVE ABEND-CODE TO RETURN-CODE.                          03560037
      ***************************************************************** 03570011
      *   9999-SQL-ERROR.                                               03580015
      *      ERROR ABEND ROUTINE                                        03590011
      ***************************************************************** 03600011
       9999-SQL-ERROR.                                                  03610015
           SET ABEND-4013 TO TRUE.                                      03620034
           DISPLAY '**  ABEND     **'.                                  03630013
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              03640013
           DISPLAY '**  PARAGRAPH **  = ' PV-ABEND-PARAGRAPH.           03650013
                                                                        03670011
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         03680011
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        03690011
                                                                        03700011
           COPY DPPD004.                                                03710013
              CALL 'ILBOABN0' USING ABEND-CODE.                         03720034
      *---------------------------------------------------------------* 03830000
      *    END OF SOURCE CODE FOR ETKUT129                            * 03840003
      *---------------------------------------------------------------* 03850000
