       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.      XSKUT101.                                       00020069
       AUTHOR.          KOHLS DEPARTMENT STORES.                        00030000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040000
       DATE-WRITTEN.    11/15/2004.                                     00050069
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      **XS DOMAIN TABLE LOAD PROCESS - FOR TABLE XST_PRCHG_TYP          00080069
      ******************************************************************00090000
      **THIS PROGRAM DELETES ALL DATA AND THEN LOADS NEW DATA INTO      00100069
      **    XST_PRCHG_TYP.                                              00110069
      ******************************************************************00120000
      *DATE:11/15/2004   MADE BY:JULIE TEWES INITIAL PROGRAM WRITE      00130069
      ******************************************************************00140000
       ENVIRONMENT DIVISION.                                            00150000
       CONFIGURATION SECTION.                                           00160000
       SOURCE-COMPUTER.        IBM-3090.                                00170000
       OBJECT-COMPUTER.        IBM-3090.                                00180000
       INPUT-OUTPUT SECTION.                                            00190000
       FILE-CONTROL.                                                    00200000
                                                                        00210000
           SELECT INPUT-FILE                                            00220069
                  ASSIGN TO INP01.                                      00230000
                                                                        00240000
       DATA DIVISION.                                                   00250000
       FILE SECTION.                                                    00260000
                                                                        00270000
       FD  INPUT-FILE                                                   00280069
           RECORDING MODE F                                             00290000
           BLOCK CONTAINS 0 RECORDS                                     00300000
           LABEL RECORDS ARE OMITTED                                    00310000
           DATA RECORD IS INPUT-RECORD.                                 00320069
       01  INPUT-RECORD                    PIC X(200).                  00330070
                                                                        00340000
       WORKING-STORAGE SECTION.                                         00350000
      ******************************************************************00360000
      **SWITCHES                                                        00370000
      ******************************************************************00380000
       01  PS-PROGRAM-SWITCHES.                                         00390000
           05  PS-END-OF-INPUT-FILE-SW         PIC X(01) VALUE 'N'.     00400069
               88  PS-END-OF-INPUT-FILE                  VALUE 'Y'.     00410069
               88  PS-NOT-END-OF-INPUT-FILE              VALUE 'N'.     00420069
      ******************************************************************00430000
      **TIMESTAMP VARIABLES                                             00440000
      ******************************************************************00450000
       01  SD-TIMESTAMP                        PIC X(26) VALUE SPACES.  00460000
       01  FILLER REDEFINES SD-TIMESTAMP.                               00470000
           05  SD-CURR-DTE                     PIC X(10).               00480000
           05  FILLER                          PIC X(01).               00490000
           05  SD-HOUR                         PIC X(02).               00500000
           05  FILLER                          PIC X(01).               00510000
           05  SD-MINUTE                       PIC X(02).               00520000
           05  FILLER                          PIC X(01).               00530000
           05  SD-SECOND                       PIC X(02).               00540000
           05  FILLER                          PIC X(01).               00550000
           05  SD-MICROSEC                     PIC X(06).               00560000
      ******************************************************************00570000
      **PROGRAM CONSTANTS                                               00580000
      ******************************************************************00590000
       01  PC-PROGRAM-CONSTANTS.                                        00600000
           05  PC-CURRENT-PROGRAM-NAME         PIC X(08)                00610000
                                                       VALUE 'XSKUT101'.00620069
      ******************************************************************00630000
      **PROGRAM COUNTERS                                                00640000
      ******************************************************************00650000
       01  PC-PROGRAM-COUNTER.                                          00660000
           05  PC-INPUT-RECORDS-READ           PIC 9(09) VALUE ZEROS.   00670069
      ******************************************************************00680000
      **ERROR HANDLING                                                  00690000
      ******************************************************************00700000
       01  PV-MISC-ABEND-FIELDS.                                        00710000
           05  PV-PARAGRAPH-NAME            PIC X(30) VALUE SPACE.      00720000
           05  PV-OPERATION-MSG-1           PIC X(40) VALUE SPACE.      00730000
           05  PV-OPERATION-MSG-2           PIC X(40) VALUE SPACE.      00740000
           05  PV-DB2-OPERATION             PIC X(30) VALUE SPACE.      00750000
       01  ABEND-CODE                  PIC S9(4) VALUE ZEROS COMP SYNC. 00760000
           88  ABEND-4013-DB2-ERROR              VALUE +4013.           00770000
      ******************************************************************00780000
      **COPYBOOKS                                                       00790000
      ******************************************************************00800000
      ***PERM EXTRACT FILE LAYOUT                                       00810069
           COPY PERD101.                                                00820069
      ***VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        00830000
           COPY DPWS004.                                                00840000
      ***DB2 ABEND COPYBOOK                                             00850000
           COPY DPWS900.                                                00860000
      ***DB2 MESSAGE AREA                                               00870000
           COPY SQLCA2.                                                 00880000
      ******************************************************************00890000
      **TABLE INCLUSIONS                                                00900000
      ******************************************************************00910000
      ***DB2 COMMUNICATIONS AREA                                        00920000
           EXEC SQL                                                     00930000
                INCLUDE SQLCA                                           00940000
           END-EXEC.                                                    00950000
      ***TABLE XST_PRCHG_TYP                                            00960069
           EXEC SQL                                                     00970000
                INCLUDE XST00083                                        00980069
           END-EXEC.                                                    00990000
                                                                        01000000
       PROCEDURE DIVISION.                                              01010000
      ***************************************************************** 01020000
       000-MAINLINE.                                                    01030000
                                                                        01040000
           PERFORM 1000-INITIALIZATION.                                 01050000
           PERFORM 2000-PROCESS-RECORDS                                 01060000
                   UNTIL PS-END-OF-INPUT-FILE.                          01070069
           PERFORM 9999-WRAPUP.                                         01080000
           GOBACK.                                                      01090000
                                                                        01100000
       EJECT.                                                           01110000
      ******************************************************************01120000
      **INITIALIZATION AREA                                             01130000
      ******************************************************************01140000
       1000-INITIALIZATION.                                             01150000
                                                                        01160000
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             01170000
                                                                        01180000
           OPEN INPUT  INPUT-FILE.                                      01190069
                                                                        01200000
           PERFORM 1100-SELECT-DB2-TIME.                                01210000
                                                                        01220069
           PERFORM 1200-DELETE-RECORDS.                                 01230069
           PERFORM 9999-COMMIT-DATA.                                    01240069
                                                                        01250000
           PERFORM 9999-READ-INPUT-FILE.                                01260069
                                                                        01270069
       EJECT.                                                           01280000
      ******************************************************************01290000
      **RETRIEVE TIME FROM DB2                                          01300000
      ******************************************************************01310000
       1100-SELECT-DB2-TIME.                                            01320000
                                                                        01330000
           MOVE '1100-SELECT-DB2-TIME' TO PV-PARAGRAPH-NAME.            01340000
                                                                        01350000
           EXEC SQL                                                     01360000
                SELECT CURRENT TIMESTAMP                                01370000
                INTO  :SD-TIMESTAMP                                     01380000
                FROM   SYSIBM.SYSDUMMY1                                 01390000
           END-EXEC.                                                    01400000
                                                                        01410000
           EVALUATE TRUE                                                01420000
              WHEN SQLCODE = ZERO                                       01430000
                   CONTINUE                                             01440000
              WHEN OTHER                                                01450000
                   PERFORM 9999-ABEND-ROUTINE                           01460069
           END-EVALUATE.                                                01470000
                                                                        01480000
       EJECT.                                                           01490000
      ******************************************************************01500069
      **DELETE ALL DATA ON TABLE XST_PRCHG_TYP                          01510069
      ******************************************************************01520069
       1200-DELETE-RECORDS.                                             01530069
                                                                        01540069
           MOVE '1200-DELETE-RECORDS' TO PV-PARAGRAPH-NAME.             01550069
                                                                        01560069
           EXEC SQL                                                     01570069
                DELETE FROM XST_PRCHG_TYP                               01580069
           END-EXEC.                                                    01590069
                                                                        01600069
           EVALUATE TRUE                                                01610069
              WHEN SQLCODE = ZERO                                       01620069
              WHEN SQLCODE = +100                                       01630070
                   CONTINUE                                             01640069
              WHEN OTHER                                                01650069
                   PERFORM 9999-DISPLAY-STATISTICS                      01660069
                   PERFORM 9999-ABEND-ROUTINE                           01670069
           END-EVALUATE.                                                01680069
                                                                        01690069
       EJECT.                                                           01700069
      ******************************************************************01710000
      **PROCESSES INPUT FILE                                            01720000
      ******************************************************************01730000
       2000-PROCESS-RECORDS.                                            01740000
                                                                        01750000
           MOVE '2000-PROCESS-RECORDS' TO PV-PARAGRAPH-NAME.            01760000
                                                                        01770000
           MOVE PE101-EVNT-CTG-CDE     TO XST00083-EVNT-CTG-CDE.        01780069
           MOVE PE101-PRCHG-TYP-CDE    TO XST00083-PRCHG-TYP-CDE.       01790069
           MOVE PE101-EVNT-CTG-DESC    TO XST00083-EVNT-CTG-DESC.       01800069
           MOVE PE101-PRCHG-TYP-DESC   TO XST00083-PRCHG-TYP-DESC.      01810069
           MOVE PE101-ACTV-IND         TO XST00083-ACTV-IND.            01820069
           MOVE PE101-SOR-LAST-UPD-TMST                                 01830069
                                       TO XST00083-SOR-LAST-UPD-TMST.   01840069
                                                                        01850069
           PERFORM 2100-INSERT-RECORD.                                  01860069
                                                                        01870069
           PERFORM 9999-READ-INPUT-FILE.                                01880069
                                                                        01890000
       EJECT.                                                           01900000
      ******************************************************************01910069
      **INSERT RECORD                                                   01920069
      ******************************************************************01930069
       2100-INSERT-RECORD.                                              01940069
                                                                        01950069
           MOVE '2100-INSERT-RECORD' TO PV-PARAGRAPH-NAME.              01960069
                                                                        01970069
           EXEC SQL                                                     01980069
                INSERT INTO XST_PRCHG_TYP                               01990069
                                    (EVNT_CTG_CDE                       02000069
                                    ,PRCHG_TYP_CDE                      02010069
                                    ,EVNT_CTG_DESC                      02020069
                                    ,PRCHG_TYP_DESC                     02030069
                                    ,ACTV_IND                           02040069
                                    ,SOR_CRE_TMST                       02050069
                                    ,SOR_LAST_UPD_TMST                  02060069
                                    ,LAST_UPD_TMST)                     02070069
                VALUES            ( :XST00083-EVNT-CTG-CDE              02080069
                                  , :XST00083-PRCHG-TYP-CDE             02090069
                                  , :XST00083-EVNT-CTG-DESC             02100069
                                  , :XST00083-PRCHG-TYP-DESC            02110069
                                  , :XST00083-ACTV-IND                  02120069
                                  , :SD-TIMESTAMP                       02130070
                                  , :XST00083-SOR-LAST-UPD-TMST         02140069
                                  , :SD-TIMESTAMP)                      02150070
           END-EXEC.                                                    02160069
                                                                        02170069
           EVALUATE TRUE                                                02180069
              WHEN SQLCODE = ZERO                                       02190069
                   CONTINUE                                             02200069
              WHEN OTHER                                                02210069
                   DISPLAY 'INSERTING INTO XST_PRCHG_TYP...'            02220070
                   DISPLAY XST00083-EVNT-CTG-CDE                        02230070
                   DISPLAY XST00083-PRCHG-TYP-CDE                       02240070
                   DISPLAY XST00083-EVNT-CTG-DESC                       02250070
                   DISPLAY XST00083-PRCHG-TYP-DESC                      02260070
                   DISPLAY XST00083-ACTV-IND                            02270070
                   DISPLAY SD-TIMESTAMP                                 02280070
                   DISPLAY XST00083-SOR-LAST-UPD-TMST                   02290070
                   DISPLAY SD-TIMESTAMP                                 02300070
                   PERFORM 9999-DISPLAY-STATISTICS                      02310069
                   PERFORM 9999-ABEND-ROUTINE                           02320069
           END-EVALUATE.                                                02330069
                                                                        02340069
       EJECT.                                                           02350069
      ***************************************************************** 02360000
      **READ INPUT FILE                                                 02370069
      ***************************************************************** 02380000
       9999-READ-INPUT-FILE.                                            02390069
                                                                        02400000
           IF  PS-NOT-END-OF-INPUT-FILE                                 02410069
               READ INPUT-FILE INTO PE101-RECORD                        02420069
                    AT END                                              02430000
                       SET PS-END-OF-INPUT-FILE TO TRUE                 02440069
               END-READ                                                 02450000
           END-IF.                                                      02460000
                                                                        02470000
           ADD +1                         TO PC-INPUT-RECORDS-READ.     02480069
                                                                        02490000
       EJECT.                                                           02500000
      ****************************************************************  02510000
      *COMMIT DATA                                                      02520069
      ****************************************************************  02530000
       9999-COMMIT-DATA.                                                02540069
                                                                        02550000
           MOVE '9999-COMMIT-DATA' TO PV-PARAGRAPH-NAME.                02560069
                                                                        02570069
           EXEC SQL                                                     02580069
                COMMIT                                                  02590069
           END-EXEC.                                                    02600069
                                                                        02610000
           EVALUATE TRUE                                                02620069
              WHEN SQLCODE = ZERO                                       02630069
                   CONTINUE                                             02640069
              WHEN OTHER                                                02650069
                   PERFORM 9999-DISPLAY-STATISTICS                      02660069
                   PERFORM 9999-ABEND-ROUTINE                           02670069
           END-EVALUATE.                                                02680069
                                                                        02690069
       EJECT.                                                           02700000
      ****************************************************************  02710069
      *END OF PROGRAM ROUTINE                                           02720069
      ****************************************************************  02730069
       9999-WRAPUP.                                                     02740069
                                                                        02750069
           CLOSE INPUT-FILE.                                            02760069
                                                                        02770069
           PERFORM 9999-COMMIT-DATA.                                    02780069
           PERFORM 9999-DISPLAY-STATISTICS.                             02790069
                                                                        02800069
       EJECT.                                                           02810069
      ****************************************************************  02820000
      *DISPLAY PROGRAM STATISTICS                                       02830000
      ****************************************************************  02840000
       9999-DISPLAY-STATISTICS.                                         02850000
                                                                        02860000
           DISPLAY '*************************************************'. 02870000
           DISPLAY '* PROGRAM..............: ' PC-CURRENT-PROGRAM-NAME. 02880000
           DISPLAY '* DATE/TIMESTAMP.......: ' SD-TIMESTAMP.            02890000
           DISPLAY '* INPUT RECORDS........: ' PC-INPUT-RECORDS-READ.   02900069
           DISPLAY '*************************************************'. 02910000
                                                                        02920000
       EJECT.                                                           02930000
      ******************************************************************02940000
      **SQL ABEND ROUTINE                                               02950000
      ******************************************************************02960000
       9999-ABEND-ROUTINE.                                              02970000
                                                                        02980000
           DISPLAY '** ABEND     **'.                                   02990000
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        03000000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              03010000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               03020000
      ******************************************************************03030000
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     03040000
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      03050000
      ******************************************************************03060000
           COPY DPPD004.                                                03070000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            03080000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03090000
                                                                        03100000
       EJECT                                                            03110000
