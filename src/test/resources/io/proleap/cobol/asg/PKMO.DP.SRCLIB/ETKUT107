       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.         ETKUT107.                                            
       AUTHOR.             SUE PARI.                                            
       DATE-WRITTEN.       02/09/00.                                            
       DATE-COMPILED.                                                           
                                                                                
************************************************************************        
******* THIS PROGRAM WILL CREATE AN EXTRACT FILE THAT WILL BE   ********        
******* USED TO UPDATE DOCUMENT TRACKING WITH THE TIMESTAMP     ********        
******* WHEN ASN'S ARE PASSED TO THE APPLICATION.               ********        
************************************************************************        
JF1202* 12-05-2002 JON FIEDLER       WR/IR #: PM00511884                        
JF1202* REMOVED WS-SUB VARIABLE AND CODE ASSOCIATED WITH VENDOR ID              
JF1202* HAVING MORE THAN 5 VENDORS SHARING THAT VENDOR ID.                      
JF1202* THIS IS BEING REMOVED BECAUSE WE HAVE A GROUP OF 7 VENDORS              
JF1202* SHARING AN ID AND 2 ARE NOT HAVING THEIR LIFE CYCLE STATUS              
JF1202* UPDATED BECAUSE OF THIS CONDITIONAL STATEMENT.                          
***********************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO IN01.                                                  
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 350 CHARACTERS                                       
           DATA RECORD IS INPUT-ASN-RECORD.                                     
                                                                                
       01  INPUT-ASN-RECORD            PIC X(350).                              
                                                                                
                                                                                
       FD  OUTPUT-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 150 CHARACTERS                                       
           DATA RECORD IS OUTPUT-RECORD.                                        
                                                                                
           COPY ETRD023.                                                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-ASN-INPUT-RECORD.                                                 
           05  WS-RECORD-TYPE            PIC X(3).                              
           05  FILLER                    PIC X(19).                             
           05  WS-ASN-NUMBER             PIC X(30).                             
           05  FILLER                    PIC X(20).                             
           05  WS-ISA-CONTROL-NBR        PIC 9(9).                              
           05  WS-GS-CONTROL-NBR         PIC 9(9).                              
           05  WS-ST-CONTROL-NBR         PIC X(9).                              
           05  WS-TIMESTAMP              PIC X(26).                             
           05  WS-SESSION-ID             PIC X(6).                              
           05  WS-QUALIFIER              PIC X(2).                              
           05  WS-PARTNER-ID             PIC X(15).                             
           05  FILLER                    PIC X(202).                            
                                                                                
       01  WS-TALLY                      PIC 9(2).                              
                                                                                
       01  WS-DB2-TIMESTAMP              PIC X(26).                             
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  END-OF-FILE-SW            PIC X(1)  VALUE 'N'.                   
               88  END-OF-FILE                     VALUE 'Y'.                   
           05  END-OF-TRDPRT-SW          PIC X(1)  VALUE 'N'.                   
               88  END-OF-TRDPRT                   VALUE 'Y'.                   
                                                                                
       01  PV-PARAGRAPH-NAME             PIC X(30).                             
       01  PV-DB2-OPER-ATTEMPTED         PIC X(30).                             
                                                                                
       01  ABEND-CODE                PIC S9(04)  VALUE +0 COMP SYNC.            
           88 ABEND-4013                         VALUE +4013.                   
                                                                                
           COPY DPWS004.                                                        
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               DECLARE TRDPRT_CURSOR CURSOR FOR                                 
                  SELECT TP_DKEY                                                
                  FROM   TTRDPRT                                                
                  WHERE  VEND_INCHG_ID_DESC = :WS-QUALIFIER                     
                    AND  VEND_INCHG_ID      = :WS-PARTNER-ID                    
DUPREC              AND  INACTV_DTE         = '9999-09-09'                      
           END-EXEC.                                                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN INPUT  INPUT-FILE                                               
                OUTPUT OUTPUT-FILE.                                             
                                                                                
           PERFORM 1100-READ-INPUT-FILE                                         
              THRU 1100-READ-INPUT-FILE-EXIT.                                   
                                                                                
           EXEC SQL                                                             
              SET :WS-DB2-TIMESTAMP = CURRENT TIMESTAMP                         
           END-EXEC.                                                            
                                                                                
           DISPLAY 'DATE/TIME ASN FILE WAS PASSED TO APPLICATION:  '            
                   WS-DB2-TIMESTAMP.                                            
                                                                                
           PERFORM 1000-PROCESS-INPUT-FILE                                      
              THRU 1000-PROCESS-INPUT-FILE-EXIT                                 
                 UNTIL END-OF-FILE.                                             
                                                                                
           CLOSE INPUT-FILE                                                     
                 OUTPUT-FILE.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
       1000-PROCESS-INPUT-FILE.                                                 
                                                                                
           MOVE INPUT-ASN-RECORD TO WS-ASN-INPUT-RECORD.                        
                                                                                
           IF WS-RECORD-TYPE = 'ID '                                            
              NEXT SENTENCE                                                     
           ELSE                                                                 
              PERFORM 1100-READ-INPUT-FILE                                      
                 THRU 1100-READ-INPUT-FILE-EXIT                                 
              GO TO 1000-PROCESS-INPUT-FILE-EXIT.                               
                                                                                
           EXEC SQL                                                             
              OPEN TRDPRT_CURSOR                                                
           END-EXEC.                                                            
           IF SQLCODE NOT = +000                                                
              MOVE '1000-PROCESS-INPUT-FILE' TO PV-PARAGRAPH-NAME               
              MOVE 'OPEN CURSOR' TO PV-DB2-OPER-ATTEMPTED                       
              PERFORM 1300-SQL-ERROR.                                           
                                                                                
           MOVE 'N' TO END-OF-TRDPRT-SW.                                        
           PERFORM 1200-GET-TP-DKEY                                             
              THRU 1200-GET-TP-DKEY-EXIT                                        
                 UNTIL END-OF-TRDPRT.                                           
                                                                                
           EXEC SQL                                                             
              CLOSE TRDPRT_CURSOR                                               
           END-EXEC.                                                            
           IF SQLCODE NOT = +000                                                
              MOVE '1000-PROCESS-INPUT-FILE' TO PV-PARAGRAPH-NAME               
              MOVE 'CLOSE CURSOR' TO PV-DB2-OPER-ATTEMPTED                      
              PERFORM 1300-SQL-ERROR.                                           
                                                                                
           PERFORM 1100-READ-INPUT-FILE                                         
              THRU 1100-READ-INPUT-FILE-EXIT.                                   
                                                                                
       1000-PROCESS-INPUT-FILE-EXIT.                                            
           EXIT.                                                                
                                                                                
                                                                                
       1100-READ-INPUT-FILE.                                                    
                                                                                
           READ INPUT-FILE                                                      
              AT END                                                            
                 SET END-OF-FILE TO TRUE.                                       
                                                                                
       1100-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       1200-GET-TP-DKEY.                                                        
                                                                                
           EXEC SQL                                                             
               FETCH TRDPRT_CURSOR                                              
               INTO :TRDPRT-TP-DKEY                                             
           END-EXEC.                                                            
                                                                                
JF1202     IF SQLCODE NOT = +000                                                
              MOVE 'Y' TO END-OF-TRDPRT-SW                                      
           ELSE                                                                 
              MOVE TRDPRT-TP-DKEY    TO ET023-TP-DKEY                           
              MOVE WS-TIMESTAMP      TO ET023-TP-DKEY-TMST                      
              MOVE WS-GS-CONTROL-NBR TO ET023-FUNC-CDE-CNTL-NBR                 
              MOVE WS-ST-CONTROL-NBR TO ET023-TRN-SET-CNTL-NBR                  
              MOVE WS-DB2-TIMESTAMP  TO ET023-TRN-RLSE-TMST                     
              MOVE '856'             TO ET023-TRN-SET-CDE                       
              MOVE ZERO TO WS-TALLY                                             
              INSPECT WS-ASN-NUMBER                                             
                 TALLYING WS-TALLY FOR LEADING ZEROS                            
              MOVE WS-ASN-NUMBER (1 + WS-TALLY:30 - WS-TALLY) TO                
                 ET023-TRN-DTL-TXT                                              
              WRITE ET023-EXTRACT-RECORD.                                       
                                                                                
       1200-GET-TP-DKEY-EXIT.                                                   
           EXIT.                                                                
                                                                                
                                                                                
       1300-SQL-ERROR.                                                          
                                                                                
           SET ABEND-4013 TO TRUE.                                              
                                                                                
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
