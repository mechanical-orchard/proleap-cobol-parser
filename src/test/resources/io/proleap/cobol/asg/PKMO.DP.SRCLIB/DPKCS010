      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
       PROGRAM-ID.                DPKCS010.                                     
       AUTHOR.                    DWYNE WALLACE.                                
       INSTALLATION.              KOHLS DEPARTMENT STORES.                      
       DATE-WRITTEN.              SEPTEMBER 1990.                               
       DATE-COMPILED.                                                           
       SKIP2                                                                    
      ******************************************************************        
      *  CICS PROGRAM - DPKCS010                                       *        
      *  TRANSACTION ID. - Z010                                        *        
      *                                                                *        
      *                   ADD MNEMONIC PROGRAM                         *        
      *                                                                *        
      *  THIS TRANSACTION PROCESSES ALL ADD REQUESTS FOR THE TMNEMONIC *        
      *  DB2 DATABASE. THIS DATABASE IS USED TO CONTROL NAVIGATION     *        
      *  WITHIN CICS.                                                  *        
      *                                                                *        
      *  PF KEY PROCESSING:                                            *        
      *                                                                *        
      *         ENTER - EDIT SCREEN                                    *        
      *         PA2   - TOGGLE FUNCTION KEY LINES                      *        
      *         PF13  - ADD TO DATABASE                                *        
      *         CLEAR - RESET SCREEN TO LAST DISPLAY                   *        
      *                                                                *        
      *  NAVIGATION PF KEY PROCESSING:                                 *        
      *                                                                *        
      *         *SEE MNEMONIC FILE FOR NAVIGATION PF KEY DEFINITIONS.  *        
      *                                                                *        
      *  CHANGED BY M. FURLICK 4/92 PER W/R 1937:                      *        
      *         ADD PROGRAM ID,                                        *        
      *             GLOBAL ACCESS INDICATOR,                           *        
      *             SCREEN TYPE CODE.                                  *        
      *      CHANGE INTER APPL COMMAREA TO PASS MNEMONIC TO            *        
      *             FKEY UPDATE AND MENU ITEM PROGRAMS.                *        
      *                                                                *        
      *    03/14/96  TKMA235  ADD LOGIC FOR CICS24X7 PROCESSING.       *        
      *                       1) MODIFIED MAP (DPKM010) AND PGM TO     *        
      *                          SET THE UPD-VSAM-IND, DISPLAY IT ON   *        
      *                          THE SCREEN, AND TO ADD IT TO THE      *        
      *                          DB2 TMNEMONIC TABLE.                  *        
      *                       2) MODIFIED MAP (DPKM010) AND PGM TO     *        
      *                          DISPLAY PROGRAM-NAME, FROM THE        *        
      *                          DB2 TMNEMONIC TABLE.                  *        
      *    09/09/99  TKMA464  ADD ACCURATE LOCATION REQUIRED INDICATOR.*        
      *                                                                *        
      ******************************************************************        
       SKIP2                                                                    
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
       EJECT                                                                    
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
       SKIP2                                                                    
       WORKING-STORAGE SECTION.                                                 
       SKIP1                                                                    
      *********************                                                     
      * PROGRAM CONSTANTS *                                                     
      *********************                                                     
       SKIP1                                                                    
       01  PROGRAM-CONSTANT-AREA.                                               
           05  PC-MAXIMUM-COMMAREA-LENGTH                                       
                                       PIC S9(4)      VALUE +4072               
                                                      COMP SYNC.                
           05  PC-CURRENT-MAP-NAME     PIC X(8)       VALUE 'DP010A  '.         
           05  PC-CURRENT-MAPSET-NAME  PIC X(8)       VALUE 'DPKM010 '.         
           05  PC-CURRENT-PROGRAM-NAME PIC X(8)       VALUE 'DPKCS010'.         
           05  PC-MRO-PROGRAM          PIC X(8)       VALUE 'DFHCRP  '.         
           05  PC-SPACES               PIC X(8)       VALUE SPACES.             
           05  PC-NAVIGATOR-TRAN-ID    PIC X(4)       VALUE 'Z008'.             
           05  PC-MENU-TRAN-ID         PIC X(4)       VALUE 'MENU'.             
           05  PC-Z007                 PIC X(4)       VALUE 'Z007'.             
           05  PC-Y                    PIC X(1)       VALUE 'Y'.                
           05  PC-P-DEFAULT-TO-PROD    PIC X(1)       VALUE 'P'.                
           05  PC-L                    PIC X(1)       VALUE 'L'.                
           05  PC-LEAVE-SYSTEM-LENGTH  PIC S9(4)      VALUE +80                 
                                                      COMP SYNC.                
           05  PC-MAX-APPL-COMM-LENGTH PIC 9(4)       VALUE 2865.               
           05  PC-MENU-COMMAREA-LEN    PIC 9(4)       VALUE 1845.               
           05  PC-MIN-APPL-COMM-LENGTH PIC 9(4)       VALUE 0003.               
           05  PC-TRANSACTION-ENABLED  PIC S9(8)      COMP                      
                                                      VALUE 23.                 
           05  PC-TRANSACTION-DISABLED PIC S9(8)      COMP                      
                                                      VALUE 24.                 
           05  PC-TRANSACTION-DISABLING                                         
                                       PIC S9(8)      COMP                      
                                                      VALUE 25.                 
           05  PC-GO-TO-KEY            PIC  X(01)     VALUE '1'.                
           05  PC-LOCAL-GO-KEY         PIC  X(01)     VALUE 'B'.                
           05  PC-TSYMSG-00005         PIC 9(5)       VALUE 00005.              
           05  PC-TSYMSG-00008         PIC 9(5)       VALUE 00008.              
           05  PC-TSYMSG-00034         PIC 9(5)       VALUE 00034.              
           05  PC-TSYMSG-00117         PIC 9(5)       VALUE 00117.              
           05  PC-TSYMSG-00224         PIC 9(5)       VALUE 00224.              
           05  PC-TSYMSG-00255         PIC 9(5)       VALUE 00255.              
           05  PC-TSYMSG-00256         PIC 9(5)       VALUE 00256.              
           05  PC-TSYMSG-00257         PIC 9(5)       VALUE 00257.              
           05  PC-TSYMSG-00258         PIC 9(5)       VALUE 00258.              
           05  PC-TSYMSG-00261         PIC 9(5)       VALUE 00261.              
       SKIP2                                                                    
      *********************                                                     
      * PROGRAM VARIABLES *                                                     
      *********************                                                     
       SKIP1                                                                    
       01  PROGRAM-VARIABLE-AREA.                                               
           05  PV-CICS-RESPONSE        PIC S9(8)      VALUE ZERO                
                                                      COMP SYNC.                
           05  PV-TERMINAL             PIC X(4)       VALUE SPACES.             
           05  PV-LEAVE-SYSTEM-MSG     PIC X(80)      VALUE SPACES.             
           05  PV-HOLD-MESSAGE         PIC X(78)      VALUE SPACES.             
           05  PV-HOLD-SEVERITY-IND    PIC X(1)       VALUE SPACES.             
           05  PV-MNEMONIC             PIC X(8)       VALUE SPACES.             
           05  PV-COMM-LENGTH-ALPHA    PIC X(4).                                
           05  PV-COMM-LENGTH-NUMERIC  REDEFINES                                
               PV-COMM-LENGTH-ALPHA    PIC 9(4).                                
       EJECT                                                                    
      ********************                                                      
      * PROGRAM SWITCHES *                                                      
      ********************                                                      
       SKIP1                                                                    
       01  PROGRAM-SWITCHES.                                                    
           05  PS-MAP-SEND-TYPE        PIC X          VALUE '1'.                
               88  PS-SEND-MAP-N-DATA-FRSET           VALUE '1'.                
               88  PS-SEND-MAP-N-DATA-FRSET-CPOSN     VALUE '2'.                
               88  PS-SEND-DATA-ONLY-FRSET            VALUE '3'.                
               88  PS-SEND-DATA-ONLY                  VALUE '4'.                
               88  PS-SEND-DATA-ONLY-ERASEAUP         VALUE '5'.                
               88  PS-SEND-MAP-ONLY-FRSET             VALUE '6'.                
           05  PS-LOCAL-FUNCTION-KEY-IND                                        
                                       PIC X          VALUE 'N'.                
               88  PS-LOCAL-FUNCTION-NO               VALUE 'N'.                
               88  PS-LOCAL-FUNCTION-YES              VALUE 'Y'.                
           05  PS-ROW-FOUND-IND        PIC X          VALUE 'N'.                
               88  PS-ROW-FOUND-NO                    VALUE 'N'.                
               88  PS-ROW-FOUND-YES                   VALUE 'Y'.                
           05  PS-EDIT-ERROR-SW        PIC X          VALUE 'N'.                
               88  PS-EDIT-ERROR-NO                   VALUE 'N'.                
               88  PS-EDIT-ERROR-YES                  VALUE 'Y'.                
           05  PS-NEW-SCREEN-SENT-SW   PIC X          VALUE 'N'.                
               88  PS-NEW-SCREEN-SENT-NO              VALUE 'N'.                
               88  PS-NEW-SCREEN-SENT-YES             VALUE 'Y'.                
       EJECT                                                                    
      ******************                                                        
      * ERROR MESSAGES *                                                        
      ******************                                                        
       SKIP1                                                                    
       01  ERROR-MESSAGES.                                                      
           05  TCICSRTE-FOUND-MSG      PIC X(78)      VALUE                     
               'MNEMONIC ALREADY ON TCICSRTE DATABASE                           
      -        '                      '.                                        
           05  NOT-CICS-TRANID-MSG     PIC X(78)      VALUE                     
               'THE TRANSACTION IS NOT DEFINED TO CICS                          
      -        '                      '.                                        
           05  NOT-NAVG-TRANID-MSG     PIC X(78)      VALUE                     
               'THE TRAN ID ENTERED MAY NOT BE Z008 OR MENU                     
      -        '                      '.                                        
           05  ENTER-PROGRAM-MSG       PIC X(78)      VALUE                     
               'NNNNN-S-THIS IS AN MRO TRANSACTION.  PLEASE ENTER THE PR        
      -        'OGRAM NAME.           '.                                        
           05  INVALID-NEXT-APPL-MSG   PIC X(78)      VALUE                     
               'NEXT APPLICATION CODE INVALID                                   
      -        '                      '.                                        
           05  INVALID-LABEL-MSG       PIC X(78)      VALUE                     
               'NNNNN-E-MNEMONIC ALREADY EXISTS AS A LABEL FOR MNEMONIC         
      -        '                      '.                                        
           05  INVALID-LABEL-MSG-RDF  REDEFINES  INVALID-LABEL-MSG.             
               10  FILLER              PIC X(57).                               
               10  INVALID-LABEL-MNEMONIC                                       
                                       PIC X(8).                                
       EJECT                                                                    
      **************************                                                
      * DB2 COMMUNICATION AREA *                                                
      **************************                                                
       SKIP1                                                                    
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
       SKIP2                                                                    
      ***********************************                                       
      * DB2 SQL RETURN CODE DEFINITIONS *                                       
      *    USING '88' LEVEL             *                                       
      ***********************************                                       
       SKIP1                                                                    
       01  SQL-RETURN-CODE             PIC S9(9)      COMP-4.                   
           88  SQL-SUCCESSFUL                         VALUE +0.                 
           88  SQL-ROW-FOUND                          VALUE +0.                 
           88  SQL-ROW-NOT-FOUND                      VALUE +100.               
           88  SQL-WARNING                            VALUE +100                
                                                            +558                
                                                            +560.               
           88  SQL-DUPLICATE-ROW                      VALUE -803.               
           88  SQL-MORE-THAN-1-ROW                    VALUE -811.               
           88  SQL-NOT-AVAILABLE                      VALUE -911.               
           88  SQL-DB2-DOWN                           VALUE -923.               
           88  SQL-NULL-VALUE                         VALUE -305.               
           88  SQL-DEPENDENT-ROW                      VALUE -532.               
       EJECT                                                                    
      **************************************                                    
      * STANDARD ATTENTION IDENTIFIER LIST *                                    
      **************************************                                    
       SKIP1                                                                    
           COPY DFHAID.                                                         
       EJECT                                                                    
      **************************************************************            
      * SYSTEM MESSAGE FILE RECORD LAYOUT                          *            
      **************************************************************            
           COPY DPRD507I.                                                       
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    WORKING STORAGE INCLUDE MEMBER USED WITH INCLUDE DPPD005   *         
      *---------------------------------------------------------------*         
           COPY DPWS006.                                                        
       EJECT                                                                    
      **************************************************************            
      * WORKING-STORAGE LAYOUT USED TO LOAD THE FUNCTION KEY LINES *            
      **************************************************************            
           COPY DPWS009.                                                        
       EJECT                                                                    
      ***************************                                               
      * COMMON ABEND DATA AREAS *                                               
      ***************************                                               
       SKIP1                                                                    
           COPY DPWS013.                                                        
       EJECT                                                                    
      *********************************************************                 
      * KOHL'S DEFINED:                                       *                 
      * STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST *                 
      *********************************************************                 
       SKIP1                                                                    
           COPY DPWS015.                                                        
       EJECT                                                                    
      **************************************                                    
      * KOHL'S:                            *                                    
      * STANDARD ATTENTION IDENTIFIER LIST *                                    
      **************************************                                    
       SKIP1                                                                    
           COPY DPWS016.                                                        
       EJECT                                                                    
      ***************************************************************           
      * WORKING-STORAGE LAYOUT USED TO GET THE SYSTEM DATE AND TIME *           
      ***************************************************************           
           COPY DPWS017.                                                        
       EJECT                                                                    
      *******************************                                           
      * STANDARD COMMUNICATION AREA *                                           
      *******************************                                           
       SKIP1                                                                    
           COPY DPWS020.                                                        
           05  FILLER                  REDEFINES                                
               DP020-VARIABLE-COMMAREA.                                         
      ***************************************                                   
      * CICSARC INTER APPLICATION DATA AREA *                                   
      ***************************************                                   
               10  INTER-APPL-COMM-AREA                                         
                                       PIC X(200).                              
           COPY DPWS050.                                                        
           COPY DPWS051.                                                        
           COPY DPWS052.                                                        
           COPY DPWS055.                                                        
      *******************************************                               
      * DPKCS021 APPLICATION SPECIFIC COMM AREA *                               
      *******************************************                               
               10  DP010-APPL-SPECIFIC-COMM-AREA.                               
                   15  FILLER          PIC X(02).                               
                   15  DP010-CURRENT-SCREEN-IMAGE.                              
                       20  DP010-SCREEN-MNEMONIC                                
                                       PIC X(8).                                
                       20  DP010-SCREEN-PROGRAM                                 
                                       PIC X(8).                                
                       20  DP010-SCREEN-TRAN-ID                                 
                                       PIC X(4).                                
                       20  DP010-SCREEN-MNEMONIC-TITLE                          
                                       PIC X(30).                               
                       20  DP010-SCREEN-AVAILABIL-IND                           
                                       PIC X.                                   
                       20  DP010-SCREEN-COMMAREA-FORMAT                         
                                       PIC X.                                   
                       20  DP010-SCREEN-SAVE-COMM-IND                           
                                       PIC X.                                   
                       20  DP010-SCREEN-SAVE-COMM-LENGTH                        
                                       PIC X(4).                                
                       20  DP010-SCREEN-GLOBAL-ACCESS                           
                                       PIC X(1).                                
                       20  DP010-SCREEN-TYPE-CODE                               
                                       PIC X(1).                                
                       20  DP010-SCREEN-DB2-PKG-CDE                             
                                       PIC X(1).                                
                       20  DP010-SCREEN-CMNT-TXT                                
                                       PIC X(40).                               
                       20  DP010-SCREEN-ACRT-LOC                                
                                       PIC X.                                   
                   15  DP010-TRAN-DEFINED-CICS                                  
                                       PIC X.                                   
                   15  DP010-SELECT-MAINT-RESULT                                
                                       PIC X.                                   
                       88  DP010-RESULT-IS-CREATE      VALUE '1'.               
                       88  DP010-RESULT-IS-UPDATE      VALUE '2'.               
                       88  DP010-RESULT-IS-DELETE      VALUE '3'.               
                       88  DP010-RESULT-IS-READ        VALUE '4'.               
                       88  DP010-RESULT-IS-ERROR       VALUE '5'.               
                   15  DP010-INTER-APPL-COMMAREA.                               
                       20  DP010-INTER-APPL-FORMAT-CODE                         
                                       PIC X(04).                               
                       20  DP010-INTER-APPL-ACKNLG-CODE                         
                                       PIC X(04).                               
                       20  DP010-MITM-AREA.                                     
                           25  DP010-MITM-MENU-NAME                             
                                       PIC X(08).                               
                           25  DP010-MITM-ITEM-MNEMONIC                         
                                       PIC X(08).                               
                           25  DP010-MITM-SEQUENCE-NUMBER                       
                                       PIC S9(4)      COMP.                     
                           25  DP010-MITM-TRAN-ID                               
                                       PIC X(04).                               
                           25  DP010-MITM-TITLE                                 
                                       PIC X(30).                               
                           25  DP010-MITM-ITEM-TITLE                            
                                       PIC X(30).                               
                           25  DP010-MITM-MAINTENANCE-RESULT                    
                                       PIC X.                                   
                               88  DP010-MITM-RESULT-IS-CREATE                  
                                       VALUE '1'.                               
                               88  DP010-MITM-RESULT-IS-UPDATE                  
                                       VALUE '2'.                               
                               88  DP010-MITM-RESULT-IS-DELETE                  
                                       VALUE '3'.                               
                               88  DP010-MITM-RESULT-IS-READ                    
                                       VALUE '4'.                               
                               88  DP010-MITM-RESULT-IS-ERROR                   
                                       VALUE '5'.                               
                           25  FILLER  PIC X(109).                              
                       20  DP010-FKEY-AREA                                      
                                       REDEFINES                                
                           DP010-MITM-AREA.                                     
                           25  DP010-FKEY-MNEMONIC                              
                                       PIC X(08).                               
                           25  DP010-FKEY-FUNCTION-KEY                          
                                       PIC S9(04)     COMP.                     
                           25  DP010-FKEY-FUNCT-KEY-MNEMONIC                    
                                       PIC X(08).                               
                           25  DP010-FKEY-MAINTENANCE-RESULT                    
                                       PIC X.                                   
                               88  DP010-FKEY-RESULT-IS-CREATE                  
                                       VALUE '1'.                               
                               88  DP010-FKEY-RESULT-IS-UPDATE                  
                                       VALUE '2'.                               
                               88  DP010-FKEY-RESULT-IS-DELETE                  
                                       VALUE '3'.                               
                               88  DP010-FKEY-RESULT-IS-READ                    
                                       VALUE '4'.                               
                               88  DP010-FKEY-RESULT-IS-ERROR                   
                                       VALUE '5'.                               
                           25  FILLER  PIC X(173).                              
                       20  DP010-CMIA-AREA                                      
                                       REDEFINES                                
                           DP010-FKEY-AREA.                                     
                           25  DP010-CMIA-MNEMONIC                              
                                       PIC X(08).                               
                           25  DP010-CMIA-TRAN-ID                               
                                       PIC X(04).                               
                           25  DP010-CMIA-TITLE                                 
                                       PIC X(30).                               
                           25  DP010-CMIA-MAINTENANCE-RESULT                    
                                       PIC X.                                   
                               88  DP010-CMIA-RESULT-IS-CREATE                  
                                       VALUE '1'.                               
                               88  DP010-CMIA-RESULT-IS-UPDATE                  
                                       VALUE '2'.                               
                               88  DP010-CMIA-RESULT-IS-DELETE                  
                                       VALUE '3'.                               
                               88  DP010-CMIA-RESULT-IS-READ                    
                                       VALUE '4'.                               
                               88  DP010-CMIA-RESULT-IS-ERROR                   
                                       VALUE '5'.                               
                           25  FILLER  PIC X(149).                              
                   15  DP010-ENTER-PROGRAM-SW         PIC X.                    
                       88  DP010-ENTER-PROGRAM        VALUE 'Y'.                
                       88  DP010-DONT-ENTER-PROGRAM   VALUE 'N'.                
                   15  DP010-TIMESTAMP     PIC X(26).                           
                   15  DP010-LAST-MNEMONIC PIC X(08).                           
                   15  DP010-ERR-00256-OVERRIDE-SW    PIC X.                    
                       88  DP010-ERR-00256-OVERRIDE-Y VALUE 'Y'.                
                       88  DP010-ERR-00256-OVERRIDE-N VALUE 'N'.                
                   15  DP010-SAVE-INTER-APPL-COMMAREA                           
                                           PIC X(200).                          
       EJECT                                                                    
      **************************************************************            
      * PARAMETER LIST USED TO CALL THE 'TRANSLATE AID' SUBROUTINE *            
      **************************************************************            
           COPY DPWS045.                                                        
        EJECT                                                                   
      ********************************                                          
      * DSECT FOR THE DPKM010 MAPSET *                                          
      ********************************                                          
       SKIP1                                                                    
           COPY DPKM010.                                                        
       SKIP2                                                                    
      ************************************************                          
      * REDEFINES OF DP010AI MAPSET OF DETAIL FIELDS *                          
      ************************************************                          
       01  MD-MAP-DATA-AREA            REDEFINES                                
           DP010AO                     PIC X(1200).                             
       EJECT                                                                    
      ******************************                                            
      * DCLGEN FOR TABLE TMNEMONIC *                                            
      ******************************                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TMNMNC                                                   
           END-EXEC.                                                            
                                                                                
      ******************************                                            
      * DCLGEN FOR TABLE TAIDKEY   *                                            
      ******************************                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TAIDKEY                                                  
           END-EXEC.                                                            
                                                                                
      *******************************                                           
      * DCLGEN FOR TABLE TCICSRTE   *                                           
      *******************************                                           
                                                                                
           EXEC SQL                                                             
               INCLUDE TCICSRTE                                                 
           END-EXEC.                                                            
                                                                                
      ************************************                                      
      * CURSOR FOR ROUTE ALT LABEL CHECK *                                      
      ************************************                                      
                                                                                
           EXEC SQL DECLARE LABEL-CURSOR CURSOR FOR                             
               SELECT                                                           
                       MNEMONIC_CODE                                            
               FROM                                                             
                       TCICSROUTE                                               
               WHERE                                                            
                       FUNC_KEY_LBL_NAME   =  :TMNEMONIC-MNEMONIC-CODE          
           END-EXEC.                                                            
                                                                                
      ************************************                                      
      * CURSOR FOR AIDKEY RETRIEVAL      *                                      
      ************************************                                      
                                                                                
           EXEC SQL DECLARE AIDKEY-CURSOR CURSOR FOR                            
               SELECT                                                           
                       AID_SEQUENCE_NMBR                                        
                     , AID_KEY_NAME                                             
                     , STNRD_ACTION_CODE                                        
                     , AID_CHARACTER_CODE                                       
                     , MNEMONIC_CODE                                            
               FROM                                                             
                       TAIDKEY                                                  
               WHERE                                                            
                       SCRN_TYP_CDE        =  :DP010-SCREEN-TYPE-CODE           
                 AND   SYSWIDE_USE_INDCT   =  :PC-Y                             
                 AND   MNDTRY_MNMNC_INDCT  =  :PC-Y                             
                 AND   MNEMONIC_CODE       >  :PC-SPACES                        
               ORDER BY                                                         
                       AID_SEQUENCE_NMBR                                        
           END-EXEC.                                                            
       EJECT                                                                    
       LINKAGE SECTION.                                                         
       SKIP2                                                                    
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * A000-ADD-MNEMONIC                                              *        
      * (Z010-00 ADD MNEMONIC)                                         *        
      * MAINLINE SECTION.                                              *        
      ******************************************************************        
                                                                                
       A000-ADD-MNEMONIC.                                                       
                                                                                
           PERFORM DP015-0000-CHECK-COMMAREA.                                   
                                                                                
      *****************************************************************         
      * THE VARIABLE 'RETURN-WITH-TRAN-ID' WILL BE SET TO 'Y' IN      *         
      * U13-CHECK-COMMAREA IF A BAD COMMAREA OR NO COMMAREA WAS       *         
      * ENCOUNTERED. IF THAT HAPPENS U13-CHECK-COMMAREA STARTS THE    *         
      * NAVIGATION TRANSACTION AND SETS THIS INDICATOR TO 'Y'. CHECK- *         
      * ING FOR A 'Y' HERE WILL PREVENT THIS TRANSACTION FROM CONTINU-*         
      * ING TO EXECUTE IF THE NAVIGATOR HAS BEEN REQUESTED TO START.  *         
      *****************************************************************         
                                                                                
           IF  DP020-SRC-RETURN-W-TRAN-ID                                       
               PERFORM A050-CONTROL-PROCESSING                                  
           END-IF.                                                              
                                                                                
           PERFORM DP003-0000-RETURN-TO-CICS.                                   
                                                                                
           GOBACK.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A050-CONTROL-PROCESSING                                        *        
      * (Z010-01 CONTROL PROCESSING)                                   *        
      ******************************************************************        
                                                                                
       A050-CONTROL-PROCESSING.                                                 
                                                                                
           MOVE SPACES               TO  DCLTCICSROUTE.                         
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                       TO TRUE                                  
                   INITIALIZE DP010-APPL-SPECIFIC-COMM-AREA                     
                   PERFORM A200-PROCESS-INTER-APPL-COMM                         
                   PERFORM A250-SEND-INITIAL-SCREEN                             
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                       TO TRUE                                  
                   PERFORM A200-PROCESS-INTER-APPL-COMM                         
                   IF  DP010-SCREEN-MNEMONIC  NOT =  SPACES    AND              
                                                     LOW-VALUES                 
                       PERFORM A450-EDIT-SCREEN                                 
                   ELSE                                                         
                       PERFORM A460-RESET-ATTRIBUTES                            
                       MOVE -1         TO AMNEML                                
                   END-IF                                                       
                   PERFORM A470-SET-COMMAND-ATTRIBUTES                          
                   PERFORM A550-DISPLAY-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-ERROR                                        
                   SET PS-SEND-DATA-ONLY-FRSET                                  
                                       TO TRUE                                  
                   PERFORM A470-SET-COMMAND-ATTRIBUTES                          
                   PERFORM A200-PROCESS-INTER-APPL-COMM                         
                   MOVE PV-HOLD-MESSAGE                                         
                                       TO  DP020-MSG-MESSAGE                    
                   MOVE PV-HOLD-SEVERITY-IND                                    
                                       TO  DP020-MSG-SEVERITY-IND               
                   PERFORM A550-DISPLAY-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                   SET PS-SEND-DATA-ONLY-FRSET                                  
                                       TO TRUE                                  
                   PERFORM A470-SET-COMMAND-ATTRIBUTES                          
                   PERFORM A100-PROCESS-MAP                                     
                                                                                
               WHEN OTHER                                                       
                   SET  DP013-OTHER-ABEND                                       
                                       TO TRUE                                  
                   MOVE 'A050-CONTROL-PROCESSING       '                        
                                       TO DP013-PARAGRAPH                       
                   MOVE INVALID-NEXT-APPL-MSG                                   
                                       TO DP013-MESSAGE-TEXT (1)                
                   MOVE DP020-NEXT-APPL-ACTIVITY                                
                                       TO DP013-MESSAGE-TEXT (2)                
                   PERFORM DP013-0000-PROCESS-ABEND                             
                                                                                
           END-EVALUATE.                                                        
       EJECT                                                                    
      ******************************************************************        
      * A100-PROCESS-MAP                                               *        
      * (Z010-03 PROCESS-MAP)                                          *        
      ******************************************************************        
                                                                                
       A100-PROCESS-MAP.                                                        
                                                                                
           IF  EIBAID  =  DFHPA2                                                
                                                                                
      * PA2 KEY  -  CHANGE PFKEYS DISPLAYED *                                   
                                                                                
               MOVE -1                 TO AMNEML                                
               MOVE DP016-PA2          TO DP020-SRC-AID                         
               PERFORM DP009-0000-LOAD-FK-LINES                                 
               PERFORM DP018-0000-GET-CURR-TIME                                 
               SET PS-SEND-DATA-ONLY                                            
                                       TO TRUE                                  
               PERFORM DP008-0000-WRITE-PANEL                                   
           ELSE                                                                 
               IF  EIBAID  =  DFHCLEAR                                          
                   MOVE DP016-CLEAR      TO DP020-SRC-AID                       
                   PERFORM A460-RESET-ATTRIBUTES                                
                   MOVE -1               TO AMNEML                              
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                         TO TRUE                                
                   PERFORM A550-DISPLAY-PANEL                                   
               ELSE                                                             
                   PERFORM DP007-0000-GET-PANEL                                 
                   IF  DP020-SRC-AID  NOT =  DP016-CLEAR                        
                       PERFORM A650-MOVE-SCREEN-TO-COMM                         
                   END-IF                                                       
                   PERFORM A200-PROCESS-INTER-APPL-COMM                         
                   PERFORM A300-CHECK-LOCAL-PF-KEYS                             
                   IF  PS-LOCAL-FUNCTION-NO                                     
                       IF  DP020-SRC-AID = DP016-PF13                           
                           PERFORM A350-LOCAL-PF-KEY-CONTROL                    
                           IF  PS-EDIT-ERROR-NO                                 
                               PERFORM DP015-1000-START-NAVIGATOR               
                           END-IF                                               
                       ELSE                                                     
                           PERFORM DP015-1000-START-NAVIGATOR                   
                       END-IF                                                   
                   ELSE                                                         
                       IF  ((DP020-SRC-AID  =  DP016-ENTER)    AND              
                            (DP020-NAV-MNEMONIC  NOT =  SPACES  AND             
                                                        LOW-VALUES))            
                           PERFORM DP015-1000-START-NAVIGATOR                   
                       ELSE                                                     
                           IF  PS-NEW-SCREEN-SENT-NO                            
      *****************************************************************         
      * THIS STRUCTURE WILL PREVENT THE PROGRAM FROM EDITTING AND     *         
      * PROCESSING THE SCREEN AFTER A NEW SCREEN HAS BEEN SENT TO THE *         
      * TERMINAL. THIS SWITCH IS SET IN MODULE A650-MOVE-SCREEN-TO-COM*         
      * TO 'Y' IF THE USER HAS HIT ERASE-EOF IN THE MNEMONIC FIELD ON *         
      * THE SCREEN, OTHERWISW IT IS ALWAYS SET TO 'N'. SINCE THIS     *         
      * FIELD IS THE KEY TO THE TMNEMONIC TABLE, IF THE USER HAS ER-  *         
      * ASED IT FROM THE SCREEN WE HAVE NO OPTION OTHER THAN TO REDIS-*         
      * PLAY A BLANK SCREEN AND PROMPT THE USER TO ENTER THE CORRECT  *         
      * MNEMONIC.                                                               
      *****************************************************************         
                               PERFORM A350-LOCAL-PF-KEY-CONTROL                
                           END-IF                                               
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A200-PROCESS-INTER-APPL-COMM                                   *        
      * (Z010-05 RESET INTER-APPL COMMAREA)                            *        
      * THE PURPOSE OF THIS MODULE IS TO MANIPULATE THE INTER-APPL-    *        
      * COMMAREA UNDER THE FOUR POSSIBLE CYCLES OF THIS TRANSACTION:   *        
      *                                                                *        
      *        1. DP020-NEXT-ACT-INITIAL                               *        
      *        2. DP020-NEXT-ACT-RETURN                                *        
      *        3. DP020-NEXT-ACT-READ-MAP                              *        
      *        4. DP020-NEXT-ACT-ERROR-RETURN                          *        
      *                                                                *        
      * THREE DATA AREAS ARE NEEDED TO CORRECTLY USE THE INTER-APPL-   *        
      * COMMAREA:                                                      *        
      *                                                                *        
      *        1. THE INTER-APPL-COMMAREA PORTION OF THE STANDARD-COMM-*        
      *           AREA                                                 *        
      *        2. AN AREA OF THE APPLICATION COMMAREA THAT WILL BE USED*        
      *           TO STORE THE CONTENTS OF THE INTER-APPL-COMMAREA SENT*        
      *           TO THIS PROGRAM. (NOTE: ONLY SAVE THOSE FIELDS       *        
      *           RELAVENT TO THIS PROGRAM).                           *        
      *        3. AN AREA OF THE APPLICATION COMMAREA THAT WILL BE USED*        
      *           TO HOLD THE DATA RELAVENT TO THE CURRENT OBJECT BEING*        
      *           PROCESSED BY THIS PROGRAM.                           *        
      ******************************************************************        
                                                                                
       A200-PROCESS-INTER-APPL-COMM.                                            
                                                                                
           IF  DP020-NEXT-ACT-INITIAL                                           
               MOVE INTER-APPL-COMM-AREA TO                                     
                    DP010-SAVE-INTER-APPL-COMMAREA                              
           ELSE                                                                 
               IF  DP020-NEXT-ACT-RETURN                                        
                   IF DP010-SCREEN-DB2-PKG-CDE = SPACES                         
                      MOVE PC-P-DEFAULT-TO-PROD                                 
                                     TO DP010-SCREEN-DB2-PKG-CDE                
                   END-IF                                                       
                   MOVE DP010-SAVE-INTER-APPL-COMMAREA                          
                                     TO  INTER-APPL-COMM-AREA                   
               ELSE                                                             
                   IF  DP020-NEXT-ACT-READ-MAP                                  
                       IF (DP020-FK-GO-TO-TRAN (DP020-SRC-AID)                  
                       OR  DP020-FK-LOCAL-GOTO (DP020-SRC-AID))                 
                           PERFORM A800-SETUP-INTER-APPL                        
                       ELSE                                                     
                           IF  DP020-FK-RETURN (DP020-SRC-AID)                  
                               PERFORM A850-RESET-INITIAL-INTER-APPL            
                           END-IF                                               
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A250-SEND-INITIAL-SCREEN                                       *        
      * (Z010-06 SEND INITIAL SCREEN)                                  *        
      * RETRIEVE THE DATA FROM THE DATABASE, FORMAT DATA ON SCREEN, AND*        
      * DISPLAY THE SCREEN TO THE USER.                                *        
      ******************************************************************        
                                                                                
       A250-SEND-INITIAL-SCREEN.                                                
                                                                                
           PERFORM A500-RETRIEVE-DATA-FOR-SCREEN.                               
                                                                                
           PERFORM A550-DISPLAY-PANEL.                                          
       EJECT                                                                    
      ******************************************************************        
      * A300-CHECK-LOCAL-PF-KEYS                                       *        
      * (Z010-09 CHECK FOR LOCAL PF KEYS)                              *        
      *                                                                *        
      * LOCAL FUNCTION KEYS:                                           *        
      *      PF5   (DP020-SRC-AID = 05)  - REFRESH SCREEN              *        
      *      PF13  (DP020-SRC-AID = 13)  - ADD ROW TO TMNEMONIC TABLE  *        
      *      PA2   (DP020-SRC-AID = 26)  - TOGGLE THE FUNCTION KEY     *        
      *                                    LINES ON SCREEN             *        
      *      CLEAR (DP020-SRC-AID = 27)  - REFRESH SCREEN              *        
      *      ENTER (DP020-SRC-AID = 28)  - EDIT SCREEN                 *        
      ******************************************************************        
                                                                                
       A300-CHECK-LOCAL-PF-KEYS.                                                
                                                                                
           SET PS-LOCAL-FUNCTION-NO    TO TRUE.                                 
                                                                                
           IF (DP020-FK-LOCAL-KEY (DP020-SRC-AID)  OR                           
               DP020-FK-CONFIRM (DP020-SRC-AID)    OR                           
               DP020-FK-REFRESH (DP020-SRC-AID)    OR                           
               DP020-FK-PREV-PAGE (DP020-SRC-AID)  OR                           
               DP020-FK-NEXT-PAGE (DP020-SRC-AID)  OR                           
               DP020-FK-FIRST-PAGE (DP020-SRC-AID) OR                           
               DP020-FK-DESELECT (DP020-SRC-AID))                               
               SET PS-LOCAL-FUNCTION-YES                                        
                                       TO TRUE                                  
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A350-LOCAL-PF-KEY-CONTROL                                      *        
      * (Z010-10 LOCAL PF KEY CONTROL)                                 *        
      ******************************************************************        
                                                                                
       A350-LOCAL-PF-KEY-CONTROL.                                               
                                                                                
           EVALUATE DP020-SRC-AID                                               
                                                                                
               WHEN  DP016-PF5                                                  
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                       TO TRUE                                  
                   PERFORM A250-SEND-INITIAL-SCREEN                             
                                                                                
               WHEN  DP016-PF13                                                 
                   PERFORM A450-EDIT-SCREEN                                     
                   IF  PS-EDIT-ERROR-NO                                         
                       PERFORM A400-PROCESS-ADD                                 
                       MOVE PC-TSYMSG-00224                                     
                                       TO  DP020-MSG-NUMBER                     
                       SET DP020-MSG-INFORMATIONAL                              
                                       TO  TRUE                                 
                   ELSE                                                         
                       SET PS-SEND-MAP-N-DATA-FRSET                             
                                       TO TRUE                                  
                       PERFORM A550-DISPLAY-PANEL                               
                   END-IF                                                       
                                                                                
               WHEN  DP016-PA2                                                  
                   MOVE -1             TO AMNEML                                
                   SET PS-SEND-DATA-ONLY                                        
                                       TO TRUE                                  
                   PERFORM DP005-0000-FORMAT-MSG-LINES                          
                   PERFORM DP009-0000-LOAD-FK-LINES                             
                   PERFORM DP018-0000-GET-CURR-TIME                             
                   PERFORM DP008-0000-WRITE-PANEL                               
                                                                                
               WHEN  DP016-CLEAR                                                
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                       TO TRUE                                  
                   MOVE -1             TO AMNEML                                
                   PERFORM A550-DISPLAY-PANEL                                   
                                                                                
               WHEN OTHER                                                       
                   PERFORM A450-EDIT-SCREEN                                     
                   PERFORM A550-DISPLAY-PANEL                                   
                                                                                
           END-EVALUATE.                                                        
       EJECT                                                                    
      ******************************************************************        
      * A400-PROCESS-ADD                                               *        
      * (Z010-12 PROCESS UPDATE TO DATABASE)                           *        
      * ALL DB2 DATABASE MAINTENANCE USES 'TIMESTAMP' VALIDATION SYNTAX*        
      * TO ENSURE THAT THE SET OF DATA BEING UPDATED IS IN THE SAME    *        
      * CONDITION AT THE TIME OF UPDATE THAT IT WAS IN WHEN THE SET OF *        
      * DATA WAS RETRIEVED FROM THE DATABASE FOR PROCESSING. THIS IS A *        
      * RECOGNITION OF THE POTENTIAL FOR CONCURRENT UPDATING ON ALL    *        
      * RELATIONS WITHIN THE DB2 ENVIRONMENT.                          *        
      ******************************************************************        
                                                                                
       A400-PROCESS-ADD.                                                        
                                                                                
           MOVE DP010-SCREEN-MNEMONIC  TO TMNEMONIC-MNEMONIC-CODE.              
           MOVE DP010-SCREEN-TRAN-ID   TO TMNEMONIC-TRANS-ID-CODE.              
           MOVE DP010-SCREEN-AVAILABIL-IND                                      
                                       TO TMNEMONIC-AVAIL-INDCT.                
                                                                                
           IF  DP010-SCREEN-COMMAREA-FORMAT = 'Y'                               
               MOVE 'K'                TO TMNEMONIC-CMAREA-FRMT-CODE            
           ELSE                                                                 
               MOVE DP010-SCREEN-COMMAREA-FORMAT                                
                                       TO TMNEMONIC-CMAREA-FRMT-CODE            
           END-IF.                                                              
                                                                                
           MOVE DP010-SCREEN-SAVE-COMM-IND                                      
                                       TO TMNEMONIC-SAVE-CMAREA-INDCT.          
           MOVE DP010-SCREEN-SAVE-COMM-LENGTH                                   
                                       TO PV-COMM-LENGTH-ALPHA                  
           MOVE PV-COMM-LENGTH-NUMERIC TO TMNEMONIC-CMAREA-LNGTH-MSRMT.         
                                                                                
           MOVE DP010-SCREEN-MNEMONIC-TITLE                                     
                                       TO TMNEMONIC-PANEL-TITLE-NAME.           
           MOVE DP010-SCREEN-PROGRAM   TO TMNEMONIC-PGM-NAME.                   
           MOVE DP010-SCREEN-GLOBAL-ACCESS                                      
                                       TO TMNEMONIC-GLBL-ACCS-IND.              
           MOVE DP010-SCREEN-TYPE-CODE                                          
                                       TO TMNEMONIC-SCRN-TYP-CDE.               
           MOVE PC-Y                   TO TMNEMONIC-UPD-VSAM-IND.               
           MOVE DP010-SCREEN-DB2-PKG-CDE                                        
                                       TO TMNEMONIC-DB2-PKG-CDE.                
           MOVE DP010-SCREEN-CMNT-TXT  TO TMNEMONIC-CMNT-TXT.                   
           MOVE DP010-SCREEN-ACRT-LOC  TO TMNEMONIC-ACRT-LOC-REQR-IND.          
                                                                                
           PERFORM X910-ADD-TMNEMONIC-ROW.                                      
                                                                                
           IF  SQL-SUCCESSFUL                                                   
               MOVE PC-TSYMSG-00034    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-INFORMATIONAL                                      
                                       TO TRUE                                  
               SET DP010-RESULT-IS-CREATE                                       
                                       TO TRUE                                  
               MOVE DP010-SCREEN-MNEMONIC                                       
                                       TO DP010-LAST-MNEMONIC                   
               MOVE DP010-SCREEN-MNEMONIC                                       
                                       TO TMNEMONIC-MNEMONIC-CODE               
                                                                                
               PERFORM X940-OPEN-TAIDKEY                                        
               PERFORM X930-FETCH-TAIDKEY                                       
               PERFORM X600-PROCESS-ROUTE-RECORDS                               
                   UNTIL SQLCODE NOT EQUAL ZERO                                 
                                                                                
               INITIALIZE AMNEMO                                                
                          ATRANIDO                                              
                          ATRNTLEO                                              
                          AFORMATO                                              
                          AAVAILO                                               
                          ACOMLENO                                              
                          ASAVCOMO                                              
                          DP010-CURRENT-SCREEN-IMAGE                            
               PERFORM A460-RESET-ATTRIBUTES                                    
               MOVE -1                 TO AMNEML                                
           ELSE                                                                 
               MOVE PC-TSYMSG-00117    TO  DP020-MSG-NUMBER                     
               MOVE -1                 TO AMNEML                                
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                       TO AMNEMA                                
               MOVE DP015-RED          TO AMNEMC                                
               MOVE DP015-REVERSE      TO AMNEMH                                
           END-IF.                                                              
           EJECT                                                                
      ******************************************************************        
      * A450-EDIT-SCREEN                                               *        
      * (Z010-11 EDIT SCREEN)                                          *        
      * EDITS WILL OCCUR FROM THE BOTTOM OF THE SCREEN TO THE TOP      *        
      ******************************************************************        
                                                                                
       A450-EDIT-SCREEN.                                                        
                                                                                
           SET PS-EDIT-ERROR-NO        TO TRUE.                                 
                                                                                
      **********************************************                            
      * VALIDATE ACCURATE LOCATION REQUIRED FIELD. *                            
      **********************************************                            
                                                                                
           IF  DP010-SCREEN-ACRT-LOC =  ('Y'  OR  'N')                          
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ALOCA                                 
               MOVE DP015-UNDERLINE    TO ALOCH                                 
               MOVE DP015-GREEN        TO ALOCC                                 
           ELSE                                                                 
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO ALOCL                                 
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                       TO ALOCA                                 
               MOVE DP015-RED          TO ALOCC                                 
               MOVE DP015-REVERSE      TO ALOCH                                 
           END-IF.                                                              
                                                                                
      **********************************************                            
      * VALIDATE COMMENT-TEXT FIELD.               *                            
      * THIS FIELD IS FREE-FORMAT AND NO ENTRY     *                            
      * IS REQUIRED; THEREFORE NO EDITS PERFORMED. *                            
      **********************************************                            
           MOVE DP015-UNP-ALP-NOR-OFF  TO ACMNTA.                               
           MOVE DP015-UNDERLINE        TO ACMNTH.                               
           MOVE DP015-GREEN            TO ACMNTC.                               
                                                                                
      **********************************************                            
      * VALIDATE DB2 PACKAGE CODE FIELD            *                            
      * FROM THE APPLICATION SPECIFIC COMMAREA.    *                            
      * REQ'D FIELD; VALID VALUES ARE 'P' AND 'A'. *                            
      **********************************************                            
                                                                                
           IF  DP010-SCREEN-DB2-PKG-CDE = ('P'  OR  'A')                        
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO APKGA                                 
               MOVE DP015-UNDERLINE    TO APKGH                                 
               MOVE DP015-GREEN        TO APKGC                                 
           ELSE                                                                 
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO APKGL                                 
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO APKGA                                 
               MOVE DP015-RED          TO APKGC                                 
               MOVE DP015-REVERSE      TO APKGH                                 
           END-IF.                                                              
                                                                                
      **********************************************                            
      * SCREEN TYPE CODE                           *                            
      **********************************************                            
                                                                                
           IF  DP010-SCREEN-TYPE-CODE  =  ('M'  OR  'L'  OR  'D')               
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ASCRTYPA                              
               MOVE DP015-UNDERLINE    TO ASCRTYPH                              
               MOVE DP015-GREEN        TO ASCRTYPC                              
                                                                                
               IF (DP010-SCREEN-TYPE-CODE = 'M'                                 
               AND DP010-SCREEN-TRAN-ID NOT EQUAL PC-Z007)                      
               OR (DP010-SCREEN-TYPE-CODE NOT = 'M'                             
               AND DP010-SCREEN-TRAN-ID EQUAL PC-Z007)                          
                                                                                
                   SET PS-EDIT-ERROR-YES                                        
                                       TO TRUE                                  
                   MOVE PC-TSYMSG-00255                                         
                                       TO  DP020-MSG-NUMBER                     
                   SET DP020-MSG-FATAL TO TRUE                                  
                   MOVE -1             TO ASCRTYPL                              
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                       TO ASCRTYPA                              
                   MOVE DP015-RED      TO ASCRTYPC                              
                   MOVE DP015-REVERSE  TO ASCRTYPH                              
               ELSE                                                             
                   IF  DP010-SCREEN-TYPE-CODE EQUAL 'M'                         
                       MOVE PC-Y       TO DP010-SCREEN-GLOBAL-ACCESS            
                                          DP010-SCREEN-SAVE-COMM-IND            
                                          DP010-SCREEN-COMMAREA-FORMAT          
                       MOVE PC-MENU-COMMAREA-LEN                                
                                       TO DP010-SCREEN-SAVE-COMM-LENGTH         
                                          ACOMLENO                              
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO ASCRTYPL                              
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                       TO ASCRTYPA                              
               MOVE DP015-RED          TO ASCRTYPC                              
               MOVE DP015-REVERSE      TO ASCRTYPH                              
           END-IF.                                                              
                                                                                
      **********************************************                            
      * VALIDATE GLOBAL ACCESS INDICATOR           *                            
      **********************************************                            
                                                                                
           IF  DP010-SCREEN-GLOBAL-ACCESS  =  ('Y'  OR  'N')                    
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO AGLBACCA                              
               MOVE DP015-UNDERLINE    TO AGLBACCH                              
               MOVE DP015-GREEN        TO AGLBACCC                              
           ELSE                                                                 
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO AGLBACCL                              
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                       TO AGLBACCA                              
               MOVE DP015-RED          TO AGLBACCC                              
               MOVE DP015-REVERSE      TO AGLBACCH                              
           END-IF.                                                              
                                                                                
      **********************************************                            
      * VALIDATE SCREEN COMMAREA LENGTH            *                            
      * FROM THE APPLICATION SPECIFIC COMMAREA     *                            
      **********************************************                            
                                                                                
           EXEC CICS                                                            
               BIF                                                              
                 DEEDIT FIELD (DP010-SCREEN-SAVE-COMM-LENGTH)                   
                       LENGTH (4)                                               
           END-EXEC.                                                            
           IF  DP010-SCREEN-SAVE-COMM-LENGTH  NUMERIC                           
               IF  DP010-SCREEN-SAVE-COMM-LENGTH  >                             
                                           PC-MAX-APPL-COMM-LENGTH              
                   SET PS-EDIT-ERROR-YES TO TRUE                                
                   MOVE PC-TSYMSG-00005  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-FATAL   TO TRUE                                
                   MOVE -1               TO ACOMLENL                            
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO ACOMLENA                            
                   MOVE DP015-RED        TO ACOMLENC                            
                   MOVE DP015-REVERSE    TO ACOMLENH                            
               ELSE                                                             
                   MOVE DP015-UNP-ALP-NOR-OFF                                   
                                         TO ACOMLENA                            
                   MOVE DP015-UNDERLINE  TO ACOMLENH                            
                   MOVE DP015-GREEN      TO ACOMLENC                            
               END-IF                                                           
           ELSE                                                                 
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00008    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO ACOMLENL                              
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                       TO ACOMLENA                              
               MOVE DP015-RED          TO ACOMLENC                              
               MOVE DP015-REVERSE      TO ACOMLENH                              
           END-IF.                                                              
                                                                                
      **********************************************                            
      * VALIDATE SCREEN SAVE COMMAREA INDICATOR    *                            
      * FROM THE APPLICATION SPECIFIC COMMAREA     *                            
      **********************************************                            
                                                                                
           IF  DP010-SCREEN-SAVE-COMM-IND  =  ('Y'  OR  'N')                    
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ASAVCOMA                              
               MOVE DP015-UNDERLINE    TO ASAVCOMH                              
               MOVE DP015-GREEN        TO ASAVCOMC                              
           ELSE                                                                 
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO ASAVCOML                              
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                       TO ASAVCOMA                              
               MOVE DP015-RED          TO ASAVCOMC                              
               MOVE DP015-REVERSE      TO ASAVCOMH                              
           END-IF.                                                              
                                                                                
      **********************************************                            
      * VALIDATE SCREEN COMMAREA FORMAT            *                            
      * FROM THE APPLICATION SPECIFIC COMMAREA     *                            
      **********************************************                            
                                                                                
           IF  DP010-SCREEN-COMMAREA-FORMAT  =  ('Y'  OR  'N')                  
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO AFORMATA                              
               MOVE DP015-UNDERLINE    TO AFORMATH                              
               MOVE DP015-GREEN        TO AFORMATC                              
           ELSE                                                                 
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO AFORMATL                              
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                       TO AFORMATA                              
               MOVE DP015-RED          TO AFORMATC                              
               MOVE DP015-REVERSE      TO AFORMATH                              
           END-IF.                                                              
                                                                                
           IF  (    DP010-SCREEN-COMMAREA-FORMAT   =  'Y'                       
               AND  DP010-SCREEN-SAVE-COMM-LENGTH  <                            
                    PC-MIN-APPL-COMM-LENGTH)                                    
                 SET PS-EDIT-ERROR-YES TO TRUE                                  
                 MOVE PC-TSYMSG-00005  TO DP020-MSG-NUMBER                      
                 SET DP020-MSG-FATAL   TO TRUE                                  
                 MOVE -1               TO ACOMLENL                              
                 MOVE DP015-UNP-ALP-BRT-OFF                                     
                                       TO ACOMLENA                              
                 MOVE DP015-RED        TO ACOMLENC                              
                 MOVE DP015-REVERSE    TO ACOMLENH                              
           END-IF.                                                              
                                                                                
      **********************************************                            
      * VALIDATE SCREEN AVAILABILITY INDICATOR     *                            
      * FROM THE APPLICATION SPECIFIC COMMAREA     *                            
      **********************************************                            
                                                                                
           IF  DP010-SCREEN-AVAILABIL-IND  =  ('Y'  OR  'N')                    
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO AAVAILA                               
               MOVE DP015-UNDERLINE    TO AAVAILH                               
               MOVE DP015-GREEN        TO AAVAILC                               
           ELSE                                                                 
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO AAVAILL                               
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                       TO AAVAILA                               
               MOVE DP015-RED          TO AAVAILC                               
               MOVE DP015-REVERSE      TO AAVAILH                               
           END-IF.                                                              
                                                                                
      **********************************************                            
      * VALIDATE SCREEN MNEMONIC TITLE             *                            
      * FROM THE APPLICATION SPECIFIC COMMAREA     *                            
      **********************************************                            
                                                                                
           IF  DP010-SCREEN-MNEMONIC-TITLE  =  (   SPACES                       
                                               OR  LOW-VALUES                   
                                               )                                
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO ATRNTLEL                              
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                       TO ATRNTLEA                              
               MOVE DP015-RED          TO ATRNTLEC                              
               MOVE DP015-REVERSE      TO ATRNTLEH                              
           ELSE                                                                 
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ATRNTLEA                              
               MOVE DP015-UNDERLINE    TO ATRNTLEH                              
               MOVE DP015-GREEN        TO ATRNTLEC                              
           END-IF.                                                              
                                                                                
      **********************************************                            
      * VALIDATE SCREEN TRAN-ID                    *                            
      * FROM THE APPLICATION SPECIFIC COMMAREA     *                            
      **********************************************                            
                                                                                
           IF  DP010-SCREEN-TRAN-ID  =  (    SPACES                             
                                        OR   LOW-VALUES                         
                                        )                                       
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO ATRANIDL                              
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                       TO ATRANIDA                              
               MOVE DP015-RED          TO ATRANIDC                              
               MOVE DP015-REVERSE      TO ATRANIDH                              
           ELSE                                                                 
      ********************************************************                  
      *    MAKE SURE THAT THE TRANSACTION ENTERED ON THE     *                  
      *    SCREEN IS NOT THE NAVIGATOR TRANSACTION. ANY      *                  
      *    ATTEMPT TO NAVIGATE TO THE NAVIGATOR THRU THE     *                  
      *    NAVIGATOR WILL CAUSE THE ARCHITECURE TO LOOP      *                  
      ********************************************************                  
               IF ( DP010-SCREEN-TRAN-ID  =  PC-NAVIGATOR-TRAN-ID OR            
                                             PC-MENU-TRAN-ID )                  
                                                                                
                   SET PS-EDIT-ERROR-YES   TO TRUE                              
                   MOVE PC-TSYMSG-00257                                         
                                       TO DP020-MSG-NUMBER                      
                   SET DP020-MSG-FATAL TO TRUE                                  
                   MOVE -1             TO ATRANIDL                              
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                       TO ATRANIDA                              
                   MOVE DP015-RED      TO ATRANIDC                              
                   MOVE DP015-REVERSE  TO ATRANIDH                              
               ELSE                                                             
                                                                                
                   IF (DP010-ENTER-PROGRAM                                      
                   AND DP010-SCREEN-PROGRAM GREATER THAN SPACES)                
                       CONTINUE                                                 
                   ELSE                                                         
                                                                                
      ********************************************************                  
      * CICS INQUIRY TO MAKE SURE TRAN-ID IS DEFINED TO CICS *                  
      * RETURN CODE:                                         *                  
      *            ZERO -  TRANSACTION FOUND                 *                  
      *             23  -  TRANSACTION ENABLED               *                  
      *             24  -  TRANSACTION DISABLED              *                  
      *             25  -  TRANSACTION DISABLING             *                  
      ********************************************************                  
                                                                                
                     EXEC CICS                                                  
                          INQUIRE                                               
                          TRANSACTION (DP010-SCREEN-TRAN-ID)                    
                          RESP    (PV-CICS-RESPONSE)                            
                          PROGRAM (DP010-SCREEN-PROGRAM)                        
                     END-EXEC                                                   
                                                                                
                     IF  PV-CICS-RESPONSE  =  (   ZEROS                         
                                          OR  PC-TRANSACTION-ENABLED            
                                          OR  PC-TRANSACTION-DISABLED           
                                          OR  PC-TRANSACTION-DISABLING          
                                          )                                     
                         MOVE DP015-UNP-ALP-NOR-OFF                             
                                         TO ATRANIDA                            
                         MOVE DP015-UNDERLINE                                   
                                         TO ATRANIDH                            
                         MOVE DP015-GREEN                                       
                                         TO ATRANIDC                            
                         IF  DP010-SCREEN-PROGRAM EQUAL PC-MRO-PROGRAM          
                             MOVE DP015-UNP-ALP-NOR-OFF                         
                                         TO APROGA                              
                             MOVE DP015-UNDERLINE                               
                                         TO APROGH                              
                             MOVE DP015-GREEN                                   
                                         TO APROGC                              
                             MOVE -1     TO APROGL                              
                             SET DP010-ENTER-PROGRAM                            
                                         TO TRUE                                
                             SET PS-EDIT-ERROR-YES                              
                                         TO TRUE                                
                             MOVE PC-TSYMSG-00258                               
                                         TO DP020-MSG-NUMBER                    
                             MOVE SPACES TO DP010-SCREEN-PROGRAM                
                         ELSE                                                   
                             SET DP010-DONT-ENTER-PROGRAM                       
                                         TO TRUE                                
                             MOVE DP010-SCREEN-PROGRAM                          
                                         TO APROGO                              
                         END-IF                                                 
                     ELSE                                                       
                         IF  DP010-ERR-00256-OVERRIDE-Y                         
      *                      ******************************************         
      *                      * THIS MESSAGE OCCURRED ON THE LAST PASS;          
      *                      * ON THIS PASS, ALLOW "ADD" TO OCCUR.              
      *                      *******************************************        
                             SET DP010-ERR-00256-OVERRIDE-N                     
                                         TO TRUE                                
                             SET PS-EDIT-ERROR-NO                               
                                         TO TRUE                                
                         ELSE                                                   
                              SET PS-EDIT-ERROR-YES                             
                                         TO TRUE                                
                              MOVE PC-TSYMSG-00256                              
                                         TO DP020-MSG-NUMBER                    
      *                       SET DP020-MSG-FATAL                               
                              SET DP020-MSG-INFORMATIONAL                       
                                         TO TRUE                                
                              SET DP010-ERR-00256-OVERRIDE-Y                    
                                         TO TRUE                                
                              MOVE -1    TO ATRANIDL                            
                              MOVE DP015-UNP-ALP-NOR-OFF                        
                                         TO ATRANIDA                            
                              MOVE DP015-RED                                    
                                         TO ATRANIDC                            
                              MOVE DP015-REVERSE                                
                                         TO ATRANIDH                            
                         END-IF                                                 
                     END-IF                                                     
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      **********************************************                            
      * VALIDATE SCREEN MNEMONIC                   *                            
      * FROM THE APPLICATION SPECIFIC COMMAREA     *                            
      **********************************************                            
                                                                                
           IF  DP010-SCREEN-MNEMONIC  =  (    SPACES                            
                                         OR   LOW-VALUES                        
                                         )                                      
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO AMNEML                                
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                       TO AMNEMA                                
               MOVE DP015-RED          TO AMNEMC                                
               MOVE DP015-REVERSE      TO AMNEMH                                
           ELSE                                                                 
               MOVE DP010-SCREEN-MNEMONIC                                       
                                       TO TMNEMONIC-MNEMONIC-CODE               
               PERFORM X900-READ-TMNEMONIC-ROW                                  
               IF  SQL-ROW-NOT-FOUND                                            
                   MOVE DP015-UNP-ALP-NOR-OFF                                   
                                       TO AMNEMA                                
                   MOVE DP015-GREEN    TO AMNEMC                                
                   MOVE DP015-UNDERLINE                                         
                                       TO AMNEMH                                
                   PERFORM A455-LABEL-CHECK                                     
                   IF  SQLCODE EQUAL ZERO                                       
                       SET PS-EDIT-ERROR-YES TO TRUE                            
                       MOVE PC-TSYMSG-00261                                     
                                       TO DP020-MSG-NUMBER                      
                       MOVE -1               TO AMNEML                          
                       MOVE DP015-UNP-ALP-BRT-OFF                               
                                             TO AMNEMA                          
                       MOVE DP015-RED        TO AMNEMC                          
                       MOVE DP015-REVERSE    TO AMNEMH                          
                   END-IF                                                       
               ELSE                                                             
                   IF  SQL-ROW-FOUND                                            
                       SET PS-EDIT-ERROR-YES TO TRUE                            
                       MOVE PC-TSYMSG-00117  TO DP020-MSG-NUMBER                
                       MOVE -1               TO AMNEML                          
                       MOVE DP015-UNP-ALP-BRT-OFF                               
                                             TO AMNEMA                          
                       MOVE DP015-RED        TO AMNEMC                          
                       MOVE DP015-REVERSE    TO AMNEMH                          
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  PS-EDIT-ERROR-NO                                                 
               MOVE -1                 TO ANAVKEYL                              
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      *    CHECK TO SEE IF THE REQUESTED MNEMONIC IS ALREADY USED AS   *        
      *    A LABEL FOR ANOTHER MNEMONIC.                               *        
      ******************************************************************        
       A455-LABEL-CHECK.                                                        
                                                                                
           EXEC SQL                                                             
                OPEN LABEL-CURSOR                                               
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE EQUAL ZERO                                               
           OR  SQLCODE EQUAL +100)                                              
               CONTINUE                                                         
           ELSE                                                                 
               SET  DP013-DB2-ABEND    TO TRUE                                  
               MOVE 'A455-LABEL-CHECK              '                            
                                       TO DP013-PARAGRAPH                       
               MOVE SQLCA              TO DP013-SQLCA                           
               MOVE 'TCICSRTE - OPEN   '                                        
                                       TO DP013-DB2-TABLE-NAME (1)              
               MOVE TCICSROUTE-MNEMONIC-CODE                                    
                                       TO DP013-MESSAGE-TEXT (1)                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           IF  SQLCODE EQUAL ZERO                                               
               EXEC SQL FETCH LABEL-CURSOR                                      
                    INTO                                                        
                       :INVALID-LABEL-MNEMONIC                                  
               END-EXEC                                                         
                                                                                
               IF (SQLCODE EQUAL ZERO                                           
               OR  SQLCODE EQUAL +100)                                          
                   CONTINUE                                                     
               ELSE                                                             
                   SET DP013-DB2-ABEND TO TRUE                                  
                   MOVE 'A455-LABEL-CHECK              '                        
                                       TO DP013-PARAGRAPH                       
                   MOVE SQLCA          TO DP013-SQLCA                           
                   MOVE 'TCICSRTE - FETCH  '                                    
                                       TO DP013-DB2-TABLE-NAME (1)              
                   MOVE TCICSROUTE-MNEMONIC-CODE                                
                                       TO DP013-MESSAGE-TEXT (1)                
                   PERFORM DP013-0000-PROCESS-ABEND                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      ******************************************************************        
      * A460-RESET-ATTRIBUTES                                          *        
      ******************************************************************        
       A460-RESET-ATTRIBUTES.                                                   
           MOVE DP015-UNP-ALP-NOR-OFF                                           
                                 TO AMNEMA.                                     
           MOVE DP015-GREEN      TO AMNEMC.                                     
           MOVE DP015-UNDERLINE  TO AMNEMH.                                     
           MOVE DP015-UNP-ALP-NOR-OFF                                           
                                 TO ATRANIDA.                                   
           MOVE DP015-GREEN      TO ATRANIDC.                                   
           MOVE DP015-UNDERLINE  TO ATRANIDH.                                   
           MOVE DP015-UNP-ALP-NOR-OFF                                           
                                 TO ATRNTLEA.                                   
           MOVE DP015-GREEN      TO ATRNTLEC.                                   
           MOVE DP015-UNDERLINE  TO ATRNTLEH.                                   
           MOVE DP015-UNP-ALP-NOR-OFF                                           
                                 TO AFORMATA.                                   
           MOVE DP015-GREEN      TO AFORMATC.                                   
           MOVE DP015-UNDERLINE  TO AFORMATH.                                   
           MOVE DP015-UNP-ALP-NOR-OFF                                           
                                 TO AAVAILA.                                    
           MOVE DP015-GREEN      TO AAVAILC.                                    
           MOVE DP015-UNDERLINE  TO AAVAILH.                                    
           MOVE DP015-UNP-ALP-NOR-OFF                                           
                                 TO ACOMLENA.                                   
           MOVE DP015-GREEN      TO ACOMLENC.                                   
           MOVE DP015-UNDERLINE  TO ACOMLENH.                                   
           MOVE DP015-UNP-ALP-NOR-OFF                                           
                                 TO ASAVCOMA.                                   
           MOVE DP015-GREEN      TO ASAVCOMC.                                   
           MOVE DP015-UNDERLINE  TO ASAVCOMH.                                   
           MOVE DP015-UNP-ALP-NOR-OFF                                           
                                 TO AGLBACCA.                                   
           MOVE DP015-GREEN      TO AGLBACCC.                                   
           MOVE DP015-UNDERLINE  TO AGLBACCH.                                   
           MOVE DP015-UNP-ALP-NOR-OFF                                           
                                 TO ALOCA.                                      
           MOVE DP015-GREEN      TO ALOCC.                                      
           MOVE DP015-UNDERLINE  TO ALOCH.                                      
       EJECT                                                                    
      *****************************************************************         
      *     EVERY TIME IN SET THE COMMAND FIELD TO EITHER RED         *         
      *     OR GREEN. THIS IS BECAUSE IT IS UNEDITTED IN THE          *         
      *     PROGRAM AND IT NEEDS RESETTING.                           *         
      *****************************************************************         
       A470-SET-COMMAND-ATTRIBUTES.                                             
           IF DP020-NEXT-ACT-ERROR                                              
               MOVE DP020-MSG-MESSAGE                                           
                                       TO PV-HOLD-MESSAGE                       
               MOVE DP020-MSG-SEVERITY-IND                                      
                                       TO PV-HOLD-SEVERITY-IND                  
               IF DP020-NAV-MNEMONIC NOT EQUAL TO SPACES                        
                   MOVE DP015-UNP-ALP-BRT-MDT                                   
                                       TO ANAVCMDA                              
                   MOVE DP015-RED                                               
                                       TO ANAVCMDC                              
                   MOVE DP015-REVERSE                                           
                                       TO ANAVCMDH                              
               ELSE                                                             
                   MOVE SPACES         TO ANAVCMDO                              
                                          DP020-NAV-MNEMONIC                    
                   MOVE DP015-UNP-ALP-NOR-OFF                                   
                                       TO ANAVCMDA                              
                   MOVE DP015-GREEN                                             
                                       TO ANAVCMDC                              
                   MOVE DP015-UNDERLINE                                         
                                       TO ANAVCMDH                              
               END-IF                                                           
           ELSE                                                                 
               IF (EIBAID  NOT EQUAL DFHPA2    AND                              
                                     DFHCLEAR)                                  
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ANAVCMDA                              
               MOVE DP015-GREEN                                                 
                                       TO ANAVCMDC                              
               MOVE DP015-UNDERLINE                                             
                                       TO ANAVCMDH                              
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A500-RETRIEVE-DATA-FOR-SCREEN                                  *        
      * (Z010-07 RETRIEVE DATA FOR SCREEN                              *        
      ******************************************************************        
                                                                                
       A500-RETRIEVE-DATA-FOR-SCREEN.                                           
                                                                                
           IF  (    DP020-NEXT-ACT-INITIAL                                      
               OR   DP020-SRC-AID  =  DP016-PF5                                 
               )                                                                
               INITIALIZE DP010-CURRENT-SCREEN-IMAGE                            
           END-IF.                                                              
           SET PS-SEND-MAP-N-DATA-FRSET TO TRUE.                                
                                                                                
           IF  DP010-SCREEN-MNEMONIC  =  ( SPACES OR LOW-VALUES )               
               MOVE -1                 TO AMNEML                                
           ELSE                                                                 
               MOVE -1                 TO APROGL                                
           END-IF.                                                              
                                                                                
           MOVE PC-P-DEFAULT-TO-PROD   TO DP010-SCREEN-DB2-PKG-CDE.             
       EJECT                                                                    
      ******************************************************************        
      * A550-DISPLAY-PANEL                                             *        
      * (Z010-02 DISPLAY PANEL)                                        *        
      ******************************************************************        
                                                                                
       A550-DISPLAY-PANEL.                                                      
                                                                                
           IF  PS-SEND-DATA-ONLY-FRSET                                          
               PERFORM DP018-0000-GET-CURR-TIME                                 
           ELSE                                                                 
               PERFORM DP017-0000-GET-CURR-DATE-TIME                            
               PERFORM DP002-0000-FORMAT-TITLE-LINES                            
           END-IF.                                                              
                                                                                
           IF  PS-SEND-MAP-N-DATA-FRSET                                         
               PERFORM A600-LOAD-DETAIL-DATA                                    
           END-IF.                                                              
                                                                                
           PERFORM DP005-0000-FORMAT-MSG-LINES.                                 
                                                                                
           IF (    PS-SEND-MAP-N-DATA-FRSET                                     
              OR   DP020-SRC-AID  =  DP016-PA2                                  
              )                                                                 
      ***************************************                                   
      * PA2 KEY  -  CHANGE PFKEYS DISPLAYED *                                   
      ***************************************                                   
              PERFORM DP009-0000-LOAD-FK-LINES                                  
           END-IF.                                                              
                                                                                
           PERFORM DP008-0000-WRITE-PANEL.                                      
       EJECT                                                                    
      ******************************************************************        
      * A600-LOAD-DETAIL-DATA                                          *        
      * (Z010-08 LOAD DETAIL DATA)                                     *        
      ******************************************************************        
                                                                                
       A600-LOAD-DETAIL-DATA.                                                   
                                                                                
           MOVE DP010-SCREEN-MNEMONIC  TO AMNEMO.                               
           MOVE DP010-SCREEN-TRAN-ID   TO ATRANIDO.                             
           MOVE DP010-SCREEN-MNEMONIC-TITLE                                     
                                       TO ATRNTLEO.                             
                                                                                
           MOVE DP010-SCREEN-AVAILABIL-IND                                      
                                       TO AAVAILO.                              
           MOVE DP010-SCREEN-COMMAREA-FORMAT                                    
                                       TO AFORMATO.                             
           MOVE DP010-SCREEN-SAVE-COMM-IND                                      
                                       TO ASAVCOMO.                             
                                                                                
           IF   DP010-SCREEN-SAVE-COMM-LENGTH = 0                               
           OR   DP010-SCREEN-SAVE-COMM-LENGTH = SPACE                           
                MOVE SPACE             TO ACOMLENO                              
           ELSE                                                                 
                MOVE DP010-SCREEN-SAVE-COMM-LENGTH                              
                                       TO ACOMLENO                              
           END-IF.                                                              
                                                                                
           MOVE DP010-SCREEN-GLOBAL-ACCESS                                      
                                       TO AGLBACCO.                             
           MOVE DP010-SCREEN-PROGRAM   TO APROGO.                               
           MOVE DP010-SCREEN-TYPE-CODE TO ASCRTYPO.                             
           MOVE DP010-SCREEN-DB2-PKG-CDE                                        
                                       TO APKGO.                                
           MOVE DP010-SCREEN-CMNT-TXT  TO ACMNTO.                               
           MOVE DP010-SCREEN-ACRT-LOC  TO ALOCO.                                
           MOVE PC-Y                   TO AUVINDO.                              
       EJECT                                                                    
      ******************************************************************        
      * A650-MOVE-SCREEN-TO-COMM                                       *        
      * (Z010-13 MOVE SCREEN TO COMMAREA)                              *        
      * IN ORDER TO MINIMIZE DATA TRANSMISSIONS, ALL SCREEN VARIABLES  *        
      * WILL BE STORED IN THE COMMAREA. ALL EDITING AND DATABASE I/O   *        
      * WILL BE FROM THESE FIELDS IN THE APPLICATION SPECIFIC COMMAREA.*        
      * ONLY ATTRIBUTE CHANGES WILL BE SENT TO THE TERMINAL AS LONG AS *        
      * THE CURRENT MAP IS STILL BEING DISPLAYED. THIS MODULE OVERLAYS *        
      * ANY COMMAREA SCREEN VARIABLE THAT HAS CHANGED (EITHER A LENGTH *        
      * GREATER THAN ZERO OR EOF HAS BEEN PRESSED) WITH THE INPUT FIELD*        
      * FROM THE SCREEN. IF ERASE-EOF HAS BEEN PRESSED WITHIN THE      *        
      * MNEMONIC FIELD ON THE SCREEN IT IS NECESSARY TO SEND AN INITIAL*        
      * SCREEN. THIS IS DONE BECAUSE MNEMONIC IS THE KEY TO THE TMNEMO-*        
      * NIC TABLE THAT THE USER IS TRYING TO ADD A ROW TO. IF THIS PRO-*        
      * GRAM HAS NO MNEMONIC, IT CAN'T ADD A ROW. THE SWITCH 'PS-NEW-  *        
      * SCREEN-SENT-SW' IS SET TO A 'Y' IN THIS SITUATION SO THAT FUR- *        
      * THER PROCESSING OF THE SCREEN IS NOT ATTEMPTED (SEE MODULE A100*        
      * -PROCESS-MAP FOR WHERE THIS SWITCH IS INTERROGATED AND PROCESS-*        
      * ING BYPASSED IF IT IS OTHER THAN 'N'.                          *        
      ******************************************************************        
                                                                                
       A650-MOVE-SCREEN-TO-COMM.                                                
                                                                                
           IF  (     ANAVKEYL       >  ZEROS                                    
               AND   ANAVCMDL       =  ZEROS                                    
               AND   DP020-SRC-AID  =  DP016-ENTER                              
               )                                                                
               SET PS-NEW-SCREEN-SENT-YES                                       
                                       TO TRUE                                  
               INITIALIZE DP010-CURRENT-SCREEN-IMAGE                            
               MOVE ANAVKEYI           TO PV-MNEMONIC                           
                                          DP010-SCREEN-MNEMONIC                 
               MOVE LOW-VALUES         TO ANAVKEYO                              
               MOVE SPACES             TO DP010-SELECT-MAINT-RESULT             
               SET PS-SEND-MAP-N-DATA-FRSET                                     
                                       TO TRUE                                  
               PERFORM A250-SEND-INITIAL-SCREEN                                 
           ELSE                                                                 
               SET PS-NEW-SCREEN-SENT-NO                                        
                                       TO TRUE                                  
               IF  AMNEMF  =  DP015-ERASE-EOF                                   
                   SET PS-NEW-SCREEN-SENT-YES                                   
                                       TO TRUE                                  
                   INITIALIZE DP010-CURRENT-SCREEN-IMAGE                        
                   MOVE SPACES         TO DP010-SCREEN-MNEMONIC                 
                                          PV-MNEMONIC                           
                   PERFORM A250-SEND-INITIAL-SCREEN                             
               ELSE                                                             
                   IF  AMNEML  >  ZERO                                          
                       MOVE AMNEMI       TO                                     
                                         DP010-SCREEN-MNEMONIC                  
                                         PV-MNEMONIC                            
                                         DP010-LAST-MNEMONIC                    
                   END-IF                                                       
                                                                                
                   IF  ACOMLENL  >  ZERO                                        
                       MOVE ACOMLENI     TO                                     
                                         DP010-SCREEN-SAVE-COMM-LENGTH          
                   ELSE                                                         
                       IF  ACOMLENF  =  DP015-ERASE-EOF                         
                           MOVE ZEROS    TO                                     
                                         DP010-SCREEN-SAVE-COMM-LENGTH          
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  ASAVCOML  >  ZERO                                        
                       MOVE ASAVCOMI     TO                                     
                                         DP010-SCREEN-SAVE-COMM-IND             
                   ELSE                                                         
                       IF  ASAVCOMF  =  DP015-ERASE-EOF                         
                           MOVE SPACES   TO                                     
                                         DP010-SCREEN-SAVE-COMM-IND             
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  AFORMATL  >  ZERO                                        
                       MOVE AFORMATI     TO                                     
                                         DP010-SCREEN-COMMAREA-FORMAT           
                   ELSE                                                         
                       IF  AFORMATF  =  DP015-ERASE-EOF                         
                           MOVE SPACES   TO                                     
                                         DP010-SCREEN-COMMAREA-FORMAT           
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  AAVAILL  >  ZERO                                         
                       MOVE AAVAILI      TO                                     
                                         DP010-SCREEN-AVAILABIL-IND             
                   ELSE                                                         
                       IF  AAVAILF  =  DP015-ERASE-EOF                          
                           MOVE SPACES   TO                                     
                                         DP010-SCREEN-AVAILABIL-IND             
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  ATRNTLEL  >  ZERO                                        
                       MOVE ATRNTLEI     TO                                     
                                         DP010-SCREEN-MNEMONIC-TITLE            
                   ELSE                                                         
                       IF  ATRNTLEF  =  DP015-ERASE-EOF                         
                           MOVE SPACES   TO                                     
                                         DP010-SCREEN-MNEMONIC-TITLE            
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  ATRANIDL  >  ZERO                                        
                       MOVE ATRANIDI     TO                                     
                                         DP010-SCREEN-TRAN-ID                   
                   ELSE                                                         
                       IF  ATRANIDF  =  DP015-ERASE-EOF                         
                           MOVE SPACES   TO                                     
                                         DP010-SCREEN-TRAN-ID                   
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  AGLBACCL  >  ZERO                                        
                       MOVE AGLBACCI     TO                                     
                                         DP010-SCREEN-GLOBAL-ACCESS             
                   ELSE                                                         
                       IF  AGLBACCF  =  DP015-ERASE-EOF                         
                           MOVE SPACES   TO                                     
                                         DP010-SCREEN-GLOBAL-ACCESS             
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  ASCRTYPL  >  ZERO                                        
                       MOVE ASCRTYPI     TO                                     
                                         DP010-SCREEN-TYPE-CODE                 
                   ELSE                                                         
                       IF  ASCRTYPF  =  DP015-ERASE-EOF                         
                           MOVE SPACES   TO                                     
                                         DP010-SCREEN-TYPE-CODE                 
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  APKGL     >  ZERO                                        
                       MOVE APKGI        TO                                     
                                         DP010-SCREEN-DB2-PKG-CDE               
                   ELSE                                                         
                       IF  APKGF     =  DP015-ERASE-EOF                         
                           MOVE SPACES   TO                                     
                                         DP010-SCREEN-DB2-PKG-CDE               
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  ACMNTL    >  ZERO                                        
                       MOVE ACMNTI       TO                                     
                                         DP010-SCREEN-CMNT-TXT                  
                   ELSE                                                         
                       IF  ACMNTF    =  DP015-ERASE-EOF                         
                           MOVE SPACES   TO                                     
                                         DP010-SCREEN-CMNT-TXT                  
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  ALOCL  >  ZERO                                           
                       MOVE ALOCI     TO                                        
                                         DP010-SCREEN-ACRT-LOC                  
                   ELSE                                                         
                       IF  ALOCF  =  DP015-ERASE-EOF                            
                           MOVE SPACES   TO                                     
                                         DP010-SCREEN-ACRT-LOC                  
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  APROGL    >  ZERO                                        
                       MOVE APROGI       TO                                     
                                         DP010-SCREEN-PROGRAM                   
                   ELSE                                                         
                       IF  APROGF    =  DP015-ERASE-EOF                         
                           MOVE SPACES   TO                                     
                                         DP010-SCREEN-PROGRAM                   
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A800-SETUP-INTER-APPL                                          *        
      * (Z019-16 SETUP INTER-APPL)                                     *        
      *                                                                *        
      * WHENEVER THIS TRANSACTION IS ABOUT TO NAVIGATE TO ANOTHER      *        
      * TRANSACTION, THIS MODULE WILL FORMAT THE INTER APPLICATION     *        
      * COMMAREA WITH THE DATA SAVED FOR THE CURRENT OBJECT BEING PRO- *        
      * CESSED BY THIS TRANSACTION.                                    *        
      ******************************************************************        
                                                                                
       A800-SETUP-INTER-APPL.                                                   
                                                                                
           MOVE INTER-APPL-COMM-AREA                                            
                                    TO DP010-SAVE-INTER-APPL-COMMAREA.          
           MOVE DP010-LAST-MNEMONIC TO DP055-MNEMONIC.                          
      *    MOVE DP010-DB2-PKG-CDE   TO DP055-DB2-PKG-CDE.                       
                                                                                
       EJECT                                                                    
      ******************************************************************        
      * A850-RESET-INITIAL-INTER-APPL                                  *        
      * (Z019-17 RESET INITIAL INTER-APPL)                             *        
      *                                                                *        
      * WHEN THIS MODULE IS ABOUT TO RETURN TO THE MODULE THAT INVOKED *        
      * IT, IT WILL RESET THE COMMAREA TO THE CONTENTS THAT WERE SENT  *        
      * TO IT.                                                         *        
      *                                                                *        
      ******************************************************************        
                                                                                
       A850-RESET-INITIAL-INTER-APPL.                                           
                                                                                
           MOVE DP010-SAVE-INTER-APPL-COMMAREA                                  
                                  TO  INTER-APPL-COMM-AREA.                     
                                                                                
           MOVE ZERO              TO  DP055-NUMBER-PROCESSED-ITEMS.             
      *    MOVE DP010-DB2-PKG-CDE TO  DP055-DB2-PKG-CDE.                        
                                                                                
       EJECT                                                                    
      *****************************************************                     
      * ROUTINE TO ADD ROUTING RECORDS TO TCICSRTE TABLE  *                     
      *****************************************************                     
                                                                                
       X600-PROCESS-ROUTE-RECORDS.                                              
                                                                                
      ******************************************************************        
      * AFTER SUCCESSFULLY ADDING THE MNEMONIC TO THE DATABASE, THIS   *        
      * MODULE ALSO ADDS THE SYSTEM WIDE FUNCTION KEYS (F3 - END, F5 - *        
      * REFRESH, PA2 - TOGGLE FUNCTION KEY LINES, CLEAR - RE-DISPLAY   *        
      * LAST SCREEN SENT TO TERMINAL, AND ENTER - EDIT SCREEN) TO THE  *        
      * TCICSRTE TABLE.                                                *        
      ******************************************************************        
                                                                                
           MOVE TMNEMONIC-MNEMONIC-CODE                                         
                                       TO TCICSROUTE-MNEMONIC-CODE.             
           MOVE TAIDKEY-AID-SEQUENCE-NMBR                                       
                                       TO TCICSROUTE-AID-SEQUENCE-NMBR.         
      *                                                                         
           IF  TAIDKEY-STNRD-ACTION-CODE = PC-GO-TO-KEY                         
                                        OR PC-LOCAL-GO-KEY                      
               MOVE TAIDKEY-MNEMONIC-CODE                                       
                                       TO TCICSROUTE-DEST-MNEMONIC-CODE         
           ELSE                                                                 
               MOVE TAIDKEY-MNEMONIC-CODE                                       
                                       TO TCICSROUTE-FUNC-KEY-LBL-NAME          
           END-IF.                                                              
      *                                                                         
           MOVE TAIDKEY-STNRD-ACTION-CODE                                       
                                       TO TCICSROUTE-ACTION-CODE.               
           MOVE SPACES                 TO TCICSROUTE-ROUTE-STACK-INDCT.         
                                                                                
           PERFORM X920-ADD-TCICSRTE-ROW.                                       
                                                                                
           PERFORM X930-FETCH-TAIDKEY.                                          
                                                                                
       EJECT                                                                    
      **********************************                                        
      * FETCH ROW FROM TABLE TMNEMONIC *                                        
      **********************************                                        
                                                                                
       X900-READ-TMNEMONIC-ROW.                                                 
                                                                                
             EXEC SQL                                                           
                 SELECT       MNEMONIC_CODE,                                    
                              TRANS_ID_CODE,                                    
                              AVAIL_INDCT,                                      
                              CMAREA_FRMT_CODE,                                 
                              SAVE_CMAREA_INDCT,                                
                              CMAREA_LNGTH_MSRMT,                               
                              PANEL_TITLE_NAME,                                 
                              LAST_UPDATE_TMESTP,                               
                              GLBL_ACCS_IND,                                    
                              PGM_NAME,                                         
                              SCRN_TYP_CDE,                                     
                              UPD_VSAM_IND,                                     
                              DB2_PKG_CDE,                                      
                              CMNT_TXT,                                         
                              ACRT_LOC_REQR_IND                                 
                       INTO   :TMNEMONIC-MNEMONIC-CODE,                         
                              :TMNEMONIC-TRANS-ID-CODE,                         
                              :TMNEMONIC-AVAIL-INDCT,                           
                              :TMNEMONIC-CMAREA-FRMT-CODE,                      
                              :TMNEMONIC-SAVE-CMAREA-INDCT,                     
                              :TMNEMONIC-CMAREA-LNGTH-MSRMT,                    
                              :TMNEMONIC-PANEL-TITLE-NAME,                      
                              :TMNEMONIC-LAST-UPDATE-TMESTP,                    
                              :TMNEMONIC-GLBL-ACCS-IND,                         
                              :TMNEMONIC-PGM-NAME,                              
                              :TMNEMONIC-SCRN-TYP-CDE,                          
                              :TMNEMONIC-UPD-VSAM-IND,                          
                              :TMNEMONIC-DB2-PKG-CDE,                           
                              :TMNEMONIC-CMNT-TXT,                              
                              :TMNEMONIC-ACRT-LOC-REQR-IND                      
                       FROM   TMNEMONIC                                         
                       WHERE  MNEMONIC_CODE  =                                  
                                      :TMNEMONIC-MNEMONIC-CODE                  
             END-EXEC.                                                          
                                                                                
      ***************************                                               
      * CHECK SQLCODE OF SELECT *                                               
      ***************************                                               
                                                                                
             MOVE SQLCODE              TO SQL-RETURN-CODE.                      
             IF  SQL-ROW-NOT-FOUND                                              
                 CONTINUE                                                       
             ELSE                                                               
             IF  NOT SQL-SUCCESSFUL                                             
                 SET  DP013-DB2-ABEND  TO TRUE                                  
                 MOVE 'X900-READ-TMNEMONIC-ROW       '                          
                                       TO DP013-PARAGRAPH                       
                 MOVE SQLCA            TO DP013-SQLCA                           
                 MOVE 'TMNMNC            '                                      
                                       TO DP013-DB2-TABLE-NAME (1)              
                 PERFORM DP013-0000-PROCESS-ABEND                               
             END-IF                                                             
             END-IF.                                                            
       EJECT                                                                    
      ******************************                                            
      * ADD ROW TO TABLE TMNEMONIC *                                            
      ******************************                                            
                                                                                
       X910-ADD-TMNEMONIC-ROW.                                                  
                                                                                
             EXEC SQL                                                           
                 INSERT INTO  TMNEMONIC                                         
                       (MNEMONIC_CODE,                                          
                        TRANS_ID_CODE,                                          
                        AVAIL_INDCT,                                            
                        CMAREA_FRMT_CODE,                                       
                        SAVE_CMAREA_INDCT,                                      
                        CMAREA_LNGTH_MSRMT,                                     
                        PANEL_TITLE_NAME,                                       
                        LAST_UPDATE_TMESTP,                                     
                        GLBL_ACCS_IND,                                          
                        PGM_NAME,                                               
                        SCRN_TYP_CDE,                                           
                        UPD_VSAM_IND,                                           
                        DB2_PKG_CDE,                                            
                        CMNT_TXT,                                               
                        ACRT_LOC_REQR_IND)                                      
                   VALUES                                                       
                       (:TMNEMONIC-MNEMONIC-CODE,                               
                        :TMNEMONIC-TRANS-ID-CODE,                               
                        :TMNEMONIC-AVAIL-INDCT,                                 
                        :TMNEMONIC-CMAREA-FRMT-CODE,                            
                        :TMNEMONIC-SAVE-CMAREA-INDCT,                           
                        :TMNEMONIC-CMAREA-LNGTH-MSRMT,                          
                        :TMNEMONIC-PANEL-TITLE-NAME,                            
                        CURRENT TIMESTAMP,                                      
                        :TMNEMONIC-GLBL-ACCS-IND,                               
                        :TMNEMONIC-PGM-NAME,                                    
                        :TMNEMONIC-SCRN-TYP-CDE,                                
                        :TMNEMONIC-UPD-VSAM-IND,                                
                        :TMNEMONIC-DB2-PKG-CDE,                                 
                        :TMNEMONIC-CMNT-TXT,                                    
                        :TMNEMONIC-ACRT-LOC-REQR-IND)                           
             END-EXEC.                                                          
                                                                                
      ************************                                                  
      * CHECK SQLCODE OF ADD *                                                  
      ************************                                                  
                                                                                
             MOVE SQLCODE              TO SQL-RETURN-CODE.                      
             IF  (  SQL-ROW-FOUND    OR   SQL-DUPLICATE-ROW  )                  
                 CONTINUE                                                       
             ELSE                                                               
                 IF  NOT SQL-SUCCESSFUL                                         
                     SET  DP013-DB2-ABEND  TO TRUE                              
                     MOVE 'X910-ADD-TMNEMONIC-ROW        '                      
                                           TO DP013-PARAGRAPH                   
                     MOVE SQLCA            TO DP013-SQLCA                       
                     MOVE 'TMNMNC            '                                  
                                           TO DP013-DB2-TABLE-NAME (1)          
                     PERFORM DP013-0000-PROCESS-ABEND                           
                 END-IF                                                         
             END-IF.                                                            
       EJECT                                                                    
      *******************************                                           
      * ADD ROW TO TCICSRTE TABLE   *                                           
      *******************************                                           
                                                                                
       X920-ADD-TCICSRTE-ROW.                                                   
                                                                                
             EXEC SQL                                                           
                 INSERT INTO  TCICSROUTE                                        
                       (MNEMONIC_CODE,                                          
                        AID_SEQUENCE_NMBR,                                      
                        DEST_MNEMONIC_CODE,                                     
                        ACTION_CODE,                                            
                        LAST_UPDATE_TMESTP,                                     
                        FUNC_KEY_LBL_NAME,                                      
                        ROUTE_STACK_INDCT)                                      
                   VALUES                                                       
                       (:TCICSROUTE-MNEMONIC-CODE,                              
                        :TCICSROUTE-AID-SEQUENCE-NMBR,                          
                        :TCICSROUTE-DEST-MNEMONIC-CODE,                         
                        :TCICSROUTE-ACTION-CODE,                                
                        CURRENT TIMESTAMP,                                      
                        :TCICSROUTE-FUNC-KEY-LBL-NAME,                          
                        :TCICSROUTE-ROUTE-STACK-INDCT)                          
             END-EXEC.                                                          
                                                                                
             INITIALIZE DCLTCICSROUTE.                                          
                                                                                
      ************************                                                  
      * CHECK SQLCODE OF ADD *                                                  
      ************************                                                  
                                                                                
             MOVE SQLCODE              TO SQL-RETURN-CODE.                      
             IF  NOT SQL-SUCCESSFUL                                             
                 SET  DP013-DB2-ABEND  TO TRUE                                  
                 MOVE 'X920-ADD-TCICSRTE-ROW         '                          
                                       TO DP013-PARAGRAPH                       
                 MOVE SQLCA            TO DP013-SQLCA                           
                 MOVE 'TCICSRTE          '                                      
                                       TO DP013-DB2-TABLE-NAME (1)              
                 MOVE TCICSROUTE-MNEMONIC-CODE                                  
                                       TO DP013-MESSAGE-TEXT (1)                
                 MOVE TCICSRTE-FOUND-MSG                                        
                                       TO DP013-MESSAGE-TEXT (2)                
                 PERFORM DP013-0000-PROCESS-ABEND                               
             END-IF.                                                            
                                                                                
       EJECT                                                                    
      *******************************                                           
      * FETCH ROW FROM AIDKEY CURSOR*                                           
      *******************************                                           
                                                                                
       X930-FETCH-TAIDKEY.                                                      
                                                                                
             EXEC SQL                                                           
                  FETCH AIDKEY-CURSOR                                           
                  INTO                                                          
                       :TAIDKEY-AID-SEQUENCE-NMBR                               
                     , :TAIDKEY-AID-KEY-NAME                                    
                     , :TAIDKEY-STNRD-ACTION-CODE                               
                     , :TAIDKEY-AID-CHARACTER-CODE                              
                     , :TAIDKEY-MNEMONIC-CODE                                   
             END-EXEC.                                                          
                                                                                
             IF (SQLCODE EQUAL ZERO                                             
             OR  SQLCODE EQUAL +100)                                            
                 CONTINUE                                                       
             ELSE                                                               
                 SET  DP013-DB2-ABEND  TO TRUE                                  
                 MOVE 'X930-FETCH-TAIDKEY            '                          
                                       TO DP013-PARAGRAPH                       
                 MOVE SQLCA            TO DP013-SQLCA                           
                 MOVE 'TAIDKEY           '                                      
                                       TO DP013-DB2-TABLE-NAME (1)              
                 MOVE TCICSROUTE-MNEMONIC-CODE                                  
                                       TO DP013-MESSAGE-TEXT (1)                
                 MOVE 'FETCH TAIDKEY CURSOR'                                    
                                       TO DP013-MESSAGE-TEXT (2)                
                 PERFORM DP013-0000-PROCESS-ABEND                               
             END-IF.                                                            
                                                                                
       EJECT                                                                    
      *******************************                                           
      * OPEN THE AIDKEY CURSOR      *                                           
      *******************************                                           
                                                                                
       X940-OPEN-TAIDKEY.                                                       
                                                                                
             EXEC SQL                                                           
                  OPEN AIDKEY-CURSOR                                            
             END-EXEC.                                                          
                                                                                
             IF (SQLCODE EQUAL ZERO                                             
             OR  SQLCODE EQUAL +100)                                            
                 CONTINUE                                                       
             ELSE                                                               
                 SET  DP013-DB2-ABEND  TO TRUE                                  
                 MOVE 'X940-FETCH-TAIDKEY            '                          
                                       TO DP013-PARAGRAPH                       
                 MOVE SQLCA            TO DP013-SQLCA                           
                 MOVE 'TAIDKEY           '                                      
                                       TO DP013-DB2-TABLE-NAME (1)              
                 MOVE TCICSROUTE-MNEMONIC-CODE                                  
                                       TO DP013-MESSAGE-TEXT (1)                
                 MOVE 'OPEN TAIDKEY CURSOR'                                     
                                       TO DP013-MESSAGE-TEXT (2)                
                 PERFORM DP013-0000-PROCESS-ABEND                               
             END-IF.                                                            
       EJECT                                                                    
      **************************************                                    
      * COPY DP002-0000-FORMAT-TITLE-LINES *                                    
      **************************************                                    
           COPY DPPD022.                                                        
       EJECT                                                                    
      **********************************                                        
      * COPY DP003-0000-RETURN-TO-CICS *                                        
      **********************************                                        
           COPY DPPD003.                                                        
       EJECT                                                                    
      ************************************                                      
      * COPY DP005-0000-FORMAT-MSG-LINES *                                      
      ************************************                                      
           COPY DPPD005.                                                        
       EJECT                                                                    
      *****************************                                             
      * COPY DP007-0000-GET-PANEL *                                             
      *****************************                                             
           COPY DPPD007.                                                        
       EJECT                                                                    
      *******************************                                           
      * COPY DP008-0000-WRITE-PANEL *                                           
      *******************************                                           
           COPY DPPD008.                                                        
       EJECT                                                                    
      *********************************                                         
      * COPY DP009-0000-LOAD-FK-LINES *                                         
      *********************************                                         
           COPY DPPD009.                                                        
       EJECT                                                                    
      *********************************                                         
      * COPY DP013-0000-PROCESS-ABEND *                                         
      *********************************                                         
           COPY DPPD013.                                                        
       EJECT                                                                    
      **********************************                                        
      * COPY DP015-0000-CHECK-COMMAREA *                                        
      **********************************                                        
           COPY DPPD015.                                                        
       EJECT                                                                    
      **************************************                                    
      * COPY DP017-0000-GET-CURR-DATE-TIME *                                    
      **************************************                                    
           COPY DPPD017.                                                        
       EJECT                                                                    
      *********************************                                         
      * COPY DP018-0000-GET-CURR-TIME *                                         
      *********************************                                         
           COPY DPPD018.                                                        
