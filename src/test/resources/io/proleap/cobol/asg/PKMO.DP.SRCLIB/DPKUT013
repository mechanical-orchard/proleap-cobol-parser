00001 *                                                                 07/24/91
00002  IDENTIFICATION DIVISION.                                         DPKUT013
00003 *                                                                    LV007
00004  PROGRAM-ID.    DPKUT013.                                            CL**4
00005 *                                                                 DPKUT013
00006  AUTHOR.        CALVIN BUSH.                                      DPKUT013
00007  INSTALLATION.  KOHLS DEPARTMENT STORES.                          DPKUT013
00008  DATE-WRITTEN.  07/11/90.                                         DPKUT013
00009  DATE-COMPILED.                                                   DPKUT013
00010 *                                                                 DPKUT013
00011 ******************************************************************   CL**6
00012 **  THIS PROGRAM CREATES THE SEQUENTIAL FILE USED TO LOAD THE   **   CL**6
00013 **  'MESSAGES' ONLINE VSAM FILE WHICH IS USED BY CICS PROGRAMS. **   CL**6
00014 ******************************************************************   CL**6
00015      EJECT                                                        DPKUT013
00016  ENVIRONMENT DIVISION.                                            DPKUT013
00017 *                                                                 DPKUT013
00018  CONFIGURATION SECTION.                                           DPKUT013
00019 *                                                                 DPKUT013
00020  SOURCE-COMPUTER.  IBM-3090.                                      DPKUT013
00021  OBJECT-COMPUTER.  IBM-3090.                                      DPKUT013
00022 *                                                                 DPKUT013
00023  INPUT-OUTPUT SECTION.                                            DPKUT013
00024 *                                                                 DPKUT013
00025  FILE-CONTROL.                                                    DPKUT013
00026      SELECT OUTPUT-MESSAGES-FILE      ASSIGN TO OUT01.            DPKUT013
00027      EJECT                                                        DPKUT013
00028  DATA DIVISION.                                                   DPKUT013
00029 *                                                                 DPKUT013
00030  FILE SECTION.                                                    DPKUT013
00031 *                                                                 DPKUT013
00032  FD  OUTPUT-MESSAGES-FILE                                         DPKUT013
00033      RECORDING MODE IS F                                          DPKUT013
00034      LABEL RECORDS ARE STANDARD                                   DPKUT013
00035      RECORD CONTAINS 75 CHARACTERS                                DPKUT013
00036      BLOCK CONTAINS 0 RECORDS.                                    DPKUT013
00037  01  OUTPUT-MESSAGES-RECORD           PIC  X(75).                 DPKUT013
00038      EJECT                                                        DPKUT013
00039  WORKING-STORAGE SECTION.                                         DPKUT013
00040  01  PV-PROGRAM-VARIABLES.                                        DPKUT013
00041      05  PV-PARAGRAPH-NAME            PIC  X(30)  VALUE SPACE.    DPKUT013
00042      05  PV-DB2-OPERATION             PIC  X(70)  VALUE SPACE.    DPKUT013
00043 *                                                                 DPKUT013
00044  01  ABEND-CODE                    PIC S9(04)  VALUE +0           DPKUT013
00045                                                COMP               DPKUT013
00046                                                SYNC.              DPKUT013
00047 *                                                                 DPKUT013
00048 **   PARAMETER LIST USED TO CALL DB2 ABEND ROUTINE DSNTIAR        DPKUT013
00049 *                                                                 DPKUT013
00050      COPY DPWS004.                                                DPKUT013
00051 *                                                                 DPKUT013
00052 **   OUTPUT RECORD LAYOUT FOR MESSAGES FILE                       DPKUT013
00053 *                                                                 DPKUT013
00054      COPY DPRD507O.                                                  CL**7
00055 *                                                                 DPKUT013
00056 **   DCLGEN FOR TABLE TSYMSG                                         CL**5
00057 *                                                                 DPKUT013
00058      EXEC SQL                                                     DPKUT013
00059          INCLUDE TSYMSG                                              CL**5
00060      END-EXEC.                                                    DPKUT013
00061 *                                                                 DPKUT013
00062 **   THE DB2 COMMUNICATIONS AREA                                  DPKUT013
00063 *                                                                 DPKUT013
00064      EXEC SQL                                                     DPKUT013
00065          INCLUDE SQLCA                                            DPKUT013
00066      END-EXEC.                                                    DPKUT013
00067 *                                                                 DPKUT013
00068      EXEC SQL                                                     DPKUT013
00069          DECLARE MESSAGES_CSR CURSOR FOR                          DPKUT013
00070          SELECT TSYMSG.SYMSG_NBR,                                    CL**5
00071                 TSYMSG.SHT_TXT                                       CL**5
00072          FROM TSYMSG                                                 CL**5
00073          ORDER BY TSYMSG.SYMSG_NBR                                   CL**5
00074      END-EXEC.                                                    DPKUT013
00075 *                                                                 DPKUT013
00076      EJECT                                                        DPKUT013
00077  PROCEDURE DIVISION.                                              DPKUT013
00078 ***************************************************************** DPKUT013
00079 **  THIS MODULE CONTROLS ALL PROCESSING DONE IN THIS PROGRAM.     DPKUT013
00080 ***************************************************************** DPKUT013
00081  A000-MAIN-DRIVER.                                                DPKUT013
00082      PERFORM B100-INITIALIZATION.                                 DPKUT013
00083      PERFORM B200-BUILD-MESSAGE-RECORDS                           DPKUT013
00084          UNTIL SQLCODE = +100.                                    DPKUT013
00085      PERFORM B300-TERMINATION.                                    DPKUT013
00086      GOBACK.                                                      DPKUT013
00087 ***************************************************************** DPKUT013
00088 **  THIS MODULE OPENS THE OUTPUT FILE AND THE CURSOR FOR          DPKUT013
00089 **  DB2 TABLE TSYMSG.    IT ALSO DOES THE INITIAL FETCH ON THE       CL**5
00090 **  TSYMSG TABLE CURSOR.                                             CL**5
00091 ***************************************************************** DPKUT013
00092  B100-INITIALIZATION.                                             DPKUT013
00093      OPEN OUTPUT OUTPUT-MESSAGES-FILE.                            DPKUT013
00094      INITIALIZE DCLTSYMSG.                                           CL**5
00095      EXEC SQL                                                     DPKUT013
00096          OPEN MESSAGES_CSR                                        DPKUT013
00097      END-EXEC.                                                    DPKUT013
00098      IF  SQLCODE = 0                                              DPKUT013
00099          CONTINUE                                                 DPKUT013
00100      ELSE                                                         DPKUT013
00101          MOVE 'B100-INITIALIZATION'  TO PV-PARAGRAPH-NAME         DPKUT013
00102          MOVE 'OPEN MESSAGES CURSOR' TO PV-DB2-OPERATION          DPKUT013
00103          PERFORM U900-SQL-ABEND                                   DPKUT013
00104      END-IF.                                                      DPKUT013
00105      PERFORM C100-FETCH-MESSAGES-CURSOR.                          DPKUT013
00106 ****************************************************************  DPKUT013
00107 **  THIS PARAGRAPH FILLS THE OUTPUT RECORD LAYOUT, WRITES THE     DPKUT013
00108 **  RECORD AND THEN FETCHES THE NEXT ROW FROM THE MESSAGES           CL**3
00109 **  CURSOR.                                                       DPKUT013
00110 ****************************************************************  DPKUT013
00111  B200-BUILD-MESSAGE-RECORDS.                                      DPKUT013
00112      INITIALIZE DP507O-MESSAGES-OUTPUT-RECORD.                    DPKUT013
00113      MOVE SYMSG-SYMSG-NBR         TO DP507O-MSG-NUMBER.              CL**5
00114      MOVE SYMSG-SHT-TXT           TO DP507O-MSG-TEXT.                CL**5
00115      PERFORM U800-WRITE-MESSAGES-RECORD.                          DPKUT013
00116      PERFORM C100-FETCH-MESSAGES-CURSOR.                          DPKUT013
00117 *                                                                 DPKUT013
00118 *                                                                 DPKUT013
00119  B300-TERMINATION.                                                DPKUT013
00120      CLOSE OUTPUT-MESSAGES-FILE.                                  DPKUT013
00121      EXEC SQL                                                     DPKUT013
00122         CLOSE MESSAGES_CSR                                        DPKUT013
00123      END-EXEC.                                                    DPKUT013
00124 *                                                                 DPKUT013
00125      IF  SQLCODE = 0                                              DPKUT013
00126          CONTINUE                                                 DPKUT013
00127      ELSE                                                         DPKUT013
00128          MOVE 'B300-TERMINATION'      TO PV-PARAGRAPH-NAME        DPKUT013
00129          MOVE 'CLOSE MESSAGES CURSOR' TO PV-DB2-OPERATION            CL**3
00130          PERFORM U900-SQL-ABEND                                   DPKUT013
00131      END-IF.                                                      DPKUT013
00132 *                                                                 DPKUT013
00133 *                                                                 DPKUT013
00134  C100-FETCH-MESSAGES-CURSOR.                                      DPKUT013
00135      EXEC SQL                                                     DPKUT013
00136          FETCH MESSAGES_CSR                                       DPKUT013
00137          INTO  :SYMSG-SYMSG-NBR,                                     CL**5
00138                :SYMSG-SHT-TXT                                        CL**5
00139      END-EXEC.                                                    DPKUT013
00140      IF  (SQLCODE = 0                                             DPKUT013
00141                  OR +100)                                         DPKUT013
00142          CONTINUE                                                 DPKUT013
00143      ELSE                                                         DPKUT013
00144          MOVE 'C100-FETCH-MESSAGES-CURSOR' TO PV-PARAGRAPH-NAME   DPKUT013
00145          MOVE 'FETCH MESSAGES CURSOR'      TO PV-DB2-OPERATION    DPKUT013
00146          PERFORM U900-SQL-ABEND                                   DPKUT013
00147      END-IF.                                                      DPKUT013
00148 ****************************************************************  DPKUT013
00149 **  THIS PARAGRAPH WRITES THE OUTPUT CONVERSION RECORDS.          DPKUT013
00150 ****************************************************************  DPKUT013
00151  U800-WRITE-MESSAGES-RECORD.                                      DPKUT013
00152      WRITE OUTPUT-MESSAGES-RECORD                                 DPKUT013
00153          FROM DP507O-MESSAGES-OUTPUT-RECORD.                         CL**3
00154 ******************************************************************DPKUT013
00155 * U900-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE      DPKUT013
00156 * EVENT THAT AN SQL ERROR CODE OCCURS.                            DPKUT013
00157 ******************************************************************DPKUT013
00158  U900-SQL-ABEND.                                                  DPKUT013
00159      DISPLAY '** ABEND **         **'.                            DPKUT013
00160      DISPLAY '** PROGRAM          ** = DPKUT013'.                    CL**4
00161      DISPLAY '** PARAGRAPH NAME   ** = ' PV-PARAGRAPH-NAME.       DPKUT013
00162      DISPLAY '** DB2 OPERATION    ** = ' PV-DB2-OPERATION.        DPKUT013
00163                                                                   DPKUT013
00164      COPY DPPD004.                                                DPKUT013
00165                                                                   DPKUT013
00166      EXEC SQL                                                     DPKUT013
00167          COMMIT                                                   DPKUT013
00168      END-EXEC.                                                    DPKUT013
00169                                                                   DPKUT013
00170      MOVE +4013 TO ABEND-CODE.                                    DPKUT013
00171      CALL 'ILBOABN0' USING ABEND-CODE.                            DPKUT013
00172                                                                   DPKUT013
