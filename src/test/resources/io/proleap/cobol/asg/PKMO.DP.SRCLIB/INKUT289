       IDENTIFICATION DIVISION.                                         00010037
       PROGRAM-ID.    INKUT289.                                         00020027
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00030001
       DATE-WRITTEN.  08-22-2004.                                       00040042
       DATE-COMPILED.                                                   00050001
                                                                        00060006
      ******************************************************************00070001
      *     INKUT289 - THIS PROGRAM HAS BEEN RE-WRITTEN TO PROCESS      00080042
      *                THE MERGED FILE BETWEEN THE PHYSICAL (TSUMINV)   00090042
      *                EXTRACT AND THE CTOFF DATABASE (INT_INV_CTOFF)   00100042
      *                EXTRACT IN THE INRD024 FORMAT AND STRIP OFF THE  00110067
      *                TOP 00100 CANDIDATES. CHANGE INCORPORATES        00120067
      *                CHANGES FOR STORE NUMBER EXPANSION (WR26600)     00130042
      *                PREVIOUS 'CHANGE HISTORY' HAS BEEN DELETED       00140042
      *                                                                 00150042
      *                CONTROL CARD (INP02) DETERMINES NUMBER OF        00160043
      *                EXCEPTIONS PER STORE WILL BE ON OUTPUT FILE      00170043
      *                                                                 00180043
      *CHANGE HISTORY:                                                  00190003
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                     00200003
      * -------  ----------  ----------------------------------------   00210003
W26600* W26600   08-22-2004  PROCESS RE-WRITE/INVENTORY STORE EXPANSION 00220042
W26600*                      - SCOTT JACKSON                            00230042
SR3403* WR03403  04-06-2012  RECOMPILE-LARGE RETAIL PROJECT             00231071
SR3403*                     - ANUJ GUPTA                                00232071
RONNA *          08-10-2021  FIX LOGIC WHEN READING A MERGE FILE        00233072
RONNA *                      AND IT IS THE END OF FILE AND THE MAX      00234072
RONNA *                      NUMBER OF EXCEPTIONS HAS NOT BEEN REACHED  00235072
      ******************************************************************00240001
                                                                        00250006
       ENVIRONMENT DIVISION.                                            00260001
       CONFIGURATION SECTION.                                           00270001
       SOURCE-COMPUTER.        IBM-3090.                                00280001
       OBJECT-COMPUTER.        IBM-3090.                                00290001
       INPUT-OUTPUT SECTION.                                            00300001
       FILE-CONTROL.                                                    00310001
                                                                        00320001
           SELECT STORE-MERGE-FILE-IN                                   00330015
               ASSIGN TO INP01.                                         00340001
                                                                        00350001
           SELECT CONTROL-FILE-IN                                       00360042
               ASSIGN TO INP02.                                         00370027
                                                                        00380001
           SELECT STORE-REPORT-FILE-OUT                                 00390048
               ASSIGN TO OUT01.                                         00400027
                                                                        00410027
       DATA DIVISION.                                                   00420001
       FILE SECTION.                                                    00430001
                                                                        00440001
       FD  STORE-MERGE-FILE-IN                                          00450015
           LABEL RECORDS ARE OMITTED                                    00460001
           RECORDING MODE IS F                                          00470001
           BLOCK CONTAINS 0 RECORDS.                                    00480001
       01  STORE-MERGE-REC-IN    PIC X(100).                            00490042
                                                                        00500001
       FD  CONTROL-FILE-IN                                              00510042
           LABEL RECORDS ARE OMITTED                                    00520000
           RECORDING MODE IS F                                          00530000
           BLOCK CONTAINS 0 RECORDS.                                    00540000
       01  CONTROL-REC-IN        PIC X(80).                             00550042
                                                                        00560001
       FD  STORE-REPORT-FILE-OUT                                        00570048
           LABEL RECORDS ARE OMITTED                                    00580027
           RECORDING MODE IS F                                          00590027
           BLOCK CONTAINS 0 RECORDS.                                    00600027
       01  STORE-REPORT-REC-OUT   PIC X(100).                           00610048
                                                                        00620027
      ******************************************************************00630001
       WORKING-STORAGE SECTION.                                         00640001
      ******************************************************************00650001
                                                                        00660001
       01  PC-PROGRAM-CONSTANTS.                                        00670001
           05  PC-PROGRAM-NAME         PIC X(20)  VALUE 'INKUT289'.     00680027
                                                                        00690001
       01  CC-CONTROL-REC.                                              00700046
           05  CC-CONTROL-COUNT        PIC 9(05)  VALUE ZERO.           00710046
           05  FILLER                  PIC X(75)  VALUE SPACES.         00720046
                                                                        00730046
       01  PV-PROGRAM-VARIABLES.                                        00740001
W26600     05  PV-MAX-PER-STORE-COUNT  PIC  9(05) VALUE ZERO.           00750047
           05  PV-STORE-INV-READ       PIC S9(09) VALUE +0  COMP.       00760046
           05  PV-LOCATION-COUNT       PIC S9(09) VALUE +0  COMP.       00770048
           05  PV-TOTAL-EXTR-WRITTEN   PIC S9(09) VALUE +0  COMP.       00780012
           05  PV-STORE-NUMBER         PIC  9(04) VALUE ZEROS.          00790048
           05  PV-HOLD-LOCATION        PIC  9(04) VALUE ZEROS.          00800048
                                                                        00810001
       01  PS-PROGRAM-SWITCHES.                                         00820001
           05  PS-STORE-INV-DONE-SW    PIC  X(01)      VALUE 'N'.       00830062
               88  PS-STORE-INV-IS-DONE                VALUE 'Y'.       00840001
               88  PS-STORE-INV-IS-NOT-DONE            VALUE 'N'.       00850001
           05  PS-FIRST-TIME-SW        PIC  X(01)      VALUE 'N'.       00860015
               88  FIRST-TIME-THRU                     VALUE 'N'.       00870015
               88  CONTINUED-PROCESSING                VALUE 'Y'.       00880015
           05  PS-CONTROL-FILE-SW      PIC  X(01)      VALUE 'N'.       00890046
               88  PS-CONTROL-FILE-EMPTY               VALUE 'Y'.       00900046
           05  PS-END-OF-LOCS-SW       PIC  X(01)      VALUE 'N'.       00910049
               88  PS-END-OF-LOCATION-RECS             VALUE 'Y'.       00920049
               88  PS-MORE-LOCATION-RECS               VALUE 'N'.       00930049
           05  PS-LOCATION-SW          PIC  X(01)      VALUE 'N'.       00940049
               88  PS-LOCATION-RECS-PROCESSED          VALUE 'Y'.       00950055
               88  PS-CONT-LOCATION                    VALUE 'N'.       00960049
                                                                        00970001
      /**************************************************************** 00980015
      * COPYBOOK - INVENTORY EXCEPTIONS BY STORE EXTRACT                00990015
      ***************************************************************** 01000001
           COPY INRD024.                                                01010015
                                                                        01020003
      ***************************************************************** 01030065
      *  INVENTORY EXCEPTIONS BY STORE EXTRACT FILE LAYOUT - WORK AREA  01040065
      ***TO BE USED AGAIN AS THE OUTPUT RECORD LAYOUT AS WELL AS INPUT  01050065
      ***************************************************************** 01060065
           COPY INRD024 REPLACING IN024 BY INWRK.                       01070065
                                                                        01080027
      /*****************************************************************01090006
      ******************************************************************01100006
       PROCEDURE DIVISION.                                              01110001
      ******************************************************************01120001
                                                                        01130001
       0100-MAINLINE-INKUT289.                                          01140027
                                                                        01150006
           PERFORM 1000-INITIALIZATION.                                 01160001
                                                                        01170001
           PERFORM 2000-PROCESS-STORE-MERGE-RECS                        01180015
               UNTIL PS-STORE-INV-IS-DONE                               01190046
                  OR PS-CONTROL-FILE-EMPTY.                             01200046
                                                                        01210001
           PERFORM 8000-EOJ.                                            01220001
                                                                        01230001
       9999-EXIT-PROGRAM.                                               01240006
           STOP RUN.                                                    01250013
                                                                        01260001
      /*****************************************************************01270006
      * 1000-INITIALIZATION.                                            01280015
      ******************************************************************01290001
       1000-INITIALIZATION.                                             01300001
                                                                        01310006
           DISPLAY '******************************************'.        01320027
           DISPLAY '*  START OF INKUT289                     *'.        01330027
           DISPLAY '******************************************'.        01340027
                                                                        01350027
           OPEN  INPUT STORE-MERGE-FILE-IN                              01360027
                 INPUT CONTROL-FILE-IN                                  01370045
                OUTPUT STORE-REPORT-FILE-OUT.                           01380049
                                                                        01390001
           PERFORM 4000-READ-MERGE-FILE.                                01400015
           IF PS-STORE-INV-IS-DONE                                      01410027
               DISPLAY '*************************************'          01420027
               DISPLAY '**  PROGRAM = INKUT289'                         01430027
               DISPLAY '**  PARAGRAPH = 1000-INITIALIZTION'             01440027
               DISPLAY '**'                                             01450027
               DISPLAY '**  NO EXCEPT RECORD TO PROCESS'                01460027
               DISPLAY '**  INP01'                                      01470027
               DISPLAY '*************************************'          01480027
           END-IF.                                                      01490027
                                                                        01500027
           PERFORM 4100-READ-CONTROL-FILE.                              01510046
           IF PS-CONTROL-FILE-EMPTY                                     01520046
               DISPLAY '*************************************'          01530046
               DISPLAY '**  PROGRAM = INKUT289'                         01540046
               DISPLAY '**  PARAGRAPH = 1000-INITIALIZTION'             01550046
               DISPLAY '**'                                             01560046
               DISPLAY '**  NO CONTROL RECORD COUNT WAS FOUND'          01570046
               DISPLAY '**  INP02'                                      01580046
               DISPLAY '*************************************'          01590046
           END-IF.                                                      01600046
                                                                        01610027
      /*****************************************************************01620006
      * 2000-PROCESS-STORE-MERGE-RECS.                                  01630015
      *   EACH LOCATION IS PROCESSED UNTIL ##### RECORDS ARE WRITTEN    01640051
      ******************************************************************01650001
       2000-PROCESS-STORE-MERGE-RECS.                                   01660015
                                                                        01670006
           MOVE IN024-STORE-NUMBER   TO PV-STORE-NUMBER                 01680048
                                        PV-HOLD-LOCATION                01690048
           MOVE +0                   TO PV-LOCATION-COUNT               01700053
                                                                        01710048
           SET PS-CONT-LOCATION      TO TRUE.                           01720068
                                                                        01730068
           PERFORM 2100-PROCESS-LOCATION                                01740066
RONNA          UNTIL PS-LOCATION-RECS-PROCESSED                         01750072
RONNA             OR PS-STORE-INV-IS-DONE.                              01750172
      *                                                                 01760055
      /*****************************************************************01770048
      * 2100-PROCESS-LOCATION.                                          01780066
      *   PROCESS EACH STORE FROM THE STORE-SORTED INPUT FILE.          01790048
      *   BASED ON THE INPUT CONTROL CARD, THE STORE WILL WRITE THE     01800048
      *    SPECIFIC NUMBER OF RECORDS PER STORE (UNLESS THE NUMBER OF   01810048
      *    RECORDS READ IS LESS THAN THE MAX - THEN END THERE)          01820048
RTL0  *   BYPASS RECORDS WHERE THE SYSTEM PRICE = $ 0.00                01830069
      ******************************************************************01840048
       2100-PROCESS-LOCATION.                                           01850066
                                                                        01860048
           IF PV-LOCATION-COUNT = PV-MAX-PER-STORE-COUNT                01870056
                                                                        01880061
               PERFORM 2300-COMPLETE-LOC-PROCESSING                     01890056
                                                                        01900060
               SET PS-LOCATION-RECS-PROCESSED TO TRUE                   01910062
                                                                        01920060
           ELSE                                                         01930060
                IF PV-STORE-NUMBER > PV-HOLD-LOCATION                   01940056
                                                                        01950048
                   MOVE 'Y' TO PS-LOCATION-SW                           01960066
               ELSE                                                     01970056
RTL0               IF IN024-SYSTEM-PRICE > 0                            01980069
                       MOVE IN024-INV-EXCEPTS-STR-REC  TO               01990069
                            INWRK-INV-EXCEPTS-STR-REC                   02000069
                                                                        02010069
                       PERFORM 5000-WRITE-EXTRACT-REC                   02020069
                                                                        02030069
                       PERFORM 4000-READ-MERGE-FILE                     02040069
RTL0               ELSE                                                 02050069
RTL0                   PERFORM 4000-READ-MERGE-FILE                     02060069
RTL0               END-IF                                               02070069
               END-IF                                                   02080069
           END-IF.                                                      02090056
                                                                        02100048
      /**************************************************************** 02110048
      * 2300-COMPLETE-LOC-PROCESSING                                    02120048
      *   PROCESS INPUT FILE UNTIL END-OF-FILE OR NEXT LOCATION FOUND   02130048
      ***************************************************************** 02140048
       2300-COMPLETE-LOC-PROCESSING.                                    02150048
                                                                        02160048
           IF PV-STORE-NUMBER > PV-HOLD-LOCATION                        02170063
               SET PS-LOCATION-RECS-PROCESSED TO TRUE                   02180062
           ELSE                                                         02190061
               PERFORM 4000-READ-MERGE-FILE                             02200061
                   UNTIL PV-STORE-NUMBER > PV-HOLD-LOCATION             02210062
                      OR PS-STORE-INV-IS-DONE                           02220062
           END-IF.                                                      02230055
                                                                        02240058
                                                                        02250058
                                                                        02260052
      /**************************************************************** 02270016
      * 4000-READ-MERGE-FILE.                                           02280045
      * INPUT FILE IS SORTED IN CORRECT ORDER.                          02290045
      ***************************************************************** 02300016
       4000-READ-MERGE-FILE.                                            02310016
                                                                        02320016
           READ STORE-MERGE-FILE-IN NEXT                                02330027
               INTO IN024-INV-EXCEPTS-STR-REC                           02340027
             AT END                                                     02350027
                 MOVE 'Y' TO PS-STORE-INV-DONE-SW                       02360062
             NOT AT END                                                 02370001
                 MOVE IN024-STORE-NUMBER TO PV-STORE-NUMBER             02380048
                 ADD +1   TO PV-STORE-INV-READ                          02390001
           END-READ.                                                    02400001
                                                                        02410001
      /**************************************************************** 02440027
      * 4100-READ THE CONTROL FILE DENOTING MAX RECORDS/LOC TO WRITE    02450046
      ***************************************************************** 02460027
       4100-READ-CONTROL-FILE.                                          02470046
                                                                        02480046
           READ CONTROL-FILE-IN                                         02490046
               INTO CC-CONTROL-REC                                      02500046
             AT END  MOVE 'Y' TO PS-CONTROL-FILE-SW                     02510046
             NOT AT END                                                 02520046
                 MOVE CC-CONTROL-COUNT TO PV-MAX-PER-STORE-COUNT        02530047
           END-READ.                                                    02540046
      *                                                                 02550045
      /*****************************************************************02560006
      * 5000-WRITE-EXTRACT-REC.                                         02570013
      * WRITE THE TOP EXCEPTION ITEMS. MAX RECORDS/LOC DEFINED BY INP02 02580045
      ******************************************************************02590001
       5000-WRITE-EXTRACT-REC.                                          02600001
                                                                        02610006
           WRITE STORE-REPORT-REC-OUT                                   02620049
               FROM INWRK-INV-EXCEPTS-STR-REC                           02630066
                                                                        02640001
           ADD +1 TO PV-TOTAL-EXTR-WRITTEN.                             02650001
           ADD +1 TO PV-LOCATION-COUNT.                                 02660048
                                                                        02670001
      /*****************************************************************02680009
      * 8000-EOJ.  FINAL PROCESSING FOR THE PROGRAM.                    02690013
      ******************************************************************02700001
       8000-EOJ.                                                        02710001
                                                                        02720006
           DISPLAY ' '.                                                 02730001
           DISPLAY '*** RECORD COUNT SUMMARY'.                          02740001
           DISPLAY 'MAXIMUM RECORDS CREATED PER LOC = '                 02750048
                 PV-MAX-PER-STORE-COUNT.                                02760048
           DISPLAY 'INVENTORY EXTRACT RECORDS READ  = '                 02770048
                 PV-STORE-INV-READ.                                     02780048
           DISPLAY 'INVENTORY EXTRACT RECORDS WRITTEN  = '              02790001
                 PV-TOTAL-EXTR-WRITTEN.                                 02800001
                                                                        02810001
           DISPLAY '******************************************'.        02820025
           DISPLAY '*  END OF INKUT289                       *'.        02830027
           DISPLAY '******************************************'.        02840025
                                                                        02850025
           CLOSE STORE-MERGE-FILE-IN                                    02860015
                 CONTROL-FILE-IN                                        02870047
                 STORE-REPORT-FILE-OUT.                                 02880050
                                                                        02890001
