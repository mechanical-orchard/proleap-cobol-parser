       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    TMKCS550.                                                 
       AUTHOR.        KAREN WALL                                                
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  JAN 2005.                                                 
       DATE-COMPILED.                                                           
      ******************************************************************        
      *    E550 - TROUBLE AUTOMATIC ACKNOWLEDGMENT AND RE-DISTRO'ING            
      *                                                                         
      *    THIS IS A CALLED ROUTINE FROM TMKCS503 AND TMKCS505 TO AUTO          
      *    RESOLVE, AUTO ACKNOWLEDGE ALL LINES OF THE PO/RECEIVER AND IF        
      *    APPLICABLE) REDISTRO AUTOMATICALLY-IF THE ENTIRE ALLOCATION          
      *    IS FOUND FOR THE PO/RECEIVER.                                        
      *                                                                         
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       DATA DIVISION.                                                           
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-A                         PIC X(01) VALUE 'A'.                
           05  PC-Y                         PIC X(01) VALUE 'Y'.                
           05  PC-SPACE                     PIC X(01) VALUE ' '.                
           05  PC-MAX-RETRIES               PIC S9(4) VALUE +3                  
                                                      COMP SYNC.                
           05  PC-CURRENT-PROGRAM-NAME      PIC X(08) VALUE                     
                                                      'TMKCS550'.               
           05  PC-CURRENT-PROGRAM-ERROR     PIC  X(03) VALUE '532'.             
           05  PC-TROUBLE-RTN               PIC X(08)  VALUE                    
                                                             'TMKCS520'.        
           05  PC-TROUBLE-RTN-LENGTH        PIC S9(04) VALUE +600               
                                                         COMP.                  
           05  PC-TMKUP501-RTN              PIC X(08)  VALUE                    
                                                             'TMKUP501'.        
           05  PC-TMKUP501-LENGTH           PIC S9(04) VALUE +800               
                                                         COMP.                  
           05  PC-AUDIT-RTN                 PIC  X(08)                          
                                                    VALUE 'RCKCS290'.           
           05  PC-AUDIT-RTN-LENGTH          PIC S9(04)                          
                                                    VALUE +300 COMP.            
           05  PC-DEFAULT-DATE              PIC X(10) VALUE             00063099
                                                      '9999-09-09'.     00064099
002398     05  PC-TSYMSG-03687          PIC 9(5)  VALUE 03687.                  
002399*    ERROR - MISSING DC SPLITS                                            
002398     05  PC-TSYMSG-03688          PIC 9(5)  VALUE 03688.                  
002399*     ERROR - MISSING STORE ALLOCATIONS                                   
001780     05  PC-TSYMSG-00984          PIC 9(5)  VALUE 00984.                  
001760*            DATABASE IS BUSY, TRY AGAIN IN A FEW MINUTES                 
           05  PV-REC-PKGNG-CDE             PIC  X(01)   VALUE SPACES.          
               88 PV-REC-PKGNG-PBS                       VALUES                 
                  ' '.                                                          
               88 PV-REC-PKGNG-NON-PBS                   VALUES                 
                  'C', 'B', 'M', 'I', 'S'.                                      
               88 PV-REC-PKGNG-BULK                      VALUES                 
                  'B'.                                                          
               88 PV-REC-PKGNG-NONPACK                   VALUES                 
                  'B', 'S', 'C'.                                                
               88 PV-REC-PKGNG-ACK-NONPK                 VALUES                 
                  'B', 'S', 'C', ' '.                                           
               88 PV-REC-PKGNG-PREPACK                   VALUES                 
                  'M', 'I'.                                                     
           05  PV-TRBL-NEEDS-REDISTO        PIC  X(04)   VALUE ZEROES.          
               88 PV-REDISTRO-TRBL                       VALUES                 
                  '0222'.                                                       
           05 PC-AUTO-VE-STP-FB             PIC S9(04)  VALUE 0426 COMP.        
           05  PV-PURCHS-PO-SEASET-SHRT-NM.                                     
               10  PV-PO-SEASET-SHRT-NM3    PIC X(03)  VALUE SPACE.             
               10  PV-PO-SEASET-SHRT-NM2    PIC X(02)  VALUE SPACE.             
           05  PV-DISTRO-QTY                PIC S9(07)   VALUE +0.              
           05  PV-BASE-DISTRO-QTY           PIC S9(07)   VALUE +0.              
           05  PV-NBR-OF-PIC-AD             PIC S9(04) COMP.                    
           05  PV-CICS-RESP                 PIC S9(04)  VALUE +0                
                                                        COMP SYNC.              
005170     05  PV-MSG-KEYS-1.                                                   
005180         10  FILLER                   PIC X(09) VALUE ' OPER ID '.        
005190         10  PV-MSG-OPER-ID           PIC X(04) VALUE SPACES.             
005200         10  FILLER                   PIC X(06) VALUE ' USER '.           
005190         10  PV-MSG-USER-ID           PIC X(08) VALUE SPACES.             
005170     05  PV-MSG-KEYS.                                                     
005180         10  FILLER                   PIC X(03) VALUE 'PO '.              
005190** CHG0094517  CHANGED PO LENGTH FROM 7 TO 8                              
005190         10  PV-MSG-KEY-PO            PIC X(08) VALUE SPACES.             
005200         10  FILLER                   PIC X(05) VALUE ' SEQ '.            
005210         10  PV-MSG-KEY-REC-SEQ-NBR.                                      
005220             15  PV-MSG-KEY-REC-SEQ-NBR-N                                 
005230                                      PIC 9(03) VALUE ZEROS.              
005240         10  FILLER                   PIC X(06) VALUE ' LINE '.           
005250         10  PV-MSG-KEY-PO-LNE-NBR.                                       
005260             15  PV-MSG-KEY-PO-LNE-NBR-N                                  
005270                                      PIC 9(03) VALUE ZEROS.              
005280         10  FILLER                   PIC X(05) VALUE ' STR '.            
005290         10  PV-MSG-KEY-STR-NBR       PIC 9(04) VALUE ZERO.               
005481     05  PV-CICS-MSG-AREA             PIC X(75) VALUE SPACE.              
                                                                                
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ERROR-SW                    PIC X(01) VALUE 'Y'.              
               88  PS-NO-ERROR                          VALUE 'Y'.              
               88  PS-ERROR                             VALUE 'N'.              
           05  PS-NO-DISTRO-SW                PIC X(01) VALUE 'Y'.              
               88  PS-NO-DISTRO-OK                      VALUE 'Y'.              
               88  PS-NO-DISTRO-NOT-OK                  VALUE 'N'.              
           05  PS-RECORD-SW                   PIC  X    VALUE 'N'.              
               88  PS-NO-MORE-RECORD                    VALUE 'N'.              
               88  PS-MORE-RECORD                       VALUE 'Y'.              
           05  PS-PKSLIP-RECORD-SW            PIC  X    VALUE 'N'.              
               88  PS-NO-MORE-PKSLIP-REC                VALUE 'N'.              
               88  PS-MORE-PKSLIP-REC                   VALUE 'Y'.              
           05  PS-PKITEM-RECORD-SW            PIC  X    VALUE 'N'.              
               88  PS-NO-MORE-PKITEM-REC                VALUE 'N'.              
               88  PS-MORE-PKITEM-REC                   VALUE 'Y'.              
           05  PS-RECORD-FOUND-SW             PIC  X    VALUE 'N'.              
               88  PS-RECORD-NOT-FOUND                  VALUE 'N'.              
               88  PS-RECORD-FOUND                      VALUE 'Y'.              
           05  PS-ACK-SW                      PIC  X    VALUE 'N'.              
               88  PS-ACK                               VALUE 'Y'.              
               88  PS-NOT-ACK                           VALUE 'N'.              
           05  PS-DONE-SW                     PIC  X    VALUE 'N'.              
               88  PS-NOT-DONE                          VALUE 'N'.              
               88  PS-DONE                              VALUE 'Y'.              
           05  PS-UPDATE-TRECEIV-SW           PIC X(01) VALUE 'N'.              
               88  PS-UPDATE-TRECEIV-YES                VALUE 'Y'.              
               88  PS-UPDATE-TRECEIV-NO                 VALUE 'N'.              
           05  PS-COMMAREA-SW                 PIC X(01) VALUE 'N'.              
               88  PS-COMMAREA-VALID                    VALUE 'Y'.              
               88  PS-COMMAREA-NOT-VALID                VALUE 'N'.              
           05  PS-REDISTRO-SW                 PIC X(01) VALUE 'N'.              
               88  PS-REDISTRO-OK                       VALUE 'Y'.              
               88  PS-REDISTRO-NOT-OK                   VALUE 'N'.              
           05  PS-TROUBLE-ALLOC-SW            PIC X(01) VALUE 'N'.              
               88  PS-TROUBLE-ALLOC-YES                 VALUE 'Y'.              
               88  PS-TROUBLE-ALLOC-NO                  VALUE 'N'.              
           05  PS-LOG-AUTO-SW                 PIC X(01) VALUE 'N'.              
               88  PS-LOG-AUTO-SUCCESS-OK               VALUE 'Y'.              
               88  PS-LOG-AUTO-NOT-OK                   VALUE 'N'.              
           05  PS-ALLOCATION-FOUND-SW         PIC X(01) VALUE 'N'.              
               88  PS-ALLOCATION-FOUND-YES              VALUE 'Y'.              
               88  PS-ALLOCATION-FOUND-NO               VALUE 'N'.              
           05 PS-PROCESS-TYPE                 PIC X(03) VALUE '   '.            
               88  PS-AUTO-REDISTRO-ALL                 VALUE 'RED'.            
               88  PS-AUTO-RSLV-ACK-ALL                 VALUE 'ACK'.            
           05 PS-REDISTRO-STAT-SW             PIC X(01) VALUE 'N'.              
               88  PS-REDISTRO-SUCCESS                  VALUE 'Y'.              
               88  PS-REDISTRO-FAILED                   VALUE 'N'.              
           05 PS-RESOLVE-STAT-SW              PIC X(01) VALUE 'N'.              
               88  PS-RESOLVE-SUCCESS                   VALUE 'Y'.              
               88  PS-RESOLVE-FAILED                    VALUE 'N'.              
           05 PS-INSERT-STATUS-SW             PIC X(01) VALUE 'N'.              
               88  PS-INSERT-STATUS                     VALUE 'Y'.              
               88  PS-NO-INSERT-STATUS                  VALUE 'N'.              
           05 PS-ACKNOW-STAT-SW               PIC X(01) VALUE 'N'.              
               88  PS-ACKNOW-SUCCESS                    VALUE 'Y'.              
               88  PS-ACKNOW-FAILED                     VALUE 'N'.              
           05 PS-PROCESS-CONT-SW              PIC X(01) VALUE 'N'.              
               88  PS-PROCESS-CONTINUE                  VALUE 'Y'.              
               88  PS-PROCESS-STOP                      VALUE 'N'.              
           05 PS-WAIT-UPDATE-SW               PIC X(01) VALUE 'N'.              
               88  PS-WAIT-UPDATED                      VALUE 'Y'.              
               88  PS-WAIT-NOT-UPDATED                  VALUE 'N'.              
           05 PS-CMPLTD-UPDATE-SW             PIC X(01) VALUE 'N'.              
               88  PS-CMPLTD-UPDATED                    VALUE 'Y'.              
               88  PS-CMPLTD-NOT-UPDATED                VALUE 'N'.              
                                                                                
                                                                                
       01  DK-DB2-KEYS.                                                         
           05  DK-PO-NBR                    PIC S9(9)  COMP VALUE +0.           
           05  DK-PO-LNE-NBR                PIC S9(9)  COMP VALUE +0.           
           05  DK-REC-SEQ-NBR               PIC S9(9)  COMP VALUE +0.           
           05  DK-OPER-UNT-ID               PIC S9(4)  COMP VALUE +0.           
           05  DK-FCLTY-ID                  PIC S9(4)  COMP VALUE +0.           
           05  DK-PREPK-ID                  PIC S9(4)  COMP VALUE ZERO.         
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-RETRY-COUNTER             PIC S9(4) VALUE ZERO                
                                                      COMP SYNC.                
           05  PV-TEMP-COMP-X4.                                                 
               10  PV-TEMP-COMP             PIC S9(9) COMP.                     
           05  PV-EIBDATE.                                                      
               10  PV-EIBDATE-N             PIC 9(5).                           
               10  FILLER                   PIC X(5).                           
           05  PV-DB2-COUNT                 PIC S9(09)  VALUE ZERO              
                                                    COMP-3.                     
           05  PV-DB2-ACK-COUNT             PIC S9(09)  VALUE ZERO              
                                                    COMP-3.                     
           05  PV-NULL-IND                  PIC S9(4) VALUE ZERO                
                                                      COMP SYNC.                
           05  PV-COUNT                     PIC S9(9) VALUE +0 COMP.            
           05  PV-PBS-STORE-COUNT           PIC S9(9) VALUE +0 COMP.            
           05  PV-PBS-PKSLIP-COUNT          PIC S9(9) VALUE +0 COMP.            
           05  PV-PKSL-ITM-QTY              PIC S9(9) VALUE +0 COMP.            
           05  PV-COUNT-ALLOC-DIFF          PIC S9(9) VALUE +0 COMP.            
           05  PV-COUNT-RESOLV              PIC S9(9) VALUE +0 COMP.            
           05  PV-COUNT-WAIT-STATUS         PIC S9(9) VALUE +0 COMP.            
                                                                                
                                                                                
      ****************************************************************          
      *  COPYBOOK AREA                                                          
      ****************************************************************          
                                                                                
      * PACK BY STORE PRORATION                                                 
           COPY TMWS508.                                                        
                                                                                
      *    AUDIT ROUTINE COPYBOOK                                      *        
           COPY RCWS031.                                                        
                                                                                
           COPY DPWS030.                                                        
                                                                                
      * TROUBLE CENTRAL PROCESSING COPYBOOK.                                    
           COPY TMWS500.                                                        
                                                                                
      * AUTO ACKNOWLEDGE LOGGING COPYBOOK.                                      
           COPY TMRD007.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    ABEND PROCESSING COPYBOOK.                                 *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS013.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    STANDARD COMMAREA.                                         *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS020.                                                        
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
                                                                                
      *---------------------------------------------------------------*         
      * INTER-APPLICATION COMMAREA.                                   *         
      * RCWS012 - INTER-APPLICATION COMMAREA FROM THE PACK SLIP UPDT  *         
      * RCWS015 - INTER-APPLICATION COMMAREA FROM THE PACK SLIP LIST  *         
      *---------------------------------------------------------------*         
                                                                                
               10  INTER-APPL-COMM-AREA       PIC X(200).                       
                                                                                
                                                                                
      ****************************************************************          
      *  TABLE DECLARATIONS                                          *          
      ****************************************************************          
                                                                                
      * DB2 - PURCHASE ORDER                                                    
           EXEC SQL                                                             
               INCLUDE TPURCHS                                                  
           END-EXEC.                                                            
                                                                                
      * DB2 - PURCHASE ORDER LINE                                               
           EXEC SQL                                                             
               INCLUDE TPURITM                                                  
           END-EXEC.                                                            
                                                                                
      * DB2 - PURCHASE ORDER                                                    
           EXEC SQL                                                             
               INCLUDE TALLPLN                                                  
           END-EXEC.                                                            
                                                                                
      * DB2 - PURCHASE ORDER                                                    
           EXEC SQL                                                             
               INCLUDE TMESTDS                                                  
           END-EXEC.                                                            
                                                                                
      * DB2 - TPKSLIP                                                           
           EXEC SQL                                                             
               INCLUDE TPKSLIP                                                  
           END-EXEC.                                                            
                                                                                
      * DB2 - TPKITEM                                                           
           EXEC SQL                                                             
               INCLUDE TPKITEM                                                  
           END-EXEC.                                                            
                                                                                
      * DB2 - RECEIVING                                                         
           EXEC SQL                                                             
               INCLUDE TRECEIV                                                  
           END-EXEC.                                                            
                                                                                
      * DB2 - RECEIVING - ITEM LEVEL RECEIVING                                  
           EXEC SQL                                                             
               INCLUDE TRECITM                                                  
           END-EXEC.                                                            
                                                                                
      * DB2 - RECDIS - STORE LEVEL RECEIVING                                    
           EXEC SQL                                                             
               INCLUDE TRECDIS                                                  
           END-EXEC.                                                            
                                                                                
      * DB2 - TROUBLE NOTIFY                                                    
           EXEC SQL                                                             
               INCLUDE TTRBNTF                                                  
           END-EXEC.                                                            
                                                                                
      * DB2 - TROUBLE REASON                                                    
           EXEC SQL                                                             
               INCLUDE TTRBRSN                                                  
           END-EXEC.                                                            
                                                                                
      *  DCLGEN FOR THE DB2 AUTO ACKNOWLEDGE TROUBLE TABLE                      
      *  RCT_AUTOACKNL_TRBL                                                     
      *                                                                         
           EXEC SQL                                                             
                INCLUDE RCT00020                                                
           END-EXEC.                                                            
                                                                                
      * DB2 COMMUNICATIONS AREA                                                 
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
                                                                                
      ******************************************************************        
      *    RESOLVE GET ALL PACKS FROM RECEIVER TO AUTO REDISTRO                 
      ******************************************************************        
           EXEC SQL                                                             
                DECLARE PREPK-ALL-CSR CURSOR FOR                                
                SELECT A.ORD_LNE_NBR                                            
                      ,A.PKSL_ITM_QTY                                           
                      ,A.DTL_CK_ITM_QTY                                         
                      ,A.PREPK_ID                                               
                      ,A.DMG_ITM_QTY                                            
                      ,A.RTV_QTY                                                
                      ,A.PREPK_ID                                               
                      ,B.NOTIF_NBR                                              
                  FROM TRECITM A                                                
                      ,TTRBNTF B                                                
                 WHERE                                                          
                       A.ORD_NBR           = :DK-PO-NBR                         
                   AND A.REC_SEQ_NBR       = :DK-REC-SEQ-NBR                    
                   AND A.VER_STATUS_CDE    = 'A'                                
                   AND A.OPER_UNT_ID       = :DK-OPER-UNT-ID                    
                   AND A.FCLTY_ID          = :DK-FCLTY-ID                       
                   AND A.PREPK_DRV_LNE_CDE = 'A'                                
                   AND A.ORD_NBR           = B.PO_NBR                           
                   AND A.REC_SEQ_NBR       = B.REC_SEQ_NBR                      
                   AND A.ORD_LNE_NBR       = B.PO_LNE_NBR                       
                   AND A.VER_STATUS_CDE    = :PC-A                              
                 ORDER BY PREPK_ID                                              
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    RESOLVE AUTO MULTIPLE TROUBLE CURSOR                                 
      ******************************************************************        
           EXEC SQL                                                             
                DECLARE AUTO-ALL-CSR CURSOR FOR                                 
                SELECT A.ORD_LNE_NBR                                            
                      ,A.PKSL_ITM_QTY                                           
                      ,A.DTL_CK_ITM_QTY                                         
                      ,A.PREPK_ID                                               
                      ,A.DMG_ITM_QTY                                            
                      ,A.RTV_QTY                                                
                  FROM TRECITM A                                                
                 WHERE A.OPER_UNT_ID    = :DK-OPER-UNT-ID                       
                   AND A.FCLTY_ID       = :DK-FCLTY-ID                          
                   AND A.ORD_NBR        = :DK-PO-NBR                            
                   AND A.REC_SEQ_NBR    = :DK-REC-SEQ-NBR                       
                   AND A.VER_STATUS_CDE = :PC-A                                 
                 ORDER BY ORD_LNE_NBR                                           
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    RESOLVE GET ALL PACKS FROM RECEIVER TO AUTO REDISTRO                 
      ******************************************************************        
           EXEC SQL                                                             
                DECLARE AUTO-ALL-CSR-ACK CURSOR FOR                             
                SELECT A.ORD_LNE_NBR                                            
                      ,A.PKSL_ITM_QTY                                           
                      ,A.DTL_CK_ITM_QTY                                         
                      ,A.PREPK_ID                                               
                      ,A.DMG_ITM_QTY                                            
                      ,A.RTV_QTY                                                
                      ,B.NOTIF_NBR                                              
                  FROM TRECITM A                                                
                      ,TTRBNTF B                                                
                      ,TTRBRSN C                                                
                 WHERE A.OPER_UNT_ID    = :DK-OPER-UNT-ID                       
                   AND A.FCLTY_ID       = :DK-FCLTY-ID                          
                   AND A.ORD_NBR        = :DK-PO-NBR                            
                   AND A.REC_SEQ_NBR    = :DK-REC-SEQ-NBR                       
                   AND A.ORD_NBR        = B.PO_NBR                              
                   AND A.REC_SEQ_NBR    = B.REC_SEQ_NBR                         
                   AND A.ORD_LNE_NBR    = B.PO_LNE_NBR                          
                   AND A.VER_STATUS_CDE = :PC-A                                 
                   AND B.TRBL_RSN_CDE   = C.TRBL_RSN_CDE                        
                   AND C.AUTOACKNL_IND  = :PC-Y                                 
                 ORDER BY ORD_LNE_NBR                                           
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    GET ALL PACKSLIPS TO BE ABLE TO CHANGE THEM WHEN NEEDED FOR          
      *    FLOWBACK REASONS.                                                    
      ******************************************************************        
                                                                                
                                                                                
           EXEC SQL                                                             
                DECLARE AUTO-PKSLP-CSR CURSOR FOR                               
                  SELECT  PO_NBR,                                               
                          SEQ_NBR,                                              
                          CRE_TMST,                                             
                          VER_STAT_CDE,                                         
                          OPER_UNT_ID,                                          
                          FCLTY_ID,                                             
                          PKSL_NBR,                                             
                          STR_DEST_NBR,                                         
                          REC_SEQ_NBR,                                          
                          BOL_NBR,                                              
                          PRO_NBR,                                              
                          PKSL_STAT_CDE,                                        
                          CRE_UID,                                              
                          PO_VER_NBR,                                           
                          CRE_SRC_CDE,                                          
                          EXPTED_PKG_QTY,                                       
                          OWNER_SCAC_ID,                                        
                          ASN_NBR,                                              
                          VEND_SHIP_DTE,                                        
                          SHIPT_ID,                                             
                          PKSL_PG_CNT,                                          
                          CONTNR_ID,                                            
                          ORIG_PKSL_SEQ_NBR                                     
                    FROM  TPKSLIP                                               
                 WHERE PO_NBR           = :DK-PO-NBR                            
                   AND OPER_UNT_ID      = :DK-OPER-UNT-ID                       
                   AND REC_SEQ_NBR      = :DK-REC-SEQ-NBR                       
                   AND VER_STAT_CDE     = :PC-A                                 
           END-EXEC.                                                            
      ******************************************************************        
      *   DECLARE FOR GETTING AND SUMMING ALL LINES FOR BEING                   
      *   ABLE TO CREATE PBS RECDIS LINES.                                      
      ******************************************************************        
012750/                                                                 01248099
012760     EXEC SQL                                                     01249099
012770          DECLARE PKSLIP-ITEM-CURSOR CURSOR FOR                   01250099
012780           SELECT SUM (PKSL_ITM_QTY),                             01251099
012790                  PKSL_LNE_NBR                                    01252099
012800             FROM TPKITEM                                         01253099
012810            WHERE PO_NBR        = :DK-PO-NBR                      01254099
012820              AND SEQ_NBR       = :PKSLIP-SEQ-NBR                 01255099
012830              AND VER_STAT_CDE  = 'A'                             01256099
012840         GROUP BY PKSL_LNE_NBR                                    01257099
012850         ORDER BY PKSL_LNE_NBR                                    01258099
012860     END-EXEC.                                                    01259099
012870                                                                  01260099
                                                                                
      ******************************************************************        
      *    LINKAGE  AREA                                                        
      ******************************************************************        
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       01  DFHCOMMAREA.                                                         
           05  FILLER                       OCCURS 1 TO 600 TIMES               
                                            DEPENDING ON EIBCALEN.              
               10  FILLER                   PIC X.                              
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1000-INITIAL-PROCESS.                                        
                                                                                
           IF PS-PROCESS-CONTINUE AND                                           
              PS-NOT-DONE                                                       
               PERFORM 2000-MAIN-PROCESSING                                     
           END-IF.                                                              
                                                                                
           MOVE TM500-FIELDS    TO DFHCOMMAREA.                                 
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      ******************************************************************        
                                                                                
       1000-INITIAL-PROCESS.                                                    
      *                                                                         
           INITIALIZE TM500-FIELDS.                                             
           INITIALIZE TM007-COMMUNICATIONS-AREA.                                
                                                                                
           IF EIBCALEN = 0                                                      
               SET PS-DONE              TO TRUE                                 
           ELSE                                                                 
               MOVE DFHCOMMAREA         TO TM500-FIELDS                         
               SET PS-NOT-DONE          TO TRUE                                 
           END-IF.                                                              
                                                                                
           MOVE TM500-PO-NBR            TO DK-PO-NBR.                           
           MOVE TM500-REC-SEQ-NBR       TO DK-REC-SEQ-NBR.                      
           MOVE TM500-OPER-UNT-ID       TO DK-OPER-UNT-ID.                      
           MOVE 1                       TO DK-FCLTY-ID.                         
           MOVE TM500-TRBL-RSN-CDE      TO PV-TRBL-NEEDS-REDISTO.               
                                                                                
           SET PS-WAIT-NOT-UPDATED                                              
               PS-CMPLTD-NOT-UPDATED    TO TRUE.                                
                                                                                
           IF PS-NOT-DONE                                                       
              PERFORM 1100-CHECK-IF-DONE-ALRDY.                                 
                                                                                
      *----------------------------------------------------------------*        
      * CHECK TO SEE IF IT HAS ALREADY BEEN ACNKOWLEDGED, IF SO, DON'T          
      * CONTINUE ANY FURTHER.                                                   
      *----------------------------------------------------------------*        
       1100-CHECK-IF-DONE-ALRDY.                                                
                                                                                
           MOVE ZERO                   TO PV-COUNT-RESOLV.                      
                                                                                
           EXEC SQL                                                             
                 SELECT COUNT (*)                                               
                 INTO                                                           
                       :PV-COUNT-RESOLV                                         
                 FROM   TTRBNTF A                                               
                       ,TTRBRSN B                                               
                 WHERE  A.PO_NBR            = :DK-PO-NBR                        
                   AND  A.REC_SEQ_NBR       = :DK-REC-SEQ-NBR                   
                   AND  A.TRBL_RSN_CDE      = B.TRBL_RSN_CDE                    
                   AND  A.RESOLVED_IND      IN ('I','N')                        
                   AND  B.AUTOACKNL_IND     = 'Y'                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                    IF PV-COUNT-RESOLV > 0                                      
                       SET PS-PROCESS-CONTINUE  TO TRUE                         
                    ELSE                                                        
                       SET PS-PROCESS-STOP                                      
                           TM500-AUTO-ACKNOW-ALRDY-DONE                         
                                             TO TRUE                            
                    END-IF                                                      
               WHEN SQLCODE = +100                                              
                    SET PS-PROCESS-CONTINUE  TO TRUE                            
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                     SET PS-DONE                                                
                         TM500-DB2-CONTENTION-IN-ALLOC                          
                                             TO TRUE                            
                     MOVE SQLCODE            TO TM500-SQLCODE                   
                     MOVE SQLCA              TO TM500-SQLCA                     
               WHEN OTHER                                                       
                     SET PS-DONE                                                
                         TM500-DB2-UNRECOVERABLE-ERROR                          
                                             TO TRUE                            
                     MOVE SQLCODE            TO TM500-SQLCODE                   
                     MOVE SQLCA              TO TM500-SQLCA                     
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * GATHER INFORMATION TO PASS TO HELP WITH DOING THE REDISTRO     *        
      *----------------------------------------------------------------*        
       2000-MAIN-PROCESSING.                                                    
           PERFORM 2100-CHECK-PO-REC-INFO.                                      
                                                                                
      * CHECK IF NO DISTRO TROUBLE CONDITION IS RESOLVED ALREADY                
      * IF SO, GET SET SWITCHS TO GET OUT OF PROGRAM.                           
                                                                                
           IF PV-REDISTRO-TRBL AND                                              
              PS-NOT-DONE                                                       
              PERFORM 2200-CHECK-NO-DISTRO-TRBL                                 
              IF PS-NO-DISTRO-OK  AND                                           
                 PV-COUNT-ALLOC-DIFF = 0                                        
                 SET PS-NO-DISTRO-OK                                            
                     PS-AUTO-REDISTRO-ALL    TO TRUE                            
              ELSE                                                              
                 SET PS-NO-DISTRO-NOT-OK                                        
                     TM500-AUTO-ACKNOW-NOT-FOUND                                
                     PS-ALLOCATION-FOUND-NO                                     
                     PS-AUTO-RSLV-ACK-ALL    TO TRUE.                           
                                                                                
      * THAN CHECK TO DETERMINE IF THE TYPE OF FLOWBACK PO CAN BE               
      * CHANGED FROM BULK TO PBS AUTOMATICALLY.                                 
           IF PS-AUTO-REDISTRO-ALL  AND                                         
              PS-NO-DISTRO-OK       AND                                         
              PS-NOT-DONE                                                       
              IF (PURCHS-PO-TYP-CDE  = 'FB' OR 'IF') AND                        
                  PV-PO-SEASET-SHRT-NM3 = 'FBP'      AND                        
                  PV-REC-PKGNG-BULK                  AND                        
                  PS-NOT-DONE                                                   
                   SET PS-MORE-PKSLIP-REC    TO TRUE                            
                   PERFORM 3000-CHECK-FLOWBACK-AUTO-PBS.                        
                                                                                
           IF PS-NOT-DONE                                                       
              IF PV-REC-PKGNG-PBS                                               
      *  DON'T DELETE ALLOCATIONS UNLESS WE ARE GOING TO RE-DISTRO              
                 IF TM500-AUTO-ACKNOW-NOT-FOUND                                 
                    CONTINUE                                                    
                 ELSE                                                           
                    PERFORM 5400-DELETE-TRECDIS                                 
                 END-IF                                                         
                 PERFORM 3700-PREPARE-FOR-PBS-ALLOC                             
                 PERFORM 3550-DELETE-LNE-TRBL                                   
                 PERFORM 3600-PBS-TMKCS508                                      
                 PERFORM 3050-GET-426-NOTIF-NBR                                 
                 PERFORM 3051-FOOL-TMKUP501                                     
              ELSE                                                              
      *  DON'T DELETE ALLOCATIONS UNLESS WE ARE GOING TO RE-DISTRO              
                 IF TM500-AUTO-ACKNOW-NOT-FOUND                                 
                    CONTINUE                                                    
                 ELSE                                                           
                    PERFORM 5400-DELETE-TRECDIS                                 
                 END-IF                                                         
                 PERFORM 4000-START-AUTO-PROCESS                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
      * IF THIS TROUBLE WAS TO BE RE-DISTRO'ED AND THERE WAS AN ERROR           
      * DO NOT CALL THE RESOLVE/ACKNOWLEDGE PROCESS, ELSE CALL IT               
      * FOR ALL PACKAGING CODE TYPES.                                           
           IF PV-REDISTRO-TRBL     AND                                          
              PS-NO-DISTRO-NOT-OK                                               
               CONTINUE                                                         
           ELSE                                                                 
              IF PS-NOT-DONE                                                    
                 SET  TM500-REDISTRO-NO                                         
                      PS-MORE-RECORD                                            
                      PS-AUTO-RSLV-ACK-ALL      TO TRUE                         
                 PERFORM 4000-START-AUTO-PROCESS.                               
                                                                                
           PERFORM 8000-PASS-BACK-MSG.                                          
                                                                                
      *----------------------------------------------------------------*        
      * GATHER INFORMATION TO PASS TO HELP WITH DOING THE REDISTRO     *        
      *----------------------------------------------------------------*        
                                                                                
       2100-CHECK-PO-REC-INFO.                                                  
           EXEC SQL                                                             
                 SELECT DISTINCT                                                
                        A.ORD_NBR                                               
                      , A.REC_SEQ_NBR                                           
                      , A.PKGNG_CDE                                             
                      , B.PO_NBR                                                
                      , B.PO_TYP_CDE                                            
                      , B.SEASET_SHRT_NM                                        
                 INTO                                                           
                       :RECEIV-ORD-NBR                                          
                      ,:RECEIV-REC-SEQ-NBR                                      
                      ,:RECEIV-PKGNG-CDE                                        
                      ,:PURCHS-PO-NBR                                           
                      ,:PURCHS-PO-TYP-CDE                                       
                      ,:PURCHS-SEASET-SHRT-NM                                   
                 FROM   TRECEIV A                                               
                       ,TPURCHS B                                               
                 WHERE  A.ORD_NBR           = :DK-PO-NBR                        
                   AND  A.REC_SEQ_NBR       = :DK-REC-SEQ-NBR                   
                   AND  A.ORD_NBR           = B.PO_NBR                          
                   AND  B.VER_STATUS_CDE    = 'A'                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                    MOVE RECEIV-PKGNG-CDE TO PV-REC-PKGNG-CDE                   
                    MOVE PURCHS-SEASET-SHRT-NM                                  
                                          TO                                    
                                       PV-PURCHS-PO-SEASET-SHRT-NM              
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                     SET TM500-DB2-CONTENTION-IN-ALLOC                          
                                             TO TRUE                            
                     MOVE SQLCODE            TO TM500-SQLCODE                   
                     MOVE SQLCA              TO TM500-SQLCA                     
               WHEN OTHER                                                       
                     SET TM500-DB2-UNRECOVERABLE-ERROR                          
                                             TO TRUE                            
                     MOVE SQLCODE            TO TM500-SQLCODE                   
                     MOVE SQLCA              TO TM500-SQLCA                     
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * THIS WILL DETECT IF ALL OF THE LINES ON TRECITM ARE ALLOCATED           
      * AT BOTH THE TALLPLN AND TMESTDS LEVEL.   IF NOT, WE DO NOT              
      * WANT TO CONTINUE AND WILL PUT THE PO/RECEIVER ON WAIT STATUS SO         
      * THAT THE BATCH SYSTEM WILL PICK IT UP.                                  
      *----------------------------------------------------------------*        
       2200-CHECK-NO-DISTRO-TRBL.                                               
                                                                                
           EXEC SQL                                                             
                SELECT DISTINCT PO_NBR                                          
                  INTO :ALLPLN-PO-NBR                                           
                  FROM TALLPLN                                                  
                 WHERE PO_NBR   = :DK-PO-NBR                                    
                   AND DEST_NBR = :DK-OPER-UNT-ID                               
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                    SET PS-ALLOCATION-FOUND-YES TO TRUE                         
               WHEN SQLCODE = +100                                              
                    SET PS-ALLOCATION-FOUND-NO  TO TRUE                         
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                     SET PS-ALLOCATION-FOUND-NO                                 
                                         TO TRUE                                
                     MOVE SQLCODE        TO TM500-SQLCODE                       
                     MOVE SQLCA          TO TM500-SQLCA                         
               WHEN OTHER                                                       
                     SET PS-ALLOCATION-FOUND-NO                                 
                                         TO TRUE                                
                     MOVE SQLCODE        TO TM500-SQLCODE                       
                     MOVE SQLCA          TO TM500-SQLCA                         
           END-EVALUATE.                                                        
                                                                                
           IF PS-ALLOCATION-FOUND-YES                                           
               MOVE 0                    TO PV-COUNT-ALLOC-DIFF                 
               EXEC SQL                                                         
                  SELECT COUNT (*)                                              
                    INTO :PV-COUNT-ALLOC-DIFF                                   
                    FROM TRECITM A                                              
                    WHERE A.ORD_NBR      = :DK-PO-NBR                           
                      AND A.OPER_UNT_ID  = :DK-OPER-UNT-ID                      
                      AND A.ORD_LNE_NBR  NOT IN(                                
                     SELECT B.PO_LNE_NBR                                        
                       FROM TALLPLN B                                           
                           ,TMESTDS C                                           
                       WHERE B.PO_NBR    = A.ORD_NBR                            
                        AND B.PO_NBR     = C.PO_NBR                             
                        AND B.PO_LNE_NBR = C.PO_LNE_NBR                         
                        AND B.PLAN_NBR   = C.PLAN_NBR                           
                        AND DEST_NBR     = A.OPER_UNT_ID)                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                    WHEN SQLCODE = +0                                           
                         SET PS-NO-DISTRO-OK     TO TRUE                        
                    WHEN SQLWARN0 NOT = SPACE                                   
                    WHEN SQLCODE = -904                                         
                    WHEN SQLCODE = -911                                         
                    WHEN SQLCODE = -913                                         
                         SET TM500-DB2-CONTENTION-IN-ALLOC                      
                             PS-DONE             TO TRUE                        
                         MOVE SQLCODE            TO TM500-SQLCODE               
                         MOVE SQLCA              TO TM500-SQLCA                 
                    WHEN OTHER                                                  
                         SET PS-DONE                                            
                             TM500-DB2-UNRECOVERABLE-ERROR                      
                                                 TO TRUE                        
                         MOVE SQLCODE            TO TM500-SQLCODE               
                         MOVE SQLCA              TO TM500-SQLCA                 
               END-EVALUATE.                                                    
                                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      * DETERMINE IF THIS PO/RECEIVER IS A CANDIDATE TO BE CHANGED              
      * FROM A BULK TO A PBS.                                                   
      * IT WILL BE DONE AUTOMATICALLY WHEN THE ALL FOLLOWING CONDITIONS         
      * ARE MET:                                                                
      *        1. RECEIVER PACKAGING CODE IS A "B"                              
      *        2. PO_TYP_CDE = 'FB' OR 'IF'                                     
      *        3. SEASONAL SHORT NAME STARTS WITH 'FBP__'                       
      *        4. FOR ALL OF THE LINES, THE STORE NMBR IS THE SAME              
      *           FOR ALL LINES.                                                
      *        5. ALSO, YOU CAN ONLY HAVE ONE ACTIVE PACKSLIP WHEN              
      *           DOING THIS FOR A PBS.                                         
      * WHEN THE CONDITIONS ABOVE ARE MET, THE RECEIVING PACKAGING CODE         
      * WILL BE CHANGED TO A PBS (SPACES) AND PACK-BY-STR-IND = "Y" ON          
      * TRECEIV, AND THE PACKSLIP HEADER(S) THAT ARE ACTIVE WILL BE             
      * UPDATED TO HAVE THE STORE NUMBER IN IT.                                 
      * IF THERE IS MORE THAN ONE STORE NUMBER, THE PROCESS WILL                
      * CONTINUE TO PROCESS THE PO/RECEIVER AS A BULK.  IT WILL NOT             
      * CREATE NEW PACKSLIPS FOR MULTIPLE STORES.                               
      * IN ADDITION TO THE ABOVE, ALL OF THE LINE LEVEL TROUBLE WILL            
      * BE DELETED NOW THAT THIS IS A PBS BEFORE RUNNING THE TMKCS508           
      * PROGRAM TO CREATE JUST PBS TROUBLE.                                     
      * ALSO SINCE THE LINE LEVEL TROUBLE IS DELETED, FOR THIS INSTANT          
      * ONLY, WE WILL NEED TO LOOK AT THE TROUBLE 426 - STOP AUTO VE            
      * FLOWBACK TROUBLE TO USE THAT NOTIF NUMBER TO PUT INTO THE               
      * RCT_AUTOACKNL_TRBL AND SET THAT TO COMPLETE SO THAT THE                 
      * REPORT WILL PICK UP THIS PO/RECEIVER AS BEING COMPLETED.                
      *----------------------------------------------------------------*        
                                                                                
       3000-CHECK-FLOWBACK-AUTO-PBS.                                            
                                                                                
           MOVE ZEROES                TO PV-PBS-STORE-COUNT.                    
                                                                                
      *FIRST CHECK THAT THERE IS ONLY ONE STORE FOR THE ALLOCATION              
                                                                                
           EXEC SQL                                                             
              SELECT COUNT (*) AS TOT2                                          
              INTO  :PV-PBS-STORE-COUNT                                         
              FROM (SELECT COUNT (*)                                            
                     FROM TALLPLN A                                             
                         ,TMESTDS B                                             
                    WHERE A.PO_NBR    = :DK-PO-NBR                              
                     AND DEST_NBR     = :DK-OPER-UNT-ID                         
                     AND A.PO_NBR     = B.PO_NBR                                
                     AND A.PO_LNE_NBR = B.PO_LNE_NBR                            
                     AND A.PLAN_NBR   = B.PLAN_NBR                              
                    GROUP BY                                                    
                          STR_NBR) TOT1                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                    SET TM500-DB2-CONTENTION-IN-ALLOC                           
                        PS-DONE              TO TRUE                            
                     MOVE SQLCODE            TO TM500-SQLCODE                   
                     MOVE SQLCA              TO TM500-SQLCA                     
               WHEN OTHER                                                       
                    SET TM500-DB2-UNRECOVERABLE-ERROR                           
                        PS-DONE                                                 
                                             TO TRUE                            
                    MOVE SQLCODE             TO TM500-SQLCODE                   
                    MOVE SQLCA               TO TM500-SQLCA                     
           END-EVALUATE.                                                        
                                                                                
                                                                                
           MOVE ZEROES                TO PV-PBS-PKSLIP-COUNT.                   
                                                                                
      *THEN CHECK THAT THERE IS ONLY ONE ACTIVE PACKSLIP FOR RECEIVER           
                                                                                
           EXEC SQL                                                             
             SELECT COUNT (*)                                                   
               INTO  :PV-PBS-PKSLIP-COUNT                                       
               FROM TPKSLIP                                                     
              WHERE PO_NBR           = :DK-PO-NBR                               
                AND OPER_UNT_ID      = :DK-OPER-UNT-ID                          
                AND REC_SEQ_NBR      = :DK-REC-SEQ-NBR                          
                AND VER_STAT_CDE     = :PC-A                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                   SET PS-DONE             TO TRUE                              
                   MOVE SQLCODE            TO TM500-SQLCODE                     
                   MOVE SQLCA              TO TM500-SQLCA                       
               WHEN OTHER                                                       
                    SET PS-DONE                                                 
                        TM500-DB2-UNRECOVERABLE-ERROR                           
                                           TO TRUE                              
                    MOVE SQLCODE           TO TM500-SQLCODE                     
                    MOVE SQLCA             TO TM500-SQLCA                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
           IF PS-NO-DISTRO-OK                                                   
              IF PS-NOT-DONE                                                    
                 IF PV-PBS-STORE-COUNT = 1 AND                                  
                    PV-PBS-PKSLIP-COUNT = 1                                     
                    PERFORM 3100-AUDIT-RECEIV                                   
                    PERFORM 3200-UPDATE-TRECEIV                                 
                    PERFORM 3300-UPDATE-ALL-PKSLIP                              
                    IF PS-UPDATE-TRECEIV-YES                                    
                       MOVE ' '            TO PV-REC-PKGNG-CDE                  
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * SINCE THE LINE LEVEL TROUBLE IS DELETED, FOR THIS INSTANT               
      * ONLY, WE WILL NEED TO LOOK AT THE TROUBLE 426 - STOP AUTO VE            
      * FLOWBACK TROUBLE TO USE THAT NOTIF NUMBER TO PUT INTO THE               
      * RCT_AUTOACKNL_TRBL AND SET THAT TO COMPLETE SO THAT THE                 
      * REPORT WILL PICK UP THIS PO/RECEIVER AS BEING COMPLETED.                
      ******************************************************************        
       3050-GET-426-NOTIF-NBR.                                                  
                                                                                
           EXEC SQL                                                             
             SELECT  NOTIF_NBR                                                  
                     ,PO_LNE_NBR                                                
               INTO  :TRBNTF-NOTIF-NBR,                                         
                     :TRBNTF-PO-LNE-NBR                                         
               FROM TTRBNTF T                                                   
               WHERE T.TRBL_RSN_CDE    = :PC-AUTO-VE-STP-FB                     
                 AND T.PO_NBR          = :DK-PO-NBR                             
                 AND T.REC_SEQ_NBR     = :DK-REC-SEQ-NBR                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
             WHEN SQLCODE = +100                                                
             WHEN SQLCODE = -811                                                
                  CONTINUE                                                      
             WHEN SQLWARN0 NOT = SPACES                                         
             WHEN SQLCODE < +0                                                  
             WHEN SQLCODE > +0                                                  
                  MOVE '3050-GET-426-NOTIF-NBR'                                 
                                        TO DP013-PARAGRAPH                      
                  MOVE 'UNSUCCESSFUL SELECT ON TTRBNTF'                         
                                         TO DP013-MESSAGE-TEXT (1)              
                  MOVE SQLCA           TO DP013-SQLCA                           
                  MOVE 'TRECITM '      TO DP013-DB2-TABLE-NAME (1)              
                  SET DP013-DB2-ABEND                                           
                      DP013-XCTL-DISPLAY-RESTART                                
                                         TO TRUE                                
                  PERFORM DP013-0000-PROCESS-ABEND                              
           END-EVALUATE.                                                        
                                                                                
      **************************************************************            
      *CALL TMKUP501 TO INSERT A NEW ROW AS COMPLETE INTO THE                   
      * RCT_AUTOACKNL_TRBL TABLE SO THAT THE REPORT CAN PICK IT UP              
      **************************************************************            
       3051-FOOL-TMKUP501.                                                      
                                                                                
           MOVE DK-PO-NBR                  TO TM007-PO-NBR.                     
           MOVE DK-REC-SEQ-NBR             TO TM007-REC-SEQ-NBR.                
           MOVE DK-OPER-UNT-ID             TO TM007-OPER-UNT-ID.                
           MOVE PC-AUTO-VE-STP-FB          TO TM007-TRBL-RSN-CDE.               
           MOVE TRBNTF-NOTIF-NBR           TO TM007-NOTIF-NBR.                  
                                                                                
      * INSERT THIS AS A WAIT STATUS FIRST.                                     
                                                                                
           SET TM007-INSERT                                                     
               TM007-WAIT-STATUS       TO TRUE.                                 
                                                                                
           PERFORM 6100-LINK-TO-TMKUP501.                                       
                                                                                
      * UPDATE IT TO A COMPLETE STATUS TMST.                                    
           SET TM007-UPDATE                                                     
               TM007-CMPLT-STATUS       TO TRUE.                                
                                                                                
           PERFORM 6100-LINK-TO-TMKUP501.                                       
                                                                                
                                                                                
      ******************************************************************        
      *    AUDIT TRECEIV BEFORE UPDATE                                          
      ******************************************************************        
                                                                                
       3100-AUDIT-RECEIV.                                                       
           MOVE 'V'                     TO RC031-TABLE-TO-AUDIT.                
           MOVE 'B'                     TO RC031-AUD-TYP-CDE.                   
           MOVE 'U'                     TO RC031-AUD-FUNC-CDE.                  
           MOVE TM500-PO-NBR            TO RC031-PO-NBR.                        
           MOVE TM500-REC-SEQ-NBR       TO RC031-REC-SEQ-NBR.                   
           MOVE TM500-OPER-UNT-ID       TO RC031-OPER-UNT-ID.                   
           MOVE TM500-FCLTY-ID          TO RC031-FCLTY-ID.                      
           EXEC CICS                                                            
               LINK PROGRAM  (PC-AUDIT-RTN)                                     
                    COMMAREA (RC031-FIELDS)                                     
                    LENGTH   (PC-AUDIT-RTN-LENGTH)                              
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
               WHEN RC031-CALL-SUCCESSFUL                                       
                   CONTINUE                                                     
                                                                                
               WHEN OTHER                                                       
                  MOVE '3100-AUDIT-RECEIV'                                      
                                        TO DP013-PARAGRAPH                      
                  MOVE 'UNSUCCESSFUL CALL TO AUDIT RTN'                         
                                        TO DP013-MESSAGE-TEXT (1)               
                  MOVE RC031-RETURN-DESC                                        
                                        TO DP013-MESSAGE-TEXT (2)               
                  MOVE RC031-SQLCA      TO DP013-SQLCA                          
                  SET DP013-DB2-ABEND                                           
                      DP013-XCTL-DISPLAY-RESTART                                
                                        TO TRUE                                 
                  PERFORM DP013-0000-PROCESS-ABEND                              
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      * UPDATE TRECEIV                                                 *        
      *----------------------------------------------------------------*        
       3200-UPDATE-TRECEIV.                                                     
           MOVE '3200-UPDATE-TRECEIV' TO TM007-RETURNED-PARA.                   
                                                                                
           INITIALIZE DCLTRECEIV.                                               
                                                                                
           EXEC SQL                                                             
               UPDATE TRECEIV                                                   
                  SET PKGNG_CDE      = :PC-SPACE                                
                     ,PACK_BY_ST_IND = :PC-Y                                    
                     ,UPD_TMST       = CURRENT TIMESTAMP                        
                     ,UPD_UID        = :TM500-RESOLVED-UID                      
                     ,UPD_PGM_ID     = :PC-CURRENT-PROGRAM-NAME                 
                WHERE ORD_NBR        = :DK-PO-NBR                               
                  AND REC_SEQ_NBR    = :DK-REC-SEQ-NBR                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN SQLCODE = +0                                                
                   SET PS-UPDATE-TRECEIV-YES TO TRUE                            
                                                                                
               WHEN SQLCODE      = -904                                         
               WHEN SQLCODE      = -911                                         
               WHEN SQLCODE      = -913                                         
                   SET TM500-DB2-CONTENTION-IN-ALLOC                            
                                         TO TRUE                                
                   MOVE SQLCODE          TO TM500-SQLCODE                       
                   MOVE SQLCA            TO TM500-SQLCA                         
                                                                                
               WHEN OTHER                                                       
                   SET TM500-DB2-UNRECOVERABLE-ERROR                            
                                        TO TRUE                                 
                   MOVE SQLCODE         TO TM500-SQLCODE                        
                   MOVE SQLCA           TO TM500-SQLCA                          
                                                                                
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * BECAUSE THERE MAY BE MORE THAN ONE PACKSLIP, WE WILL NEED TO            
      * UPDATE ALL OF THE ACTIVE PACKSLIPS TO THE STORE NUMBER WHEN             
      * CHANGING IT TO PACK BY STORE.                                           
      *----------------------------------------------------------------*        
       3300-UPDATE-ALL-PKSLIP.                                                  
                                                                                
           PERFORM 3400-OPEN-AUTO-PKSLP-CSR.                                    
                                                                                
           IF PS-NOT-DONE                                                       
              PERFORM 3410-FETCH-TPKSLIP                                        
           END-IF.                                                              
                                                                                
           PERFORM 3420-CLOSE-AUTO-PKSLP-CSR.                                   
                                                                                
      *----------------------------------------------------------------*        
      *    OPEN AUTO-PKSLP-CSR                                                  
      *----------------------------------------------------------------*        
       3400-OPEN-AUTO-PKSLP-CSR.                                                
                                                                                
           EXEC SQL                                                             
                OPEN AUTO-PKSLP-CSR                                             
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
              SET PS-DONE                 TO TRUE                               
              MOVE '3400-OPEN-AUTO-PKSLP'                                       
                                          TO DP013-PARAGRAPH                    
              MOVE 'UNSUCCESSFUL OPEN OF TPKSLIP'                               
                                          TO DP013-MESSAGE-TEXT (1)             
              MOVE SQLCA                  TO DP013-SQLCA                        
              MOVE 'TPKSLIP'              TO DP013-DB2-TABLE-NAME (1)           
              SET DP013-DB2-ABEND                                               
                  DP013-XCTL-DISPLAY-RESTART                                    
                                          TO TRUE                               
              PERFORM DP013-0000-PROCESS-ABEND                                  
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    SELECT ALL FROM TPKSLIP TO PREPARE TO VERSION IT.   ALL THAT         
      *    WILL BE DIFFERENT WILL BE THE STORE DEST NUMBER.                     
      *----------------------------------------------------------------*        
       3410-FETCH-TPKSLIP.                                                      
                                                                                
               EXEC SQL                                                         
                   FETCH  AUTO-PKSLP-CSR                                        
                    INTO :PKSLIP-PO-NBR,                                        
                         :PKSLIP-SEQ-NBR,                                       
                         :PKSLIP-CRE-TMST,                                      
                         :PKSLIP-VER-STAT-CDE,                                  
                         :PKSLIP-OPER-UNT-ID,                                   
                         :PKSLIP-FCLTY-ID,                                      
                         :PKSLIP-PKSL-NBR,                                      
                         :PKSLIP-STR-DEST-NBR,                                  
                         :PKSLIP-REC-SEQ-NBR,                                   
                         :PKSLIP-BOL-NBR,                                       
                         :PKSLIP-PRO-NBR,                                       
                         :PKSLIP-PKSL-STAT-CDE,                                 
                         :PKSLIP-CRE-UID,                                       
                         :PKSLIP-PO-VER-NBR,                                    
                         :PKSLIP-CRE-SRC-CDE,                                   
                         :PKSLIP-EXPTED-PKG-QTY,                                
                         :PKSLIP-OWNER-SCAC-ID,                                 
                         :PKSLIP-ASN-NBR,                                       
                         :PKSLIP-VEND-SHIP-DTE,                                 
                         :PKSLIP-SHIPT-ID,                                      
                         :PKSLIP-PKSL-PG-CNT,                                   
                         :PKSLIP-CONTNR-ID,                                     
                         :PKSLIP-ORIG-PKSL-SEQ-NBR                              
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                        PERFORM 3500-UPDATE-PKSLIP                              
                   WHEN SQLCODE = +100                                          
                        SET PS-NO-MORE-PKSLIP-REC                               
                                             TO TRUE                            
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                        SET PS-DONE          TO TRUE                            
                        PERFORM 9900-SOFT-ABORT                                 
                                                                                
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE  NOT = ZERO                                     
                        SET PS-DONE          TO TRUE                            
                        MOVE '3410-FETCH-TPKSLIP'                               
                                             TO DP013-PARAGRAPH                 
                        MOVE 'UNSUCCESSFUL FETCH OF TPKSLIP'                    
                                             TO DP013-MESSAGE-TEXT (1)          
                        MOVE SQLCA           TO DP013-SQLCA                     
                        MOVE 'TPKSLIP'       TO DP013-DB2-TABLE-NAME (1)        
                        SET DP013-DB2-ABEND                                     
                            DP013-XCTL-DISPLAY-RESTART                          
                                             TO TRUE                            
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE.                                                    
                                                                                
      *----------------------------------------------------------------*        
      *----------------------------------------------------------------*        
       3420-CLOSE-AUTO-PKSLP-CSR.                                               
                                                                                
           EXEC SQL                                                             
                CLOSE AUTO-PKSLP-CSR                                            
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
              SET PS-DONE                    TO TRUE                            
              MOVE '3420-CLOSE-AUTO-PKSLP'                                      
                                             TO DP013-PARAGRAPH                 
              MOVE 'UNSUCCESSFUL CLOSE OF TPKSLIP'                              
                                             TO DP013-MESSAGE-TEXT (1)          
              MOVE SQLCA                     TO DP013-SQLCA                     
              MOVE 'TPKSLIP'                 TO DP013-DB2-TABLE-NAME (1)        
              SET DP013-DB2-ABEND                                               
                  DP013-XCTL-DISPLAY-RESTART TO TRUE                            
              PERFORM DP013-0000-PROCESS-ABEND                                  
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    UPDATE THE CURRENT PACKSLIP ROW TO INACTIVE AND MOVE THE             
      *    PBS STORE NUMBER TO THE ACTIVE ROW WHICH WILL BE INSERTED.           
      *----------------------------------------------------------------*        
       3500-UPDATE-PKSLIP.                                                      
                                                                                
           PERFORM 3510-SET-CURR-PKSLIP-TO-INACT.                               
                                                                                
           EXEC SQL                                                             
                SELECT DISTINCT STR_NBR                                         
                  INTO :MESTDS-STR-NBR                                          
                  FROM TALLPLN A                                                
                      ,TMESTDS B                                                
                  WHERE A.PO_NBR     = :DK-PO-NBR                               
                    AND DEST_NBR     = :DK-OPER-UNT-ID                          
                    AND A.PO_NBR     = B.PO_NBR                                 
                    AND A.PO_LNE_NBR = B.PO_LNE_NBR                             
                    AND A.PLAN_NBR   = B.PLAN_NBR                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
                 MOVE MESTDS-STR-NBR     TO PKSLIP-STR-DEST-NBR                 
                 MOVE DK-REC-SEQ-NBR     TO PKSLIP-REC-SEQ-NBR                  
                 PERFORM 3520-INSERT-NEW-PKSLIP-STATUS                          
             WHEN SQLCODE NOT = +0                                              
                 SET PS-DONE             TO TRUE                                
                 MOVE '3500-UPDATE-PKSLIP'                                      
                                         TO DP013-PARAGRAPH                     
                 MOVE 'UNSUCCESSFUL SELECT OF STR-NBR'                          
                                         TO DP013-MESSAGE-TEXT (1)              
                 MOVE SQLCA              TO DP013-SQLCA                         
                 MOVE 'TMESTDS'          TO DP013-DB2-TABLE-NAME (1)            
                 SET DP013-DB2-ABEND                                            
                     DP013-XCTL-DISPLAY-RESTART                                 
                                         TO TRUE                                
                 PERFORM DP013-0000-PROCESS-ABEND                               
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    SET THE CURRENT ROW IN TPKSLIP TO INACTIVE.  THIS WILL ONLY          
      *    HAPPEN WHEN THERE IS A FLOWBACK PO THAT IS MET TO BE CHANGED         
      *    FROM A BULK TO A PBS.  THE PACKSLIP WILL ALSO NEED TO BE             
      *    CHANGED TO CONTAIN THE STORE NUMBER IN THE PACKSLIP HEADER.          
      *----------------------------------------------------------------*        
       3510-SET-CURR-PKSLIP-TO-INACT.                                           
                                                                                
               EXEC SQL                                                         
                 UPDATE TPKSLIP                                                 
                    SET VER_STAT_CDE  = 'I'                                     
                  WHERE PO_NBR        = :DK-PO-NBR                              
                    AND SEQ_NBR       = :PKSLIP-SEQ-NBR                         
                    AND REC_SEQ_NBR   = :DK-REC-SEQ-NBR                         
                    AND OPER_UNT_ID   = :DK-OPER-UNT-ID                         
                    AND VER_STAT_CDE  = 'A'                                     
               END-EXEC.                                                        
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                        CONTINUE                                                
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                        SET PS-DONE       TO TRUE                               
                        PERFORM 9900-SOFT-ABORT                                 
                                                                                
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE  NOT = ZERO                                     
                        SET PS-DONE      TO TRUE                                
                        MOVE '3510-SET-CURR-PKSLIP-TO-INACT'                    
                                         TO DP013-PARAGRAPH                     
                        MOVE 'UNSUCCESSFUL UPDATE OF TPKSLIP'                   
                                         TO DP013-MESSAGE-TEXT (1)              
                        MOVE SQLCA       TO DP013-SQLCA                         
                        MOVE 'TPKSLIP'   TO DP013-DB2-TABLE-NAME (1)            
                        SET DP013-DB2-ABEND                                     
                            DP013-XCTL-DISPLAY-RESTART                          
                                         TO TRUE                                
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE.                                                    
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    INSERT NEW PACKSLIP HEADER FOR FLOWBACK PO THAT WAS CHANGED          
      *    ON THE RECEIVING END FROM BULK TO PBS.  THE ONLY DIFFERENCE          
      *    IS THE STORE NUMBER.                                                 
      *----------------------------------------------------------------*        
       3520-INSERT-NEW-PKSLIP-STATUS.                                           
                                                                                
               EXEC SQL                                                         
                   INSERT INTO TPKSLIP                                          
                         (PO_NBR,                                               
                          SEQ_NBR,                                              
                          CRE_TMST,                                             
                          VER_STAT_CDE,                                         
                          OPER_UNT_ID,                                          
                          FCLTY_ID,                                             
                          PKSL_NBR,                                             
                          STR_DEST_NBR,                                         
                          REC_SEQ_NBR,                                          
                          SHIPT_ID,                                             
                          BOL_NBR,                                              
                          PRO_NBR,                                              
                          PKSL_STAT_CDE,                                        
                          CRE_UID,                                              
                          PO_VER_NBR,                                           
                          CRE_SRC_CDE,                                          
                          EXPTED_PKG_QTY,                                       
                          OWNER_SCAC_ID,                                        
                          ASN_NBR,                                              
                          VEND_SHIP_DTE,                                        
                          PKSL_PG_CNT,                                          
                          CONTNR_ID,                                            
                          ORIG_PKSL_SEQ_NBR)                                    
                  VALUES (:DK-PO-NBR,                                           
                          :PKSLIP-SEQ-NBR,                                      
                          CURRENT TIMESTAMP,                                    
                          'A',                                                  
                          :PKSLIP-OPER-UNT-ID,                                  
                          :PKSLIP-FCLTY-ID,                                     
                          :PKSLIP-PKSL-NBR,                                     
                          :PKSLIP-STR-DEST-NBR,                                 
                          :PKSLIP-REC-SEQ-NBR,                                  
                          :PKSLIP-SHIPT-ID,                                     
                          :PKSLIP-BOL-NBR,                                      
                          :PKSLIP-PRO-NBR,                                      
                          'AC',                                                 
                          'SYSM PBS',                                           
                          :PKSLIP-PO-VER-NBR,                                   
                          :PKSLIP-CRE-SRC-CDE,                                  
                          :PKSLIP-EXPTED-PKG-QTY,                               
                          :PKSLIP-OWNER-SCAC-ID,                                
                          :PKSLIP-ASN-NBR,                                      
                          :PKSLIP-VEND-SHIP-DTE,                                
                          :PKSLIP-PKSL-PG-CNT,                                  
                          :PKSLIP-CONTNR-ID,                                    
                          :PKSLIP-ORIG-PKSL-SEQ-NBR)                            
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                        CONTINUE                                                
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                        SET PS-DONE      TO TRUE                                
                        PERFORM 9900-SOFT-ABORT                                 
                                                                                
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE  NOT = ZERO                                     
                        SET PS-DONE      TO TRUE                                
                        MOVE '5630-INSERT-NEW-PKSLIP-STATUS'                    
                                         TO DP013-PARAGRAPH                     
                        MOVE 'UNSUCCESSFUL INSERT OF TPKSLIP'                   
                                         TO DP013-MESSAGE-TEXT (1)              
      *                 MOVE ASC-KEY-PO-NBR                                     
      *                                  TO PV-MSG-KEY-PO                       
      *                 MOVE ASC-KEY-REC-SEQ-NBR                                
      *                                  TO PV-MSG-KEY-REC-SEQ-NBR-N            
      *                 MOVE PKSLIP-STR-DEST-NBR                                
      *                                  TO PV-MSG-KEY-STR-NBR                  
      *                 MOVE PV-MSG-KEYS TO DP013-MESSAGE-TEXT (2)              
                        MOVE SQLCA       TO DP013-SQLCA                         
                        MOVE 'TPKSLIP'   TO DP013-DB2-TABLE-NAME (1)            
                        SET DP013-DB2-ABEND                                     
                            DP013-XCTL-DISPLAY-RESTART                          
                                         TO TRUE                                
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE.                                                    
                                                                                
      *----------------------------------------------------------------*        
      * FOR BULK FLOWBACK WITH A SEASONAL SET CODE LIKE 'FBP%' ONLY.            
      * WHEN THE RECEIVER PACKAGING CODE OF BULK IS CHANGED TO A PBS,           
      * DELETE ALL OF THE LINE LEVEL TROUBLES.  THIS IS TO MAKE SURE            
      * THAT THERE IS NO CONFUSION BETWEEN (FOR EXAMPLE) A "REGULAR             
      * OVERAGE" (NON-PBS) THAT WOULD NEED TO BE LOOKED AT VERSUS A             
      * PBS OVERAGE, WHICH DOES NOT NEED TO BE LOOKED AT.                       
      *----------------------------------------------------------------*        
       3550-DELETE-LNE-TRBL.                                                    
                                                                                
           EXEC SQL                                                             
                DELETE                                                          
                  FROM TTRBNTF                                                  
                  WHERE PO_NBR         = :DK-PO-NBR                             
                    AND REC_SEQ_NBR    = :DK-REC-SEQ-NBR                        
                    AND OPER_UNT_ID    = :DK-OPER-UNT-ID                        
                    AND FCLTY_ID       = :DK-FCLTY-ID                           
                    AND PO_LNE_NBR     > 0                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
               WHEN SQLCODE = +100                                              
                    CONTINUE                                                    
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                    PERFORM 9900-SOFT-ABORT                                     
               WHEN OTHER                                                       
                    MOVE '3550-DELETE-LNE-TRBL'                                 
                                     TO DP013-PARAGRAPH                         
                    MOVE 'UNSUCCESSFUL DELETE OF TTRBNTF'                       
                                     TO DP013-MESSAGE-TEXT (1)                  
                    MOVE SQLCA             TO DP013-SQLCA                       
                    MOVE 'TTRBNTF '        TO DP013-DB2-TABLE-NAME (1)          
                    SET DP013-DB2-ABEND                                         
                        DP013-XCTL-DISPLAY-RESTART                              
                                           TO TRUE                              
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * CALL TMKCS508 TO PERFORM RE-DISTRO FOR PACK BY STORE                    
      *----------------------------------------------------------------*        
                                                                                
       3600-PBS-TMKCS508.                                                       
           INITIALIZE TM508-COMMAREA.                                           
           MOVE DK-PO-NBR               TO TM508-PO-NBR.                        
           MOVE DK-REC-SEQ-NBR          TO TM508-REC-SEQ-NBR.                   
           MOVE DK-OPER-UNT-ID          TO TM508-OPER-UNT-ID.                   
           MOVE DK-FCLTY-ID             TO TM508-FCLTY-ID.                      
                                                                                
           EXEC CICS LINK                                                       
               PROGRAM  ('TMKCS508')                                            
               COMMAREA (TM508-COMMAREA)                                        
               RESP     (PV-CICS-RESP)                                          
           END-EXEC.                                                            
                                                                                
           IF PV-CICS-RESP = DFHRESP(NORMAL)                                    
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND                                             
                   DP013-NO-ROLLBACK   TO TRUE                                  
               MOVE '3600-PBS-TMKCS508'                                         
                                       TO DP013-PARAGRAPH                       
               MOVE 'UNSUCCESSFUL LINK TO TMKCS508'                             
                                       TO DP013-MESSAGE-TEXT (1)                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
147456     MOVE DK-PO-NBR            TO PV-MSG-KEY-PO                           
152476     MOVE DK-PO-LNE-NBR        TO PV-MSG-KEY-PO-LNE-NBR-N                 
147458     MOVE DK-REC-SEQ-NBR       TO PV-MSG-KEY-REC-SEQ-NBR-N                
147458     MOVE MESTDS-STR-NBR       TO PV-MSG-KEY-STR-NBR                      
028150     MOVE DP020-USERID         TO PV-MSG-USER-ID                          
028150     MOVE DK-OPER-UNT-ID       TO PV-MSG-OPER-ID                          
           EVALUATE TRUE                                                        
           WHEN TM508-RETURN-CODE = ZERO                                        
                SET PS-REDISTRO-OK                                              
                    PS-ACKNOW-SUCCESS                                           
                    PS-REDISTRO-SUCCESS     TO TRUE                             
150065     WHEN TM508-RETURN-CODE = 11                                          
146599          INITIALIZE PV-CICS-MSG-AREA                                     
146600          STRING 'TMKCS550-'                                              
146601                 '3600-PBS-TMKCS508'                                      
146602                               DELIMITED BY SIZE                          
146603                               INTO PV-CICS-MSG-AREA                      
146604          PERFORM 9903-WRITE-CICS-LOG                                     
146599          INITIALIZE PV-CICS-MSG-AREA                                     
146600          STRING PV-MSG-KEYS                                              
146601                 PV-MSG-KEYS-1                                            
146602                               DELIMITED BY SIZE                          
146603                               INTO PV-CICS-MSG-AREA                      
146604          PERFORM 9903-WRITE-CICS-LOG                                     
151955          MOVE PC-TSYMSG-03687        TO DP020-MSG-NUMBER                 
151969          SET PS-ERROR                                                    
151970              DP020-MSG-FATAL         TO TRUE                             
                MOVE '3600-PBS-TMKCS508'    TO DP013-PARAGRAPH                  
151971          MOVE 'PROGRAM EXIT CALL'    TO DP013-MESSAGE-TEXT (1)           
151973          PERFORM DP013-0000-PROCESS-ABEND                                
150065     WHEN TM508-RETURN-CODE = 12                                          
146599          INITIALIZE PV-CICS-MSG-AREA                                     
146600          STRING 'TMKCS550-'                                              
146601                 '3600-PBS-TMKCS508'                                      
146602                               DELIMITED BY SIZE                          
146603                               INTO PV-CICS-MSG-AREA                      
146604          PERFORM 9903-WRITE-CICS-LOG                                     
146599          INITIALIZE PV-CICS-MSG-AREA                                     
146600          STRING PV-MSG-KEYS                                              
146601                 PV-MSG-KEYS-1                                            
146602                               DELIMITED BY SIZE                          
146603                               INTO PV-CICS-MSG-AREA                      
146604          PERFORM 9903-WRITE-CICS-LOG                                     
151955          MOVE PC-TSYMSG-03688        TO DP020-MSG-NUMBER                 
151969          SET PS-ERROR                                                    
151970              DP020-MSG-FATAL         TO TRUE                             
                MOVE '3600-PBS-TMKCS508'    TO DP013-PARAGRAPH                  
151971          MOVE 'PROGRAM EXIT CALL'    TO DP013-MESSAGE-TEXT (1)           
151973          PERFORM DP013-0000-PROCESS-ABEND                                
150065     WHEN TM508-RETURN-CODE = 13                                          
146599          INITIALIZE PV-CICS-MSG-AREA                                     
146600          STRING 'TMKCS550-'                                              
146601                 '3600-PBS-TMKCS508'                                      
146602                               DELIMITED BY SIZE                          
146603                               INTO PV-CICS-MSG-AREA                      
146604          PERFORM 9903-WRITE-CICS-LOG                                     
146599          INITIALIZE PV-CICS-MSG-AREA                                     
146600          STRING PV-MSG-KEYS                                              
146601                 PV-MSG-KEYS-1                                            
146602                               DELIMITED BY SIZE                          
146603                               INTO PV-CICS-MSG-AREA                      
146604          PERFORM 9903-WRITE-CICS-LOG                                     
151955          MOVE PC-TSYMSG-00984        TO DP020-MSG-NUMBER                 
151969          SET PS-ERROR                                                    
151970              DP020-MSG-WARNING       TO TRUE                             
                MOVE '3600-PBS-TMKCS508'    TO DP013-PARAGRAPH                  
151971          MOVE 'PROGRAM EXIT CALL'    TO DP013-MESSAGE-TEXT (1)           
151973          PERFORM DP013-0000-PROCESS-ABEND                                
           WHEN OTHER                                                           
               SET PS-REDISTRO-NOT-OK                                           
                   PS-REDISTRO-FAILED       TO TRUE                             
               EVALUATE TRUE                                                    
               WHEN TM508-SOFT-ABORT                                            
               WHEN TM508-CROAK                                                 
                   PERFORM 9900-SOFT-ABORT                                      
               WHEN OTHER                                                       
                   MOVE TM508-SEVERITY-IND  TO DP020-MSG-SEVERITY-IND           
                   MOVE +1096               TO DP020-MSG-NUMBER                 
               END-EVALUATE                                                     
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * FOR ALLOCATING TO PBS, GET THE PACKSLIP INFORMATION.                    
      ******************************************************************        
       3700-PREPARE-FOR-PBS-ALLOC.                                              
                                                                                
           SET PS-MORE-PKITEM-REC         TO TRUE.                              
                                                                                
           PERFORM 3710-OPEN-AUTO-PKITEM-CSR.                                   
                                                                                
           IF PS-NOT-DONE                                                       
              PERFORM 3720-FETCH-TPKITEM                                        
                UNTIL PS-NO-MORE-PKITEM-REC OR PS-DONE                          
           END-IF.                                                              
                                                                                
           PERFORM 3730-CLOSE-AUTO-PKITEM-CSR.                                  
                                                                                
      *----------------------------------------------------------------*        
      *    OPEN AUTO-PKITEM-CSR.                                                
      *----------------------------------------------------------------*        
       3710-OPEN-AUTO-PKITEM-CSR.                                               
                                                                                
           EXEC SQL                                                             
                OPEN PKSLIP-ITEM-CURSOR                                         
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
              SET PS-DONE                   TO TRUE                             
              MOVE '3710-OPEN-AUTO-PKITEM-CSR'                                  
                                            TO DP013-PARAGRAPH                  
              MOVE 'UNSUCCESSFUL OPEN OF TPKITEM'                               
                                            TO DP013-MESSAGE-TEXT (1)           
              MOVE SQLCA                    TO DP013-SQLCA                      
              MOVE 'TPKITEM'                TO DP013-DB2-TABLE-NAME (1)         
              SET DP013-DB2-ABEND                                               
                  DP013-XCTL-DISPLAY-RESTART                                    
                                            TO TRUE                             
              PERFORM DP013-0000-PROCESS-ABEND                                  
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    SELECT ALL FROM TPKSLIP TO PREPARE TO VERSION IT.   ALL THAT         
      *    WILL BE DIFFERENT WILL BE THE STORE DEST NUMBER.                     
      *----------------------------------------------------------------*        
       3720-FETCH-TPKITEM.                                                      
                                                                                
               EXEC SQL                                                         
                   FETCH  PKSLIP-ITEM-CURSOR                                    
                    INTO :PV-PKSL-ITM-QTY,                                      
                         :PKITEM-PKSL-LNE-NBR                                   
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                        PERFORM 3800-PREPARE-TRECDIS-FOR-PBS                    
                   WHEN SQLCODE = +100                                          
                        SET PS-NO-MORE-PKITEM-REC                               
                                            TO TRUE                             
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                        SET PS-DONE         TO TRUE                             
                        PERFORM 9900-SOFT-ABORT                                 
                                                                                
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE  NOT = ZERO                                     
                        SET PS-DONE         TO TRUE                             
                        MOVE '3720-FETCH-TPKITEM'                               
                                            TO DP013-PARAGRAPH                  
                        MOVE 'UNSUCCESSFUL FETCH OF TPKITEM'                    
                                            TO DP013-MESSAGE-TEXT (1)           
      *                 MOVE DK-PO-NBR      TO PV-MSG-KEY-PO                    
      *                 MOVE PKSLIP-STR-DEST-NBR                                
      *                                     TO PV-MSG-KEY-STR-NBR               
      **                MOVE PV-MSG-KEYS    TO DP013-MESSAGE-TEXT (2)           
                        MOVE SQLCA          TO DP013-SQLCA                      
                        MOVE 'TPKITEM'      TO DP013-DB2-TABLE-NAME (1)         
                        SET DP013-DB2-ABEND                                     
                            DP013-XCTL-DISPLAY-RESTART                          
                                            TO TRUE                             
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE.                                                    
                                                                                
      *----------------------------------------------------------------*        
      *----------------------------------------------------------------*        
       3730-CLOSE-AUTO-PKITEM-CSR.                                              
                                                                                
           EXEC SQL                                                             
                CLOSE PKSLIP-ITEM-CURSOR                                        
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
              SET PS-DONE                   TO TRUE                             
              MOVE '3730-CLOSE-AUTO-PKITEM' TO DP013-PARAGRAPH                  
              MOVE 'UNSUCCESSFUL CLOSE OF TPKITEM'                              
                                            TO DP013-MESSAGE-TEXT (1)           
              MOVE SQLCA                    TO DP013-SQLCA                      
              MOVE 'TPKITEM' TO DP013-DB2-TABLE-NAME (1)                        
              SET DP013-DB2-ABEND                                               
                  DP013-XCTL-DISPLAY-RESTART                                    
                                            TO TRUE                             
              PERFORM DP013-0000-PROCESS-ABEND                                  
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * IF EVERYTHING IS OK WITH THE PBS OVERAGE, WE WILL INSERT RECORDS        
      * INTO TRECDIS.  NOTE: THIS IS A COPY FROM PROGRAM RCKCS213 THAT          
      * DOES THIS DURING TRANSCRIPTION.                                         
      ******************************************************************        
       3800-PREPARE-TRECDIS-FOR-PBS.                                            
                                                                                
           INITIALIZE DCLTRECDIS.                                               
                                                                                
           MOVE DK-PO-NBR          TO RECDIS-ORD-NBR.                           
           MOVE PKITEM-PKSL-LNE-NBR                                             
                                   TO RECDIS-ORD-LNE-NBR.                       
           MOVE DK-REC-SEQ-NBR     TO RECDIS-REC-SEQ-NBR.                       
           MOVE DK-OPER-UNT-ID     TO RECDIS-OPER-UNT-ID.                       
           MOVE PC-CURRENT-PROGRAM-NAME                                         
                                   TO RECDIS-CRE-UID.                           
           MOVE 'A'                TO RECDIS-VER-STATUS-CDE.                    
           MOVE PV-PKSL-ITM-QTY    TO RECDIS-EXPTED-ITM-QTY                     
                                      RECDIS-VERIFY-ITM-QTY.                    
           MOVE MESTDS-STR-NBR     TO RECDIS-STR-NBR.                           
           MOVE 1                  TO RECDIS-FCLTY-ID.                          
           MOVE 1                  TO RECDIS-STR-FCLTY-ID.                      
                                                                                
           MOVE PC-DEFAULT-DATE    TO RECDIS-AP-PRC-DTE.                        
                                                                                
           PERFORM 3810-INSERT-TRECDIS.                                         
                                                                                
      ******************************************************************        
      **   INSERT TRECDIS ONE LINE AT A TIME FOR PBS FLOWBACK                   
      ******************************************************************        
       3810-INSERT-TRECDIS.                                                     
                                                                                
               EXEC SQL                                                         
                    INSERT INTO TRECDIS                                         
                           (ORD_NBR                                             
                         ,  ORD_LNE_NBR                                         
                         ,  REC_SEQ_NBR                                         
                         ,  OPER_UNT_ID                                         
                         ,  FCLTY_ID                                            
                         ,  CRE_TMST                                            
                         ,  CRE_UID                                             
                         ,  VER_STATUS_CDE                                      
                         ,  EXPTED_ITM_QTY                                      
                         ,  VERIFY_ITM_QTY                                      
                         ,  AP_VERIFY_ITM_QTY                                   
                         ,  STR_NBR                                             
                         ,  STR_FCLTY_ID                                        
                         ,  PLAN_ITM_QTY                                        
                         ,  AP_PRC_CDE                                          
                         ,  AP_PRC_DTE)                                         
                    VALUES (:RECDIS-ORD-NBR                                     
                         ,  :RECDIS-ORD-LNE-NBR                                 
                         ,  :RECDIS-REC-SEQ-NBR                                 
                         ,  :RECDIS-OPER-UNT-ID                                 
                         ,  :RECDIS-FCLTY-ID                                    
                         ,  CURRENT TIMESTAMP                                   
                         ,  :RECDIS-CRE-UID                                     
                         ,  :RECDIS-VER-STATUS-CDE                              
                         ,  :RECDIS-EXPTED-ITM-QTY                              
                         ,  :RECDIS-VERIFY-ITM-QTY                              
                         ,  :RECDIS-AP-VERIFY-ITM-QTY                           
                         ,  :RECDIS-STR-NBR                                     
                         ,  :RECDIS-STR-FCLTY-ID                                
                         ,  :RECDIS-PLAN-ITM-QTY                                
                         ,  :RECDIS-AP-PRC-CDE                                  
                         ,  :RECDIS-AP-PRC-DTE)                                 
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                        CONTINUE                                                
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                        PERFORM 9900-SOFT-ABORT                                 
                   WHEN OTHER                                                   
                       SET PS-DONE       TO TRUE                                
                       MOVE '3810-INSERT-TRECDIS'                               
                                         TO DP013-PARAGRAPH                     
                       MOVE 'DB2 ERROR INSERTING A PACK BY STR RECDIS'          
                                         TO DP013-MESSAGE-TEXT (1)              
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'TRECDIS'    TO DP013-DB2-TABLE-NAME (1)            
                       SET DP013-DB2-ABEND                                      
                                         TO TRUE                                
                       SET DP013-XCTL-DISPLAY-RESTART                           
                                         TO TRUE                                
                       PERFORM DP013-0000-PROCESS-ABEND                         
                   END-EVALUATE.                                                
                                                                                
                                                                                
      ******************************************************************        
      * THIS IS PARAGRAPH IS FOR NON-PBS RECEIVERS FOR REDISTRO'ING AND         
      * ALL RECEIVERS FOR AUTO RESOLVE, AND AUTO ACKNOWLEDGE OF                 
      * OF THE RECEIVER (IF APPLICABLE)                                         
      ******************************************************************        
       4000-START-AUTO-PROCESS.                                                 
                                                                                
           SET PS-MORE-RECORD TO TRUE.                                          
                                                                                
      *FOR AUTO RESOLVING AND ACKNOWLEDGING, PBS GO INTO THIS PARA-             
      *GRAPH.  FOR REDISTRO'ING, PBS WILL NOT GET INTO THIS                     
      *PARAGRAPH.                                                               
           PERFORM 4010-GET-FIRST-NOTIF-NBR.                                    
                                                                                
           IF PV-REC-PKGNG-ACK-NONPK                                            
              PERFORM 4100-OPEN-AUTO-ALL-CSR                                    
              IF PS-NOT-DONE                                                    
                  PERFORM 4200-FETCH-AUTO-ALL-CSR                               
                     UNTIL PS-NO-MORE-RECORD OR PS-DONE                         
              END-IF                                                            
              PERFORM 4300-CLOSE-AUTO-ALL-CSR                                   
           ELSE                                                                 
              SET PS-NOT-DONE           TO TRUE                                 
              PERFORM 4400-OPEN-PRPK-ALL-CSR                                    
                                                                                
              IF PS-NOT-DONE                                                    
                  PERFORM 4500-FETCH-PRPK-ALL-CSR                               
                     UNTIL PS-NO-MORE-RECORD OR PS-DONE                         
              END-IF                                                            
                                                                                
               PERFORM 4600-CLOSE-PRPK-ALL-CSR                                  
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * GET THE FIRST LINE NUMBER'S NOTIF NUMBER TO BE ABLE TO PUT INTO         
      * RCT_AUTOACKNL_TRBL FOR AUTO ACKNOWLEDGMENT TRACKING.                    
      ******************************************************************        
       4010-GET-FIRST-NOTIF-NBR.                                                
                                                                                
           EXEC SQL                                                             
             SELECT   NOTIF_NBR                                                 
                     ,PO_LNE_NBR                                                
               INTO  :TRBNTF-NOTIF-NBR,                                         
                     :TRBNTF-PO-LNE-NBR                                         
               FROM TTRBNTF T                                                   
                   ,TTRBRSN B                                                   
               WHERE T.TRBL_RSN_CDE    = B.TRBL_RSN_CDE                         
                 AND B.AUTOACKNL_IND   = :PC-Y                                  
                 AND T.PO_NBR          = :DK-PO-NBR                             
                 AND T.REC_SEQ_NBR     = :DK-REC-SEQ-NBR                        
                 AND T.RESOLVED_IND    IN ('N','I')                             
                 AND PO_LNE_NBR =                                               
                    (SELECT MIN(ORD_LNE_NBR)                                    
                    FROM TRECITM R                                              
                    WHERE T.PO_NBR = R.ORD_NBR                                  
                      AND T.REC_SEQ_NBR =  R.REC_SEQ_NBR                        
                      AND VER_STATUS_CDE = :PC-A)                               
                    GROUP BY NOTIF_NBR                                          
                            ,PO_NBR                                             
                            ,REC_SEQ_NBR                                        
                            ,PO_LNE_NBR                                         
                  FETCH FIRST 1 ROWS ONLY                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
             WHEN SQLCODE = +100                                                
                  CONTINUE                                                      
             WHEN SQLWARN0 NOT = SPACES                                         
             WHEN SQLCODE < +0                                                  
             WHEN SQLCODE > +0                                                  
                  MOVE '4010-GET-FIRST-NOTIF-NBR'                               
                                        TO DP013-PARAGRAPH                      
                  MOVE 'UNSUCCESSFUL SELECT ON PO ITEM'                         
                                         TO DP013-MESSAGE-TEXT (1)              
                  MOVE SQLCA           TO DP013-SQLCA                           
                  MOVE 'TRECITM '      TO DP013-DB2-TABLE-NAME (1)              
                  SET DP013-DB2-ABEND                                           
                      DP013-XCTL-DISPLAY-RESTART                                
                                         TO TRUE                                
                  PERFORM DP013-0000-PROCESS-ABEND                              
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * OPEN CURSOR TO GET ALL LINES/PACKS TO RE-DISTRO                         
      ******************************************************************        
       4100-OPEN-AUTO-ALL-CSR.                                                  
                                                                                
           IF PS-AUTO-REDISTRO-ALL                                              
              EXEC SQL                                                          
                  OPEN AUTO-ALL-CSR                                             
              END-EXEC                                                          
           ELSE                                                                 
              EXEC SQL                                                          
                  OPEN AUTO-ALL-CSR-ACK                                         
              END-EXEC.                                                         
                                                                                
           IF SQLCODE NOT = +0                                                  
               SET PS-DONE                   TO TRUE                            
               MOVE SQLCA                    TO TM500-SQLCA                     
               MOVE SQLCODE                  TO TM500-SQLCODE                   
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *GET NON-PACK INFORMATION TO SEND TO TROUBLE PROGRAM TMKCS520             
      ******************************************************************        
       4200-FETCH-AUTO-ALL-CSR.                                                 
                                                                                
           IF PS-AUTO-REDISTRO-ALL                                              
           EXEC SQL                                                             
                FETCH AUTO-ALL-CSR                                              
                 INTO :RECITM-ORD-LNE-NBR                                       
                     ,:RECITM-PKSL-ITM-QTY                                      
                     ,:RECITM-DTL-CK-ITM-QTY                                    
                     ,:RECITM-PREPK-ID                                          
                     ,:RECITM-DMG-ITM-QTY                                       
                     ,:RECITM-RTV-QTY                                           
           END-EXEC                                                             
           ELSE                                                                 
           EXEC SQL                                                             
                FETCH AUTO-ALL-CSR-ACK                                          
                 INTO :RECITM-ORD-LNE-NBR                                       
                     ,:RECITM-PKSL-ITM-QTY                                      
                     ,:RECITM-DTL-CK-ITM-QTY                                    
                     ,:RECITM-PREPK-ID                                          
                     ,:RECITM-DMG-ITM-QTY                                       
                     ,:RECITM-RTV-QTY                                           
                     ,:TRBNTF-NOTIF-NBR                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                    PERFORM 4210-AUTO-RSLV-ACK-REDISTO                          
                    SET TM500-LINE-LEVEL       TO TRUE                          
               WHEN SQLCODE = +100                                              
                  SET PS-NO-MORE-RECORD TO TRUE                                 
               WHEN OTHER                                                       
                    SET PS-DONE                 TO TRUE                         
                    MOVE SQLCA                  TO TM500-SQLCA                  
                    MOVE SQLCODE                TO TM500-SQLCODE                
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *EXECUTES THE AUTO ACKNOWLEDGE PROCESS AND IF NEEDED THE                  
      *REDISTRO PROCESS.                                                        
      *----------------------------------------------------------------*        
       4210-AUTO-RSLV-ACK-REDISTO.                                              
                                                                                
      *AUTO REDISTRO ALL LINES/PACKS OF PO/RECEIVER IF THE TROUBLE              
      *IS SET UP REQUIRING REDISTRO'ING.                                        
           IF PS-AUTO-REDISTRO-ALL                                              
              IF TM500-AUTO-ACKNOW-NOT-FOUND                                    
                 CONTINUE                                                       
              ELSE                                                              
              IF PV-REDISTRO-TRBL AND                                           
                 PS-NO-DISTRO-OK                                                
                 SET PS-REDISTRO-FAILED                                         
                      TM500-CREATE-AUTO          TO TRUE                        
                 PERFORM 5300-POPULATE-COMMON-FIELDS                            
                 PERFORM 5000-PREPARE-520-REDISTRO                              
                 PERFORM 5200-LINK-TO-TMKCS520                                  
                 IF TM500-CALL-SUCCESSFUL         OR                            
                    TM500-DUPLICATE-TROUBLE       OR                            
                    TM500-TTRBNTF-UPDATE-NOT-FND  OR                            
                    TM500-INVALID-PRORATION                                     
                       SET PS-REDISTRO-SUCCESS      TO TRUE                     
                  ELSE                                                          
                     IF TM500-DIDNT-ALLOC-DUE-TO-TRBL OR                        
                        TM500-PREPACK-ERROR                                     
                         SET PS-TROUBLE-ALLOC-YES                               
                             PS-NO-MORE-RECORD                                  
                             PS-REDISTRO-NOT-OK                                 
                             PS-REDISTRO-FAILED   TO TRUE.                      
                                                                                
      *AUTO RESOLVE ALL LINES OF THE RECEIVER THAT HAS THE SAME                 
      *TROUBLE.                                                                 
             IF PS-AUTO-RSLV-ACK-ALL AND                                        
                PS-TROUBLE-ALLOC-NO                                             
                 SET  TM500-RESOLVE-MANUAL                                      
                      PS-RESOLVE-FAILED                                         
                      PS-ACKNOW-FAILED            TO TRUE                       
                 PERFORM 5300-POPULATE-COMMON-FIELDS                            
                 PERFORM 5100-PREPARE-520-ACKNOW                                
                 PERFORM 5200-LINK-TO-TMKCS520                                  
                 IF TM500-CALL-SUCCESSFUL         OR                            
                    TM500-DUPLICATE-TROUBLE       OR                            
                    TM500-TTRBNTF-UPDATE-NOT-FND  OR                            
                    TM500-INVALID-PRORATION                                     
                       SET PS-RESOLVE-SUCCESS     TO TRUE                       
                       IF PS-WAIT-NOT-UPDATED                                   
                          IF PS-NOT-DONE                                        
                             PERFORM 6000-UPD-AUTO-LOG                          
                             SET PS-WAIT-UPDATED    TO TRUE                     
                          END-IF                                                
                       END-IF                                                   
                 END-IF.                                                        
                                                                                
                                                                                
      *  AUTO ACKNOWLEDGE IF REDISTRO WAS NEEDED AND PASSED.                    
                                                                                
             IF PS-AUTO-RSLV-ACK-ALL                                            
                IF PV-REDISTRO-TRBL AND                                         
                   (PS-REDISTRO-FAILED OR                                       
                    PS-REDISTRO-NOT-OK)                                         
                   CONTINUE                                                     
                ELSE                                                            
                   IF PS-NO-DISTRO-OK                                           
                      SET  TM500-ACKNOWLEDGE          TO TRUE                   
                      PERFORM 5300-POPULATE-COMMON-FIELDS                       
                      PERFORM 5100-PREPARE-520-ACKNOW                           
                      PERFORM 5200-LINK-TO-TMKCS520                             
                      IF TM500-CALL-SUCCESSFUL         OR                       
                         TM500-DUPLICATE-TROUBLE       OR                       
                         TM500-TTRBNTF-UPDATE-NOT-FND  OR                       
                         TM500-INVALID-PRORATION                                
                         SET PS-ACKNOW-SUCCESS     TO TRUE                      
                         IF PV-REC-PKGNG-PREPACK                                
                             SET PS-NO-MORE-RECORD TO TRUE.                     
                                                                                
           IF PS-ACKNOW-SUCCESS AND                                             
              PS-CMPLTD-NOT-UPDATED                                             
              PERFORM 6000-UPD-AUTO-LOG                                         
              SET PS-CMPLTD-UPDATED                TO TRUE.                     
                                                                                
      *----------------------------------------------------------------*        
      *----------------------------------------------------------------*        
       4300-CLOSE-AUTO-ALL-CSR.                                                 
                                                                                
           IF PS-AUTO-REDISTRO-ALL                                              
              EXEC SQL                                                          
                   CLOSE AUTO-ALL-CSR                                           
              END-EXEC                                                          
           ELSE                                                                 
              EXEC SQL                                                          
                 CLOSE AUTO-ALL-CSR-ACK                                         
              END-EXEC.                                                         
                                                                                
           IF SQLCODE NOT = +0                                                  
               SET PS-DONE                   TO TRUE                            
               MOVE SQLCA                    TO TM500-SQLCA                     
               MOVE SQLCODE                  TO TM500-SQLCODE                   
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * OPEN CURSOR TO GET ALL PACKS TO RE-DISTRO                               
      ******************************************************************        
       4400-OPEN-PRPK-ALL-CSR.                                                  
                                                                                
           EXEC SQL                                                             
                OPEN PREPK-ALL-CSR                                              
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               SET PS-DONE                   TO TRUE                            
               MOVE SQLCA                    TO TM500-SQLCA                     
               MOVE SQLCODE                  TO TM500-SQLCODE                   
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * FETCH PACK INFORMATION TO SEND TO TROUBLE PROGRAM TMKCS520              
      ******************************************************************        
       4500-FETCH-PRPK-ALL-CSR.                                                 
                                                                                
           EXEC SQL                                                             
                FETCH PREPK-ALL-CSR                                             
                 INTO :RECITM-ORD-LNE-NBR                                       
                     ,:RECITM-PKSL-ITM-QTY                                      
                     ,:RECITM-DTL-CK-ITM-QTY                                    
                     ,:RECITM-PREPK-ID                                          
                     ,:RECITM-DMG-ITM-QTY                                       
                     ,:RECITM-RTV-QTY                                           
                     ,:RECITM-PREPK-ID                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                    PERFORM 4210-AUTO-RSLV-ACK-REDISTO                          
                    SET  TM500-PACK-LEVEL    TO TRUE                            
               WHEN SQLCODE = +100                                              
                    SET PS-NO-MORE-RECORD TO TRUE                               
               WHEN OTHER                                                       
                    SET PS-DONE              TO TRUE                            
                    MOVE SQLCA               TO TM500-SQLCA                     
                    MOVE SQLCODE             TO TM500-SQLCODE                   
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *----------------------------------------------------------------*        
       4600-CLOSE-PRPK-ALL-CSR.                                                 
                                                                                
           EXEC SQL                                                             
                CLOSE PREPK-ALL-CSR                                             
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               SET PS-DONE  TO TRUE                                             
               MOVE SQLCA   TO TM500-SQLCA                                      
               MOVE SQLCODE TO TM500-SQLCODE                                    
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * CALL TMKCS520 TO PERFORM RE-DISTRO FOR BULK, CASE, SINGLE               
      * AND PREPACKS.                                                           
      *----------------------------------------------------------------*        
       5000-PREPARE-520-REDISTRO.                                               
                                                                                
           SET  TM500-CREATE-AUTO                                               
                TM500-NO-DETAILS                                                
                TM500-REDISTRO-YES       TO TRUE.                               
                                                                                
           IF RECITM-DTL-CK-ITM-QTY > ZERO                                      
              MOVE RECITM-DTL-CK-ITM-QTY TO PV-BASE-DISTRO-QTY                  
           ELSE                                                                 
              MOVE RECITM-PKSL-ITM-QTY   TO PV-BASE-DISTRO-QTY                  
           END-IF.                                                              
                                                                                
           COMPUTE PV-DISTRO-QTY = PV-BASE-DISTRO-QTY -                         
                                  (RECITM-DMG-ITM-QTY + RECITM-RTV-QTY).        
                                                                                
           PERFORM 6150-GET-PURITM.                                             
           IF PV-NBR-OF-PIC-AD > 0                                              
              MOVE 'Y'                   TO TM500-AD-SW                         
           ELSE                                                                 
              MOVE 'N'                   TO TM500-AD-SW.                        
                                                                                
           MOVE PC-CURRENT-PROGRAM-NAME  TO TM500-NOTIF-UID.                    
           MOVE DK-REC-SEQ-NBR           TO TM500-REC-SEQ-NBR.                  
                                                                                
           IF PV-REC-PKGNG-ACK-NONPK                                            
              MOVE RECITM-ORD-LNE-NBR      TO TM500-PO-LNE-NBR                  
              SET TM500-LINE-LEVEL         TO TRUE                              
              MOVE PV-DISTRO-QTY           TO TM500-UNITS-TO-ALLOC              
           ELSE                                                                 
             IF PV-REC-PKGNG-PREPACK                                            
                MOVE RECITM-ORD-LNE-NBR    TO TM500-PO-LNE-NBR                  
                MOVE PV-DISTRO-QTY         TO TM500-UNITS-TO-ALLOC              
                SET TM500-PACK-LEVEL       TO TRUE.                             
                                                                                
           SET PS-REDISTRO-OK              TO TRUE.                             
                                                                                
      *----------------------------------------------------------------*        
      *    SET APPROPRIATE FIELDS FOR ACKNOWLEDGE PROCESS.             *        
      *----------------------------------------------------------------*        
       5100-PREPARE-520-ACKNOW.                                                 
                                                                                
           MOVE TRBNTF-NOTIF-NBR        TO TM500-NOTIF-NBR.                     
                                                                                
             IF TM500-ACKNOWLEDGE                                               
                SET  TM500-ADD-DETAILS  TO TRUE                                 
                IF PV-REDISTRO-TRBL                                             
                   MOVE 'AUTO ACKNOWLEDGED AND AUTO REDISTRO'                   
                                        TO TM500-FREEFORM-TEXT                  
                ELSE                                                            
                   MOVE 'AUTO ACKNOWLEDGED'                                     
                                        TO TM500-FREEFORM-TEXT.                 
                                                                                
           IF PV-REC-PKGNG-PREPACK                                              
              MOVE ZERO                 TO TM500-PREPK-ID.                      
                                                                                
      *----------------------------------------------------------------*        
      *    LINK TO PROGRAM TMKCS520                                  *          
      *----------------------------------------------------------------*        
       5200-LINK-TO-TMKCS520.                                                   
           EXEC CICS                                                            
               LINK PROGRAM  (PC-TROUBLE-RTN)                                   
                    COMMAREA (TM500-FIELDS)                                     
                    LENGTH   (PC-TROUBLE-RTN-LENGTH)                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN TM500-CALL-SUCCESSFUL                                       
               WHEN TM500-DUPLICATE-TROUBLE                                     
               WHEN TM500-TTRBNTF-UPDATE-NOT-FND                                
               WHEN TM500-RESOLVE-UPDATE-NOT-FND                                
                    SET PS-REDISTRO-OK TO TRUE                                  
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    SET PS-REDISTRO-NOT-OK TO TRUE                              
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    POPULATE COMMON FIELDS FOR TMKCS520 MODULE.               *          
      *----------------------------------------------------------------*        
       5300-POPULATE-COMMON-FIELDS.                                             
                                                                                
                                                                                
           MOVE DK-PO-NBR                  TO TM500-PO-NBR.                     
           MOVE DK-OPER-UNT-ID             TO TM500-OPER-UNT-ID.                
           MOVE DK-FCLTY-ID                TO TM500-FCLTY-ID.                   
           MOVE PC-CURRENT-PROGRAM-NAME    TO TM500-NOTIF-UID.                  
           MOVE PV-TRBL-NEEDS-REDISTO      TO TM500-TRBL-RSN-CDE.               
           MOVE RECEIV-PKGNG-CDE           TO TM500-PKGNG-CDE.                  
           MOVE RECITM-ORD-LNE-NBR         TO TM500-PO-LNE-NBR                  
           MOVE PV-DISTRO-QTY              TO TM500-UNITS-TO-ALLOC              
                                                                                
           IF PV-REC-PKGNG-ACK-NONPK                                            
              SET  TM500-LINE-LEVEL        TO TRUE                              
           ELSE                                                                 
             IF PV-REC-PKGNG-PREPACK                                            
                MOVE RECITM-PREPK-ID       TO TM500-PREPK-ID                    
                SET TM500-PACK-LEVEL       TO TRUE.                             
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    DELETE ENTIRE TRECDIS FOR PO / RECEIVER BEFORE DOING A               
      *    REDISTRO                                                             
      *----------------------------------------------------------------*        
       5400-DELETE-TRECDIS.                                                     
                                                                                
           EXEC SQL                                                             
                DELETE                                                          
                  FROM TRECDIS                                                  
                  WHERE ORD_NBR        = :DK-PO-NBR                             
                    AND REC_SEQ_NBR    = :DK-REC-SEQ-NBR                        
                    AND OPER_UNT_ID    = :DK-OPER-UNT-ID                        
                    AND FCLTY_ID       = :DK-FCLTY-ID                           
                    AND VER_STATUS_CDE = 'A'                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
               WHEN SQLCODE = +100                                              
                    CONTINUE                                                    
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                    PERFORM 9900-SOFT-ABORT                                     
               WHEN OTHER                                                       
                    MOVE '5400-DELETE-TRECDIS'                                  
                                     TO DP013-PARAGRAPH                         
                    MOVE 'UNSUCCESSFUL DELETE OF TRECDIS'                       
                                     TO DP013-MESSAGE-TEXT (1)                  
                    MOVE SQLCA             TO DP013-SQLCA                       
                    MOVE 'TRECDIS '        TO DP013-DB2-TABLE-NAME (1)          
                    SET DP013-DB2-ABEND                                         
                        DP013-XCTL-DISPLAY-RESTART                              
                                           TO TRUE                              
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    DETERMINE WHAT SWITCHES TO SET FOR CALLING THE LOGGING               
      *    PROGRAM OF TMKUP501.  THAT PROGRAM CONTROLS DB2 TABLE                
      *    RCT_AUTOACKNL_TRBL.                                                  
      *    IT WILL SEND THE LAST NOTIF-NBR THAT WAS RETRIEVED BY                
      *    THE INITIAL CURSOR.                                                  
      *----------------------------------------------------------------*        
       6000-UPD-AUTO-LOG.                                                       
                                                                                
           MOVE DK-PO-NBR                  TO TM007-PO-NBR.                     
           MOVE DK-REC-SEQ-NBR             TO TM007-REC-SEQ-NBR.                
           MOVE DK-OPER-UNT-ID             TO TM007-OPER-UNT-ID.                
           MOVE TM500-TRBL-RSN-CDE         TO TM007-TRBL-RSN-CDE.               
           MOVE TM500-NOTIF-NBR            TO TM007-NOTIF-NBR.                  
                                                                                
      **BECAUSE OF A TROUBLE BUG, YOU CAN HAVE MORE THAN ONE NO DISTRO          
      **TROUBLE CREATED FOR THE SAME LINE BUT ONE BE IN RESOLVED STATUS         
      **AND ONE BEING IN NEW STATUS.  WE ONLY WANT TO PUT OUT ONE               
      **OCCURANCE OF THE TROUBLE TO THE ACKNOWLEDGE TABLE PER                   
      **PO/RECEIVER.                                                            
           IF PS-RESOLVE-SUCCESS                                                
              SET TM007-INSERT                                                  
                  TM007-WAIT-STATUS        TO TRUE                              
              PERFORM 6001-CHECK-ACKNOW-TABLE.                                  
                                                                                
                                                                                
           IF PS-ACKNOW-SUCCESS                                                 
              SET TM007-UPDATE                                                  
                  PS-INSERT-STATUS                                              
                  TM007-CMPLT-STATUS       TO TRUE                              
              IF PV-COUNT-WAIT-STATUS = 1                                       
                  PERFORM 6002-GET-CURRENT-NOTIF                                
                  MOVE TRBNTF-NOTIF-NBR    TO TM007-NOTIF-NBR.                  
                                                                                
                                                                                
           IF (PS-RESOLVE-SUCCESS OR                                            
               PS-ACKNOW-SUCCESS) AND                                           
              PS-INSERT-STATUS                                                  
               PERFORM 6100-LINK-TO-TMKUP501.                                   
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    COUNT NUMBER OF ACKNOWLEDGEMENTS.  ONLY ONE SHOULD BE IN             
      *   READY OR WAIT STATUS AT A TIME FOR A GIVEN PO/RECEIVER.               
      *----------------------------------------------------------------*        
       6001-CHECK-ACKNOW-TABLE.                                                 
           MOVE ZERO                   TO PV-COUNT-WAIT-STATUS.                 
                                                                                
           EXEC SQL                                                             
                 SELECT COUNT (*)                                               
                 INTO                                                           
                       :PV-COUNT-WAIT-STATUS                                    
                 FROM   TTRBNTF A                                               
                       ,RCT_AUTOACKNL_TRBL B                                    
                 WHERE  A.PO_NBR            = :DK-PO-NBR                        
                   AND  A.REC_SEQ_NBR       = :DK-REC-SEQ-NBR                   
                   AND  A.TRBL_RSN_CDE      = :TM500-TRBL-RSN-CDE               
                   AND  A.NOTIF_NBR         = B.NOTIF_NBR                       
                   AND  B.AUTOACKNL_STAT_CDE IN                                 
                                    ('WAIT', 'READY')                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                    IF PV-COUNT-WAIT-STATUS > 0                                 
                       SET PS-NO-INSERT-STATUS  TO TRUE                         
                    ELSE                                                        
                       SET PS-INSERT-STATUS     TO TRUE                         
                    END-IF                                                      
               WHEN SQLCODE = +100                                              
                    SET PS-INSERT-STATUS        TO TRUE                         
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                     SET PS-DONE                                                
                         TM500-DB2-CONTENTION-IN-ALLOC                          
                                             TO TRUE                            
                     MOVE SQLCODE            TO TM500-SQLCODE                   
                     MOVE SQLCA              TO TM500-SQLCA                     
               WHEN OTHER                                                       
                     SET PS-DONE                                                
                         TM500-DB2-UNRECOVERABLE-ERROR                          
                                             TO TRUE                            
                     MOVE SQLCODE            TO TM500-SQLCODE                   
                     MOVE SQLCA              TO TM500-SQLCA                     
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    LINK TO PROGRAM TMKCS501                                  *          
      *----------------------------------------------------------------*        
       6100-LINK-TO-TMKUP501.                                                   
                                                                                
           CALL PC-TMKUP501-RTN USING  DFHEIBLK                                 
                                     TM007-COMMAREA                             
                                     TM007-COMMUNICATIONS-AREA.                 
                                                                                
           EVALUATE TRUE                                                        
               WHEN  TM007-CALL-SUCCESSFUL                                      
               WHEN  TM007-SQLCODE = -803                                       
                    SET PS-LOG-AUTO-SUCCESS-OK                                  
                                           TO TRUE                              
               WHEN OTHER                                                       
                    MOVE '6000-LINK-TO-TMKUP501'                                
                                           TO DP013-PARAGRAPH                   
                    MOVE 'UNSUCCESSFUL CALL TO LOG AUTO RTN'                    
                                           TO DP013-MESSAGE-TEXT (1)            
                    MOVE TM007-SQLCODE                                          
                                           TO DP013-MESSAGE-TEXT (2)            
                    MOVE TM007-RETURNED-PARA                                    
                                           TO DP013-MESSAGE-TEXT (3)            
                    MOVE TM500-NOTIF-NBR                                        
                                           TO DP013-MESSAGE-TEXT (4)            
                    MOVE TM007-SQLCA       TO DP013-SQLCA                       
                    SET DP013-DB2-ABEND                                         
                        DP013-XCTL-DISPLAY-RESTART                              
                                           TO TRUE                              
                    PERFORM DP013-0000-PROCESS-ABEND                            
                                                                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    IF THERE IS ONLY ONE NOTIF-NBR ALREADY ON THE RCT_A* TABLE           
      *    THAN PASS THAT TO TMKUP501 MODULE TO UPDATE TO COMPLETE.             
      ******************************************************************        
       6002-GET-CURRENT-NOTIF.                                                  
                                                                                
           EXEC SQL                                                             
                 SELECT A.NOTIF_NBR                                             
                 INTO                                                           
                       :TRBNTF-NOTIF-NBR                                        
                 FROM   TTRBNTF A                                               
                       ,RCT_AUTOACKNL_TRBL B                                    
                 WHERE  A.PO_NBR            = :DK-PO-NBR                        
                   AND  A.REC_SEQ_NBR       = :DK-REC-SEQ-NBR                   
                   AND  A.TRBL_RSN_CDE      = :TM500-TRBL-RSN-CDE               
                   AND  A.NOTIF_NBR         = B.NOTIF_NBR                       
                   AND  B.AUTOACKNL_STAT_CDE IN                                 
                                    ('WAIT', 'READY')                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                    CONTINUE                                                    
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                     SET PS-DONE                                                
                         TM500-DB2-CONTENTION-IN-ALLOC                          
                                             TO TRUE                            
                     MOVE SQLCODE            TO TM500-SQLCODE                   
                     MOVE SQLCA              TO TM500-SQLCA                     
               WHEN OTHER                                                       
                    SET PS-DONE                                                 
                        TM500-DB2-UNRECOVERABLE-ERROR                           
                                             TO TRUE                            
                    MOVE SQLCODE            TO TM500-SQLCODE                    
                    MOVE SQLCA              TO TM500-SQLCA                      
                    MOVE '6002-GET-CURRENT-NOTIF'                               
                                     TO DP013-PARAGRAPH                         
                    MOVE 'UNSUCCESSFUL DELETE OF TRECDIS'                       
                                     TO DP013-MESSAGE-TEXT (1)                  
                    MOVE SQLCA             TO DP013-SQLCA                       
                    MOVE 'TRECDIS '        TO DP013-DB2-TABLE-NAME (1)          
                    SET DP013-DB2-ABEND                                         
                        DP013-XCTL-DISPLAY-RESTART                              
                                           TO TRUE                              
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *    DETERMINE IF PO HAS A PICTURED AD                                    
      ******************************************************************        
       6150-GET-PURITM.                                                         
                                                                                
                                                                                
           EXEC SQL                                                             
                SELECT COUNT(*)                                                 
                  INTO :PV-NBR-OF-PIC-AD                                        
                  FROM TPURCHS A,                                               
                       TPURITM B,                                               
                       TRECITM C                                                
                 WHERE A.PO_NBR         = :DK-PO-NBR                            
                   AND A.VER_STATUS_CDE = 'A'                                   
                   AND A.PO_NBR         = B.PO_NBR                              
                   AND A.PO_NBR         = C.ORD_NBR                             
                   AND B.PIC_ITM_IND    = 'Y'                                   
                   AND C.ORD_LNE_NBR    = B.APP_PO_LNE_NBR                      
                   AND C.PO_VER_NBR     = B.VER_NBR                             
                   AND C.VER_STATUS_CDE = 'A'                                   
                   AND C.REC_SEQ_NBR    = :DK-REC-SEQ-NBR                       
                                                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
               WHEN SQLCODE = +100                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < +0                                                
               WHEN SQLCODE > +0                                                
                    MOVE '6150-GET-PURITM'                                      
                                         TO DP013-PARAGRAPH                     
                    MOVE 'UNSUCCESSFUL SELECT ON PO ITEM'                       
                                         TO DP013-MESSAGE-TEXT (1)              
                    MOVE SQLCA           TO DP013-SQLCA                         
                    MOVE 'TPURITM '      TO DP013-DB2-TABLE-NAME (1)            
                    SET DP013-DB2-ABEND                                         
                        DP013-XCTL-DISPLAY-RESTART                              
                                         TO TRUE                                
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *    SELECT RECEIVER ITEM DATA                                            
      ******************************************************************        
                                                                                
       6200-SELECT-RECITM-DATA.                                                 
                                                                                
           EXEC SQL                                                             
                 SELECT PKSL_ITM_QTY                                            
                      , DTL_CK_ITM_QTY                                          
                   INTO :RECITM-PKSL-ITM-QTY                                    
                      , :RECITM-DTL-CK-ITM-QTY                                  
                   FROM TRECITM                                                 
                  WHERE ORD_NBR            = :DK-PO-NBR                         
                    AND ORD_LNE_NBR        = :DK-PO-LNE-NBR                     
                    AND REC_SEQ_NBR        = :DK-REC-SEQ-NBR                    
                    AND OPER_UNT_ID        = :DK-OPER-UNT-ID                    
                    AND FCLTY_ID           = :DK-FCLTY-ID                       
                    AND VER_STATUS_CDE     = :PC-A                              
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE      NOT = ZERO                                     
                    MOVE '6200-SELECT-RECITM-DATA'                              
                                        TO DP013-PARAGRAPH                      
                    MOVE 'UNSUCCESSFUL SELECT OF RECITM HEADER DATA'            
                                        TO DP013-MESSAGE-TEXT (1)               
                    MOVE SQLCA          TO DP013-SQLCA                          
                    MOVE 'TRECITM'      TO DP013-DB2-TABLE-NAME (1)             
                    SET DP013-DB2-ABEND                                         
                        DP013-XCTL-DISPLAY-RESTART                              
                                        TO TRUE                                 
                                                                                
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *    DETERMINE THE CORRECT MESSAGE TO SEND BACK TO CALLING PGM            
      ******************************************************************        
       8000-PASS-BACK-MSG.                                                      
           IF TM500-AUTO-ACKNOW-NOT-FOUND  OR                                   
              PS-ALLOCATION-FOUND-NO                                            
              SET TM500-AUTO-ACKNOW-NOT-FOUND                                   
                                        TO TRUE                                 
           ELSE                                                                 
           IF PV-REDISTRO-TRBL       AND                                        
              PS-REDISTRO-SUCCESS AND                                           
              (PS-ACKNOW-SUCCESS   OR                                           
               PV-REC-PKGNG-PBS)                                                
              SET TM500-ACK-REDISTRO-DONE                                       
                                        TO TRUE                                 
           ELSE                                                                 
              IF PV-REDISTRO-TRBL                                               
                 IF PS-ACKNOW-FAILED OR                                         
                    PS-REDISTRO-FAILED                                          
                    SET TM500-AUTO-REDISTRO-FAILED                              
                                        TO TRUE                                 
              ELSE                                                              
                 IF PS-ACKNOW-FAILED                                            
                    SET TM500-AUTO-ACKNOW-FAILED                                
                                        TO TRUE                                 
                 ELSE                                                           
                    SET TM500-AUTO-ACKNOW-DONE                                  
                                        TO TRUE                                 
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    THE DB2 RESOURCE IS NOT AVAILABLE, SET THE ERROR MESSAGE    *        
      *    AND THE ERROR SWITCHES.                                     *        
      *----------------------------------------------------------------*        
       9900-SOFT-ABORT.                                                         
           SET PS-ERROR                                                         
               DP020-MSG-FATAL                                                  
               DP020-NEXT-ACT-APPL-ERROR                                        
                                        TO TRUE.                                
           MOVE 'PROGRAM EXIT CALL'     TO DP013-MESSAGE-TEXT (1).              
                                                                                
155102 9903-WRITE-CICS-LOG.                                                     
155103                                                                          
155104     EXEC CICS                                                            
155105         WRITEQ TD                                                        
155106             QUEUE  ('CSSL')                                              
155107             FROM   (PV-CICS-MSG-AREA)                                    
155108             RESP   (PV-CICS-RESP)                                        
155109     END-EXEC.                                                            
155110     MOVE SPACES                  TO PV-CICS-MSG-AREA.                    
155111/                                                                         
      *----------------------------------------------------------------*        
      *    ABEND PROCESSOR MODULE                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD013.                                                        
