000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.              INKUT200.                                       
000300 AUTHOR.                  BRUCE GRAHAM.                                   
000400 INSTALLATION.            STRATAGEM CONSULTING FOR KOHLS.                 
000500 DATE-WRITTEN.            04/05/06.                                       
000600 DATE-COMPILED.                                                           
000700                                                                          
000800******************************************************************        
000900*  THIS PROGRAM WILL CHECK FOR THE EXISTANCE OF THE INVENTORY             
001000*  FILES FROM THE STORES AND UPDATE THE TABLE TINVLAP WITH A              
001100*  STATUS.                                                                
001200*                                                                         
001300*  THE PROGRAM EXPECTS FROM 1 TO FIVE FILES.                              
001400*  PROCESSING WILL STOP IF AN UNEXPECTED RETURN CODE IS RETURNED          
001500*  FROM THE CALL TO ASSEMBLER PGM DP023.                                  
001600*  RETURN CODES OF 4090 THROUGH 4095 WILL BE USED TO INDICATE THE         
001700*  PRESENCE OF 0 TO FIVE FILES.                                           
001800*  A RETURN CODE OF ZEROS FROM DP023 MEANS THAT THE FILE IS FOUND.        
001900*  A RETURN CODE OF 4009 FROM DP023 MEANS THAT THE FILE IS NOT            
002000*  FOUND.                                                                 
002010*  UPDATE TINVLAP WITH THE STATUS OF 'W' FOR WORK IN PROCESS OR           
002020*  A STATUS OF 'F' IF THE FILE IS NOT FOUND.                              
002100*                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------ ---------- ----------- ----------------------------------        
P28545* W28545 03-24-2006 ROD JANSSEN CYCLE INVENTORY PRICE MODULE SETUP        
P28545*                               (TINCNTL/TINVPAR MODIFICATIONS)           
S28545* S28545 09-13-2006 S. JACKSON DEFAULT HIGH LEVEL DSN NAME TO PKMO        
S28545*                              FOR INV TRANSMISSION DATA FILES            
      * W33304 05-01-2008 JILL LIN                                              
      *                   HOLDING INVENTORY LEDGER OPEN ACROSS                  
      *                   PERIODS INVENTORY SHORTAGE REPORTING                  
RONNAM* CHG0289948        R. MCDONALD CHECK TO SEE IF THE TINVPAR               
RONNAM* JUNE 27, 2023     FILE BALANCED FLAG INDICATES THAT THE                 
RONNAM*                   FILE IS BALANCED (VALUE IS 'Y' FOR BALANCED)          
RONNAM*                   AND IF IT IS BALANCED DISPLAY A MESSAGE               
RONNAM*                   IN THE JOB OUTPUT THAT THE FILE BALANCED              
RONNAM*                   AND DO NOT UPDATE THE STATUS ON THE ST RECORD.        
RONNAM*                   THE STATUS ON THE ST RECORD WILL GET SET TO           
RONNAM*                   EITHER FI OR FT IF THE STORE HAS NOT                  
RONNAM*                   BALANCED YET.                                         
002200******************************************************************        
002300                                                                          
002400 ENVIRONMENT DIVISION.                                                    
002500                                                                          
002600 CONFIGURATION SECTION.                                                   
002700                                                                          
002800 SOURCE-COMPUTER. IBM-3090.                                               
002900 OBJECT-COMPUTER. IBM-3090.                                               
003000                                                                          
003100 INPUT-OUTPUT SECTION.                                                    
003200                                                                          
003300 FILE-CONTROL.                                                            
003400                                                                          
003500       SELECT INP-CONTROL-FILE1                                           
003600           ASSIGN TO INP01.                                               
003700                                                                          
003800 DATA DIVISION.                                                           
003900                                                                          
004000 FILE SECTION.                                                            
004100                                                                          
004200 FD  INP-CONTROL-FILE1                                                    
004300     LABEL RECORDS ARE STANDARD                                           
004400     RECORDING MODE IS F                                                  
004500     BLOCK  CONTAINS 0 RECORDS                                            
004600     RECORD CONTAINS 80 CHARACTERS                                        
004700     DATA RECORD IS INP-CONTROL-RECORD1.                                  
004800                                                                          
004900 01  INP-CONTROL-RECORD1            PIC X(80).                            
005000                                                                          
005100 WORKING-STORAGE SECTION.                                                 
005200                                                                          
005300 01  PC-PROGRAM-CONSTANTS.                                                
005400     05  PC-MAX-RETRIES             PIC S9(03)  VALUE +3                  
005500                                                COMP SYNC.                
W33304     05  PC-STAT-CDE-SUCCESS        PIC  X(02)  VALUE 'FI'.               
W33304     05  PC-STAT-CDE-FAILURE        PIC  X(02)  VALUE 'FT'.               
005520     05  PC-LPTP-NBR                PIC  X(02)  VALUE 'ST'.               
005550     05  PC-ENVIRONMENT-TEST        PIC  X(01)  VALUE 'T'.                
005560     05  PC-ENVIRONMENT-PROD        PIC  X(01)  VALUE 'P'.                
005570     05  PC-DSN-TEST                PIC  X(04)  VALUE 'TKMA'.             
005580     05  PC-DSN-PROD                PIC  X(04)  VALUE 'PKMO'.             
005600                                                                          
005700 01  PS-PROGRAM-SWITCHES.                                                 
005800     05  PS-FOUND-DSN-SW-TABLE OCCURS 5 TIMES.                            
005900         10  PS-FOUND-DSN-SW        PIC X(01)   VALUE 'N'.                
006000             88  PS-FOUND-DSN-YES               VALUE 'Y'.                
006100             88  PS-FOUND-DSN-NO                VALUE 'N'.                
006200     05  PS-BAD-RETURN-FROM-23-SW   PIC X(01)   VALUE 'N'.                
006300         88  PS-BAD-RETURN-DP023-YES            VALUE 'Y'.                
006400         88  PS-BAD-RETURN-DP023-NO             VALUE 'N'.                
006500                                                                          
006600 01  PV-PROGRAM-VARIABLES.                                                
006700     05  PV-INV-DTE                 PIC  X(06)  VALUE SPACES.             
006800     05  PV-INV-DSN.                                                      
S28545         10  PV-INV-DSN-P-OR-T      PIC  X(04)  VALUE 'PKMO'.             
006910         10  PV-INV-DSN-COMMON      PIC  X(13)                            
007000               VALUE '.IN.KOHLS.STR'.                                     
007100         10  PV-INV-DSN-STR-LOC     PIC  9(04)  VALUE ZEROS.              
007200         10  PV-INV-DSN-G000        PIC  X(05)  VALUE '.G000'.            
007300         10  PV-INV-DSN-GEN         PIC  9(01)  VALUE ZERO.               
007400         10  PV-INV-DSN-V00         PIC  X(03)  VALUE 'V00'.              
007410     05  PV-INV-STR-LOC-COMP        PIC  S9(04) VALUE ZEROS               
007420                                                COMP SYNC.                
007500     05  PV-PROGRAM-NAME            PIC  X(08)  VALUE 'INKUT200'.         
007600     05  PV-PARAGRAPH-NAME          PIC  X(30)  VALUE SPACES.             
007700     05  PV-ABEND-MESSAGE           PIC  X(80)  VALUE SPACES.             
007800     05  PV-RETURN-CODE             PIC S9(04)  VALUE ZEROS               
007801                                                COMP SYNC.                
W33304     05  PV-TINVLAP-STAT-CDE        PIC  X(02)  VALUE 'FI'.               
007820                                                                          
008000 01  ABEND-CODE                     PIC S9(04)  COMP SYNC.                
008100                                                                          
008200 01  CR-CONTROL-RECORD1.                                                  
008300     05  CR1-STR-LOC                   PIC X(04)   VALUE SPACES.          
008400     05  FILLER                        PIC X(01)   VALUE SPACES.          
008410     05  CR1-ENVIRONMENT               PIC X(01)   VALUE SPACES.          
008420     05  FILLER                        PIC X(01)   VALUE SPACES.          
008500     05  CR1-JOBID                     PIC X(02)   VALUE SPACES.          
008510     05  FILLER                        PIC X(01)   VALUE SPACES.          
008600     05  CR1-PLND-INV-DTE              PIC X(10)   VALUE SPACES.          
008700     05  FILLER                        PIC X(60)   VALUE SPACES.          
008800                                                                          
008900****************************************************                      
009000*  DYNAMIC DSN PRESENT CHECKER WORK AREA           *                      
009100****************************************************                      
009200     COPY DPWS023.                                                        
009300                                                                          
009400*****************************************************************         
009500*  SQL ABEND ROUTINE WORKING STORAGE                                      
009600*****************************************************************         
009700                                                                          
009800     COPY DPWS004.                                                        
009900                                                                          
010000*****************************************************************         
010100* DB2 TABLE DCLGENS NEEDED FOR THIS PROGRAM.                              
010200*****************************************************************         
010300                                                                          
010400     EXEC SQL                                                             
010500         INCLUDE TINVLAP                                                  
010600     END-EXEC.                                                            
010700                                                                          
P28545******************************************************************        
P28545* TINCNTL - INVENTORY MASTER CONTROL TABLE                                
P28545******************************************************************        
P28545     EXEC SQL                                                             
P28545         INCLUDE TINCNTL                                                  
P28545     END-EXEC.                                                            
P28545                                                                          
010800******************************************************************        
010900* TINVPAR - INVENTORY PARAMETERS INFORMATION                              
011000******************************************************************        
011100     EXEC SQL                                                             
011200         INCLUDE TINVPAR                                                  
011300     END-EXEC.                                                            
011400                                                                          
011500******************************************************************        
011600* SQLCA   - DB2 SQL COMMUNICATIONS AREA                                   
011700******************************************************************        
011800                                                                          
011900     EXEC SQL                                                             
012000         INCLUDE SQLCA                                                    
012100     END-EXEC.                                                            
012200                                                                          
012300      EJECT.                                                              
012400                                                                          
012500 LINKAGE SECTION.                                                         
012800 PROCEDURE DIVISION.                                                      
012900                                                                          
013000******************************************************************        
013100* MAIN MODULE                                                             
013200* THE PROGRAM EXPECTS FROM 1 TO FIVE FILES.                               
013300* PROCESSING WILL STOP IF AN UNEXPECTED RETURN CODE IS RETURNED           
013400* FROM THE CALL TO ASSEMBLER PGM DP023.                                   
013500* RETURN CODES OF 4090 THROUGH 4095 WILL BE USED TO INDICATE THE          
013600* PRESENCE OF 0 TO FIVE FILES.                                            
013700* A RETURN CODE OF ZEROS FROM DP023 MEANS THAT THE FILE IS FOUND.         
013800* A RETURN CODE OF 4009 FROM DP023 MEANS THAT THE FILE IS NOT             
013900* FOUND.                                                                  
013910* UPDATE TINVLAP WITH THE STATUS OF 'C' FOR WORK IN PROCESS OR            
013920* A STATUS OF 'F' IF THE FILE IS NOT FOUND.                               
014000******************************************************************        
014100                                                                          
014200     PERFORM 1000-INITIALIZE.                                             
014300     DISPLAY ' CONTROL FILE REC ' CR-CONTROL-RECORD1.                     
014310                                                                          
014400     PERFORM 2000-DSN-PRESENT-CHK                                         
014500         VARYING PV-INV-DSN-GEN FROM 1 BY 1                               
014600             UNTIL PV-INV-DSN-GEN > 5                                     
014700                OR PS-BAD-RETURN-DP023-YES.                               
014800                                                                          
014900     IF PS-BAD-RETURN-DP023-NO                                            
015000         MOVE +4090 TO PV-RETURN-CODE                                     
015100         PERFORM 4000-SET-RETURN-CODE                                     
015200             VARYING PV-INV-DSN-GEN FROM 1 BY 1                           
015300                 UNTIL PV-INV-DSN-GEN > 5                                 
015400         MOVE PV-RETURN-CODE TO RETURN-CODE                               
015500     END-IF.                                                              
015600                                                                          
015700     GOBACK.                                                              
015800                                                                          
015900******************************************************************        
016000*     THIS ROUTINE ACCESSES THE CONTROL FILE FOR INITIAL                  
016100*     INFORMATION.                                                        
016200******************************************************************        
016300                                                                          
016400 1000-INITIALIZE.                                                         
016500                                                                          
016600     SET PS-FOUND-DSN-NO (1) TO TRUE.                                     
016700     SET PS-FOUND-DSN-NO (2) TO TRUE.                                     
016800     SET PS-FOUND-DSN-NO (3) TO TRUE.                                     
016900     SET PS-FOUND-DSN-NO (4) TO TRUE.                                     
017000     SET PS-FOUND-DSN-NO (5) TO TRUE.                                     
017100                                                                          
017200     OPEN INPUT  INP-CONTROL-FILE1.                                       
017300                                                                          
017400     READ INP-CONTROL-FILE1 INTO CR-CONTROL-RECORD1                       
017500         AT END                                                           
017600           MOVE 'CONTROL RECORD IS MISSING' TO PV-ABEND-MESSAGE           
017700           DISPLAY 'CONTROL RECORD IS MISSING'                            
017800           MOVE +4080 TO ABEND-CODE                                       
017900           PERFORM 9999-ABEND.                                            
018000                                                                          
018100     CLOSE INP-CONTROL-FILE1.                                             
018200                                                                          
018210     IF CR1-ENVIRONMENT = PC-ENVIRONMENT-TEST                             
018220         MOVE PC-DSN-TEST TO PV-INV-DSN-P-OR-T                            
018230     END-IF.                                                              
018240                                                                          
018300******************************************************************        
018400*  THIS ROUTINE CHECKS FOR THE PRESENCE OF THE INCOMING INVENTORY         
018500*  FILE.                                                                  
018600******************************************************************        
018700 2000-DSN-PRESENT-CHK.                                                    
018800                                                                          
018900     INITIALIZE DP023-DYNALC-PARMS.                                       
019000     MOVE 'INP99'                TO DP023-DDNAME.                         
019100     MOVE CR1-STR-LOC            TO PV-INV-DSN-STR-LOC.                   
019200     MOVE PV-INV-DSN             TO DP023-DSNAME.                         
019300     SET DP023-DISP-SHARE        TO TRUE.                                 
019400     SET DP023-FREE-CLOSE-NO     TO TRUE.                                 
019500     SET DP023-RECFM-FIXED-BLOCK TO TRUE.                                 
019600     MOVE 80                     TO DP023-RECSZ.                          
019700     SET DP023-RECOUNT-ALLOCATE-TAPE TO TRUE.                             
019800                                                                          
019900     SET DP023-DISABLE-DISPLAY-ABEND TO TRUE.                             
020000                                                                          
020100     DISPLAY ' ALLOCATING ' DP023-DDNAME                                  
020200             ' TO '         DP023-DSNAME.                                 
020300                                                                          
020400*    DISPLAY '** PARMS = ' DP023-DYNALC-PARMS.                            
020500     PERFORM DP023-0000-DYNALC.                                           
020600     DISPLAY '** PARMS = ' DP023-DYNALC-PARMS.                            
020700     IF DP023-LASTRC = ZERO                                               
020800         DISPLAY ' *** EXISTING  DSN: ' DP023-DSNAME                      
020900         SET PS-FOUND-DSN-YES (PV-INV-DSN-GEN) TO TRUE                    
021000         IF PV-INV-DSN-GEN = 1                                            
021010             MOVE PC-STAT-CDE-SUCCESS    TO                               
021011                              PV-TINVLAP-STAT-CDE                         
021100             PERFORM 3000-LOCATE-TINVPAR                                  
021200         END-IF                                                           
021300     ELSE                                                                 
021400         IF DP023-LASTRC = 4009                                           
021500             DISPLAY ' *** DYNAMIC ALLOCATION FAIL ***'                   
021600             DISPLAY '** PARMS = ' DP023-DYNALC-PARMS                     
021700             SET PS-FOUND-DSN-NO (PV-INV-DSN-GEN) TO TRUE                 
021800             MOVE ZEROS TO RETURN-CODE                                    
021900             IF PV-INV-DSN-GEN = 1                                        
021901                 MOVE PC-STAT-CDE-FAILURE TO                              
021902                                  PV-TINVLAP-STAT-CDE                     
022000                 PERFORM 3000-LOCATE-TINVPAR                              
022100             END-IF                                                       
022200          ELSE                                                            
022300             SET PS-BAD-RETURN-DP023-YES TO TRUE                          
022400          END-IF                                                          
022500     END-IF.                                                              
022600                                                                          
022700                                                                          
022800 3000-LOCATE-TINVPAR.                                                     
RONNAM***********************************************************               
RONNAM* BRING BACK THE CYCLE ID AND THE FILE TOTALS BALANCE CODE                
RONNAM* BECAUSE IF THE FILE TOTALS ARE BALANCED THE TINVLAP TABLE               
RONNAM* DOES NOT NEED TO BE UPDATED                                             
RONNAM***********************************************************               
RONNAM*                                                                         
022910     MOVE CR1-STR-LOC TO PV-INV-STR-LOC-COMP.                             
022920                                                                          
023000     EXEC SQL                                                             
023001                                                                          
P28545          SELECT A.INV_ID                                                 
RONNAM                ,A.FIL_TOT_BAL_CDE                                        
023011              INTO :INVPAR-INV-ID                                         
RONNAM                  ,:INVPAR-FIL-TOT-BAL-CDE                                
P28545              FROM TINVPAR A,                                             
P28545                   TINCNTL B                                              
P28545              WHERE B.ACTV_IND = 'Y'                                      
P28545                AND A.INV_ID = B.INV_ID                                   
P28545                AND LOC_NBR = :PV-INV-STR-LOC-COMP                        
023040                AND PLND_INV_DTE = :CR1-PLND-INV-DTE                      
P28545              WITH UR                                                     
023051     END-EXEC.                                                            
023052                                                                          
023053     EVALUATE TRUE                                                        
023054           WHEN SQLCODE = 0                                               
RONNAM                IF INVPAR-FIL-TOT-BAL-CDE = 'Y'                           
RONNAM                   DISPLAY 'LOCATION ALREADY BALANCED'                    
RONNAM                ELSE                                                      
023055                   PERFORM 4000-UPDATE-TINVLAP-STATUS                     
RONNAM                END-IF                                                    
023058           WHEN SQLCODE = +100                                            
023059                DISPLAY '****************'                                
023060                DISPLAY 'TINVPAR ROW NOT FOUND FOR STORE '                
023061                        PV-INV-STR-LOC-COMP ' AND DATE '                  
023062                        CR1-PLND-INV-DTE                                  
023063                DISPLAY '****************'                                
023064                MOVE '3000-LOCATE-TINVPAR'                                
023065                               TO PV-PARAGRAPH-NAME                       
023068                PERFORM 9999-SQL-ABEND                                    
023069           WHEN OTHER                                                     
023070                MOVE '3000-LOCATE-TINVPAR'                                
023071                               TO PV-PARAGRAPH-NAME                       
023074                PERFORM 9999-SQL-ABEND                                    
023075       END-EVALUATE.                                                      
023076                                                                          
023077 4000-UPDATE-TINVLAP-STATUS.                                              
023078                                                                          
023080     EXEC SQL                                                             
023090                                                                          
023091          UPDATE TINVLAP                                                  
023092                                                                          
023093          SET INV_STAT_CDE   = :PV-TINVLAP-STAT-CDE                       
023095              WHERE INV_ID   = :INVPAR-INV-ID                             
023096                AND LOC_NBR  = :PV-INV-STR-LOC-COMP                       
023096                AND LPTP_NBR = :PC-LPTP-NBR                               
023100                                                                          
023110     END-EXEC.                                                            
023130                                                                          
023140     EVALUATE TRUE                                                        
023150           WHEN SQLCODE = 0                                               
023160                CONTINUE                                                  
023170           WHEN SQLCODE = +100                                            
023180                DISPLAY '****************'                                
023190                DISPLAY 'TINVLAP ROW NOT FOUND FOR STORE '                
023191                        PV-INV-STR-LOC-COMP  ' AND INV ID '               
023192                        INVLAP-INV-ID                                     
023192                         ' AND PC-LPTP-NBR ' PC-LPTP-NBR                  
023193                DISPLAY '****************'                                
023194                MOVE '4000-UPDATE-TINVLAP'                                
023195                               TO PV-PARAGRAPH-NAME                       
023196                PERFORM 9999-SQL-ABEND                                    
023197           WHEN OTHER                                                     
023198                MOVE '4000-UPDATE-TINVLAP'                                
023199                               TO PV-PARAGRAPH-NAME                       
023200                PERFORM 9999-SQL-ABEND                                    
023201       END-EVALUATE.                                                      
023210******************************************************************        
023300*  THIS ROUTINE WILL SET THE RETURN CODE BASED UPON THE FILES             
023400*  FOUND.                                                                 
023500******************************************************************        
023600                                                                          
023700 4000-SET-RETURN-CODE.                                                    
023800                                                                          
023900     IF PS-FOUND-DSN-YES (PV-INV-DSN-GEN)                                 
024000         ADD +1 TO PV-RETURN-CODE                                         
024100     END-IF.                                                              
024200                                                                          
024300                                                                          
024400******************************************************************        
024500*  THIS COPYBOOK FOR DYNAMIC ALLOCATION CHECK                             
024600******************************************************************        
024700                                                                          
024800     COPY DPPD023.                                                        
024900                                                                          
025000******************************************************************        
025100*  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM.         
025200******************************************************************        
025300 9999-ABEND.                                                              
025400                                                                          
025500     DISPLAY '**  ABEND     **'.                                          
025600     DISPLAY '**  PROGRAM   **  = ' PV-PROGRAM-NAME.                      
025700     DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
025800     DISPLAY '**  ' PV-ABEND-MESSAGE.                                     
025900     CALL 'ILBOABN0'  USING ABEND-CODE.                                   
026000                                                                          
026100                                                                          
026200******************************************************************        
026300*  STANDARD DB2 ERROR ABEND ROUTINE                                       
026400*  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
026500******************************************************************        
026600 9999-SQL-ABEND.                                                          
026700                                                                          
026800     MOVE +4013                  TO ABEND-CODE.                           
026900     DISPLAY '**  ABEND     **'.                                          
027000     DISPLAY '**  PROGRAM   **  = ' PV-PROGRAM-NAME.                      
027100     DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
027200                                                                          
027300*    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
027400*    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
027500                                                                          
027600     COPY DPPD004.                                                        
027700                                                                          
027800     CALL 'ILBOABN0'  USING ABEND-CODE.                                   
027900                                                                          
