       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         INKUT021.                                            
       AUTHOR.             THERESE RAMSEYER.                            00040024
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       07-16-2009.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *      INKUT021 - NEW PROGRAM FOR HILO                           *        
      *                                                                *        
      *  THIS PROGRAM REMOVES INACTIVE DATA FOR LOCATIONS THAT ARE     *        
      *  NOT PART OF AN ACTIVE CYCLE                                   *        
      *                                                                *        
      *THIS PROGRAM READS DATA WAREHOUSE AUDIT VARIANCE FILE AS INPUT  *        
      *  WRITES OUT EACH RECORD TO EITHER A NEW FILE OR DROPPED FILE   *        
      *  DEPENDING ON THE STORE NUMBER ON EACH RECORD.                 *        
      *  IF THE STORE IS PART OF AN ACTIVE CYCLE, NOT FINANCIALLY      *        
      *  BOOKED AND NOT SOFT DELETED, THEN THAT RECORD GOES TO THE NEW *        
      *  AUDIT FILE, OTHERS GO TO THE DROPPED FILE.                    *        
      *                                                                *        
      *   THIS IS A PART OF INK811 WHICH WILL CURRENTLY RUN MONTHLY.            
      *                                                                         
      *   SELECT 1                                                              
      *   INTO :PV-EXISTS                                                       
      *   FROM TINVPAR A                                                        
      *       ,TINCNTL B                                                        
      *   WHERE A.LOC_NBR          = :INVPAR-LOC-NBR                            
      *     AND A.LOC_INV_STAT_CDE = :PC-INITIAL                                
      *     AND A.ACTL_FIN_BK_DTE  = :PC-EMPTY-NINES                            
      *     AND B.ACTV_IND         = :PC-ACTIVE                                 
      *     AND A.INV_ID           = B.INV_ID                                   
      *                                                                         
      *                                                                         
      *                                                                *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                     00113027
      * -------  ----------  ----------------------------------------   00114027
      * WR33304  07/16/2009  INITIAL INSTALLATION.                      00119027
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT DW-VARIANCE-FILES-INP   ASSIGN TO INP01.                      
           SELECT DW-VARIANCE-FILE-OUT    ASSIGN TO OUT01.                      
           SELECT DW-VARIANCE-DROP-FILE   ASSIGN TO OUT02.                      
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  DW-VARIANCE-FILES-INP                                                
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DW-VARIANCE-FILES-RECORD         PIC  X(60).                         
                                                                                
       FD  DW-VARIANCE-FILE-OUT                                                 
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DW-VARIANCE-FILE-OUTPUT          PIC  X(60).                         
                                                                                
       FD  DW-VARIANCE-DROP-FILE                                                
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DW-VARIANCE-DROP-REC             PIC  X(60).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04) COMP SYNC                
                                                       VALUE +0.                
                                                                                
       01  PROGRAM-ACCUMULATORS.                                                
           05  PA-ALL-IN-COUNT              PIC S9(09) COMP-3 VALUE +0.         
           05  PA-CUM-OUT-COUNT             PIC S9(09) COMP-3 VALUE +0.         
           05  PA-DRP-OUT-COUNT             PIC S9(09) COMP-3 VALUE +0.         
                                                                                
       01  PROGRAM-CONSTANTS.                                                   
           05  PC-PROGRAM-NAME           PIC  X(08) VALUE 'INKUT021'.           
           05  PC-INITIAL                PIC  X(02) VALUE 'IN'.                 
           05  PC-ACTIVE                 PIC  X(01) VALUE 'Y'.                  
           05  PC-EMPTY-NINES            PIC  X(10) VALUE '9999-09-09'.         
           05  PC-MAX-RETRIES            PIC S9(04) COMP SYNC                   
                                                       VALUE +2.                
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  PS-END-OF-INPUT-SW           PIC X      VALUE 'N'.               
               88  PS-END-OF-INPUT                     VALUE 'Y'.               
               88  PS-NOT-END-INPUT                    VALUE 'N'.               
           05  PS-TINVPAR-FOUND-SW          PIC X      VALUE 'N'.               
               88  PS-TINVPAR-FOUND                    VALUE 'Y'.               
               88  PS-TINVPAR-NOT-FOUND                VALUE 'N'.               
                                                                                
       01  PROGRAM-VARIABLES.                                                   
           05  PV-EXISTS                    PIC S9(04) VALUE +0 COMP.           
           05  PV-HOLD-STR-NBR              PIC  9(05) VALUE ZERO.              
           05  PV-DB2-OPER-ATTEMPTED        PIC  X(30) VALUE SPACE.             
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACE.             
           05  PV-SQL-SUB                   PIC  9(03) VALUE ZERO.              
                                                                                
      ****************************************************************          
      * RECORD LAYOUT FOR VARIANCE RECORD                                       
      ****************************************************************          
           COPY INRD009.                                                        
      *****************************************************************         
      * DB2 FORMATTED ERROR MESSAGE                                             
      *****************************************************************         
           COPY DPWS004.                                                        
      *****************************************************************         
      * DCLGEN FOR THE INVENTORY PARAMETERS TABLE                               
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
      *****************************************************************         
      * DCLGEN FOR THE INVENTORY CYCLE CONTROL TABLE                            
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
      ******************************************************************        
      * SQL COMMUNICATIONS WORK AREA                                            
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAINLINE-PROCESSING.                                                
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-INPUT                                           
               UNTIL PS-END-OF-INPUT.                                           
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
      *  OPEN ALL FILES AND READ AN INPUT FILE RECORD.                 *        
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  DW-VARIANCE-FILES-INP                                    
                OUTPUT DW-VARIANCE-FILE-OUT                                     
                       DW-VARIANCE-DROP-FILE.                                   
                                                                                
           PERFORM 1100-READ-INPUT-ALL-FILE.                                    
                                                                                
      ******************************************************************03500024
      * READS THE INPUT BOOKING ADJUSTMENTS FILE                        03510024
      ******************************************************************03520024
       1100-READ-INPUT-ALL-FILE.                                        03530024
           READ DW-VARIANCE-FILES-INP INTO IN009-VARIANCE-RECORD        03540024
               AT END                                                   03550024
                   SET PS-END-OF-INPUT TO TRUE                          03550024
               NOT AT END                                                       
                   ADD 1 TO PA-ALL-IN-COUNT.                                    
                                                                                
      ******************************************************************        
      *  FOR EACH INPUT FILE RECORD, VALIDATE THE STORE                *        
      ******************************************************************        
       2000-PROCESS-INPUT.                                                      
                                                                                
           IF IN009-STR-NBR EQUAL PV-HOLD-STR-NBR                               
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 3000-STORE-VALIDATION                                    
               MOVE IN009-STR-NBR TO PV-HOLD-STR-NBR                            
           END-IF.                                                              
                                                                                
           IF PS-TINVPAR-FOUND                                                  
              PERFORM 4000-WRITE-OUTPUT-FILE                                    
           ELSE                                                                 
              PERFORM 5000-WRITE-OUTPUT-DROP-FILE                               
           END-IF.                                                              
                                                                                
           PERFORM 1100-READ-INPUT-ALL-FILE.                                    
                                                                                
      ******************************************************************        
      *  PART OF ACTIVE CYCLE? NOT FINANCIALLY BKD, NOT SOFT DELETED            
      ******************************************************************        
       3000-STORE-VALIDATION.                                                   
                                                                                
           MOVE IN009-STR-NBR  TO INVPAR-LOC-NBR.                               
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   SELECT 1                                                     
                   INTO :PV-EXISTS                                              
                   FROM TINVPAR A                                               
                       ,TINCNTL B                                               
                   WHERE A.LOC_NBR          = :INVPAR-LOC-NBR                   
                     AND A.LOC_INV_STAT_CDE = :PC-INITIAL                       
                     AND A.ACTL_FIN_BK_DTE  = :PC-EMPTY-NINES                   
                     AND B.ACTV_IND         = :PC-ACTIVE                        
                     AND A.INV_ID           = B.INV_ID                          
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +100                                          
                       SET PS-TINVPAR-NOT-FOUND TO TRUE                         
                       DISPLAY 'DROP REC :' INVPAR-LOC-NBR                      
                   WHEN SQLCODE = 0                                             
                       SET PS-TINVPAR-FOUND TO TRUE                             
                       DISPLAY 'KEEP REC :' INVPAR-LOC-NBR                      
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '3000-STORE-VALIDATION'                             
                           TO PV-PARAGRAPH-NAME                                 
                       MOVE 'SELECT TINVPAR'                                    
                           TO PV-DB2-OPER-ATTEMPTED                             
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '3000-STORE-VALIDATION' TO PV-PARAGRAPH-NAME                
               MOVE 'MAX RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  WRITE TO OUTPUT DATA WAREHOUSE OUTPUT FILE                   *         
      ******************************************************************        
       4000-WRITE-OUTPUT-FILE.                                                  
                                                                                
           WRITE DW-VARIANCE-FILE-OUTPUT FROM IN009-VARIANCE-RECORD.            
           ADD +1 TO PA-CUM-OUT-COUNT.                                          
                                                                                
      ******************************************************************        
      *  WRITE TO OUTPUT DROPPED FILE FOR LOCATIONS THAT DO NOT QUALIFY*        
      ******************************************************************        
       5000-WRITE-OUTPUT-DROP-FILE.                                             
                                                                                
           WRITE DW-VARIANCE-DROP-REC FROM IN009-VARIANCE-RECORD.               
           ADD +1 TO PA-DRP-OUT-COUNT.                                          
                                                                                
      ******************************************************************        
      *    - CLOSE THE FILES PROCESSED BY PROGRAM                      *        
      *    - DISPLAY OUTPUT RECORD COUNTS                              *        
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE DW-VARIANCE-FILES-INP                                          
                 DW-VARIANCE-FILE-OUT                                           
                 DW-VARIANCE-DROP-FILE.                                         
                                                                                
           DISPLAY SPACE.                                                       
           DISPLAY '*************************************************'. 06840000
           DISPLAY '***********    INKUT021 RUN STATS    ************'. 06840000
           DISPLAY '*************************************************'. 06840000
           DISPLAY '*** NUMBER OF INPUT RECS READ   = '                         
               PA-ALL-IN-COUNT.                                                 
           DISPLAY '*** NUMBER OF RECS WRITTEN TO CUM FILE = '                  
               PA-CUM-OUT-COUNT.                                                
           DISPLAY '*** NUMBER OF RECS WRITTEN TO DROP FILE = '                 
               PA-DRP-OUT-COUNT.                                                
           DISPLAY SPACE.                                                       
                                                                                
                                                                                
      ******************************************************************        
      * ABEND THE PROGRAM IN THE EVENT THAT AN SQL ERROR OR WARNING             
      * CODE OCCURS.                                                            
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                        
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPER-ATTEMPTED.                  
                                                                                
      *-----------------------------------------------------------------        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      *-----------------------------------------------------------------        
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
