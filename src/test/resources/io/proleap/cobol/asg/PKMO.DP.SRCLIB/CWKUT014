      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.     CWKUT014.                                                
       AUTHOR.         THOMAS DUFFY.                                            
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   05/20/03.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  STORE TO DC GROUP COMMON EXTRACT PROGRAM                               
      *  (STORE TO DC GROUP HISTORY PROGRAM)                                    
      *  THIS PROGRAM CREATES A CHANGE EXTRACT FILE BY COMPARING                
      *  TWO GENERATIONS OF AN RMS STORE TO DC COMMON EXTRACT FILE.             
      *  NOTE: THERE ARE NO UPDATE(U) RECORDS GENERATED BY THIS PROGRAM         
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT CW-CURRENT-FILE                                               
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT CW-PREVIOUS-FILE                                              
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT CW-CHANGE-FILE                                                
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CW-CURRENT-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-CURRENT-RECORD                  PIC X(30).                        
                                                                                
       FD  CW-PREVIOUS-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-PREVIOUS-RECORD                 PIC X(30).                        
                                                                                
       FD  CW-CHANGE-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-CHANGE-RECORD                   PIC X(31).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  CURRENT-REC.                                                         
           05  CR-CURRENT-RECORD              PIC   X(30).                      
           05  CR-CURR-REC REDEFINES CR-CURRENT-RECORD.                         
               10 CR-LOC-NBR                  PIC   X(04).                      
               10 FILLER                      PIC   X(01).                      
               10 CR-EFF-BEG-DTE              PIC   X(10).                      
               10 CR-DC-NBR                   PIC   X(04).                      
               10 FILLER                      PIC   X(01).                      
               10 CR-PO-STRT-DTE              PIC   X(10).                      
                                                                                
       01  PREVIOUS-REC.                                                        
           05  PR-PREVIOUS-RECORD             PIC   X(30).                      
           05  PR-PREV-REC REDEFINES PR-PREVIOUS-RECORD.                        
               10 PR-LOC-NBR                  PIC   X(04).                      
               10 FILLER                      PIC   X(01).                      
               10 PR-EFF-BEG-DTE              PIC   X(10).                      
               10 PR-DC-NBR                   PIC   X(04).                      
               10 FILLER                      PIC   X(01).                      
               10 PR-PO-STRT-DTE              PIC   X(10).                      
                                                                                
       01  CHANGE-OUTPUT-REC.                                                   
           05  COR-CHANGE-DATA                PIC   X(30).                      
           05  COR-CHANGE-INDICATOR           PIC   X(01).                      
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC X(08) VALUE 'CWKUT014'.            
           05  PC-OPERCO                 PIC X(02) VALUE '03'.                  
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-RECORDS          PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-PRE-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-CUR-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-OUTPUT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-INSERT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-DELETE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-NO-CHANGE-RECORDS      PIC  9(9)       VALUE ZERO.            
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-MAIN-ROUTINE                                            
               UNTIL CR-LOC-NBR = HIGH-VALUES                                   
                     AND                                                        
                     PR-LOC-NBR = HIGH-VALUES.                                  
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  INITIALIZE                                                             
      ******************************************************************        
                                                                                
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  CW-CURRENT-FILE                                          
                       CW-PREVIOUS-FILE                                         
                OUTPUT CW-CHANGE-FILE.                                          
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
                                                                                
           PERFORM 8000-READ-CURRENT.                                           
           PERFORM 8100-READ-PREVIOUS.                                          
                                                                                
                                                                                
      ******************************************************************        
      * MAIN PROCESSING.                                                        
      ******************************************************************        
       2000-MAIN-ROUTINE.                                                       
           INITIALIZE CHANGE-OUTPUT-REC.                                        
           IF CR-LOC-NBR  =  PR-LOC-NBR                                         
               PERFORM 2100-CHECK-SECOND-KEY                                    
           ELSE                                                                 
               IF CR-LOC-NBR  <  PR-LOC-NBR                                     
                   PERFORM 3100-INSERT                                          
               ELSE                                                             
                   PERFORM 3200-DELETE                                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  CHECKS SECOND KEY (DC-NUMBER) FOR EQUALITY                             
      ******************************************************************        
       2100-CHECK-SECOND-KEY.                                                   
           IF CR-DC-NBR  =  PR-DC-NBR                                           
               PERFORM 2200-CHECK-THIRD-KEY                                     
           ELSE                                                                 
               IF CR-DC-NBR  <  PR-DC-NBR                                       
                   PERFORM 3100-INSERT                                          
               ELSE                                                             
                   PERFORM 3200-DELETE                                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  CHECKS THIRD KEY (EFF-BEG-DTE) FOR EQUALITY                            
      ******************************************************************        
       2200-CHECK-THIRD-KEY.                                                    
           IF CR-EFF-BEG-DTE  =  PR-EFF-BEG-DTE                                 
               PERFORM 2300-CHECK-FOURTH-KEY                                    
           ELSE                                                                 
               IF CR-EFF-BEG-DTE  <  PR-EFF-BEG-DTE                             
                   PERFORM 3100-INSERT                                          
               ELSE                                                             
                   PERFORM 3200-DELETE                                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  CHECKS FOURTH KEY (PO-STRT-DTE) FOR EQUALITY                           
      *  IF DATA IS NOT EQUAL, THEN RECORD IS AN INSERT                         
      *  NOTE THAT THERE ARE NO UPDATED RECORDS                                 
      ******************************************************************        
       2300-CHECK-FOURTH-KEY.                                                   
           IF CR-PO-STRT-DTE  =  PR-PO-STRT-DTE                                 
               ADD +1            TO PA-NO-CHANGE-RECORDS                        
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
           ELSE                                                                 
               PERFORM 3100-INSERT                                              
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  CREATE AN INSERT RECORD                                                
      ******************************************************************        
       3100-INSERT.                                                             
            MOVE 'I'                        TO COR-CHANGE-INDICATOR.            
            MOVE CR-CURRENT-RECORD          TO COR-CHANGE-DATA.                 
            ADD +1                          TO PA-INSERT-RECORDS.               
            PERFORM 7100-WRITE.                                                 
            PERFORM 8000-READ-CURRENT.                                          
                                                                                
      ******************************************************************        
      *  CREATE A DELETE RECORD                                                 
      ******************************************************************        
       3200-DELETE.                                                             
            MOVE 'D'                    TO COR-CHANGE-INDICATOR.                
            MOVE PR-PREVIOUS-RECORD     TO COR-CHANGE-DATA.                     
            ADD +1                      TO PA-DELETE-RECORDS.                   
            PERFORM 7100-WRITE.                                                 
            PERFORM 8100-READ-PREVIOUS.                                         
                                                                                
      ******************************************************************        
      *  WRITE THE OUTPUT RECORD                                                
      ******************************************************************        
       7100-WRITE.                                                              
           WRITE  CW-CHANGE-RECORD FROM CHANGE-OUTPUT-REC.                      
           ADD +1            TO PA-OUTPUT-RECORDS.                              
                                                                                
      ******************************************************************        
      *  READ THE CURRENT FILE.                                        *        
      ******************************************************************        
       8000-READ-CURRENT.                                                       
           READ CW-CURRENT-FILE                                                 
               INTO CURRENT-REC                                                 
               AT END                                                           
                   MOVE HIGH-VALUES  TO  CR-LOC-NBR                             
               NOT AT END                                                       
                   ADD 1           TO  PA-INPUT-RECORDS                         
                   ADD 1           TO  PA-INPUT-CUR-RECORDS                     
           END-READ.                                                            
                                                                                
      ******************************************************************        
      *  READ THE PREVIOUS FILE.                                                
      ******************************************************************        
       8100-READ-PREVIOUS.                                                      
           READ CW-PREVIOUS-FILE                                                
               INTO PREVIOUS-REC                                                
               AT END                                                           
                   MOVE HIGH-VALUES  TO  PR-LOC-NBR                             
               NOT AT END                                                       
                   ADD 1           TO  PA-INPUT-RECORDS                         
                   ADD 1           TO  PA-INPUT-PRE-RECORDS                     
           END-READ.                                                            
                                                                                
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
           DISPLAY '   '.                                                       
                                                                                
           DISPLAY 'INPUT RECS READ    : ' PA-INPUT-RECORDS.                    
           DISPLAY 'INPUT PREVIOUS RECS: ' PA-INPUT-PRE-RECORDS.                
           DISPLAY 'INPUT CURRENT RECS : ' PA-INPUT-CUR-RECORDS.                
           DISPLAY '   '.                                                       
           DISPLAY 'OUTPUT RECS WRITTEN: ' PA-OUTPUT-RECORDS.                   
           DISPLAY 'INSERT RECS WRITTEN: ' PA-INSERT-RECORDS.                   
           DISPLAY 'DELETE RECS WRITTEN: ' PA-DELETE-RECORDS.                   
           DISPLAY 'NO CHANGE RECORDS  : ' PA-NO-CHANGE-RECORDS.                
                                                                                
           CLOSE CW-CURRENT-FILE                                                
                 CW-PREVIOUS-FILE                                               
                 CW-CHANGE-FILE.                                                
                                                                                
      ******************************************************************        
      ******************************************************************        
