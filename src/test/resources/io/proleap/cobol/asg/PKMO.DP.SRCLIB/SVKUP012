      ****************************************************************  00010000
       IDENTIFICATION DIVISION.                                         00020000
      ****************************************************************  00030000
                                                                        00040000
       PROGRAM-ID.             SVKUP012.                                00050000
       AUTHOR.                 DAN PAYNTER.                             00060000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070000
       DATE-WRITTEN            MAY 2022.                                00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ****************************************************************  00110000
      *                                                                 00120000
      *   ***  RESET STORED VALUE CARD FRAUD ATTEMPTS TABLE ***         00130000
      *                                                                 00140000
      *  RELEASE LOCKS ON STORED VALUE CARDS BY COPYING DATA FROM       00150000
      *  SVT_CARD_FAILED_ATTEMPTS DB2 TABLE TO THE HISTORY VERSION OF   00160000
      *  THE SAME TABLE (SVT_CARD_FAILED_ATTEMPTS_HIST).                00170000
      *  THIS PARTICULAR PROGRAM MERELY EXTRACTS THE RECORDS FROM THE   00180000
      *  SVT_CARD_FAILED_ATTEMPTS DB2 TABLE TO A FILE SO THEY CAN BE    00190000
      *  INSERTED INTO THE HISTORY TABLE.                               00200000
      *                                                                 00210000
      ****************************************************************  00220000
      * CHANGE LOG: CHG0270185                                          00240003
      * DATE:  2022-05-17                                               00250003
      * PRGMR: DAN PAYNTER                                              00260000
      * WHY:   NEW PROGRAM TO RELEASE LOCKED STORED VALUE CARDS         00270000
      ******************************************************************00330000
      ****************************************************************  00340000
       ENVIRONMENT DIVISION.                                            00350000
      ****************************************************************  00360000
                                                                        00370000
       CONFIGURATION SECTION.                                           00380000
                                                                        00390000
       SOURCE-COMPUTER.        IBM-3090.                                00400000
                                                                        00410000
       OBJECT-COMPUTER.        IBM-3090.                                00420000
                                                                        00430000
       INPUT-OUTPUT SECTION.                                            00440000
                                                                        00450000
       FILE-CONTROL.                                                    00460000
                                                                        00470000
      ****************************************************************  00480000
       DATA DIVISION.                                                   00490000
      ****************************************************************  00500000
                                                                        00510000
       FILE SECTION.                                                    00520000
                                                                        00530000
                                                                        00540000
      ***************************************************************** 00550000
       WORKING-STORAGE SECTION.                                         00560000
      ***************************************************************** 00570000
       01  FILLER                            PIC X(50)  VALUE           00580000
           ' *** WORKING STORAGE STARTS HERE FOR SVKUP012 *** '.        00590000
                                                                        00600000
      ***************************************************************** 00610000
      *                         SWITCHES                                00620000
      ***************************************************************** 00630000
       01  PROGRAM-SWITCHES.                                            00640000
           05  PS-RESTART-REQUIRED-SW       PIC  X(01) VALUE 'N'.       00650000
               88  PS-RESTART-REQUIRED                 VALUE 'Y'.       00660000
               88  PS-RESTART-NOT-REQUIRED             VALUE 'N'.       00670000
                                                                        00680000
      ******************************************************************00690000
      *                       ACCUMULATORS                              00700000
      ******************************************************************00710000
       01  PROGRAM-ACCUM.                                               00720000
           05  PA-READ                      PIC  9(11) VALUE ZEROS.     00730000
           05  PA-CKPT                      PIC  9(11) VALUE ZEROS.     00740000
                                                                        00750000
      ******************************************************************00760000
      *                           CONSTANTS                             00770000
      ******************************************************************00780000
       01  PROGRAM-CONSTANTS.                                           00790000
           05  PC-MAX-RETRIES               PIC S9(04) VALUE +3         00800000
                                                       COMP SYNC.       00810000
           05  PC-MAX-DEADLOCKS             PIC S9(04) VALUE +3         00820000
                                                       COMP SYNC.       00830000
                                                                        00840000
           05  PC-CKPT-JOB-NAME             PIC  X(08) VALUE 'SVK026  '.00850000
           05  PC-CKPT-PLAN-NAME            PIC  X(08) VALUE 'SVKUP012'.00860000
           05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'SVKUP012'.00870000
                                                                        00880000
                                                                        00890000
           05  PC-TRAN-PRES-FUNC-CODES.                                 00900000
               10  PC-TRAN-PRES-FUNC-OPEN   PIC  X(05) VALUE 'OPEN '.   00910000
               10  PC-TRAN-PRES-FUNC-TRANS  PIC  X(05) VALUE 'TRANS'.   00920000
               10  PC-TRAN-PRES-FUNC-RSTRT  PIC  X(05) VALUE 'RSTRT'.   00930000
               10  PC-TRAN-PRES-FUNC-CLOSE  PIC  X(05) VALUE 'CLOSE'.   00940000
                                                                        00950000
           05  PC-MINUS-ONE                 PIC S9(01) VALUE -1.        00960000
           05  PC-SYSTEM                    PIC  X(08) VALUE 'SYSTEM  '.00970000
                                                                        00980000
      ******************************************************************00990000
      *                          VARIABLES                              01000000
      ******************************************************************01010000
       01  PROGRAM-VARIABLES.                                           01020000
           05  PV-SQL-SUB                   PIC  9(03) VALUE ZERO.      01030000
           05  PV-DEADLOCK-COUNTER          PIC  9(03) VALUE ZERO.      01040000
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACE.     01050000
           05  PV-DB2-OPERATION             PIC  X(30) VALUE SPACE.     01060000
           05  PV-OPERATION-MSG             PIC  X(35) VALUE SPACE.     01070000
           05  PV-RET-CODE                  PIC  9(04) VALUE ZEROS.     01080000
                                                                        01090000
       01  PV-DB2-VARIABLES.                                            01100000
           05  PV-DB2-PARTN-NBR             PIC S9(04) COMP             01110000
                                                       VALUE ZERO.      01120000
                                                                        01130000
       01  PV-ABEND-DISPLAYS.                                           01181000
           05  PV-ABEND-CRD-NBR            PIC  9(12)    VALUE ZERO.    01182000
           05  PV-ABEND-TRAN-TYPE-CODE     PIC  X(02)    VALUE ZERO.    01183000
                                                                        01184000
                                                                        01190000
      ******************************************************************01200000
      *               SYSTEM TIME AND DATE VARIABLES                    01210000
      ******************************************************************01220000
       01  SYS-TMST-FULL.                                               01230003
      * SAMPLE TIMESTAMP VALUE = 2022-05-17-08.51.00.000000             01231003
           05  SYS-TMST-CC                  PIC  X(02) VALUE SPACES.    01232002
           05  SYS-TMST-YY                  PIC  X(02) VALUE SPACES.    01240003
           05  FILLER                       PIC  X(01) VALUE '-'.       01241002
           05  SYS-TMST-MN                  PIC  X(02) VALUE SPACES.    01250003
           05  FILLER                       PIC  X(01) VALUE '-'.       01250102
           05  SYS-TMST-DD                  PIC  X(02) VALUE SPACES.    01250203
           05  FILLER                       PIC  X(01) VALUE '-'.       01250302
           05  SYS-TMST-HH                  PIC  X(02) VALUE SPACES.    01250403
           05  FILLER                       PIC  X(01) VALUE '.'.       01250502
           05  SYS-TMST-MM                  PIC  X(02) VALUE SPACES.    01250603
           05  FILLER                       PIC  X(01) VALUE '.'.       01250702
           05  SYS-TMST-SS                  PIC  X(02) VALUE '00'.      01250803
           05  FILLER                       PIC  X(01) VALUE '.'.       01251002
           05  SYS-TMST-MILI                PIC  X(06) VALUE '000000'.  01252003
                                                                        01260000
       01  SYS-DATE-TIME.                                               01261002
           05  SYS-DATE                     PIC  X(08) VALUE SPACES.    01262002
           05  SYS-TIME                     PIC  X(08) VALUE SPACES.    01263002
                                                                        01264002
       01  ST-BEG-TIME.                                                 01270000
           05  ST-BEG-HR                    PIC  9(02) VALUE ZEROS.     01280000
           05  FILLER                       PIC  X(01) VALUE ':'.       01290000
           05  ST-BEG-MN                    PIC  9(02) VALUE ZEROS.     01300000
                                                                        01310000
       01  ST-END-TIME.                                                 01320000
           05  ST-END-HR                    PIC  9(02) VALUE ZEROS.     01330000
           05  FILLER                       PIC  X(01) VALUE ':'.       01340000
           05  ST-END-MN                    PIC  9(02) VALUE ZEROS.     01350000
                                                                        01360000
       01  SD-EDIT-DATE.                                                01370000
           05  SD-MONTH                     PIC  9(02) VALUE ZEROS.     01380000
           05  FILLER                       PIC  X(01) VALUE '-'.       01390000
           05  SD-DAY                       PIC  9(02) VALUE ZEROS.     01400000
           05  FILLER                       PIC  X(01) VALUE '-'.       01410000
           05  SD-CENT                      PIC  9(02) VALUE ZEROS.     01420000
           05  SD-YEAR                      PIC  9(02) VALUE ZEROS.     01430000
                                                                        01440000
      ******************************************************************01450000
      *                        ERROR HANDLING                           01460000
      ******************************************************************01470000
       01  ABEND-CODE                       PIC S9(04) VALUE ZEROS      01480000
                                                       COMP SYNC.       01490000
           88  ABEND-4002-READ-ERROR                   VALUE +4002.     01500000
           88  ABEND-4013-DB2-ERROR                    VALUE +4013.     01510000
           88  ABEND-4019-CAL-SUB-ERROR                VALUE +4019.     01520000
                                                                        01530000
                                                                        01540000
      ***************************************************************** 01550002
      *  WS FIELDS FOR SVT_CARD_FAILED_ATTEMPTS DELETES AND             01560002
      *  SVT_CARD_FAILED_ATTEMPTS_HIST INSERTS                          01570002
      ***************************************************************** 01580002
           COPY SVRD024.                                                01581002
                                                                        01590000
      ******************************************************************01600000
      *  DB2 FORMATTED ERROR MESSAGE                                    01610000
      ******************************************************************01620000
           COPY DPWS004.                                                01630000
                                                                        01640000
      ******************************************************************01650000
      *  SQL COMMUNICATIONS WORK AREA                                   01660000
      ******************************************************************01670000
           COPY SQLCA2.                                                 01680000
                                                                        01690000
      ******************************************************************01700000
      *  COMMUNICATIONS AREA FOR DB2                                    01710000
      ******************************************************************01720000
           EXEC SQL                                                     01730000
               INCLUDE SQLCA                                            01740000
           END-EXEC.                                                    01750000
                                                                        01760000
      ***************************************************************** 01840001
      *  STORED VALUE FAILED ATTEMPTS TABLE                             01850003
      ***************************************************************** 01860001
           EXEC SQL                                                     01870003
               INCLUDE SVT00019                                         01880003
           END-EXEC.                                                    01890003
                                                                        01900003
      ***************************************************************** 01901002
      *  STORED VALUE FAILED ATTEMPTS HISTORY TABLE                     01902003
      ***************************************************************** 01903002
           EXEC SQL                                                     01904003
               INCLUDE SVT00020                                         01905003
           END-EXEC.                                                    01906003
                                                                        01907003
                                                                        01908002
      ******************************************************************01910000
      *  TRANSACTION PREPARATION MODULE COMMUNICATIONS AREA             01920000
      ******************************************************************01930000
           COPY DPWS991.                                                01940002
                                                                        01940100
           COPY DPLS001.                                                01941000
           05  WORK-STORAGE-PASSED.                                     01950000
                                                                        01960000
      ******************************************************************01970000
      *PA - PROGRAM ACCUMULATORS                                        01980000
      ******************************************************************01990000
               10  PROGRAM-ACCUMULATORS.                                02000000
                   15  PA-FAILED-ROWS-INSERTED   PIC S9(9) VALUE ZERO   02010001
                                                           COMP-3.      02020000
                   15  PA-FAILED-ROWS-DELETED    PIC S9(9) VALUE ZERO   02030001
                                                           COMP-3.      02030102
                   15  PA-FAILED-ROWS-MISSING    PIC S9(9) VALUE ZERO   02031002
                                                           COMP-3.      02040000
                                                                        02050000
       01  FILLER                           PIC X(4096)  VALUE SPACE.   02060000
                                                                        02070000
      ***************************************************************** 02080000
      * PROCEDURE DIVISION.                                           * 02090000
      ***************************************************************** 02100000
       PROCEDURE DIVISION.                                              02110000
                                                                        02120000
      ***************************************************************** 02130000
      * 0000-MAINLINE-PARAGRAPH                                         02140000
      ***************************************************************** 02150000
       0000-MAINLINE-PARAGRAPH.                                         02160000
                                                                        02170000
           PERFORM A100-INITIALIZE.                                     02180000
                                                                        02190000
           PERFORM B100-PROCESS-FAILED-RECORDS                          02200002
             UNTIL DP001-PREP-TRANS-EOF.                                02210000
                                                                        02220000
           PERFORM Z990-EOJ-ROUTINE.                                    02230000
                                                                        02240000
           GOBACK.                                                      02250000
                                                                        02260000
                                                                        02270000
      ******************************************************************02280000
      * PREFORMS TASK INITIATION                                        02290000
      ******************************************************************02300000
       A100-INITIALIZE.                                                 02310000
                                                                        02320000
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 02330000
                                                                        02340000
           MOVE SYS-TIME (1:2) TO ST-BEG-HR                             02350002
                                  SYS-TMST-HH.                          02351002
           MOVE SYS-TIME (3:2) TO ST-BEG-MN                             02360002
                                  SYS-TMST-MM.                          02370002
           MOVE SYS-DATE (1:2) TO SD-CENT                               02380002
                                  SYS-TMST-CC.                          02381002
           MOVE SYS-DATE (3:2) TO SD-YEAR                               02390002
                                  SYS-TMST-YY.                          02391002
           MOVE SYS-DATE (5:2) TO SD-MONTH                              02400002
                                  SYS-TMST-MN.                          02401002
           MOVE SYS-DATE (7:2) TO SD-DAY                                02410002
                                  SYS-TMST-DD.                          02411002
                                                                        02420000
           MOVE PC-TRAN-PRES-FUNC-OPEN TO DP001-FUNC-CODE.              02430000
                                                                        02440000
           PERFORM X100-BUILD-COMM-AREA-TRAN-PRES.                      02450000
                                                                        02460000
                                                                        02470000
      ******************************************************************02480000
      * PROCESS UPDATE RECORDS                                          02490000
      ******************************************************************02500000
       B100-PROCESS-FAILED-RECORDS.                                     02510002
                                                                        02520000
           SET PS-RESTART-NOT-REQUIRED TO TRUE.                         02530000
                                                                        02540000
           PERFORM C100-DELETE-FAILS.                                   02550002
           IF SV024-DEL-AND-INSRT-REC                                   02551002
               PERFORM C200-INSERT-FAILS                                02552002
           END-IF.                                                      02553002
                                                                        02560000
      *----------------------------------------------------------------*02570000
      *  IF A RESTART IS REQUIRED, ISSUE A RESTART CALL RATHER THAN A  *02580000
      *  TRANS CALL.  THIS WILL ALLOW PROCESSING TO RESUME WITH THE    *02590000
      *  FIRST TRANSACTION AFTER THE LAST COMMIT.                      *02600000
      *----------------------------------------------------------------*02610000
           IF  PS-RESTART-REQUIRED                                      02620000
               MOVE PC-TRAN-PRES-FUNC-RSTRT TO DP001-FUNC-CODE          02630000
           ELSE                                                         02640000
               MOVE PC-TRAN-PRES-FUNC-TRANS TO DP001-FUNC-CODE          02650000
           END-IF.                                                      02660000
                                                                        02670000
           PERFORM X100-BUILD-COMM-AREA-TRAN-PRES.                      02680000
                                                                        02690000
           ADD 1 TO PA-READ.                                            02700000
                                                                        02710000
                                                                        02720000
      ******************************************************************02730000
      * SETUP THE KEY FOR THE DELETE FROM SVT_CARD_FAILED_ATTEMPTS.     02740002
      ******************************************************************02750000
       C100-DELETE-FAILS.                                               02760002
                                                                        02770000
           MOVE SV024-CRD-NBR          TO SVT00019-CRD-NBR.             02780002
           MOVE SV024-TRAN-TYPE-CODE   TO SVT00019-TRAN-TYPE-CODE.      02790002
                                                                        02980000
           PERFORM 5100-DELETE-FAILED-TABLE.                            03000002
                                                                        03010000
      ******************************************************************03011002
      * SETUP THE FIELDS FOR INSERT TO SVT_CARD_FAILED_ATTEMPTS_HIST.   03012002
      ******************************************************************03013002
       C200-INSERT-FAILS.                                               03014002
                                                                        03015002
           MOVE SV024-CRD-NBR          TO SVT00020-CRD-NBR.             03016002
           MOVE SV024-TRAN-TYPE-CODE   TO SVT00020-TRAN-TYPE-CODE.      03017002
           MOVE SYS-TMST-FULL          TO SVT00020-HISTORY-TMST.        03019002
           MOVE SV024-FAIL-COUNT       TO SVT00020-FAIL-COUNT.          03019102
                                                                        03019202
           PERFORM 5000-INSERT-FAILED-HIST-TABLE.                       03019302
                                                                        03020000
      ***************************************************************** 03030000
      * 5000-INSERT-FAILED-HIST-TABLE                                   03040002
      *     INSERT THE SVT_CARD_FAILED_ATTEMPTS_HIST TABLE ROW          03050002
      ***************************************************************** 03060000
       5000-INSERT-FAILED-HIST-TABLE.                                   03070002
                                                                        03080000
           PERFORM WITH TEST AFTER                                      03090000
             VARYING PV-SQL-SUB                                         03100000
             FROM 1 BY 1                                                03110000
             UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                         03120000
                    OR SQLCODE = -911                                   03130000
                    OR SQLCODE = 0)                                     03140000
                                                                        03150000
             EXEC SQL                                                   03160000
               INSERT INTO SVT_CARD_FAILED_ATTEMPTS_HIST                03170002
                                   ( CRD_NBR                            03171002
                                    ,TRAN_TYPE_CODE                     03180002
                                    ,HISTORY_TMST                       03190002
                                    ,FAIL_COUNT )                       03200002
                           VALUES  (:SVT00020-CRD-NBR,                  03280002
                                    :SVT00020-TRAN-TYPE-CODE,           03290002
                                    :SVT00020-HISTORY-TMST,             03300002
                                    :SVT00020-FAIL-COUNT )              03310002
             END-EXEC                                                   03390000
                                                                        03400000
           EVALUATE TRUE                                                03410000
               WHEN SQLCODE = 0                                         03420000
                   ADD +1 TO PA-FAILED-ROWS-INSERTED                    03430001
                                                                        03440000
               WHEN SQLCODE = -911                                      03450000
                   ADD +1 TO PV-DEADLOCK-COUNTER                        03460000
                   DISPLAY '                   '                        03470000
                   DISPLAY 'DEADLOCK INCOUNTERED -911'                  03480000
                   DISPLAY 'DEADLOCK-COUNTER = ' PV-DEADLOCK-COUNTER    03490000
                   IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS            03500000
                       MOVE '5000-INSERT-FAILED-HIST-TABLE'             03510002
                                            TO PV-PARAGRAPH-NAME        03520000
                       MOVE 'MAXIMUM DEADLOCKS EXCEEDED    '            03530000
                                            TO PV-DB2-OPERATION         03540000
                       PERFORM Z200-SQL-ABEND                           03550000
                   ELSE                                                 03560000
                       SET PS-RESTART-REQUIRED TO TRUE                  03570000
                   END-IF                                               03580000
                                                                        03590000
               WHEN OTHER                                               03600000
                  DISPLAY '               '                             03610000
                  DISPLAY 'CARD-NBR     = ' SVT00020-CRD-NBR            03620002
                  DISPLAY 'TRN-TYP-CDE  = ' SVT00020-TRAN-TYPE-CODE     03630002
                  DISPLAY 'HIST-TMST    = ' SVT00020-HISTORY-TMST       03640002
                  DISPLAY 'FAIL-CNT     = ' SVT00020-FAIL-COUNT         03650002
                  DISPLAY '               '                             03720000
                  MOVE '5000-INSERT-FAILED-HIST-TABLE'                  03730002
                                                 TO PV-PARAGRAPH-NAME   03731002
                  MOVE 'ERROR ON FAIL HIST INS'  TO PV-DB2-OPERATION    03740002
                  PERFORM Z200-SQL-ABEND                                03750000
           END-EVALUATE                                                 03760000
           END-PERFORM.                                                 03770000
                                                                        03780000
           IF PV-SQL-SUB > PC-MAX-RETRIES                               03790000
               MOVE '5000-INSERT-FAILED-HIST-TABLE'                     03791002
                                                 TO PV-PARAGRAPH-NAME   03792002
               MOVE 'VERIFY RETRIES EXCEEDED'  TO PV-DB2-OPERATION      03810000
               PERFORM Z200-SQL-ABEND                                   03820000
           END-IF.                                                      03830000
                                                                        03840000
                                                                        03850000
      ***************************************************************** 03860000
      * 5100-DELETE-FAILED-TABLE                                        03870002
      *     DELETE ROWS FROM SVT_CARD_FAILED_ATTEMPTS                   03880002
      ***************************************************************** 03890000
       5100-DELETE-FAILED-TABLE.                                        03900002
                                                                        03910000
           PERFORM WITH TEST AFTER                                      03920000
             VARYING PV-SQL-SUB                                         03930000
             FROM 1 BY 1                                                03940000
             UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                         03950000
                    OR SQLCODE = -911                                   03960000
                    OR SQLCODE = +100                                   03970000
                    OR SQLCODE = 0)                                     03980000
                                                                        03990000
           EXEC SQL                                                     04000002
                DELETE                                                  04010002
                  FROM SVT_CARD_FAILED_ATTEMPTS                         04020002
                 WHERE CRD_NBR        = :SVT00019-CRD-NBR               04030002
                   AND TRAN_TYPE_CODE = :SVT00019-TRAN-TYPE-CODE        04040002
           END-EXEC                                                     04050002
                                                                        04130000
           EVALUATE TRUE                                                04140000
               WHEN SQLCODE = 0                                         04150000
                   ADD +1 TO PA-FAILED-ROWS-DELETED                     04160002
                                                                        04170000
               WHEN SQLCODE = -911                                      04180000
                   ADD +1 TO PV-DEADLOCK-COUNTER                        04190000
                   DISPLAY '                   '                        04200000
                   DISPLAY 'DEADLOCK INCOUNTERED -911'                  04210000
                   DISPLAY 'DEADLOCK-COUNTER = ' PV-DEADLOCK-COUNTER    04220000
                   IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS            04230000
                       MOVE '5100-DELETE-FAILED-TABLE'                  04240002
                                            TO PV-PARAGRAPH-NAME        04250000
                       MOVE 'MAXIMUM DEADLOCKS EXCEEDED    '            04260000
                                            TO PV-DB2-OPERATION         04270000
                       PERFORM Z200-SQL-ABEND                           04280000
                   ELSE                                                 04290000
                       SET PS-RESTART-REQUIRED TO TRUE                  04300000
                   END-IF                                               04310000
                                                                        04320000
               WHEN SQLCODE = +100                                      04330002
                  ADD +1 TO PA-FAILED-ROWS-MISSING                      04331002
                  DISPLAY '               '                             04340000
                  DISPLAY 'CARD-NBR     = ' SVT00019-CRD-NBR            04341002
                  DISPLAY 'TRN-TYP-CDE  = ' SVT00019-TRAN-TYPE-CODE     04342002
                  DISPLAY '               '                             04420000
                  DISPLAY '5100-DELETE-FAILED-TABLE'                    04440002
                  DISPLAY 'ROW NOT FOUND FOR DELETE'                    04460002
                                                                        04470002
               WHEN OTHER                                               04471002
                  DISPLAY '               '                             04472002
                  DISPLAY 'CARD-NBR     = ' SVT00019-CRD-NBR            04473002
                  DISPLAY 'TRN-TYP-CDE  = ' SVT00019-TRAN-TYPE-CODE     04474002
                  DISPLAY '               '                             04475002
                  MOVE '5100-DELETE-FAILED-TABLE'                       04477002
                                                  TO PV-PARAGRAPH-NAME  04478002
                  MOVE 'ERROR ON FAILED DELETE'  TO PV-DB2-OPERATION    04479002
                  PERFORM Z200-SQL-ABEND                                04479102
           END-EVALUATE                                                 04480000
           END-PERFORM.                                                 04490000
                                                                        04500000
           IF PV-SQL-SUB > PC-MAX-RETRIES                               04510000
               MOVE '5100-DELETE-FAILED-TABLE'                          04520002
                                              TO PV-PARAGRAPH-NAME      04530000
               MOVE 'VERIFY RETRIES EXCEEDED' TO PV-DB2-OPERATION       04540000
               PERFORM Z200-SQL-ABEND                                   04550000
           END-IF.                                                      04560000
                                                                        04570000
                                                                        04580000
      ******************************************************************04590000
      * BUILD THE COMMUNICATION AREA FOR A CALL TO THE TRANSACTION      04600000
      * PRESENTATION MODULE                                             04610000
      ******************************************************************04620000
       X100-BUILD-COMM-AREA-TRAN-PRES.                                  04630000
                                                                        04640000
           MOVE LENGTH OF                                               04650000
                WORK-STORAGE-PASSED TO DP001-WORK-STRG-LENGTH.          04660000
           MOVE PC-CKPT-JOB-NAME    TO DP001-JOB-NAME.                  04670000
           MOVE PC-CKPT-PLAN-NAME   TO DP001-PLAN-NAME.                 04680000
                                                                        04690000
           PERFORM X200-CALL-TRANS-PRES-MODULE.                         04700000
                                                                        04710000
           MOVE DP001-TRANS-RECORD  TO SV024-CARD-UPDT-RECORD.          04720002
                                                                        04730000
           IF  DP001-CKPT-TAKEN                                         04740000
               ADD 1 TO PA-CKPT                                         04750000
                                                                        04760000
               INITIALIZE PV-DEADLOCK-COUNTER                           04770000
           END-IF.                                                      04780000
                                                                        04790000
                                                                        04800000
      ******************************************************************04810000
      * VERIFY THE RETURN CODE ON THE CALLED MODULE                     04820000
      ******************************************************************04830000
       X200-CALL-TRANS-PRES-MODULE.                                     04840000
                                                                        04850000
           PERFORM Z900-CALL-TRANS-PRES-MODULE.                         04860000
                                                                        04870000
           IF  DP001-GOOD-RET-CODE                                      04880000
           OR  DP001-CKPT-TAKEN                                         04890000
           OR  DP001-RESTART                                            04900000
               CONTINUE                                                 04910000
                                                                        04920000
           ELSE                                                         04930000
               MOVE 'X200-CALL-TRANS-PRES-MODULE' TO PV-PARAGRAPH-NAME  04940000
               MOVE '** BAD CALL TO TRANS PREP MODULE **'               04950000
                                                  TO PV-OPERATION-MSG   04960000
               MOVE DP001-RET-CODE                TO PV-RET-CODE        04970000
                                                                        04980000
               SET ABEND-4019-CAL-SUB-ERROR TO TRUE                     04990000
                                                                        05000000
               PERFORM Z100-ABEND                                       05010000
           END-IF.                                                      05020000
                                                                        05030000
                                                                        05040000
      ******************************************************************05050000
      * NON-DB2 ABEND                                                   05060000
      ******************************************************************05070000
       Z100-ABEND.                                                      05080000
                                                                        05090000
           PERFORM Z999-CONTROLS.                                       05100000
                                                                        05110000
           DISPLAY '                     '.                             05120000
           DISPLAY '** ABEND           **'.                             05130000
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          05140000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        05150000
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.         05160000
           DISPLAY '** RETURN CODE     ** = ' PV-RET-CODE.              05170000
                                                                        05180000
           CALL 'ILBOABN0' USING ABEND-CODE.                            05190000
                                                                        05200000
                                                                        05210000
      ******************************************************************05220000
      * ABEND THE PROGRAM IN THE EVENT THAT A SQL ERROR OR WARNING      05230000
      * CODE OCCURS.                                                    05240000
      ******************************************************************05250000
       Z200-SQL-ABEND.                                                  05260000
                                                                        05270000
           PERFORM Z999-CONTROLS.                                       05280000
                                                                        05290000
           DISPLAY '** ABEND     **'.                                   05300000
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                05310000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              05320000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               05330000
                                                                        05340000
      ******************************************************************05350000
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    05360000
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         05370000
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     05380000
      * FORMAT.                                                         05390000
      ******************************************************************05400000
           COPY DPPD004.                                                05410000
                                                                        05420000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            05430000
                                                                        05440000
           CALL 'ILBOABN0' USING ABEND-CODE.                            05450000
                                                                        05460000
                                                                        05470000
      ******************************************************************05480000
      * PERFORMS TASK TERMINATION PROCESSING                            05490000
      ******************************************************************05500000
       Z990-EOJ-ROUTINE.                                                05510000
                                                                        05520000
           MOVE PC-TRAN-PRES-FUNC-CLOSE TO DP001-FUNC-CODE.             05530000
                                                                        05540000
           PERFORM X100-BUILD-COMM-AREA-TRAN-PRES.                      05550000
                                                                        05560000
           PERFORM Z999-CONTROLS.                                       05570000
                                                                        05580000
                                                                        05590000
      ******************************************************************05600000
      * DISPLAY CONTROLS FROM THE PROGRAM                               05610000
      ******************************************************************05620000
       Z999-CONTROLS.                                                   05630000
                                                                        05640000
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 05650000
                                                                        05660000
           MOVE SYS-TIME (1:2) TO ST-END-HR.                            05670000
           MOVE SYS-TIME (3:2) TO ST-END-MN.                            05680000
                                                                        05690000
           DISPLAY '                    '.                              05700000
           DISPLAY '**********************'.                            05710000
           DISPLAY '   SVKUP012 CONTROLS  '.                            05720000
           DISPLAY '**********************'.                            05730000
           DISPLAY 'RUN DATE  = ' SD-EDIT-DATE.                         05740000
           DISPLAY 'BEG TIME  = ' ST-BEG-TIME.                          05750000
           DISPLAY 'END TIME  = ' ST-END-TIME.                          05760000
           DISPLAY '======================'.                            05770000
           DISPLAY 'RECORDS READ   = ' PA-READ.                         05780000
           DISPLAY 'CHECKPOINTS    = ' PA-CKPT.                         05790000
           DISPLAY '                '.                                  05800000
           DISPLAY 'FAILED ROWS INSERTED = ' PA-FAILED-ROWS-INSERTED.   05810001
           DISPLAY 'FAILED ROWS DELETED  = ' PA-FAILED-ROWS-DELETED.    05820001
           DISPLAY 'FAILED ROWS MISSING  = ' PA-FAILED-ROWS-MISSING.    05821002
           DISPLAY '                '.                                  05830000
                                                                        05840000
                                                                        05850000
      ******************************************************************05860000
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    05870000
      * MENTS THAT ARE REQUIRED TO INVOKE THE TRANSACTION PRESENTATION  05880000
      * MODULE (DPKUT901) OF THE DB2 CHECKPOINT RESTART SYSTEM.         05890000
      ******************************************************************05900000
           COPY DPPD001.                                                05910000
                                                                        05920000
