000100******************************************************************02/19/96
000200 IDENTIFICATION DIVISION.                                         RCK00188
000300******************************************************************   LV029
000400                                                                     CL**2
000500 PROGRAM-ID.    RCKUT035.                                            CL**2
000600 AUTHOR.        MARY SARDINA.                                        CL**2
000700 INSTALLATION.  KOHLS.                                               CL**2
000800 DATE-WRITTEN   JANUARY, 1996.                                       CL**2
000900 DATE-COMPILED.                                                      CL**2
001000                                                                     CL**2
001100******************************************************************   CL**2
001200*  THIS PROGRAM DELETES ROWS FROM THE RECEIVER AUDIT TABLE           CL**3
001300*  AUDIT TABLE.  IT ALSO CREATES A TIMESTAMP CONTROL FILE.           CL**2
001400******************************************************************   CL**2
001500                                                                     CL**2
001600******************************************************************   CL**2
001700 ENVIRONMENT DIVISION.                                               CL**2
001800******************************************************************   CL**2
001900                                                                     CL**2
002000 CONFIGURATION SECTION.                                              CL**2
002100                                                                     CL**2
002200 SOURCE-COMPUTER.        IBM-3090.                                   CL**2
002300 OBJECT-COMPUTER.        IBM-3090.                                   CL**2
002400                                                                     CL**2
002500 INPUT-OUTPUT SECTION.                                               CL**2
002600                                                                     CL**2
002700 FILE-CONTROL.                                                       CL**2
002800                                                                     CL**2
002900                                                                     CL**2
003000     SELECT TIMESTAMP-FILE                                           CL**2
003100         ASSIGN TO IN01.                                             CL**7
003200                                                                     CL**2
003300******************************************************************   CL**2
003400 DATA DIVISION.                                                      CL**2
003500******************************************************************   CL**2
003600                                                                     CL**2
003700 FILE SECTION.                                                       CL**2
003800                                                                     CL**2
003900 FD  TIMESTAMP-FILE                                                  CL**2
004000     RECORDING MODE IS F                                             CL**2
004100     LABEL RECORDS ARE STANDARD                                      CL**2
004200     BLOCK CONTAINS 0 RECORDS.                                       CL**2
004300                                                                     CL**2
004400 01  TIMESTAMP-RECORD           PIC  X(50).                          CL**2
004500                                                                     CL**2
004600*                                                                    CL**2
004700******************************************************************   CL**2
004800 WORKING-STORAGE SECTION.                                            CL**2
004900******************************************************************   CL**2
005000*                                                                    CL**2
005100*                                                                    CL**2
005600*                                                                    CL**2
005700*                                                                    CL**2
005800******************************************************************   CL**2
005900*  RECORD LAYOUT FOR THE TIMESTAMP FILE.                             CL**2
006000******************************************************************   CL**2
006100     COPY RCRD011.                                                   CL**2
006200*                                                                    CL**2
006300*                                                                    CL**2
006400******************************************************************   CL**2
006500*  RECORD LAYOUT FOR THE RECEIVER AUDIT TABLE FILE.                  CL**2
006600******************************************************************   CL**2
006700     COPY RCRD012.                                                   CL**2
006800*                                                                    CL**2
006900*                                                                    CL**2
007000******************************************************************   CL**2
007100*  DB2 FORMATTED MESSAGE AREA                                        CL**2
007200******************************************************************   CL**2
007300     COPY DPWS004.                                                   CL**2
007400*                                                                    CL**2
007500*                                                                    CL**2
007600     COPY SQLCA2.                                                    CL*19
007700*                                                                    CL**2
007800******************************************************************   CL**2
007900*  COMMUNICATIONS AREA FOR DB2                                       CL**2
008000******************************************************************   CL**2
008100*                                                                    CL**2
008200     EXEC SQL                                                        CL*20
008300          INCLUDE SQLCA                                              CL*20
008400     END-EXEC.                                                       CL*20
008500*                                                                    CL*18
008600*                                                                    CL**2
008700******************************************************************   CL**2
008800*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE RECEIVER AUDIT        CL**2
008900*  TABLE                                                             CL**2
009000******************************************************************   CL**2
009100     EXEC SQL                                                        CL**2
009200          INCLUDE TAURECV                                            CL**2
009300     END-EXEC.                                                       CL**2
009400*                                                                    CL**2
009500 01  ABEND-CODE                      PIC S9(04)    VALUE +0          CL**2
009600                                                   COMP SYNC.        CL**2
009700                                                                     CL**2
009800 01  PS-PROGRAM-SWITCHES.                                            CL**2
009900     05  PS-USE-SHIPPED-ITEM-SW      PIC  X(01)    VALUE SPACE.      CL**2
010000         88  PS-USE-SHIPPED-ITEM                   VALUE 'Y'.        CL**2
010100     05  PS-EOF-AUDIT-RECV-CSR-SW    PIC  X(01)    VALUE SPACE.      CL**2
010200         88  PS-EOF-AUDIT-RECV-CSR                 VALUE 'Y'.        CL**2
010300                                                                     CL**2
010400                                                                     CL**2
010500 01  PC-PROGRAM-CONSTANTS.                                           CL**2
010600     05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100        CL**2
010700                                                   COMP SYNC.        CL**2
010800     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE             CL**2
010900         'RCKUT035'.                                                 CL**2
011000     05  PC-CURRENT-OPERATING-COMPANY                                CL**2
011100                                     PIC  X(02)    VALUE '03'.       CL**2
011200     05  PC-MAX-RETRIES              PIC S9(04)    VALUE +3          CL**2
011300                                                   COMP SYNC.        CL**2
011400     05  PC-CURRENT-TIMESTAMP        PIC  X(26)    VALUE SPACES.     CL**2
011500                                                                     CL**3
011600                                                                     CL**3
011700     05  PC-COMMIT-DEL-COUNT         PIC 9(09)     VALUE ZERO.       CL**3
011800                                                                     CL**2
011900     05  PC-COMMIT-HOLD              PIC 9(3)      VALUE 099.        CL*29
012000                                                                     CL**3
012100 01  PV-PROGRAM-VARIABLES.                                           CL**2
012200     05  PV-ITEM-NBR                 PIC  9(15)    VALUE ZERO.       CL**2
012300     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.     CL**2
012400     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.     CL**2
012500     05  PV-OPEN-COUNTER             PIC S9(04)    VALUE ZERO        CL**2
012600                                                   COMP SYNC.        CL**2
012700     05  PV-CLOSE-COUNTER            PIC S9(04)    VALUE ZERO        CL**2
012800                                                   COMP SYNC.        CL**2
012900     05  PV-FETCH-COUNTER            PIC S9(04)    VALUE ZERO        CL**2
013000                                                   COMP SYNC.        CL**2
013100     05  PV-WRITE-COUNTER            PIC S9(04)    VALUE ZERO        CL**2
013200                                                   COMP SYNC.        CL**2
013300     05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                CL**2
013400                                                                     CL**2
013500     05  PV-DB2-COUNT                PIC S9(09)    VALUE ZERO        CL**2
013600                                                   COMP-3.           CL**2
013700     05  PV-OPER-UNT-ID              PIC 9(04)     VALUE ZERO.       CL**2
013800                                                                     CL**2
013900     05  PV-RECV-SEQ-NBR             PIC 9(03)     VALUE ZERO.       CL**2
014000                                                                     CL**2
014100     05  PV-PO-NBR                   PIC S9(08)    VALUE ZERO.       CL**2
014200                                                                     CL**2
014300     05  PV-PO-LNE-NBR               PIC 9(03)     VALUE ZERO.       CL**2
014400                                                                     CL**2
014500     05  PV-DELETE-COUNTER           PIC S9(04)  VALUE +0            CL**3
014600                                                    COMP SYNC.       CL**3
014700     05  PV-DELETE-COUNT             PIC S9(04)  VALUE +0            CL**6
014800                                                    COMP SYNC.       CL**6
014900     05  PV-COMMIT-COUNT             PIC S9(04)  VALUE +0            CL**3
015000                                                    COMP SYNC.       CL**3
015100     05  PV-COMMIT-COUNTER           PIC 9(03)     VALUE ZERO.       CL**3
015200                                                                     CL**3
015300 01  PV-COMMIT-INFO.                                                 CL**3
015400     05  FILLER                      PIC X(23) VALUE                 CL**3
015500        ' ORD /SEQ/TIMESTAMP: '.                                     CL**3
015600     05  PV-COMMIT-KEY.                                              CL**3
015700         10  PV-COMMIT-ORD-NBR       PIC X(09).                      CL*24
015800         10  FILLER                  PIC X(01) VALUE SPACE.          CL*24
015900         10  PV-COMMIT-SEQ-NBR       PIC X(09).                      CL*24
016000         10  FILLER                  PIC X(01) VALUE SPACE.          CL*24
016100         10  PV-COMMIT-TIMESTAMP     PIC X(26).                      CL**3
016200     05  FILLER                      PIC X(12) VALUE                 CL**3
016300        ' AUDIT CDE: '.                                              CL*17
016400     05  PV-AUDIT-CODE               PIC X(01).                      CL*28
016500     05  FILLER                      PIC X(24) VALUE SPACE.          CL*25
016600     05  FILLER                      PIC X(22) VALUE                 CL*24
016700        ' # RECS DELETED     : '.                                    CL**3
016800     05  PV-RECS-DELETED-COUNT       PIC 9(7)  VALUE 0.              CL**3
016900*                                                                    CL**2
017000*                                                                    CL**2
017100 01  SD-SYSTEM-DATE.                                                 CL**2
017200     05  SD-SYSTEM-DATE-YY           PIC  9(02)      VALUE ZERO.     CL**2
017300     05  SD-SYSTEM-DATE-MM           PIC  9(02)      VALUE ZERO.     CL**2
017400     05  SD-SYSTEM-DATE-DD           PIC  9(02)      VALUE ZERO.     CL**2
017500                                                                     CL**2
017600*                                                                    CL**2
017700*  CURSOR DECLARATION FOR ALL EXCEPTION ACTIVITY ON THE              CL**2
017800*  UPC CERTIFICATION ACTIVITY LOG TABLE                              CL**2
017900*                                                                    CL**2
018000                                                                     CL**2
018100     EXEC SQL                                                        CL**2
018200          DECLARE AUDIT_RECV_CURSOR CURSOR WITH HOLD FOR             CL*14
018300           SELECT                                                    CL**2
018400             ORD_NBR                                                 CL**2
018500            ,REC_SEQ_NBR                                             CL**2
018600            ,CRE_TMST                                                CL**2
018700            ,CRE_UID                                                 CL**2
018800            ,PO_VER_NBR                                              CL**2
018900            ,OPER_UNT_ID                                             CL**2
019000            ,FCLTY_ID                                                CL**2
019100            ,STATUS_CDE                                              CL**2
019200            ,SYST_CRE_CDE                                            CL**2
019300            ,REC_TYP_CDE                                             CL**2
019400            ,VER_STATUS_CDE                                          CL**2
019500            ,PACK_BY_ST_IND                                          CL**2
019600            ,REC_TMST                                                CL**2
019700            ,EXPTED_CART_CNT                                         CL**2
019800            ,ACTL_CART_CNT                                           CL**2
019900            ,EXPTED_ITM_QTY                                          CL**2
020000            ,ACTL_ITM_QTY                                            CL**2
020100            ,RCVR_NBR                                                CL**2
020200            ,ML_POST_TMST                                            CL**2
020300            ,FS_POST_TMST                                            CL**2
020400            ,PRE_TKT_IND                                             CL**2
020500            ,AD_INFO_DESC                                            CL**2
020600            ,IN_TRBL_TMST                                            CL**2
020700            ,IN_TRBL_MIN_CNT                                         CL**2
020800            ,IN_TRBL_HOUR_CNT                                        CL**2
020900            ,VERFYD_TMST                                             CL**2
021000            ,RCVG_CLERK_INIT_ID                                      CL**2
021200            ,REFER_BACK_SEQ_NBR                                      CL**2
021300            ,AP_PRC_CDE                                              CL**2
021400            ,AP_PRC_DTE                                              CL**2
021500            ,MIS_DIRECT_IND                                          CL**2
021600            ,IMPRT_IND                                               CL**2
021700            ,CRE_OPER_UNT_ID                                         CL**2
021800            ,CRE_FCLTY_ID                                            CL**2
021900            ,CURR_OPER_UNT_ID                                        CL**2
022000            ,CURR_FCLTY_ID                                           CL**2
022100            ,UPD_TMST                                                CL**2
022200            ,UPD_UID                                                 CL**2
022300            ,UPD_PGM_ID                                              CL**2
022400            ,APRON_LOC_DESC                                          CL**2
022500            ,MKNG_APRN_PRT_TMST                                      CL**2
022600            ,GOH_IND                                                 CL**2
022700            ,AUD_TYP_CDE                                             CL**2
022800            ,AUD_FUNC_CDE                                            CL**2
022900            ,NOTIF_NBR                                               CL*23
022901            ,VRFD_USR_ID                                                  
022902            ,AP_RVRSL_IND                                                 
022910            ,AP_RVRSL_DTE                                                 
                  ,PKGNG_CDE                                                    
023000             FROM TAURECV                                            CL**2
023100            WHERE UPD_TMST = :PC-CURRENT-TIMESTAMP                   CL**2
023200               OR UPD_TMST < :PC-CURRENT-TIMESTAMP                   CL**2
023300                                                                     CL**2
023400     END-EXEC.                                                       CL**2
023500                                                                     CL**2
023600                                                                     CL**2
023700******************************************************************   CL**2
023800 PROCEDURE DIVISION.                                                 CL**2
023900******************************************************************   CL**2
024000                                                                     CL**2
024100 0000-PROGRAM-MAINLINE.                                              CL**2
024200******************************************************************   CL**2
024300* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE        CL**2
024400*      PROGRAM.                                                      CL**2
024500******************************************************************   CL**2
024600                                                                     CL**2
024700     PERFORM 1000-INITIALIZE.                                        CL**2
024800     PERFORM 2000-PROCESS-AUDIT-RECEIVERS                            CL**2
024900       UNTIL PS-EOF-AUDIT-RECV-CSR.                                  CL**2
025000     PERFORM 9000-EOJ-ROUTINE.                                       CL**2
025100                                                                     CL**2
025200     STOP RUN.                                                       CL**2
025300*0000-EXIT.                                                          CL**2
025400*                                                                    CL**2
025500*                                                                    CL**2
025600 1000-INITIALIZE.                                                    CL**2
025700******************************************************************   CL**2
025800*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                       CL**2
025900*    -- OPEN CURSOR                                                  CL**2
026000*    -- FETCH CURSOR                                                 CL**2
026100******************************************************************   CL**2
026200*                                                                    CL**2
026300     ACCEPT SD-SYSTEM-DATE FROM DATE.                                CL**2
026400* *  MOVE SD-SYSTEM-DATE-YY      TO DP105-DATE1M-YY.                 CL**2
026500* *  MOVE SD-SYSTEM-DATE-MM      TO DP105-DATE1M-MM.                 CL**2
026600* *  MOVE SD-SYSTEM-DATE-DD      TO DP105-DATE1M-DD.                 CL**2
026700*                                                                    CL**2
026800     OPEN INPUT  TIMESTAMP-FILE.                                     CL**7
026900                                                                     CL**7
027000     PERFORM 1100-READ-TIMESTAMP-FILE.                               CL**7
027100                                                                     CL**2
027200     MOVE +0 TO PV-COMMIT-COUNTER                                    CL*21
027300                                                                     CL*21
027400     PERFORM 7000-OPEN-AUDIT-RECV-CURSOR.                            CL**2
027500                                                                     CL**7
027600     PERFORM 7100-FETCH-AUDIT-RECV-CURSOR.                           CL**2
027700*1000-EXIT.                                                          CL**2
027800*                                                                    CL**2
027900                                                                     CL**2
028000 1100-READ-TIMESTAMP-FILE.                                           CL**7
028100******************************************************************   CL**7
028200*  READ   THE CONTROL CARD.                                          CL**7
028300******************************************************************   CL**7
028400      READ TIMESTAMP-FILE INTO                                       CL**8
028500          RC011-CONTROL-RECORD.                                      CL**7
028600      MOVE RC011-CONTROL-TMST TO PC-CURRENT-TIMESTAMP.               CL**7
028700     DISPLAY 'RCKUT035 CONTROL CARD -- ' RC011-CONTROL-TMST.         CL*12
028800     DISPLAY 'TAURECV TABLE'.                                        CL*27
028900*                                                                    CL**2
029000*1100-EXIT.                                                          CL**2
029100*                                                                    CL**2
029200 2000-PROCESS-AUDIT-RECEIVERS.                                       CL**2
029300******************************************************************   CL**2
029400*  PROCESS THE CERTIFICATION REQUESTS (LOOP THROUGH THE RESULTS      CL**2
029500*  TABLE).  FORMAT THE OUTPUT RECORD.  WRITE THE RECORDS THAT        CL**2
029600*  MEET THE CRITERIA.                                                CL**2
029700******************************************************************   CL**2
029800                                                                     CL**2
029900     PERFORM 8000-DELETE-AUDIT-RECV-RECORD.                          CL**3
030000                                                                     CL**3
030100     ADD +1 TO PV-COMMIT-COUNTER.                                    CL**3
030200                                                                     CL**3
030300     IF PV-COMMIT-COUNTER > PC-COMMIT-HOLD                           CL**3
030400         EXEC SQL                                                    CL**3
030500            COMMIT                                                   CL**3
030600         END-EXEC                                                    CL**3
030700                                                                     CL**3
030800         IF SQLCODE NOT = +0                                         CL**3
030900             DISPLAY '### COMMIT FAILED ! ###'                       CL**3
031000             PERFORM 9100-SQL-ABEND                                  CL**6
031100         END-IF                                                      CL**3
031200                                                                     CL**3
031300         ADD PV-COMMIT-COUNTER           TO PV-COMMIT-COUNT          CL*21
031400         MOVE AURECV-ORD-NBR             TO PV-COMMIT-ORD-NBR        CL**6
031500         MOVE AURECV-REC-SEQ-NBR         TO PV-COMMIT-SEQ-NBR        CL**6
031600         MOVE AURECV-UPD-TMST            TO PV-COMMIT-TIMESTAMP      CL**6
031700         MOVE AURECV-AUD-TYP-CDE         TO PV-AUDIT-CODE            CL*22
031800                                                                     CL**3
031900         MOVE PV-COMMIT-COUNT    TO PV-RECS-DELETED-COUNT            CL*21
032000         DISPLAY 'COMMIT TAKEN: ' PV-COMMIT-INFO                     CL**5
032100                                                                     CL**3
032200         MOVE +0 TO PV-COMMIT-COUNTER                                CL*21
032300     END-IF.                                                         CL**3
032400                                                                     CL**3
032500     PERFORM 7100-FETCH-AUDIT-RECV-CURSOR.                           CL**3
032600     EJECT                                                           CL**3
032700*2000-EXIT.                                                          CL**2
032800                                                                     CL**2
032900*                                                                    CL**2
033000 7000-OPEN-AUDIT-RECV-CURSOR.                                        CL**2
033100******************************************************************   CL**2
033200*  OPEN THE CURSOR.                                                  CL**2
033300******************************************************************   CL**2
033400     PERFORM WITH TEST AFTER                                         CL**2
033500       VARYING PV-OPEN-COUNTER FROM 1 BY 1                           CL**2
033600         UNTIL (PV-OPEN-COUNTER > PC-MAX-RETRIES                     CL**2
033700                OR                                                   CL**2
033800                SQLCODE = ZERO)                                      CL**2
033900                                                                     CL**2
034000         EXEC SQL                                                    CL**2
034100              OPEN AUDIT_RECV_CURSOR                                 CL**2
034200         END-EXEC                                                    CL**2
034300                                                                     CL**2
034400         EVALUATE TRUE                                               CL**2
034500             WHEN SQLCODE = -904                                     CL**2
034600             WHEN SQLCODE = -911                                     CL**2
034700                 CONTINUE                                            CL**2
034800             WHEN SQLWARN0 NOT = SPACES                              CL**2
034900             WHEN SQLCODE < ZERO                                     CL**2
035000             WHEN SQLCODE > ZERO                                     CL**2
035100                 MOVE '7000-OPEN-AUDIT-RECV-CURSOR    '              CL**2
035200                                 TO PV-PARAGRAPH-NAME                CL**2
035300                 MOVE 'OPEN THE AUDIT_RECV_CURSOR    '               CL**2
035400                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**2
035500                 PERFORM 9100-SQL-ABEND                              CL**2
035600             WHEN SQLCODE = ZERO                                     CL**2
035700                 CONTINUE                                            CL**2
035800         END-EVALUATE                                                CL**2
035900     END-PERFORM.                                                    CL**2
036000                                                                     CL**2
036100     IF PV-OPEN-COUNTER > PC-MAX-RETRIES                             CL**2
036200         MOVE '7000-OPEN-AUDIT-RECV-CURSOR   '                       CL**2
036300                                 TO PV-PARAGRAPH-NAME                CL**2
036400         MOVE 'OPEN MAX RETRIES EXCEEDED     '                       CL**2
036500                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**2
036600         PERFORM 9100-SQL-ABEND                                      CL**2
036700     END-IF.                                                         CL**2
036800*7000-EXIT.                                                          CL**2
036900*                                                                    CL**2
037000*                                                                    CL**2
037100 7100-FETCH-AUDIT-RECV-CURSOR.                                       CL**2
037200******************************************************************   CL**2
037300*  FETCH THE ROW FROM THE RESULTS TABLE USING THE CURSOR.            CL**2
037400******************************************************************   CL**2
037500     EXEC SQL                                                        CL**2
037600          FETCH AUDIT_RECV_CURSOR                                    CL**2
037700           INTO :AURECV-ORD-NBR,                                     CL**2
037800                :AURECV-REC-SEQ-NBR,                                 CL**2
037900                :AURECV-CRE-TMST,                                    CL**2
038000                :AURECV-CRE-UID,                                     CL**2
038100                :AURECV-PO-VER-NBR,                                  CL**2
038200                :AURECV-OPER-UNT-ID,                                 CL**2
038300                :AURECV-FCLTY-ID,                                    CL**2
038400                :AURECV-STATUS-CDE,                                  CL**2
038500                :AURECV-SYST-CRE-CDE,                                CL**2
038600                :AURECV-REC-TYP-CDE,                                 CL**2
038700                :AURECV-VER-STATUS-CDE,                              CL**2
038800                :AURECV-PACK-BY-ST-IND,                              CL**2
038900                :AURECV-REC-TMST,                                    CL**2
039000                :AURECV-EXPTED-CART-CNT,                             CL**2
039100                :AURECV-ACTL-CART-CNT,                               CL**2
039200                :AURECV-EXPTED-ITM-QTY,                              CL**2
039300                :AURECV-ACTL-ITM-QTY,                                CL**2
039400                :AURECV-RCVR-NBR,                                    CL**2
039500                :AURECV-ML-POST-TMST,                                CL**2
039600                :AURECV-FS-POST-TMST,                                CL**2
039700                :AURECV-PRE-TKT-IND,                                 CL**2
039800                :AURECV-AD-INFO-DESC,                                CL**2
039900                :AURECV-IN-TRBL-TMST,                                CL**2
040000                :AURECV-IN-TRBL-MIN-CNT,                             CL**2
040100                :AURECV-IN-TRBL-HOUR-CNT,                            CL**2
040200                :AURECV-VERFYD-TMST,                                 CL**2
040300                :AURECV-RCVG-CLERK-INIT-ID,                          CL**2
040500                :AURECV-REFER-BACK-SEQ-NBR,                          CL**2
040600                :AURECV-AP-PRC-CDE,                                  CL**2
040700                :AURECV-AP-PRC-DTE,                                  CL**2
040800                :AURECV-MIS-DIRECT-IND,                              CL**2
040900                :AURECV-IMPRT-IND,                                   CL**2
041000                :AURECV-CRE-OPER-UNT-ID,                             CL**2
041100                :AURECV-CRE-FCLTY-ID,                                CL**2
041200                :AURECV-CURR-OPER-UNT-ID,                            CL**2
041300                :AURECV-CURR-FCLTY-ID,                               CL**2
041400                :AURECV-UPD-TMST,                                    CL**2
041500                :AURECV-UPD-UID,                                     CL**2
041600                :AURECV-UPD-PGM-ID,                                  CL**2
041700                :AURECV-APRON-LOC-DESC,                              CL**2
041800                :AURECV-MKNG-APRN-PRT-TMST,                          CL**2
041900                :AURECV-GOH-IND,                                     CL**2
042000                :AURECV-AUD-TYP-CDE,                                 CL**2
042100                :AURECV-AUD-FUNC-CDE,                                CL**2
042200                :AURECV-NOTIF-NBR,                                   CL*23
042210                :AURECV-VRFD-USR-ID,                                      
042220                :AURECV-AP-RVRSL-IND,                                     
042230                :AURECV-AP-RVRSL-DTE,                                     
                      :AURECV-PKGNG-CDE                                         
042300     END-EXEC.                                                       CL**2
042400                                                                     CL**2
042500     EVALUATE TRUE                                                   CL**2
042600         WHEN SQLCODE = ZERO                                         CL**2
042700             CONTINUE                                                CL**2
042800         WHEN SQLCODE = +100                                         CL**2
042900             SET PS-EOF-AUDIT-RECV-CSR                               CL**2
043000                                 TO TRUE                             CL**2
043100         WHEN SQLWARN0 NOT = SPACES                                  CL**2
043200         WHEN SQLCODE < ZERO                                         CL**2
043300         WHEN SQLCODE > ZERO                                         CL**2
043400             MOVE '7100-FETCH-AUDIT-RECV-CURSOR  '                   CL**2
043500                                 TO PV-PARAGRAPH-NAME                CL**2
043600             MOVE 'FETCH THE AUDIT-RECV-CURSOR   '                   CL**2
043700                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**2
043800             PERFORM 9100-SQL-ABEND                                  CL**2
043900     END-EVALUATE.                                                   CL**2
044000*7100-EXIT.                                                          CL**2
044100*                                                                    CL**2
044200 8000-DELETE-AUDIT-RECV-RECORD.                                      CL**3
044300******************************************************************   CL**2
044400*  DELETE THE ROW FORM THE RECEIVER AUDIT TABLE                      CL**3
044500******************************************************************   CL**2
044600                                                                     CL**3
044700     PERFORM                                                         CL**3
044800         WITH TEST AFTER                                             CL**3
044900         VARYING PV-DELETE-COUNTER                                   CL**3
045000         FROM 1 BY 1                                                 CL**3
045100         UNTIL (PV-DELETE-COUNTER > PC-MAX-RETRIES                   CL**3
045200                   OR SQLCODE = 0                                    CL**3
045300                   OR SQLCODE = +100)                                CL**3
045400                                                                     CL**3
045500         EXEC SQL                                                    CL**3
045600             DELETE                                                  CL**3
045700                 FROM  TAURECV                                       CL**3
045800                 WHERE CURRENT OF AUDIT_RECV_CURSOR                  CL**3
045900         END-EXEC                                                    CL**3
046000                                                                     CL**3
046100         EVALUATE TRUE                                               CL**3
046200             WHEN SQLCODE = 0                                        CL**3
046300                 ADD +1           TO PV-DELETE-COUNT                 CL**3
046400             WHEN SQLCODE = -904                                     CL**3
046500             WHEN SQLCODE = -913                                     CL**3
046600             WHEN SQLCODE = +100                                     CL**3
046700                 CONTINUE                                            CL**3
046800             WHEN SQLCODE < 0                                        CL**3
046900             WHEN SQLCODE > 0                                        CL**3
047000             WHEN SQLWARN0 = 'W'                                     CL**3
047100                 MOVE '8000-DELETE-AUDIT-RECEIV-REC'                 CL**3
047200                             TO PV-PARAGRAPH-NAME                    CL**3
047300                 MOVE 'DELETE OF TAURECV'                            CL**3
047400                             TO PV-DB2-OPERATION-ATTEMPTED           CL**3
047500                 PERFORM 9100-SQL-ABEND                              CL**6
047600         END-EVALUATE                                                CL**3
047700     END-PERFORM.                                                    CL**3
047800                                                                     CL**3
047900     IF PV-DELETE-COUNTER > PC-MAX-RETRIES                           CL**3
048000         MOVE '8000-DELETE-AUDIT-RECIEV-REC'                         CL**3
048100             TO PV-PARAGRAPH-NAME                                    CL**3
048200         MOVE 'DEADLOCK * DELETE TAURCEV'                            CL**3
048300             TO PV-DB2-OPERATION-ATTEMPTED                           CL**3
048400         PERFORM 9100-SQL-ABEND                                      CL**6
048500     END-IF.                                                         CL**3
048600                                                                     CL**3
048700*8000-EXIT.                                                          CL**2
048800*                                                                    CL**2
048900*                                                                    CL**2
049000 9000-EOJ-ROUTINE.                                                   CL**2
049100******************************************************************   CL**2
049200*  CLOSE OUTPUT FILE.  CLOSE THE CURSOR.                             CL**2
049300******************************************************************   CL**2
049400     CLOSE TIMESTAMP-FILE.                                           CL*10
049500     DISPLAY 'TOTAL RECORDS DELETED: '  PV-DELETE-COUNT.             CL*21
049600     PERFORM WITH TEST AFTER                                         CL**2
049700       VARYING PV-CLOSE-COUNTER FROM 1 BY 1                          CL**2
049800         UNTIL (PV-CLOSE-COUNTER > PC-MAX-RETRIES                    CL**2
049900                OR                                                   CL**2
050000                SQLCODE = ZERO)                                      CL**2
050100                                                                     CL**2
050200         EXEC SQL                                                    CL**2
050300              CLOSE AUDIT_RECV_CURSOR                                CL**2
050400         END-EXEC                                                    CL**2
050500                                                                     CL**2
050600         EVALUATE TRUE                                               CL**2
050700             WHEN SQLCODE = -904                                     CL**2
050800             WHEN SQLCODE = -911                                     CL**2
050900                 CONTINUE                                            CL**2
051000             WHEN SQLWARN0 NOT = SPACES                              CL**2
051100             WHEN SQLCODE < ZERO                                     CL**2
051200             WHEN SQLCODE > ZERO                                     CL**2
051300                 MOVE '9000-EOJ-ROUTINE               '              CL**2
051400                                 TO PV-PARAGRAPH-NAME                CL**2
051500                 MOVE 'CLOSE THE AUDIT_RECV_CURSOR    '              CL**2
051600                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**2
051700                 PERFORM 9100-SQL-ABEND                              CL**2
051800             WHEN SQLCODE = ZERO                                     CL**2
051900                 CONTINUE                                            CL**2
052000         END-EVALUATE                                                CL**2
052100     END-PERFORM.                                                    CL**2
052200                                                                     CL**2
052300     IF PV-CLOSE-COUNTER > PC-MAX-RETRIES                            CL**2
052400         MOVE '9000-EOJ-ROUTINE               '                      CL**2
052500                                 TO PV-PARAGRAPH-NAME                CL**2
052600         MOVE 'CLOSE MAX RETRIES EXCEEDED     '                      CL**2
052700                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**2
052800         PERFORM 9100-SQL-ABEND                                      CL**2
052900     END-IF.                                                         CL**2
053000*9000-EXIT.                                                          CL**2
053100*                                                                    CL**2
053200*                                                                    CL**2
053300 9100-SQL-ABEND.                                                     CL*21
053400******************************************************************   CL**2
053500*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL       CL**2
053600*  ERROR OR WARNING CODE OCCURS.                                     CL**2
053700******************************************************************   CL**2
053800     DISPLAY '9100-SQL-ABEND'.                                       CL*21
053900     DISPLAY '** ABEND     **'.                                      CL*21
054000     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.           CL*21
054100     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                 CL*21
054200     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.        CL*21
054300     DISPLAY '***'                                                   CL*21
054400     DISPLAY '*** LAST COMMIT TAKEN **' PV-COMMIT-INFO               CL*21
054500     DISPLAY '***'                                                   CL*21
054600     DISPLAY '*** COMMITTED DELETE COUNTS FOLLOW: ***'               CL*21
054700     DISPLAY '***'                                                   CL*21
054800     EXEC SQL                                                        CL*21
054900          ROLLBACK                                                   CL*21
055000     END-EXEC.                                                       CL*21
055100******************************************************************   CL**2
055200* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-       CL**2
055300* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE            CL**2
055400* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE        CL**2
055500* FORMAT.                                                            CL**2
055600******************************************************************   CL**2
055700     COPY DPPD004.                                                   CL**2
055800                                                                     CL**2
055900     MOVE +4013                  TO ABEND-CODE.                      CL**2
056000     CALL 'ILBOABN0' USING ABEND-CODE.                               CL**2
056100*9100-EXIT.                                                          CL**2
056200*9100-SQL-ABEND.                                                     CL*21
056300******************************************************************   CL*21
056400*  ABEND ON DB2 ERROR.                                               CL*21
056500******************************************************************   CL*21
056600*    MOVE SQLCODE                    TO SQLCODE2.                    CL*21
056700*    MOVE SQLERRD(3)                 TO SQLERRD3.                    CL*21
056800*                                                                    CL*21
056900*    DISPLAY '** ABEND          **  IN RCKUT035'.                    CL*21
057000*    DISPLAY '** SQLERROR       **'.                                 CL*21
057100*    DISPLAY '** SQLCODE        ** ' SQLCODE2.                       CL*21
057200*    DISPLAY '** ROWS PROCESSED ** ' SQLERRD3.                       CL*21
057300*    DISPLAY '** SQLERRM        ** ' SQLERRM.                        CL*21
057400*    DISPLAY '** SQLERRMC       ** ' SQLERRMC.                       CL*21
057500*                                                                    CL*21
057600*    EXEC SQL                                                        CL*21
057700*         ROLLBACK                                                   CL*21
057800*    END-EXEC.                                                       CL*21
057900*                                                                    CL*21
058000*    MOVE 4013                       TO ABEND-CODE.                  CL*21
058100*    CALL 'ILBOABN0' USING ABEND-CODE.                               CL*21
058200*                                                                    CL*21
058300*                                                                    CL*21
