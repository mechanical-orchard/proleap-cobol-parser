000100******************************************************************04/11/95
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************   LV002
000400                                                                          
000500 PROGRAM-ID.                VLKCS504.                                     
000600 AUTHOR.                    MICHAEL WALTERS.                              
000700 INSTALLATION.              KOHLS DEPARTMENT STORES.                      
000800 DATE-WRITTEN.              06-03-96.                                     
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  CICS PROGRAM - VLKCS504 - V504                                *        
001300*                                                                *        
001400*       VL SYSTEM PRETICKET QUEUE MAINTENANCE SCREEN             *        
001500*                                                                *        
001600*  THIS SCREEN WILL LIST THE ACTIVE, RUSH, AND HOLD PRETICKET    *        
001700*  REQUESTS BY PRINT ORDER (PRINT DUE DATE, PRIORITY)                     
001800******************************************************************        
263627*----------------------------------------------------------------*        
263627* CHANGED 11/05/2021    BY JIM HUNTER      WR: CHG0263627                 
263627* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263627* MAKING THE CALL DYNAMIC VS STATIC.                                      
263627*----------------------------------------------------------------*        
CICS  * CHANGED 11/11/2022    BY JIM HUNTER      WR: CHG0278236                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *****************************************************************         
001900 ENVIRONMENT DIVISION.                                                    
002000                                                                          
002100 DATA DIVISION.                                                           
002200                                                                          
002300 WORKING-STORAGE SECTION.                                                 
002400                                                                          
002500 01  DK-DB2-KEYS.                                                         
002600     15  DK-PO-REQ-NBR                  PIC S9(09) VALUE +0               
002700                                                   COMP.                  
002800     15  DK-PO-NUMBER                   PIC S9(09) VALUE +0               
002900                                                   COMP.                  
003000     15  DK-DEPT-NBR                    PIC  X(03) VALUE SPACE.           
003100     15  DK-ENT-ID                      PIC S9(09) VALUE +0               
003200                                                   COMP.                  
003300                                                                          
003400 01  PC-PROGRAM-CONSTANTS.                                                
003500     05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
003600                                                    'VL504A  '.           
003700     05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
003800                                                    'VLKM504 '.           
003900     05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
004000                                                    'VLKCS504'.           
004100     05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
004200     05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.              
004300     05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.            
004400     05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +13             
004500                                                    COMP SYNC.            
004600     05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
004700                                                    COMP SYNC.            
004800     05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
004900                                                    +1000000              
005000                                                    COMP-3.               
005100     05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
005200                                                    COMP SYNC.            
005300     05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3               
005400                                                    COMP-3.               
005500     05  PC-PERCENT                     PIC  X(01)  VALUE '%'.            
005600     05  PC-INQ                         PIC  X(03)  VALUE 'INQ'.          
005700     05  PC-UPD                         PIC  X(03)  VALUE 'UPD'.          
005800     05  PC-DEL                         PIC  X(03)  VALUE 'DEL'.          
005800     05  PC-CAN                         PIC  X(03)  VALUE 'CAN'.          
005900     05  PC-ERR                         PIC  X(03)  VALUE 'ERR'.          
006000*                                                                         
006100     05  PC-TSYMSG-NUMBERS.                                               
006200         10  PC-TSYMSG-00004            PIC  9(05)  VALUE 00004.          
006300         10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
006400         10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
006500         10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.          
006600         10  PC-TSYMSG-00047            PIC  9(05)  VALUE 00047.          
006700         10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.          
006800         10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.          
006900         10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.          
007000         10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.          
007100         10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.          
007200         10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
007300         10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
007400         10  PC-TSYMSG-00115            PIC  9(05)  VALUE 00115.          
007500         10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
007600         10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
007700         10  PC-TSYMSG-00118            PIC  9(05)  VALUE 00118.          
007800         10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
007800         10  PC-TSYMSG-00522            PIC  9(05)  VALUE 00522.          
007800         10  PC-TSYMSG-01381            PIC  9(05)  VALUE 01381.          
007900         10  PC-TSYMSG-99901            PIC  9(05)  VALUE 99901.          
008000*                                                                         
008100 01  PS-PROGRAM-SWITCHES.                                                 
008200     05  PS-SEL-RECORD-SW               PIC  X(01)  VALUE 'Y'.            
008300         88  PS-SEL-RECORD-FOUND                    VALUE 'Y'.            
008400         88  PS-SEL-RECORD-NOT-FOUND                VALUE 'N'.            
008500     05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
008600         88  PS-NO-ERROR                            VALUE 'Y'.            
008700         88  PS-ERROR                               VALUE 'N'.            
008800     05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
008900         88  PS-COMMAREA-VALID                      VALUE 'Y'.            
009000         88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
009100*                                                                         
009200 01  PV-PROGRAM-VARIABLES.                                                
009300     05  PV-REQUEST-CMNT-ID             PIC S9(09) COMP VALUE +0.         
009300     05  PV-SHIPT-CMNT-ID               PIC S9(09) COMP VALUE +0.         
009300     05  PV-DISPLAY-ABEND-MESSAGE       PIC  X(79) VALUE SPACES.          
009400     05  PV-DISPLAY-COMP1               PIC  9(09) VALUE ZERO.            
009500     05  PV-DISPLAY-COMP2               PIC  9(09) VALUE ZERO.            
009600     05  PV-DISPLAY-COMP3               PIC  9(09) VALUE ZERO.            
009700     05  PV-WORK-DATE.                                                    
009800         10  PV-WORK-CC                 PIC XX.                           
009900         10  PV-WORK-YY                 PIC XX.                           
010000         10  FILLER                     PIC X.                            
010100         10  PV-DISPLAY-DATE.                                             
010200             15 PV-WORK-MM              PIC XX.                           
010300             15 FILLER                  PIC X.                            
010400             15 PV-WORK-DD              PIC XX.                           
010500     05  PV-REQUEST-TICKET-TOTAL        PIC S9(09)  VALUE +0              
010600                                                    COMP SYNC.            
010700     05  PV-REQUEST-LINE-COUNT          PIC S9(04)  VALUE +0              
010800                                                    COMP SYNC.            
010900     05  PV-SUB                         PIC S9(04)  VALUE +0              
011000                                                    COMP SYNC.            
011100     05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
011200                                                    COMP SYNC.            
011300     05  PV-READ-COUNT                  PIC S9(09)  VALUE +0              
011400                                                    COMP-3.               
011500     05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0              
011600                                                    COMP-3.               
011700     05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
011800                                                    COMP-3.               
011900     05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0              
012000                                                    COMP-3.               
012100     05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
012200                                                    COMP-3.               
012300     05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
012400                                                    COMP-3.               
012500     05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)  VALUE +0              
012600                                                    COMP-3.               
012700     05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
012800                                                    COMP SYNC.            
013500     05  PV-SAVE-BLOCK-NUMBER           PIC S9(04)  VALUE +0              
013600                                                    COMP SYNC.            
012900     05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
013000                                                    COMP SYNC.            
013100     05  PV-ITEM                        PIC S9(04)  VALUE +0              
013200                                                    COMP SYNC.            
013300     05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0              
013400                                                    COMP SYNC.            
013500     05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0              
013600                                                    COMP SYNC.            
013700     05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0              
013800                                                    COMP SYNC.            
013900     05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0              
014000                                                    COMP SYNC.            
014100     05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0              
014200                                                    COMP SYNC.            
014300     05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
014400                                                    COMP SYNC.            
014500     05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
014600                                                    COMP SYNC.            
014700     05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0              
014800                                                    COMP SYNC.            
014900     05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0              
015000                                                    COMP SYNC.            
015100     05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0              
015200                                                    COMP SYNC.            
015300     05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0              
015400                                                    COMP SYNC.            
015500     05  PV-SQL-SUB                     PIC S9(04)  VALUE +0              
015600                                                    COMP SYNC.            
015700     05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0              
015800                                                    COMP SYNC.            
015900******************************************************************        
016000**  THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.   **        
016100**  THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE           **        
016200**  APPLICATION-SPECIFIC COMMAREA.                              **        
016300******************************************************************        
016400 01  LQW-LIST-QUEUE-WORK-AREA.                                            
016500     05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.          
016600     05  LQW-ITEM-ENTRIES               OCCURS 13 TIMES                   
016700                                        INDEXED BY LQW-IDX.               
016800         10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.               
016900         10  LQW-TEMP-SELECTED-SW       PIC  X(01).                       
017000         10  LQW-SELECTED-SW            PIC  X(01).                       
017100             88  LQW-SELECT                         VALUE 'S'.            
017200             88  LQW-DESELECT                       VALUE ' '.            
017300         10  LQW-ACTION                 PIC  X(03).                       
017400         10  LQW-PO-NUMBER              PIC  9(09).                       
017500         10  LQW-RQST-NBR               PIC S9(09) COMP.                  
017600         10  LQW-ENT-ID                 PIC S9(09) COMP.                  
017700         10  LQW-DEPT-NBR               PIC  X(03).                       
017800         10  LQW-RQST-TYPE              PIC  X(01).                       
017900         10  LQW-RQST-STAT              PIC  X(01).                       
018000         10  LQW-RQSTD-TICKETS          PIC S9(07) COMP.                  
018100         10  LQW-VENDOR-NAME            PIC  X(16).                       
018200         10  LQW-REQUEST-DATE           PIC  X(05).                       
018300         10  LQW-VENDOR-DUE-DATE        PIC  X(05).                       
018400         10  LQW-PRINT-DUE-DATE         PIC  X(05).                       
018500         10  LQW-PRIORITY               PIC  9(02).                       
018600         10  LQW-LINE-COUNT-X.                                            
018600             15  LQW-LINE-COUNT         PIC  9(04).                       
018700                                                                          
018800     EJECT                                                                
018900*---------------------------------------------------------------*         
019000*  RECORD LAYOUT FOR THE MESSAGE SELECTION TEMP STORAGE QUEUE.  *         
019100*---------------------------------------------------------------*         
019200                                                                          
019300     COPY VLWS006.                                                        
019400                                                                          
019500     EJECT                                                                
019600*---------------------------------------------------------------*         
019700*    MAP LAYOUT                                                 *         
019800*---------------------------------------------------------------*         
019900                                                                          
020000     COPY VLKM504.                                                        
020100*                                                                         
020200 01  MR-OCCURS-LAYOUT                   REDEFINES  VL504AO.               
020300     05  FILLER                         PIC X(42).                        
020400     05  MR-SCROLL-AREA                 OCCURS 13 TIMES.                  
020500         10  MR-SELECTL                 PIC S9(04) COMP.                  
020600         10  MR-SELECTA                 PIC  X(01).                       
020700         10  MR-SELECTC                 PIC  X(01).                       
020800         10  MR-SELECTH                 PIC  X(01).                       
020900         10  MR-SELECTI                 PIC  X(01).                       
021000                                                                          
021100         10  MR-ACTIONL                 PIC S9(04) COMP.                  
021200         10  MR-ACTIONA                 PIC  X(01).                       
021300         10  MR-ACTIONC                 PIC  X(01).                       
021400         10  MR-ACTIONH                 PIC  X(01).                       
021500         10  MR-ACTION                  PIC  X(03).                       
021600                                                                          
021100         10  MR-PO-NUMBERL              PIC S9(04) COMP.                  
021200         10  MR-PO-NUMBERA              PIC  X(01).                       
021300         10  MR-PO-NUMBERC              PIC  X(01).                       
021400         10  MR-PO-NUMBERH              PIC  X(01).                       
021800         10  MR-PO-NUMBER.                                                
021900             15  MR-PO-NUMBER-N         PIC  9(08).                       
022000                                                                          
021100         10  MR-RQST-TYPEL              PIC S9(04) COMP.                  
021200         10  MR-RQST-TYPEA              PIC  X(01).                       
021300         10  MR-RQST-TYPEC              PIC  X(01).                       
021400         10  MR-RQST-TYPEH              PIC  X(01).                       
022200         10  MR-RQST-TYPE               PIC  X(01).                       
022300                                                                          
021100         10  MR-RQST-STATL              PIC S9(04) COMP.                  
021200         10  MR-RQST-STATA              PIC  X(01).                       
021300         10  MR-RQST-STATC              PIC  X(01).                       
021400         10  MR-RQST-STATH              PIC  X(01).                       
022500         10  MR-RQST-STAT               PIC  X(01).                       
022600                                                                          
021100         10  MR-RQSTD-TICKETSL          PIC S9(04) COMP.                  
021200         10  MR-RQSTD-TICKETSA          PIC  X(01).                       
021300         10  MR-RQSTD-TICKETSC          PIC  X(01).                       
021400         10  MR-RQSTD-TICKETSH          PIC  X(01).                       
022800         10  MR-RQSTD-TICKETS-X.                                          
022900             15  MR-RQSTD-TICKETS       PIC  ZZZZ,ZZ9.                    
023000                                                                          
021100         10  MR-VENDOR-NAMEL            PIC S9(04) COMP.                  
021200         10  MR-VENDOR-NAMEA            PIC  X(01).                       
021300         10  MR-VENDOR-NAMEC            PIC  X(01).                       
021400         10  MR-VENDOR-NAMEH            PIC  X(01).                       
023200         10  MR-VENDOR-NAME             PIC  X(16).                       
023300                                                                          
021100         10  MR-REQUEST-DATEL           PIC S9(04) COMP.                  
021200         10  MR-REQUEST-DATEA           PIC  X(01).                       
021300         10  MR-REQUEST-DATEC           PIC  X(01).                       
021400         10  MR-REQUEST-DATEH           PIC  X(01).                       
023500         10  MR-REQUEST-DATE            PIC  X(05).                       
023600                                                                          
021100         10  MR-VENDOR-DUE-DATEL        PIC S9(04) COMP.                  
021200         10  MR-VENDOR-DUE-DATEA        PIC  X(01).                       
021300         10  MR-VENDOR-DUE-DATEC        PIC  X(01).                       
021400         10  MR-VENDOR-DUE-DATEH        PIC  X(01).                       
023800         10  MR-VENDOR-DUE-DATE         PIC  X(05).                       
023900                                                                          
021100         10  MR-PRINT-DUE-DATEL         PIC S9(04) COMP.                  
021200         10  MR-PRINT-DUE-DATEA         PIC  X(01).                       
021300         10  MR-PRINT-DUE-DATEC         PIC  X(01).                       
021400         10  MR-PRINT-DUE-DATEH         PIC  X(01).                       
024100         10  MR-PRINT-DUE-DATE          PIC  X(05).                       
024200                                                                          
021100         10  MR-PRIORITYL               PIC S9(04) COMP.                  
021200         10  MR-PRIORITYA               PIC  X(01).                       
021300         10  MR-PRIORITYC               PIC  X(01).                       
021400         10  MR-PRIORITYH               PIC  X(01).                       
024400         10  MR-PRIORITY                PIC  X(02).                       
024500                                                                          
021100         10  MR-LINE-COUNTL             PIC S9(04) COMP.                  
021200         10  MR-LINE-COUNTA             PIC  X(01).                       
021300         10  MR-LINE-COUNTC             PIC  X(01).                       
021400         10  MR-LINE-COUNTH             PIC  X(01).                       
024700         10  MR-LINE-COUNT              PIC  X(04).                       
024800                                                                          
024900     EJECT                                                                
025000*---------------------------------------------------------------*         
025100*    ATTRIBUTE SETTINGS COPYBOOK.                               *         
025200*---------------------------------------------------------------*         
025300                                                                          
025400     COPY DPWS015.                                                        
025500                                                                          
025600*---------------------------------------------------------------*         
025700*    FUNCTION KEYS COPYBOOK.                                    *         
025800*---------------------------------------------------------------*         
025900                                                                          
026000     COPY DPWS016.                                                        
026100                                                                          
026200*---------------------------------------------------------------*         
026300*    NUMERIC EDIT LAYOUT.                                       *         
026400*---------------------------------------------------------------*         
026500                                                                          
026600     COPY DPWS010I.                                                       
026700 EJECT                                                                    
026800*---------------------------------------------------------------*         
026900*    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
027000*---------------------------------------------------------------*         
027100                                                                          
027200     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
027300 EJECT                                                                    
027400*---------------------------------------------------------------*         
027500*    STANDARD COMMAREA.                                         *         
027600*---------------------------------------------------------------*         
027700                                                                          
027800     COPY DPWS020.                                                        
027900     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
028000                                                                          
028100*---------------------------------------------------------------*         
028200* INTER-APPLICATION COMMAREA.                                   *         
028300*   DPWS054 - IS THE INPUT LAYOUT WHEN COMING FROM THE CRIT.    *         
028400*             SPEC SCREEN.                                      *         
028500*   DPWS056 - IS THE OUTPUT LAYOUT THAT WILL BE PASSED FROM     *         
028600*             THIS LIST PROGRAMMING FOR A LOCAL-GO FUNCTION.    *         
028700*---------------------------------------------------------------*         
028800                                                                          
028900         10  INTER-APPL-COMM-AREA       PIC X(200).                       
029000     COPY VLWS005.                                                        
029100*---------------------------------------------------------------*         
029200*  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
029300*---------------------------------------------------------------*         
029400         10  ASC-SPECIFIC-COMMAREA.                                       
029500             15  ASC-LIST-KEYS.                                           
029600                 20  ASC-KEY-DEPT-NBR                PIC  X(03).          
029700                 20  ASC-KEY-PO-NUMBER               PIC S9(09)           
029800                                                     COMP.              86
029900                 20  ASC-KEY-ENT-ID                  PIC S9(09)           
030000                                                     COMP.              86
030100                 20  ASC-KEY-VENDOR-NAME             PIC  X(30).          
030200             15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
030300                 20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).          
030400                     88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.           
030500                     88  ASC-BLOCK-MODIFIED          VALUE 'Y'.           
030600                                                                          
030700                 20  ASC-ITEM-ARRAY.                                      
030800                     25  ASC-ITEM-ENTRIES OCCURS 13 TIMES.                
030900                         30  ASC-LIST-ITEM-NUMBER                         
031000                                                     PIC S9(09)           
031100                                                     COMP-3.              
031200                         30  ASC-TEMP-SELECTED-SW    PIC  X(01).          
031300                             88  ASC-VALID-SELECT    VALUE ' '            
031400                                                           'S'            
031500                                                           'B'            
031600                                                           'E'.           
031700                             88  ASC-TEMP-NO-SELECT  VALUE ' '.           
031800                             88  ASC-TEMP-SELECT     VALUE 'S'.           
031900                             88  ASC-TEMP-BEGIN      VALUE 'B'.           
032000                             88  ASC-TEMP-END        VALUE 'E'.           
032100                         30  ASC-SELECTED-SW         PIC  X(01).          
032200                             88  ASC-NO-SELECT       VALUE ' '.           
032300                             88  ASC-SELECT          VALUE 'S'.           
032400                             88  ASC-BEGIN           VALUE 'B'.           
032500                             88  ASC-END             VALUE 'E'.           
032600                         30  ASC-ACTION              PIC  X(03).          
032700                         30  ASC-PO-NUMBER           PIC  9(09).          
032800                         30  ASC-RQST-NBR            PIC S9(09)           
032900                                                         COMP.            
033000                         30  ASC-ENT-ID              PIC S9(09)           
033100                                                         COMP.            
033200                         30  ASC-DEPT-NBR            PIC X(03).           
033300                         30  ASC-RQST-TYPE           PIC X(01).           
033400                         30  ASC-RQST-STAT           PIC X(01).           
033500                         30  ASC-RQSTD-TICKETS       PIC S9(07)           
033600                                                         COMP.            
033700                         30  ASC-VENDOR-NAME         PIC X(16).           
033800                         30  ASC-RQST-DATE           PIC X(05).           
033900                         30  ASC-VENDOR-DUE-DATE     PIC X(05).           
034000                         30  ASC-PRINT-DUE-DATE      PIC X(05).           
034100                         30  ASC-PRIORITY            PIC 9(02).           
034200                         30  ASC-LINE-COUNT-X.                            
034200                             35  ASC-LINE-COUNT      PIC 9(04).           
034300                                                                          
034400             15  ASC-BLK-SUB                         PIC S9(04)           
034500                                                          COMP            
034600                                                          SYNC.           
034700             15  ASC-ITEM-SUB                        PIC S9(04)           
034800                                                          COMP            
034900                                                          SYNC.           
035000             15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
035100                                                          COMP-3.         
035200             15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
035300                                                          COMP-3.         
035400             15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
035500                                                          COMP-3.         
035600             15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
035700                                                          COMP-3.         
035800             15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
035900                                                          COMP-3.         
036000             15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
036100                                                          COMP            
036200                                                          SYNC.           
036300             15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)           
036400                                                          COMP-3.         
036500             15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
036600                                                          COMP-3.         
036700             15  ASC-DELETE-INDICATOR                PIC  X(01).          
036800                 88  ASC-NO-DELETE                   VALUE ' '.           
036900                 88  ASC-DELETE-PENDING              VALUE 'P'.           
037000             15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
037100                 88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.           
037200                 88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.           
037300             15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).          
037400                 88  ASC-UPDATE-TSQ                  VALUE 'Y'.           
037500                 88  ASC-NO-TSQ-UPDATE               VALUE ' '.           
037600             15  ASC-LIST-QUEUE-NAME.                                     
037700                 20  ASC-LIST-TERM-ID                PIC  X(04).          
037800                 20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
037900                                                          COMP-3.         
038000                 20  ASC-LIST-SEQ                    PIC  9(01).          
038100             15  ASC-SEL-QUEUE-NAME.                                      
038200                 20  ASC-SEL-TERM-ID                 PIC  X(04).          
038300                 20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
038400                                                          COMP-3.         
038500                 20  ASC-SEL-SEQ                     PIC  9(01).          
038600                                                                          
038700             15  ASC-CURSOR-SW                       PIC  X(01).          
038800                 88  ASC-NO-CURSOR                   VALUE ' '.           
038900                 88  ASC-PO-CURSOR                   VALUE 'P'.           
039000                 88  ASC-DPT-CURSOR                  VALUE 'D'.           
039100                 88  ASC-VND-CURSOR                  VALUE 'V'.           
039200                 88  ASC-DPTVND-CURSOR               VALUE 'B'.           
039300             15  ASC-START-ITEM-REQUEST.                                  
039400                 20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
039500     EJECT                                                                
039600*---------------------------------------------------------------*         
039700*    ABEND PROCESSING COPYBOOK.                                 *         
039800*---------------------------------------------------------------*         
039900                                                                          
040000     COPY DPWS013.                                                        
040100                                                                          
040200 EJECT                                                                    
040300*---------------------------------------------------------------*         
040400*    DB2 AREA FOR TMESSAGE                                      *         
040500*---------------------------------------------------------------*         
040600     EXEC SQL                                                             
040700          INCLUDE TSYMSG                                                  
040800     END-EXEC.                                                            
040900*---------------------------------------------------------------*         
041000*    DB2 AREA FOR COMMUNICATIONS                                *         
041100*---------------------------------------------------------------*         
041200     EXEC SQL                                                             
041300          INCLUDE SQLCA                                                   
041400     END-EXEC.                                                            
041500                                                                          
041600*---------------------------------------------------------------*         
041700*    DB2 AREA THE PRETICKET REQUEST TABLE                       *         
041800*---------------------------------------------------------------*         
041900     EXEC SQL                                                             
042000          INCLUDE TPTKREQ                                                 
042100     END-EXEC.                                                            
042200                                                                          
042300*---------------------------------------------------------------*         
042400*    DB2 AREA THE PRETICKET SHIPMENT TABLE                      *         
042500*---------------------------------------------------------------*         
042600     EXEC SQL                                                             
042700          INCLUDE TPTKSHP                                                 
042800     END-EXEC.                                                            
042200                                                                          
042300*---------------------------------------------------------------*         
042400*    DB2 AREA THE PRETICKET REQUEST/SHIPMENT ASSOCIATION TABLE  *         
042500*---------------------------------------------------------------*         
042600     EXEC SQL                                                             
042700          INCLUDE TREQSHP                                                 
042800     END-EXEC.                                                            
042200                                                                          
042300*---------------------------------------------------------------*         
042400*    DB2 AREA THE PRETICKET REQEUST LINE TABLE                  *         
042500*---------------------------------------------------------------*         
042600     EXEC SQL                                                             
042700          INCLUDE TPTKLNE                                                 
042800     END-EXEC.                                                            
042900                                                                          
043000*---------------------------------------------------------------*         
043100*    DB2 AREA THE PRETICKET COMMENT TABLE                       *         
043200*---------------------------------------------------------------*         
043300     EXEC SQL                                                             
043400          INCLUDE TPTKCMT                                                 
043500     END-EXEC.                                                            
043600                                                                          
043700*---------------------------------------------------------------*         
043800*    DB2 AREA THE PRETICKET REASON CODE ASSOCIATION TABLE       *         
043900*---------------------------------------------------------------*         
044000     EXEC SQL                                                             
044100          INCLUDE TPTKRSN                                                 
044200     END-EXEC.                                                            
044300                                                                          
044400*---------------------------------------------------------------*         
044500*    DB2 AREA THE ENTERPRISE NAME TABLE                         *         
044600*---------------------------------------------------------------*         
044700     EXEC SQL                                                             
044800          INCLUDE TENTNME                                                 
044900     END-EXEC.                                                            
045000                                                                          
045100*---------------------------------------------------------------*         
045200*    REQEUST CURSOR WHEN THE DEPTARTMENT NUMBER IS SUPPLIED     *         
045300*---------------------------------------------------------------*         
045400                                                                          
045500     EXEC SQL                                                             
045600          DECLARE REQUEST_CURSOR  CURSOR                                86
045700          FOR SELECT                                                      
045800                A.PO_REQ_NBR                                              
045900              , A.PO_NBR                                                  
046000              , A.DEPT_NBR                                                
046100              , A.REQ_TYP_CDE                                             
046200              , A.REQ_STAT_CDE                                            
046400              , D.ENT_NAME_DESC                                           
046500              , A.ENT_ID                                                  
046600              , A.REQ_CREN_DTE                                            
046700              , A.VND_DUE_DTE                                             
046800              , A.PRT_BY_DTE                                              
046900              , A.PRTY_STAT_CDE                                           
047100          FROM                                                            
047200                TPTKREQ A                                               86
047400              , TENTNME D                                               86
047500          WHERE                                                           
047600                A.REQ_STAT_CDE IN ('A','R','H', 'I')                      
047800            AND A.ENT_ID     = D.ENT_ID                                   
047900            AND D.TYPE_CDE   = '01'                                       
048000          GROUP BY                                                        
048100                A.PO_REQ_NBR                                              
048200              , A.PO_NBR                                                  
048300              , A.DEPT_NBR                                                
048400              , A.REQ_TYP_CDE                                             
048500              , A.REQ_STAT_CDE                                            
048600              , D.ENT_NAME_DESC                                           
048700              , A.ENT_ID                                                  
048800              , A.REQ_CREN_DTE                                            
048900              , A.VND_DUE_DTE                                             
049000              , A.PRT_BY_DTE                                              
049100              , A.PRTY_STAT_CDE                                           
049200          ORDER BY                                                        
049300                A.PRT_BY_DTE, A.PRTY_STAT_CDE                             
049400     END-EXEC.                                                            
049500                                                                          
049600 EJECT                                                                    
049700 LINKAGE SECTION.                                                         
049800                                                                          
049900 01  DFHCOMMAREA.                                                         
050000     05  FILLER                         OCCURS  1 TO 4072 TIMES           
050100                                        DEPENDING ON EIBCALEN.            
050200         10  FILLER                     PIC X.                            
050300                                                                          
050400 EJECT                                                                    
050500 PROCEDURE DIVISION.                                                      
050600******************************************************************        
050700**  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **        
050800**  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **        
050900**  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **        
051000******************************************************************        
051100 0000-MAIN-MODULE.                                                        
051200     INITIALIZE DP030-CICS-API-FIELDS.                                    
051300     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
051400     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
051500     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
051600     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
051700     MOVE LENGTH OF VL504AI        TO DP030-MAP-LENGTH (1).               
051800     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
051900     PERFORM 0001-CALL-CICS-ARCH-API.                                     
052000*                                                                         
052100     PERFORM 1000-CONTROL-PROCESSING                                      
052200*                                                                         
052300     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
052400     PERFORM 0001-CALL-CICS-ARCH-API.                                     
052500                                                                          
052600******************************************************************        
052700**  THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   **        
052800**  AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         **        
052900******************************************************************        
053000 0001-CALL-CICS-ARCH-API.                                                 
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
053200                                    DFHCOMMAREA                           
053300                                    DP030-CICS-API-FIELDS                 
053400                                    DP020-STANDARD-COMMAREA               
053500                                    VL504AI.                              
053600*                                                                         
053700     IF  DP030-RC-CALL-SUCCESSFUL                                         
053800         CONTINUE                                                         
053900     ELSE                                                                 
054000         SET DP013-NO-ROLLBACK                                            
054100             DP013-XCTL-DISPLAY-RESTART                                   
054200             DP013-CICS-ABEND      TO TRUE                                
054300         MOVE '0001-CALL-CICS-ARCH-API'                                   
054400                                   TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
054700         MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
054800         PERFORM DP013-0000-PROCESS-ABEND                                 
054900     END-IF.                                                              
055000*----------------------------------------------------------------*        
055100* PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
055200* COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
055300*                                                                *        
055400* NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
055500*  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
055600*  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
055700*  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
055800*----------------------------------------------------------------*        
055900                                                                          
056000 1000-CONTROL-PROCESSING.                                                 
056100                                                                          
056200     EVALUATE TRUE                                                        
056300         WHEN DP020-NEXT-ACT-INITIAL                                      
056400             INITIALIZE ASC-SPECIFIC-COMMAREA                             
056500             MOVE EIBTRMID         TO ASC-LIST-TERM-ID                    
056600                                      ASC-SEL-TERM-ID                     
056700             MOVE DP020-SRC-TASK-NUMBER                                   
056800                                   TO ASC-LIST-TASK-NUMBER                
056900                                      ASC-SEL-TASK-NUMBER                 
057000             MOVE PC-LIST-QUEUE-SEQ                                       
057100                                   TO ASC-LIST-SEQ                        
057200             MOVE PC-SEL-QUEUE-SEQ TO ASC-SEL-SEQ                         
057300             PERFORM 1100-PROCESS-INTER-APPL-COMM                         
057400***-----$TEMPLATE NOTE$-------------------------------------------        
057500*** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,        
057600*** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO          
057700*** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.        
057800***---------------------------------------------------------------        
057900             IF  PS-COMMAREA-NOT-VALID                                    
058000***              MOVE PC-TSYMSG-99901                                     
058100***                                TO DP020-MSG-NUMBER                    
058200***              SET DP020-MSG-FATAL                                      
058300***                                TO TRUE                                
058400                 SET DP020-NEXT-ACT-APPL-ERROR                            
058500                                   TO TRUE                                
058600             ELSE                                                         
058700                 PERFORM 4000-BUILD-INITIAL-PANEL                         
058800             END-IF                                                       
058900                                                                          
059000         WHEN DP020-NEXT-ACT-READ-MAP                                     
059100             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
059200                                   TO PV-TOP-ITEM                         
059300             PERFORM 2000-PROCESS-PANEL                                   
059400                                                                          
059500         WHEN DP020-NEXT-ACT-RETURN                                       
059600             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
059700                                   TO PV-TOP-ITEM                         
059800             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
059900             PERFORM 6000-REFRESH-PANEL-FROM-TEMP                         
060000             PERFORM 3000-EDIT-DATA-IN-COMMAREA                      CL**2
060100             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
060200                                                                          
060300         WHEN OTHER                                                       
060400             SET DP013-LOGIC-ABEND TO TRUE                                
060500             SET DP013-NO-ROLLBACK TO TRUE                                
060600             MOVE '1000-CONTROL-PROCESSING'                               
060700                                   TO DP013-PARAGRAPH                     
060800             MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
060900                                   TO DP013-MESSAGE-TEXT(1)               
061000             PERFORM DP013-0000-PROCESS-ABEND                             
061100     END-EVALUATE.                                                        
061200                                                                          
061300 EJECT                                                                    
061400*----------------------------------------------------------------*        
061500* DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
061600* IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
061700*----------------------------------------------------------------*        
061800                                                                          
061900 1100-PROCESS-INTER-APPL-COMM.                                            
062000                                                                          
062100     SET PS-COMMAREA-VALID         TO TRUE.                               
062200     MOVE SPACE                    TO ASC-START-ITEM-REQUEST.             
062300                                                                          
062400 EJECT                                                                    
062500*----------------------------------------------------------------*        
062600* DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
062700* ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
062800* USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
062900* OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
063000* FUNCTION KEYS CAN GET THIS FAR.                                *        
063100*----------------------------------------------------------------*        
063200                                                                          
063300 2000-PROCESS-PANEL.                                                      
063400     EVALUATE TRUE                                                        
063500         WHEN DP020-SRC-AID = DP016-CLEAR                                 
063600             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
063700             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
063800             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
063900                                                                          
064000         WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
064100             MOVE ASC-KEY-DEPT-NBR  TO DK-DEPT-NBR                        
064200             MOVE ASC-KEY-PO-NUMBER TO DK-PO-NUMBER                       
064300             MOVE ASC-KEY-ENT-ID    TO DK-ENT-ID                        86
064400             PERFORM 2050-DELETE-LIST-QUEUE                               
064500             PERFORM 4000-BUILD-INITIAL-PANEL                             
064600                                                                          
064700         WHEN DP020-FK-DESELECT(DP020-SRC-AID)                            
064800             PERFORM 2140-DESELECT-ALL-ITEMS                              
064900             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
065000                                                                          
065100         WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                          
065200             PERFORM 2150-SELECT-ALL-ITEMS                                
065300             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
065400                                                                          
065500         WHEN OTHER                                                       
065600             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
065700             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
065800             IF  PS-NO-ERROR                                              
065900                 SET DP030-CONTINUE-GO                                    
066000                                 TO TRUE                                  
066100                 PERFORM 2100-CHECK-FUNCTION-KEY                          
066200             ELSE                                                         
066300                 SET DP030-OVERRIDE-GO                                    
066400                                 TO TRUE                                  
066500             END-IF                                                       
066600     END-EVALUATE.                                                        
066700 EJECT                                                                    
066800*----------------------------------------------------------------*        
066900*  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *        
067000*----------------------------------------------------------------*        
067100 2050-DELETE-LIST-QUEUE.                                                  
067200                                                                          
067300     EXEC CICS                                                            
067400         DELETEQ TS                                                       
067500             QUEUE (ASC-LIST-QUEUE-NAME)                                  
067600             RESP  (PV-CICS-RESP)                                         
067700     END-EXEC.                                                            
067800                                                                          
067900     IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
068000        (PV-CICS-RESP = DFHRESP(QIDERR))                                  
068100         CONTINUE                                                         
068200     ELSE                                                                 
068300         SET DP013-CICS-ABEND      TO TRUE                                
068400         SET DP013-NO-ROLLBACK     TO TRUE                                
068500                                                                          
068600         MOVE '2050-DELETE-LIST-QUEUE'                                    
068700                                   TO DP013-PARAGRAPH                     
068800         MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'            
068900                                   TO DP013-MESSAGE-TEXT (1)              
069000         PERFORM DP013-0000-PROCESS-ABEND                                 
069100     END-IF.                                                              
069200                                                                          
069300     MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.          
069400 EJECT                                                                    
069500*----------------------------------------------------------------*        
069600* ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
069700* NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
069800* CICS ARCHITECTURE API.                                         *        
069900*----------------------------------------------------------------*        
070000 2100-CHECK-FUNCTION-KEY.                                                 
070100     EVALUATE TRUE                                                        
070200                                                                          
070300*        UPDATE KEY WAS PRESSED                                           
070400         WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)                       
070500             IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO                      
070600                 INITIALIZE VL005-INTER-APPL-COMMAREA                     
070700                 MOVE ASC-NUMBER-OF-SELECTED-ITEMS                      36
070800                                TO VL005-NUMBER-OF-SELECTED-ITEMS       36
070900                 PERFORM 5000-PREPARE-SEL-QUEUE                         36
071000                                                                          
071100             ELSE                                                         
071200                 SET DP030-OVERRIDE-GO                                    
071300                                   TO TRUE                                
071400                 MOVE PC-TSYMSG-00057                                     
071500                                   TO DP020-MSG-NUMBER                    
071600                 SET DP020-MSG-WARNING                                    
071700                                   TO TRUE                                
071800             END-IF                                                       
071801*                                                                         
071810         WHEN DP020-FK-LOCAL-FUNC-04(DP020-SRC-AID)                       
071820              CONTINUE                                                    
071900                                                                          
072000*        POINQ KEY WAS PRESSED                                            
072100         WHEN DP020-FK-LOCAL-FUNC-03(DP020-SRC-AID)                       
072200             IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO                      
072300                 IF  ASC-NUMBER-OF-SELECTED-ITEMS = 1                     
072400                     INITIALIZE VL005-INTER-APPL-COMMAREA                 
072500                     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                  36
072600                                TO VL005-NUMBER-OF-SELECTED-ITEMS       36
072700                     PERFORM 5000-PREPARE-SEL-QUEUE                     36
072800                                                                          
072900                 ELSE                                                     
073000                     SET DP030-OVERRIDE-GO                                
073100                                       TO TRUE                            
073200                     MOVE PC-TSYMSG-00047                                 
073300                                       TO DP020-MSG-NUMBER                
073400                     SET DP020-MSG-FATAL                                  
073500                                       TO TRUE                            
073600                 END-IF                                                   
073700             ELSE                                                         
073800                 SET DP030-OVERRIDE-GO                                    
073900                                   TO TRUE                                
074000                 MOVE PC-TSYMSG-00057                                     
074100                                   TO DP020-MSG-NUMBER                    
074200                 SET DP020-MSG-WARNING                                    
074300                                   TO TRUE                                
074400             END-IF                                                       
074500                                                                          
074600*        DELETE KEY WAS PRESSED                                           
074700         WHEN DP020-FK-LOCAL-FUNC-02(DP020-SRC-AID)                       
074800              SET ASC-DELETE-PENDING TO TRUE                              
074900              MOVE PC-TSYMSG-00004     TO DP020-MSG-NUMBER                
075000              SET DP020-MSG-INFORMATIONAL                                 
075100                                       TO TRUE                            
075200                                                                          
075300*        CONFIRM KEY WAS PRESSED                                          
075400         WHEN DP020-FK-CONFIRM (DP020-SRC-AID)                            
075500             IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO                      
075600                 IF  ASC-DELETE-PENDING                                   
075700                     PERFORM 7000-DELETE-SELECTED-REQUESTS                
                                                                                
                           MOVE ASC-ITEM-AT-TOP-OF-PANEL                        
                                             TO PV-TOP-ITEM                     
                           MOVE +0           TO ASC-ITEM-AT-TOP-OF-PANEL        
075800                     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 
075900                     SET ASC-NO-DELETE TO TRUE                            
076000                 ELSE                                                     
076100                     MOVE PC-TSYMSG-00118                                 
076200                                       TO DP020-MSG-NUMBER                
076300                     SET DP020-MSG-INFORMATIONAL                          
076400                                       TO TRUE                            
076500                 END-IF                                                   
076600             ELSE                                                         
076700                 SET DP030-OVERRIDE-GO                                    
076800                                   TO TRUE                                
076900                 MOVE PC-TSYMSG-00057                                     
077000                                   TO DP020-MSG-NUMBER                    
077100                 SET DP020-MSG-WARNING                                    
077200                                   TO TRUE                                
077300             END-IF                                                       
077400                                                                          
077500         WHEN DP020-FK-LOCAL-GOTO(DP020-SRC-AID)                          
077600             IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO                      
077700                 INITIALIZE VL005-INTER-APPL-COMMAREA                     
077800                 MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        
077900                                TO VL005-NUMBER-OF-SELECTED-ITEMS         
078000                 PERFORM 5000-PREPARE-SEL-QUEUE                           
078100             ELSE                                                         
078200                 SET DP030-OVERRIDE-GO                                    
078300                                   TO TRUE                                
078400                 MOVE PC-TSYMSG-00057                                     
078500                                   TO DP020-MSG-NUMBER                    
078600                 SET DP020-MSG-WARNING                                    
078700                                   TO TRUE                                
078800             END-IF                                                       
078900                                                                          
079000         WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
079100             MOVE +1               TO PV-TOP-ITEM                         
079200             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
079300                                                                          
079400         WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
079500             PERFORM 2110-BROWSE-BACKWARD                                 
079600             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
079700                                                                          
079800         WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
079900             PERFORM 2120-BROWSE-FORWARD                                  
080000             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
080100                                                                          
080200         WHEN DP020-SRC-AID = DP016-ENTER                                 
080300             PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
080400             IF  PS-ERROR                                                 
080500                 CONTINUE                                                 
080600             ELSE                                                         
080700                 PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
080800             END-IF                                                       
080900                                                                          
081000         WHEN OTHER                                                       
081100             SET DP013-NO-ROLLBACK                                        
081200                 DP013-XCTL-DISPLAY-RESTART                               
081300                 DP013-CICS-ABEND  TO TRUE                                
081400             MOVE '2100-CHECK-FUNCTION-KEY'                               
081500                                   TO DP013-PARAGRAPH                     
081600             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
081700                                   TO DP013-MESSAGE-TEXT (1)              
081800             PERFORM DP013-0000-PROCESS-ABEND                             
081900     END-EVALUATE.                                                        
082000*                                                                         
082100*----------------------------------------------------------------*        
082200*    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
082300*    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
082400*    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
082500*    MESSAGE.                                                    *        
082600*----------------------------------------------------------------*        
082700                                                                          
082800 2110-BROWSE-BACKWARD.                                                    
082900     SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
083000         GIVING PV-TOP-ITEM.                                              
083100*                                                                         
083200     IF  ((PV-TOP-ITEM < +1)                                              
083300       OR (PV-TOP-ITEM = +1))                                             
083400         MOVE +1                   TO PV-TOP-ITEM                         
083500*        -- TOP OF LIST --                                                
083600         MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
083700         SET DP020-MSG-INFORMATIONAL TO TRUE                              
083800     END-IF.                                                              
083900                                                                          
084000 EJECT                                                                    
084100*----------------------------------------------------------------*        
084200*    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
084300*    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
084400*    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *        
084500*----------------------------------------------------------------*        
084600                                                                          
084700 2120-BROWSE-FORWARD.                                                     
084800                                                                          
084900     ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
085000         GIVING PV-TOP-ITEM.                                              
085100     IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
085200         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
085300                                   TO PV-TOP-ITEM                         
085400*        -- BOTTOM OF LIST --                                             
085500         MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
085600         SET DP020-MSG-INFORMATIONAL                                      
085700                                   TO TRUE                                
085800     ELSE                                                                 
085900         COMPUTE PV-BOTTOM-ITEM =                                         
086000             PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
086100         IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
086200*            -- BOTTOM OF LIST --                                         
086300             MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
086400             SET DP020-MSG-INFORMATIONAL                                  
086500                                   TO TRUE                                
086600         END-IF                                                           
086700     END-IF.                                                              
086800 EJECT                                                                    
086900*----------------------------------------------------------------*        
087000*    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
087100*    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
087200*    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
087300*    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *        
087400*    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
087500*    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
087600*----------------------------------------------------------------*        
087700                                                                          
087800 2130-CHECK-FOR-JUMP-BROWSE.                                              
087900     SET PS-NO-ERROR               TO TRUE.                               
088000     IF (ASC-START-ITEM-REQUEST = SPACE)                                  
088100         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
088200                                   TO PV-TOP-ITEM                         
088300     ELSE                                                                 
088400         MOVE ASC-START-ITEM-REQUEST                                      
088500                                   TO DP010I-UNEDITED-FIELD               
088600         MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
088700         MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
088800         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
088900                                   TO TRUE                                
263627         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263627              USING DP010I-NUMERIC-EDIT-AREA                              
089100                                                                          
089200         IF  NOT DP010I-ERROR-DETECTED                                    
089300             MOVE DP010I-NUMERIC-FIELD                                    
089400                                   TO  ASC-START-ITEM-REQUEST-N           
089500             IF  ASC-START-ITEM-REQUEST-N > ZERO                          
089600                 IF  ASC-START-ITEM-REQUEST-N >                           
089700                                          ASC-NUMBER-OF-ITEMS-READ        
089800                     MOVE SPACE TO ASC-START-ITEM-REQUEST                 
089900                     COMPUTE PV-TOP-ITEM =                                
090000                             ASC-NUMBER-OF-ITEMS-READ                     
090100                             - PC-ITEMS-PER-PANEL + 1                     
090200                     IF  PV-TOP-ITEM < +1                                 
090300                         MOVE +1   TO PV-TOP-ITEM                         
090400                         MOVE PC-TSYMSG-00070                             
090500                                   TO DP020-MSG-NUMBER                    
090600                         SET DP020-MSG-INFORMATIONAL                      
090700                                   TO TRUE                                
090800                     END-IF                                               
090900                 ELSE                                                     
091000                     MOVE ASC-START-ITEM-REQUEST-N                        
091100                                   TO PV-TOP-ITEM                         
091200                     MOVE SPACE    TO ASC-START-ITEM-REQUEST              
091300                     COMPUTE PV-BOTTOM-ITEM =                             
091400                         PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
091500                     IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
091600*                        -- BOTTOM OF LIST --                             
091700                         MOVE PC-TSYMSG-00070                             
091800                                   TO DP020-MSG-NUMBER                    
091900                         SET DP020-MSG-INFORMATIONAL                      
092000                                   TO TRUE                                
092100                     END-IF                                               
092200                 END-IF                                                   
092300             ELSE                                                         
092400*                --- VALUE OUT OF RANGE ----                              
092500                 MOVE PC-TSYMSG-00101                                     
092600                                   TO DP020-MSG-NUMBER                    
092700                 MOVE -1           TO ASTRTITL                            
092800                 SET DP030-SET-CURSOR-APPL-1                              
092900                                   TO TRUE                                
093000                 MOVE DP015-REVERSE                                       
093100                                   TO ASTRTITH                            
093200                 MOVE DP015-RED    TO ASTRTITC                            
093300                 MOVE DP015-UNP-NUM-BRT-MDT                               
093400                                   TO ASTRTITA                            
093500                 SET DP020-MSG-FATAL                                      
093600                                   TO TRUE                                
093700                 SET PS-ERROR      TO TRUE                                
093800             END-IF                                                       
093900                                                                          
094000         ELSE                                                             
094100             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
094200             MOVE -1               TO ASTRTITL                            
094300             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
094400             MOVE DP015-REVERSE    TO ASTRTITH                            
094500             MOVE DP015-RED        TO ASTRTITC                            
094600             MOVE DP015-UNP-NUM-BRT-MDT                                   
094700                                   TO ASTRTITA                            
094800             SET DP020-MSG-FATAL   TO TRUE                                
094900             SET PS-ERROR          TO TRUE                                
095000         END-IF                                                           
095100     END-IF.                                                              
095200 EJECT                                                                    
095300*----------------------------------------------------------------*        
095400*    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *        
095500*    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *        
095600*----------------------------------------------------------------*        
095700 2140-DESELECT-ALL-ITEMS.                                                 
095800     MOVE +1                       TO ASC-BLK-SUB.                        
095900                                                                          
096000     PERFORM 2141-CLEAR-SELECTED-INDS                                     
096100         VARYING ASC-ITEM-SUB                                             
096200         FROM 1 BY 1                                                      
096300         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
096400                                                                          
096500     ADD +1                        TO ASC-BLK-SUB.                        
096600                                                                          
096700     PERFORM 2141-CLEAR-SELECTED-INDS                                     
096800         VARYING ASC-ITEM-SUB                                             
096900         FROM 1 BY 1                                                      
097000         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
097100                                                                          
097200     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
097300                                   TO PV-LIST-ITEM-NUMBER.                
097400                                                                          
097500     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
097600         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
097700                                                                          
097800     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
097900        (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)              
098000         MOVE +1                   TO ASC-BLK-SUB                         
098100                                                                          
098200         PERFORM 5100-WRITE-TEMP-STORAGE                                  
098300                                                                          
098400         MOVE +1                   TO PV-BLOCK-NUMBER                     
098500                                                                          
098600         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
098700             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
098800                                                                          
098900         PERFORM                                                          
099000             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
099100                                                                          
099200             PERFORM 4410-READ-TEMP-STORAGE                               
099300                                                                          
099400             PERFORM 2141-CLEAR-SELECTED-INDS                             
099500                 VARYING ASC-ITEM-SUB FROM 1 BY 1                         
099600                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
099700                                                                          
099800             PERFORM 5100-WRITE-TEMP-STORAGE                              
099900                                                                          
100000             ADD +1                TO PV-BLOCK-NUMBER                     
100100                                                                          
100200         END-PERFORM                                                      
100300                                                                          
100400         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
100500             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
100600                                                                          
100700         PERFORM                                                          
100800             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
100900                   ASC-HIGHEST-BLOCK-WRITTEN                              
101000                                                                          
101100             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
101200                                   TO PV-BLOCK-NUMBER                     
101300                                                                          
101400             PERFORM 4410-READ-TEMP-STORAGE                               
101500                                                                          
101600             PERFORM 2141-CLEAR-SELECTED-INDS                             
101700                 VARYING ASC-ITEM-SUB                                     
101800                 FROM 1 BY 1                                              
101900                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
102000                                                                          
102100             PERFORM 5100-WRITE-TEMP-STORAGE                              
102200                                                                          
102300             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
102400                                                                          
102500         END-PERFORM                                                      
102600                                                                          
102700         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
102800                                   TO PV-BLOCK-NUMBER                     
102900                                                                          
103000         PERFORM 4410-READ-TEMP-STORAGE                                   
103100     END-IF.                                                              
103200                                                                          
103300                                                                          
103400     MOVE +0                     TO ASC-NUMBER-OF-SELECTED-ITEMS.         
103500     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE.        
103600     MOVE +0                       TO ASC-END-OF-SELECTED-RANGE.          
103700                                                                          
103800     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
103900                                                                          
104000 EJECT                                                                    
104100*----------------------------------------------------------------*        
104200*    MARK EACH ITEM AS BEING NOT SELECTED.                       *        
104300*----------------------------------------------------------------*        
104400                                                                          
104500 2141-CLEAR-SELECTED-INDS.                                                
104600                                                                          
104700     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
104800             NOT EQUAL ZERO                                               
104900         IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
105000             SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
105100                 ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            
105200                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
105300                                   TO TRUE                                
105400         END-IF                                                           
105500     END-IF.                                                              
105600                                                                          
105700 EJECT                                                                    
105800*----------------------------------------------------------------*        
105900*  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTIO SWITCH  *        
106000*  FOR ALL ITEMS.                                                *        
106100*----------------------------------------------------------------*        
106200                                                                          
106300 2150-SELECT-ALL-ITEMS.                                                   
106400                                                                          
106500     MOVE +1                       TO ASC-BLK-SUB.                        
106600                                                                          
106700     PERFORM 2151-SET-SELECTED-INDS                                       
106800         VARYING ASC-ITEM-SUB                                             
106900         FROM 1 BY 1                                                      
107000         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
107100                                                                          
107200     ADD +1                        TO ASC-BLK-SUB.                        
107300                                                                          
107400     PERFORM 2151-SET-SELECTED-INDS                                       
107500         VARYING ASC-ITEM-SUB                                             
107600         FROM 1 BY 1                                                      
107700         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
107800                                                                          
107900     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
108000                                   TO PV-LIST-ITEM-NUMBER.                
108100                                                                          
108200     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
108300         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
108400                                                                          
108500     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
108600        (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)            
108700         MOVE +1                   TO ASC-BLK-SUB                         
108800                                                                          
108900         PERFORM 5100-WRITE-TEMP-STORAGE                                  
109000                                                                          
109100         MOVE +1                   TO PV-BLOCK-NUMBER                     
109200                                                                          
109300         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
109400             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
109500                                                                          
109600         PERFORM                                                          
109700             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
109800                                                                          
109900             PERFORM 4410-READ-TEMP-STORAGE                               
110000                                                                          
110100             PERFORM 2151-SET-SELECTED-INDS                               
110200                 VARYING ASC-ITEM-SUB                                     
110300                 FROM 1 BY 1                                              
110400                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
110500                                                                          
110600             PERFORM 5100-WRITE-TEMP-STORAGE                              
110700                                                                          
110800             ADD +1                TO PV-BLOCK-NUMBER                     
110900                                                                          
111000         END-PERFORM                                                      
111100                                                                          
111200         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
111300             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
111400                                                                          
111500         PERFORM                                                          
111600             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
111700                   ASC-HIGHEST-BLOCK-WRITTEN                              
111800                                                                          
111900             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
112000                                   TO PV-BLOCK-NUMBER                     
112100                                                                          
112200             PERFORM 4410-READ-TEMP-STORAGE                               
112300                                                                          
112400             PERFORM 2151-SET-SELECTED-INDS                               
112500                 VARYING ASC-ITEM-SUB                                     
112600                 FROM 1 BY 1                                              
112700                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
112800                                                                          
112900             PERFORM 5100-WRITE-TEMP-STORAGE                              
113000                                                                          
113100             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
113200                                                                          
113300         END-PERFORM                                                      
113400                                                                          
113500         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
113600                                   TO PV-BLOCK-NUMBER                     
113700                                                                          
113800         PERFORM 4410-READ-TEMP-STORAGE                                   
113900     END-IF.                                                              
114000                                                                          
114100                                                                          
114200     MOVE ASC-NUMBER-OF-ITEMS-READ TO                                     
114300                       ASC-NUMBER-OF-SELECTED-ITEMS.                      
114400                                                                          
114500     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
114600                                                                          
114700 EJECT                                                                    
114800*----------------------------------------------------------------*        
114900*    MARK EACH ITEM AS BEING SELECTED.                           *        
115000*----------------------------------------------------------------*        
115100                                                                          
115200 2151-SET-SELECTED-INDS.                                                  
115300                                                                          
115400     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
115500             NOT EQUAL ZERO                                               
115600         IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
115700             SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
115800                 ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                
115900                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
116000                               TO  TRUE                                   
116100         END-IF                                                           
116200     END-IF.                                                              
116300******************************************************************        
116400** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **        
116500** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **        
116600******************************************************************        
116700 2200-MOVE-SCREEN-TO-COMMAREA.                                            
116800                                                                          
116900     MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.                     
117000     MOVE +1                       TO PV-SUB.                             
117100*                                                                         
117200     PERFORM 3100-SET-SUBSCRIPTS.                                         
117300     IF  ASTRTITL > ZERO                                                  
117400         MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST                  
117500     ELSE                                                                 
117600         IF  ASTRTITF = DP015-ERASE-EOF                                   
117700             INITIALIZE ASC-START-ITEM-REQUEST                            
117800         END-IF                                                           
117900     END-IF.                                                              
118000     PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
118100         VARYING PV-SUB                                                   
118200         FROM 1 BY 1                                                      
118300         UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                              
118400******************************************************************        
118500** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**        
118600******************************************************************        
118700 2210-MOVE-LINES-TO-COMMAREA.                                             
118800     IF  MR-SELECTL (PV-SUB) > ZERO                                       
118900         SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE                     
119000         MOVE MR-SELECTI (PV-SUB)                                         
119100                                   TO ASC-TEMP-SELECTED-SW                
119200                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
119300     ELSE                                                                 
119400         IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                        
119500                 SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             
119600                 MOVE SPACE        TO ASC-TEMP-SELECTED-SW                
119700                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
119800         END-IF                                                           
119900     END-IF.                                                              
120000     ADD +1                        TO ASC-ITEM-SUB.                       
120100     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
120200         ADD +1                    TO ASC-BLK-SUB                         
120300         MOVE +1                   TO ASC-ITEM-SUB                        
120400     END-IF.                                                              
120500******************************************************************        
120600** EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE      **        
120700** COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY). **        
120800******************************************************************        
120900 3000-EDIT-DATA-IN-COMMAREA.                                              
121000     SET PS-NO-ERROR               TO TRUE.                               
121100     PERFORM 3200-RESET-ATTRIBUTES.                                       
121200     MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                             
121300**                                                                        
121400* CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.                      
121500* EDITTING IS DONE FROM THE BOTTOM UP.                                    
121600**                                                                        
121700     COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                               
121800         ASC-ITEMS-DISPLAYED - 1.                                         
121900     PERFORM 3100-SET-SUBSCRIPTS.                                         
122000     PERFORM 3300-EDIT-SELECTIONS                                         
122100         VARYING PV-SUB                                                   
122200         FROM PV-SUB BY -1                                                
122300         UNTIL PV-SUB < +1                                                
122400*                                                                         
122500     IF  PS-NO-ERROR                                                      
122600         PERFORM 3400-CHECK-RANGE-SEQUENCE                                
122700     END-IF.                                                              
122800*                                                                         
122900     IF  ((PS-NO-ERROR)                                                   
123000       AND (ASC-START-OF-SELECTED-RANGE > ZERO)                           
123100       AND (ASC-END-OF-SELECTED-RANGE > ZERO))                            
123200             PERFORM 3500-MARK-RANGE-AS-SELECTED                          
123300     END-IF.                                                              
123400 EJECT                                                                    
123500*----------------------------------------------------------------*        
123600*    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
123700*    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
123800*    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
123900*                                                                *        
124000*    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
124100*    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
124200*    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
124300*    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
124400*----------------------------------------------------------------*        
124500                                                                          
124600 3100-SET-SUBSCRIPTS.                                                     
124700                                                                          
124800     COMPUTE ASC-ITEM-SUB =                                               
124900         (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.                
125000                                                                          
125100     IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                 
125200         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
125300                                                                          
125400         MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB                       
125500     ELSE                                                                 
125600         MOVE +1                     TO ASC-BLK-SUB                       
125700     END-IF.                                                              
125800******************************************************************        
125900** RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.     **        
126000******************************************************************        
126100                                                                          
126200 3200-RESET-ATTRIBUTES.                                                   
126300     MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
126400     MOVE DP015-GREEN              TO ASTRTITC.                           
126500     MOVE DP015-UNDERLINE          TO ASTRTITH.                           
126600*                                                                         
126700     PERFORM                                                              
126800         VARYING PV-SUB                                                   
126900         FROM 1 BY 1                                                      
127000         UNTIL PV-SUB > PC-ITEMS-PER-PANEL                                
127100         IF MR-PO-NUMBER (PV-SUB) = SPACES                                
127200             MOVE DP015-ASK-DRK-OFF                                       
127300                                   TO MR-SELECTA (PV-SUB)                 
127400             MOVE DP015-HL-OFF     TO MR-SELECTH (PV-SUB)                 
127500         ELSE                                                             
127600             MOVE DP015-UNP-ALP-NOR-OFF                                   
127700                                   TO MR-SELECTA (PV-SUB)                 
127800             MOVE DP015-GREEN      TO MR-SELECTC (PV-SUB)                 
127900             MOVE DP015-UNDERLINE  TO MR-SELECTH (PV-SUB)                 
128000         END-IF                                                           
128100     END-PERFORM.                                                         
128200******************************************************************        
128300** EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS    **        
128400** WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'   **        
128500** IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR    **        
128600** WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.             **        
128700******************************************************************        
128800 3300-EDIT-SELECTIONS.                                                    
128900     EVALUATE TRUE                                                        
129000         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
129100             = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)                 
129200                 CONTINUE                                                 
129300         WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)                 
129400             IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                
129500                 ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS           
129600                 MOVE ASC-TEMP-SELECTED-SW                                
129700                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
129800                                TO ASC-SELECTED-SW                        
129900                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
130000             END-IF                                                       
130100         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
130200                 = SPACE                                                  
130300             EVALUATE TRUE                                                
130400                 WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)               
130500                     SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS        
130600                     MOVE SPACE TO ASC-SELECTED-SW                        
130700                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
130800                 WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                
130900                     MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE            
131000                     MOVE SPACE TO ASC-SELECTED-SW                        
131100                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
131200                 WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)                  
131300                     MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE              
131400                     MOVE SPACE TO ASC-SELECTED-SW                        
131500                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
131600             END-EVALUATE                                                 
131700         WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                   
131800             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
131900                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
132000             END-IF                                                       
132100             PERFORM 3310-EDIT-RANGE                                      
132200         WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)                     
132300             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
132400                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
132500             END-IF                                                       
132600             PERFORM 3310-EDIT-RANGE                                      
132700         WHEN OTHER                                                       
132800             MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER                   
132900             SET DP020-MSG-FATAL    TO TRUE                               
133000             MOVE -1                TO MR-SELECTL (PV-SUB)                
133100             SET DP030-SET-CURSOR-APPL-1                                  
133200                                    TO TRUE                               
133300             MOVE DP015-RED         TO MR-SELECTC (PV-SUB)                
133400             MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)                
133500             SET PS-ERROR TO TRUE                                         
133600             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
133700                 SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS             
133800                 MOVE SPACE         TO ASC-SELECTED-SW                    
133900                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
134000             END-IF                                                       
134100     END-EVALUATE.                                                        
134200                                                                          
      * CANNOT TAKE FURTHER ACTION ON A REQUEST THAT HAS ALREADY BEEN DELETED   
      * OR CANCELLED                                                            
           IF ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                             
           AND ( ASC-ACTION (ASC-BLK-SUB ASC-ITEM-SUB) = PC-DEL                 
            OR   ASC-ACTION (ASC-BLK-SUB ASC-ITEM-SUB) = PC-CAN)                
015030     AND ( DP020-FK-DISPN-LOCAL (DP020-SRC-AID)                           
015040     OR    DP020-FK-DISPN-GO    (DP020-SRC-AID)                           
012100     OR    DP020-FK-LOCAL-GOTO  (DP020-SRC-AID) )                         
132800         MOVE PC-TSYMSG-01381       TO DP020-MSG-NUMBER                   
132900         SET DP020-MSG-FATAL                                              
133500             PS-ERROR                                                     
133100             DP030-SET-CURSOR-APPL-1                                      
133200                                    TO TRUE                               
133000         MOVE -1                    TO MR-SELECTL (PV-SUB)                
133300         MOVE DP015-RED             TO MR-SELECTC (PV-SUB)                
133400         MOVE DP015-REVERSE         TO MR-SELECTH (PV-SUB)                
           END-IF.                                                              
                                                                                
134300     SUBTRACT +1                    FROM PV-ITEM-IN-USE.                  
134400     SUBTRACT +1                    FROM ASC-ITEM-SUB.                    
134500     IF  ASC-ITEM-SUB < 1                                                 
134600         SUBTRACT +1 FROM ASC-BLK-SUB                                     
134700         MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                        
134800     END-IF.                                                              
134900******************************************************************        
135000** IF A BEGINNING AND END OF RANGE WAS ENTERED TO SELECT A      **        
135100** RANGE OF LINES, MAKE SURE THERE IS ONLY 1 BEGINNING AND 1    **        
135200** ENDING AND MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN)**        
135300** THE ENDING.                                                  **        
135400******************************************************************        
135500 3310-EDIT-RANGE.                                                         
135600     IF  ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                        
135700         IF  ASC-START-OF-SELECTED-RANGE = ZERO                           
135800             MOVE ASC-TEMP-SELECTED-SW  (ASC-BLK-SUB ASC-ITEM-SUB)        
135900                                   TO ASC-SELECTED-SW                     
136000                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
136100             MOVE PV-ITEM-IN-USE   TO ASC-START-OF-SELECTED-RANGE         
136200         ELSE                                                             
136300             IF  ASC-START-OF-SELECTED-RANGE = PV-ITEM-IN-USE             
136400                 CONTINUE                                                 
136500             ELSE                                                         
136600*                -- ONLY ONE BEGINNING-OF-RANGE-ALLOWED --                
136700                 MOVE PC-TSYMSG-00054                                     
136800                                   TO DP020-MSG-NUMBER                    
136900                 SET DP020-MSG-FATAL                                      
137000                                   TO TRUE                                
137100                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
137200                 SET DP030-SET-CURSOR-APPL-1                              
137300                                   TO TRUE                                
137400                 MOVE DP015-RED    TO MR-SELECTC (PV-SUB)                 
137500                 MOVE DP015-UNDERLINE                                     
137600                                   TO MR-SELECTH (PV-SUB)                 
137700                 SET PS-ERROR      TO TRUE                                
137800             END-IF                                                       
137900         END-IF                                                           
138000     ELSE                                                                 
138100         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
138200             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
138300                                   TO ASC-SELECTED-SW                     
138400                                     (ASC-BLK-SUB ASC-ITEM-SUB)           
138500             MOVE PV-ITEM-IN-USE   TO ASC-END-OF-SELECTED-RANGE           
138600         ELSE                                                             
138700             IF  ASC-END-OF-SELECTED-RANGE = PV-ITEM-IN-USE               
138800                 CONTINUE                                                 
138900             ELSE                                                         
139000*                -- ONLY ONE END-OF-RANGE ALLOWED --                      
139100                 MOVE PC-TSYMSG-00055                                     
139200                                   TO DP020-MSG-NUMBER                    
139300                 SET DP020-MSG-FATAL                                      
139400                                   TO TRUE                                
139500                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
139600                 SET DP030-SET-CURSOR-APPL-1                              
139700                                   TO TRUE                                
139800                 SET PS-ERROR      TO TRUE                                
139900             END-IF                                                       
140000         END-IF                                                           
140100     END-IF.                                                              
140200*                                                                         
140300******************************************************************        
140400** IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,    **        
140500** MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.  **        
140600******************************************************************        
140700 3400-CHECK-RANGE-SEQUENCE.                                               
140800     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
140900       AND  ASC-END-OF-SELECTED-RANGE > ZERO                              
141000       AND  ASC-START-OF-SELECTED-RANGE >                                 
141100                                         ASC-END-OF-SELECTED-RANGE        
141200*        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                        
141300         MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                         
141400         SET DP020-MSG-FATAL TO TRUE                                      
141500         SET PS-ERROR TO TRUE                                             
141600         PERFORM 3410-POSITION-AT-ERROR                                   
141700     END-IF.                                                              
141800******************************************************************        
141900** BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE    **        
142000** PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR **        
142100** SHOULD BE POSITIONED ON A RANGE ERROR.                       **        
142200******************************************************************        
142300 3410-POSITION-AT-ERROR.                                                  
142400     SET DP030-SET-CURSOR-APPL-1                                          
142500                                   TO TRUE.                               
142600     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
142700                    - PV-TOP-ITEM + 1.                                    
142800     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
142900         CONTINUE                                                         
143000     ELSE                                                                 
143100         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
143200         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
143300         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
143400     END-IF.                                                              
143500*                                                                         
143600     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
143700                    - PV-TOP-ITEM + 1.                                    
143800     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
143900         CONTINUE                                                         
144000     ELSE                                                                 
144100         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
144200         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
144300         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
144400     END-IF.                                                              
144500 EJECT                                                                    
144600*----------------------------------------------------------------*        
144700*    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *        
144800*    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *        
144900*    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *        
145000*----------------------------------------------------------------*        
145100                                                                          
145200 3500-MARK-RANGE-AS-SELECTED.                                             
145300     MOVE +1                       TO  ASC-BLK-SUB.                       
145400     PERFORM 3510-MARK-SELECTED-INDS                                      
145500         VARYING ASC-ITEM-SUB                                             
145600         FROM 1 BY 1                                                      
145700         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
145800                                                                          
145900     ADD +1                        TO ASC-BLK-SUB.                        
146000     PERFORM 3510-MARK-SELECTED-INDS                                      
146100         VARYING ASC-ITEM-SUB                                             
146200         FROM 1 BY 1                                                      
146300         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
146400                                                                          
146500     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
146600                                   TO  PV-LIST-ITEM-NUMBER.               
146700     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
146800         1 + ((PV-LIST-ITEM-NUMBER - 1) /                                 
146900               PC-ITEMS-PER-PANEL).                                       
147000                                                                          
147100     MOVE ASC-START-OF-SELECTED-RANGE                                     
147200                                   TO PV-START-OF-SELECTED-RANGE.         
147300     COMPUTE PV-FIRST-BLOCK-IN-RANGE =                                    
147400         1 + ((PV-START-OF-SELECTED-RANGE - 1) /                          
147500               PC-ITEMS-PER-PANEL).                                       
147600                                                                          
147700     MOVE ASC-END-OF-SELECTED-RANGE                                       
147800                                   TO  PV-END-OF-SELECTED-RANGE.          
147900     COMPUTE PV-LAST-BLOCK-IN-RANGE =                                     
148000         1 + ((PV-END-OF-SELECTED-RANGE - 1) /                            
148100               PC-ITEMS-PER-PANEL).                                       
148200     COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                     
148300         PV-FIRST-BLOCK-IN-ARRAY + 1.                                     
148400                                                                          
148500     IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR            
148600        (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)                
148700         MOVE +1                   TO ASC-BLK-SUB                         
148800         PERFORM 5100-WRITE-TEMP-STORAGE                                  
148900         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
149000             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
149100                                                                          
149200         PERFORM                                                          
149300             UNTIL PV-FIRST-BLOCK-IN-RANGE >                              
149400                   PV-PREV-BLOCK-IN-ARRAY                                 
149500             MOVE PV-FIRST-BLOCK-IN-RANGE                                 
149600                                   TO PV-BLOCK-NUMBER                     
149700             PERFORM 4410-READ-TEMP-STORAGE                               
149800             PERFORM 3510-MARK-SELECTED-INDS                              
149900                 VARYING ASC-ITEM-SUB                                     
150000                 FROM 1 BY 1                                              
150100                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
150200             PERFORM 5100-WRITE-TEMP-STORAGE                              
150300             ADD +1                TO PV-FIRST-BLOCK-IN-RANGE             
150400         END-PERFORM                                                      
150500                                                                          
150600         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
150700             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
150800         PERFORM                                                          
150900             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
151000                   PV-LAST-BLOCK-IN-RANGE                                 
151100             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
151200                                   TO PV-BLOCK-NUMBER                     
151300             PERFORM 4410-READ-TEMP-STORAGE                               
151400             PERFORM 3510-MARK-SELECTED-INDS                              
151500                 VARYING ASC-ITEM-SUB                                     
151600                 FROM 1 BY 1                                              
151700                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
151800             PERFORM 5100-WRITE-TEMP-STORAGE                              
151900             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
152000         END-PERFORM                                                      
152100                                                                          
152200         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
152300                                   TO PV-BLOCK-NUMBER                     
152400         PERFORM 4410-READ-TEMP-STORAGE                                   
152500     END-IF.                                                              
152600                                                                          
152700     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE         
152800                                      ASC-END-OF-SELECTED-RANGE.          
152900                                                                          
153000 EJECT                                                                    
153100*----------------------------------------------------------------*        
153200* SET THE SELECTION INDICATOR TO 'S' WITHIN THE RANGE.           *        
153300*----------------------------------------------------------------*        
153400                                                                          
153500 3510-MARK-SELECTED-INDS.                                                 
153600                                                                          
153700     IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
153800                                ASC-ITEM-SUB) >                           
153900         ASC-START-OF-SELECTED-RANGE)                                     
154000     OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
154100                                ASC-ITEM-SUB) =                           
154200         ASC-START-OF-SELECTED-RANGE)                                     
154300                                                                          
154400         IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
154500                                    ASC-ITEM-SUB) <                       
154600             ASC-END-OF-SELECTED-RANGE)                                   
154700         OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
154800                                    ASC-ITEM-SUB) =                       
154900             ASC-END-OF-SELECTED-RANGE)                                   
155000                                                                          
155100             IF NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
155200                 SET ASC-SELECT     (ASC-BLK-SUB  ASC-ITEM-SUB)           
155300                     ASC-TEMP-SELECT(ASC-BLK-SUB  ASC-ITEM-SUB)           
155400                               TO TRUE                                    
155500                 SET ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      
155600                               TO TRUE                                    
155700                 ADD +1        TO ASC-NUMBER-OF-SELECTED-ITEMS            
155800             END-IF                                                       
155900         END-IF                                                           
156000     END-IF.                                                              
156100                                                                          
156200 EJECT                                                                    
156300*----------------------------------------------------------------*        
156400*    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
156500*    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
156600*    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
156700*    DISPLAY THE PANEL TO USER.                                  *        
156800*----------------------------------------------------------------*        
156900                                                                          
157000 4000-BUILD-INITIAL-PANEL.                                                
157100                                                                          
157200     INITIALIZE                  ASC-BLOCK-ENTRIES (1)                    
157300                                 ASC-BLOCK-ENTRIES (2).                   
157400     MOVE +1                 TO ASC-BLK-SUB.                              
157500     SET ASC-ITEMS-LEFT-ON-DB                                             
157600                             TO TRUE.                                     
157700                                                                          
157800     MOVE +0                 TO ASC-ITEM-SUB                              
157900                                ASC-NUMBER-OF-ITEMS-READ                  
158000                                ASC-NUMBER-OF-SELECTED-ITEMS              
158100                                ASC-HIGHEST-BLOCK-WRITTEN                 
158200                                ASC-START-OF-SELECTED-RANGE               
158300                                ASC-END-OF-SELECTED-RANGE                 
158400                                ASC-ITEM-AT-TOP-OF-PANEL.                 
158500     PERFORM 4010-OPEN-CURSOR.                                            
158600                                                                          
158700     PERFORM 4300-READ-ITEMS-FROM-DB.                                     
158800                                                                          
158900     PERFORM 4020-CLOSE-CURSOR.                                           
159000                                                                          
159100     MOVE +1                   TO  PV-TOP-ITEM.                           
159200     IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
159300         IF DP020-FK-REFRESH (DP020-SRC-AID)                              
159400             CONTINUE                                                     
159500         ELSE                                                             
159600             SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                        
159700         END-IF                                                           
159800         SET DP020-MSG-FATAL   TO  TRUE                                   
159900         MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
160000     ELSE                                                                 
160100         MOVE -1               TO MR-SELECTL (1)                          
160200         SET DP030-SET-CURSOR-APPL-1                                      
160300                               TO TRUE                                    
160400         PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
160500     END-IF.                                                              
160600 EJECT                                                                    
160700*----------------------------------------------------------------*        
160800* OPEN THE TMESSAGE CURSOR.                                      *        
160900*   IN THIS PARTICULAR APPLICATION ONE OF 4 DIFFERENT CURSORS    *        
161000*   WILL BE OPENED DEPENDING ON THE CRITERIA SPECIFIED BY THE    *        
161100*   USER.                                                        *        
161200*----------------------------------------------------------------*        
161300                                                                          
161400 4010-OPEN-CURSOR.                                                        
161500     PERFORM                                                              
161600         WITH TEST AFTER                                                  
161700         VARYING PV-RETRY-COUNTER                                         
161800         FROM 1 BY 1                                                      
161900         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
162000                   OR (SQLCODE = ZERO))                                   
162100                                                                          
162200         EXEC SQL                                                         
162300             OPEN REQUEST_CURSOR                                          
162400         END-EXEC                                                         
162500                                                                          
162600         EVALUATE TRUE                                                    
162700             WHEN SQLCODE = ZERO                                          
162800             WHEN SQLCODE = -904                                          
162900             WHEN SQLCODE = -913                                          
163000                 CONTINUE                                                 
163100             WHEN SQLWARN0 NOT = SPACES                                   
163200             WHEN SQLCODE < ZERO                                          
163300             WHEN SQLCODE > ZERO                                          
163400                 MOVE '4010-OPEN-CURSOR'                                  
163500                                   TO DP013-PARAGRAPH                     
163600                 MOVE 'OPEN PRETICKET REQUEST CURSOR - '                  
163700                                   TO DP013-MESSAGE-TEXT (1)              
163800                 MOVE SQLCA        TO DP013-SQLCA                         
163900                 MOVE 'TPTKREQ '   TO DP013-DB2-TABLE-NAME (1)            
164000                 MOVE 'TPTKLNE '   TO DP013-DB2-TABLE-NAME (2)            
164100                 MOVE 'TENTNME '   TO DP013-DB2-TABLE-NAME (3)            
164200                 SET DP013-DB2-ABEND                                      
164300                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
164400                 PERFORM DP013-0000-PROCESS-ABEND                         
164500         END-EVALUATE                                                     
164600     END-PERFORM.                                                         
164700*                                                                         
164800     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
164900         MOVE '4010-OPEN-CURSOR'   TO DP013-PARAGRAPH                     
165000         MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN CSR'           
165100                                   TO DP013-MESSAGE-TEXT (1)              
165200         MOVE SQLCA                TO DP013-SQLCA                         
165300         MOVE 'TPTKREQ '           TO DP013-DB2-TABLE-NAME (1)            
165400         MOVE 'TPTKLNE '           TO DP013-DB2-TABLE-NAME (2)            
165500         MOVE 'TENTNME '           TO DP013-DB2-TABLE-NAME (3)            
165600         SET DP013-DB2-ABEND                                              
165700             DP013-XCTL-DISPLAY-RESTART                                   
165800                                   TO TRUE                                
165900         PERFORM DP013-0000-PROCESS-ABEND                                 
166000     END-IF.                                                              
166100                                                                          
166200 EJECT                                                                    
166300*----------------------------------------------------------------*        
166400* CLOSE THE TMESSAGE CURSOR.                                     *        
166500*   IN THIS PARTICULAR APPLICATION ONE OF 3 DIFFERENT CURSORS    *        
166600*   WILL BE CLOSED DEPENDING ON THE CRITERIA SPECIFIED BY THE    *        
166700*   USER.                                                        *        
166800*----------------------------------------------------------------*        
166900                                                                          
167000 4020-CLOSE-CURSOR.                                                       
167100     EXEC SQL                                                             
167200         CLOSE REQUEST_CURSOR                                             
167300     END-EXEC.                                                            
167400*                                                                         
167500     EVALUATE TRUE                                                        
167600         WHEN SQLCODE = ZERO                                              
167700             CONTINUE                                                     
167800         WHEN SQLWARN0 NOT = SPACES                                       
167900         WHEN SQLCODE < ZERO                                              
168000         WHEN SQLCODE > ZERO                                              
168100             MOVE '4020-CLOSE-CURSOR'                                     
168200                               TO DP013-PARAGRAPH                         
168300             MOVE 'CLOSE PRETICKET REQUEST CURSOR'                        
168400                               TO DP013-MESSAGE-TEXT (1)                  
168500             MOVE SQLCA        TO DP013-SQLCA                             
168600             MOVE 'TPTKREQ '   TO DP013-DB2-TABLE-NAME (1)                
168700             MOVE 'TPTKLNE '   TO DP013-DB2-TABLE-NAME (2)                
168800             MOVE 'TENTNME '   TO DP013-DB2-TABLE-NAME (3)                
168900             SET DP013-DB2-ABEND                                          
169000                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
169100             PERFORM DP013-0000-PROCESS-ABEND                             
169200     END-EVALUATE.                                                        
169300                                                                          
169400 EJECT                                                                    
169500*----------------------------------------------------------------*        
169600*    SET UP THE HOST VARIABLES.                                  *        
169700*----------------------------------------------------------------*        
169800                                                                          
169900 4100-PREPARE-CURSOR.                                                     
170000                                                                          
170100     SET ASC-NO-CURSOR         TO TRUE.                                   
170200                                                                          
170300     EVALUATE TRUE                                                        
170400         WHEN ASC-KEY-PO-NUMBER GREATER THAN ZERO                       86
170500             SET ASC-PO-CURSOR     TO TRUE                                
170600                                                                        86
170700         WHEN ASC-KEY-DEPT-NBR GREATER THAN SPACES                        
170800             IF  ASC-KEY-VENDOR-NAME GREATER THAN SPACES                  
170900                 SET ASC-DPTVND-CURSOR                                    
171000                                   TO TRUE                                
171100             ELSE                                                         
171200                 SET ASC-DPT-CURSOR                                       
171300                                   TO TRUE                                
171400             END-IF                                                       
171500                                                                          
171600         WHEN ASC-KEY-VENDOR-NAME GREATER THAN SPACES                     
171700             SET ASC-VND-CURSOR TO TRUE                                   
171800     END-EVALUATE.                                                        
171900 EJECT                                                                    
172000*----------------------------------------------------------------*        
172100*    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
172200*    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
172300*    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
172400*    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
172500*    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
172600*                                                                *        
172700*    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
172800*    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
172900*    UPON INITIALIZATION.                                        *        
173000*----------------------------------------------------------------*        
173100                                                                          
173200 4300-READ-ITEMS-FROM-DB.                                                 
173300                                                                          
173400     PERFORM                                                              
173500         VARYING PV-READ-COUNT                                            
173600         FROM 1 BY 1                                                      
173700         UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                            
173800            OR (ASC-NO-ITEMS-LEFT-ON-DB))                                 
173900                                                                          
174000         PERFORM 4310-FETCH                                               
174100                                                                          
174200         IF  ASC-ITEMS-LEFT-ON-DB                                         
                                                                                
                   PERFORM 4320-GET-DETAIL-INFO                                 
                                                                                
174300             IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        
174400                 ADD +1            TO ASC-ITEM-SUB                        
174500             ELSE                                                         
174600                 ADD +1            TO ASC-BLK-SUB                         
174700                 MOVE +1           TO ASC-ITEM-SUB                        
174800                                                                          
174900                 IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 
175000                     MOVE +1       TO ASC-BLK-SUB                         
175100                     SET ASC-BLOCK-MODIFIED (1)                           
175200                                   TO TRUE                                
175300                     PERFORM 5100-WRITE-TEMP-STORAGE                      
175400                     MOVE ASC-BLOCK-ENTRIES                               
175500                               (PC-MAX-BLOCKS-IN-ARRAY)                   
175600                                TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)         
175700                     ADD +1        TO ASC-BLK-SUB                         
175800                     INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)           
175900                     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)              
176000                                   TO TRUE                                
176100                 END-IF                                                   
176200             END-IF                                                       
176300             ADD +1                TO ASC-NUMBER-OF-ITEMS-READ            
176400             MOVE ASC-NUMBER-OF-ITEMS-READ                                
176500                                   TO ASC-LIST-ITEM-NUMBER                
176600                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
176700             MOVE PTKREQ-PO-NBR    TO ASC-PO-NUMBER                       
176800                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
176900             MOVE PTKREQ-PO-REQ-NBR                                       
177000                                   TO ASC-RQST-NBR                        
177100                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
177200             MOVE PTKREQ-REQ-TYP-CDE                                      
177300                                   TO ASC-RQST-TYPE                       
177400                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
177500             MOVE PTKREQ-REQ-STAT-CDE                                     
177600                                   TO ASC-RQST-STAT                       
177700                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
177800             MOVE PTKREQ-PRTY-STAT-CDE                                    
177900                                   TO ASC-PRIORITY                        
178000                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
178100             MOVE ENTNME-ENT-NAME-DESC                                    
178200                                   TO ASC-VENDOR-NAME                     
178300                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
178400             MOVE PTKREQ-ENT-ID    TO ASC-ENT-ID                          
178500                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
178600             MOVE PTKREQ-DEPT-NBR  TO ASC-DEPT-NBR                        
178700                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
178800                                                                          
178900             MOVE PTKREQ-REQ-CREN-DTE                                     
179000                                   TO PV-WORK-DATE                        
179100             MOVE PV-DISPLAY-DATE  TO ASC-RQST-DATE                       
179200                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
179300                                                                          
179400             MOVE PTKREQ-PRT-BY-DTE                                       
179500                                   TO PV-WORK-DATE                        
179600             MOVE PV-DISPLAY-DATE  TO ASC-PRINT-DUE-DATE                  
179700                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
179800                                                                          
179900             MOVE PTKREQ-VND-DUE-DTE                                      
180000                                   TO PV-WORK-DATE                        
180100             MOVE PV-DISPLAY-DATE  TO ASC-VENDOR-DUE-DATE                 
180200                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
180300                                                                          
180400             IF PV-REQUEST-LINE-COUNT > 9999                              
289600                 MOVE '4400-READ-ITEMS-FROM-DB'                           
289700                               TO DP013-PARAGRAPH                         
289800                 MOVE 'LINE COUNT TOO LARGE FOR FIELDS'                   
289900                               TO DP013-MESSAGE-TEXT (1)                  
290000                 MOVE PTKREQ-PO-NBR                                       
290100                               TO PV-DISPLAY-COMP1                        
290101                 MOVE PV-REQUEST-LINE-COUNT                               
290102                               TO PV-DISPLAY-COMP2                        
290103                 STRING 'PO: '          DELIMITED BY SIZE                 
290104                        PV-DISPLAY-COMP1                                  
290105                                        DELIMITED BY SIZE                 
290106                        ' - LINE COUNT: '                                 
290107                                        DELIMITED BY SIZE                 
290108                        PV-DISPLAY-COMP2                                  
290109                                        DELIMITED BY SIZE                 
290110                               INTO PV-DISPLAY-ABEND-MESSAGE              
290111                 MOVE PV-DISPLAY-ABEND-MESSAGE                            
290112                               TO DP013-MESSAGE-TEXT (2)                  
290113                 MOVE PV-REQUEST-LINE-COUNT                               
290114                               TO DP013-MESSAGE-TEXT (3)                  
290200                 SET DP013-LOGIC-ABEND                                    
290300                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
290400                 PERFORM DP013-0000-PROCESS-ABEND                         
290500             ELSE                                                         
290600                 MOVE PV-REQUEST-LINE-COUNT                               
290700                                   TO ASC-LINE-COUNT                      
290800                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
290900             END-IF                                                       
291000                                                                          
291001             IF PV-REQUEST-TICKET-TOTAL > 9999999                         
291002                 MOVE '4400-READ-ITEMS-FROM-DB'                           
291003                               TO DP013-PARAGRAPH                         
291004                 MOVE 'TICKET QUANTITY TOO LARGE FOR FIELDS'              
291005                               TO DP013-MESSAGE-TEXT (1)                  
291006                 MOVE PTKREQ-PO-NBR                                       
291007                               TO PV-DISPLAY-COMP1                        
291008                 MOVE PV-REQUEST-TICKET-TOTAL                             
291009                               TO PV-DISPLAY-COMP2                        
291010                 STRING 'PO: '          DELIMITED BY SIZE                 
291020                        PV-DISPLAY-COMP1                                  
291030                                        DELIMITED BY SIZE                 
291040                        ' - TICKET QUANTITY: '                            
291050                                        DELIMITED BY SIZE                 
291060                        PV-DISPLAY-COMP2                                  
291070                                        DELIMITED BY SIZE                 
291071                               INTO PV-DISPLAY-ABEND-MESSAGE              
291072                 MOVE PV-DISPLAY-ABEND-MESSAGE                            
291073                               TO DP013-MESSAGE-TEXT (2)                  
291074                 MOVE PV-REQUEST-TICKET-TOTAL                             
291075                               TO DP013-MESSAGE-TEXT (3)                  
291076                 SET DP013-LOGIC-ABEND                                    
291077                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
291078                 PERFORM DP013-0000-PROCESS-ABEND                         
291079             ELSE                                                         
291080                 MOVE PV-REQUEST-TICKET-TOTAL                             
291090                                   TO ASC-RQSTD-TICKETS                   
291100                                      (ASC-BLK-SUB ASC-ITEM-SUB)        86
291200             END-IF                                                       
291300         END-IF                                                           
291400     END-PERFORM.                                                         
291500                                                                          
291600     IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
291700         MOVE +1               TO ASC-BLK-SUB                             
291800         PERFORM 5100-WRITE-TEMP-STORAGE                                  
291900         ADD +1                TO ASC-BLK-SUB                             
292000         PERFORM 5100-WRITE-TEMP-STORAGE                                  
292100                                                                          
292200         MOVE 1                TO ASC-BLK-SUB                             
292300                                  PV-BLOCK-NUMBER                         
292400         PERFORM 4410-READ-TEMP-STORAGE                                   
292500         IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
292600             MOVE 2            TO ASC-BLK-SUB                             
292700                                  PV-BLOCK-NUMBER                         
292800             PERFORM 4410-READ-TEMP-STORAGE                               
292900         END-IF                                                           
293000     END-IF.                                                              
293100 EJECT                                                                    
293200*----------------------------------------------------------------*        
293300*    FETCH THE MESSAGE CURSOR.                                   *        
293400*----------------------------------------------------------------*        
293500                                                                          
293600 4310-FETCH.                                                              
293700                                                                          
293800     EXEC SQL                                                             
293900          FETCH REQUEST_CURSOR                                            
294000          INTO                                                            
294100                :PTKREQ-PO-REQ-NBR                                        
294200              , :PTKREQ-PO-NBR                                            
294300              , :PTKREQ-DEPT-NBR                                          
294400              , :PTKREQ-REQ-TYP-CDE                                       
294500              , :PTKREQ-REQ-STAT-CDE                                      
294700              , :ENTNME-ENT-NAME-DESC                                     
294800              , :PTKREQ-ENT-ID                                            
294900              , :PTKREQ-REQ-CREN-DTE                                      
295000              , :PTKREQ-VND-DUE-DTE                                       
295100              , :PTKREQ-PRT-BY-DTE                                        
295200              , :PTKREQ-PRTY-STAT-CDE                                     
295400     END-EXEC                                                             
295500                                                                          
295600     EVALUATE TRUE                                                        
295700         WHEN SQLCODE = ZERO                                              
295800         WHEN SQLCODE = +100                                              
295900              CONTINUE                                                    
296000         WHEN SQLWARN0 NOT = SPACE                                        
296100         WHEN SQLCODE NOT = ZERO                                          
296200              MOVE '4310-FETCH-CURSOR'                                    
296300                               TO DP013-PARAGRAPH                         
296400              MOVE 'FETCH REQUEST CURSOR'                                 
296500                               TO DP013-MESSAGE-TEXT (1)                  
296600              MOVE SQLCA       TO DP013-SQLCA                             
296700              SET DP013-DB2-ABEND                                         
296800                               TO TRUE                                    
296900              PERFORM DP013-0000-PROCESS-ABEND                            
297000     END-EVALUATE.                                                        
297100                                                                          
297200     IF  SQLCODE EQUAL +100                                               
297300         SET ASC-NO-ITEMS-LEFT-ON-DB                                      
297400                                   TO TRUE                                
297500     END-IF.                                                              
297600                                                                          
293200*----------------------------------------------------------------*        
293300*    SELECT THE DETAIL INFORMATION                               *        
293400*----------------------------------------------------------------*        
293600 4320-GET-DETAIL-INFO.                                                    
293700                                                                          
293800     EXEC SQL                                                             
                SELECT SUM(A.TKT_QTY)                                           
                     , COUNT(DISTINCT A.PO_LNE_NBR)                             
                INTO                                                            
                      :PV-REQUEST-TICKET-TOTAL                                  
295300              , :PV-REQUEST-LINE-COUNT                                    
                FROM TPTKLNE A                                                  
                WHERE A.PO_REQ_NBR = :PTKREQ-PO-REQ-NBR                         
295400     END-EXEC.                                                            
295500                                                                          
295600     EVALUATE TRUE                                                        
295700         WHEN SQLCODE = ZERO                                              
295900              CONTINUE                                                    
295800         WHEN SQLCODE = -305                                              
295800         WHEN SQLCODE = +100                                              
295900              MOVE +0 TO PV-REQUEST-TICKET-TOTAL                          
                               PV-REQUEST-LINE-COUNT                            
296000         WHEN SQLWARN0 NOT = SPACE                                        
296100         WHEN SQLCODE NOT = ZERO                                          
296200              MOVE '4320-GET-DETAIL-INFO.'                                
296300                               TO DP013-PARAGRAPH                         
296400              MOVE 'SELECT PRETICKET DETAIL TABLES'                       
296500                               TO DP013-MESSAGE-TEXT (1)                  
296600              MOVE SQLCA       TO DP013-SQLCA                             
296700              SET DP013-DB2-ABEND                                         
296800                               TO TRUE                                    
296900              PERFORM DP013-0000-PROCESS-ABEND                            
297000     END-EVALUATE.                                                        
297100                                                                          
297600                                                                          
297700 EJECT                                                                    
297800*----------------------------------------------------------------*        
297900* MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
298000*----------------------------------------------------------------*        
298100                                                                          
298200 4400-MOVE-COMMAREA-TO-SCREEN.                                            
298300     IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
298400         AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
298500         OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
298600             MOVE +1    TO ASC-BLK-SUB                                    
298700             PERFORM 5100-WRITE-TEMP-STORAGE                              
298800             ADD +1     TO ASC-BLK-SUB                                    
298900             PERFORM 5100-WRITE-TEMP-STORAGE                              
299000             DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
299100                 GIVING PV-BLOCK-NUMBER                                   
299200                 REMAINDER PV-REMAINDER                                   
299300             IF  PV-REMAINDER > ZERO                                      
299400                 ADD +1 TO PV-BLOCK-NUMBER                                
299500             END-IF                                                       
299600             MOVE +1    TO ASC-BLK-SUB                                    
299700             PERFORM 4410-READ-TEMP-STORAGE                               
299800             ADD +1     TO ASC-BLK-SUB                                    
299900                           PV-BLOCK-NUMBER                                
300000             PERFORM 4410-READ-TEMP-STORAGE                               
300100     END-IF.                                                              
300200     MOVE +1            TO ASC-BLK-SUB.                                   
300300     COMPUTE ASC-ITEM-SUB =                                               
300400         ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
300500         + PC-ITEMS-PER-PANEL).                                           
300600*                                                                         
300700     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
300800         ADD +1         TO ASC-BLK-SUB                                    
300900         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
301000     END-IF.                                                              
301100*                                                                         
301200     COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
301300         ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
301400     PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                                
301500     PERFORM 4420-FORMAT-LIST-LINES                                       
301600         VARYING PV-SUB                                                   
301700         FROM PC-ITEMS-PER-PANEL BY -1                                    
301800         UNTIL PV-SUB < +1.                                               
301900*                                                                         
302000     COMPUTE ASC-ITEMS-DISPLAYED =                                        
302100         ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
302200     MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
302300                                      ASTRTITO.                           
302400     MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
302500     MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
302600     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
302700                                   TO ASELITO.                            
302800*                                                                         
302900     IF  DP020-MSG-NUMBER-X = SPACE                                       
303000         IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
303100*            --- MORE ITEMS TO DISPLAY ----                               
303200             MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
303300             SET DP020-MSG-INFORMATIONAL TO TRUE                          
303400         END-IF                                                           
303500     END-IF.                                                              
303600*----------------------------------------------------------------*        
303700*    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
303800*----------------------------------------------------------------*        
303900                                                                          
304000 4410-READ-TEMP-STORAGE.                                                  
304100                                                                          
304200     EXEC CICS                                                            
304300         READQ TS                                                         
304400             QUEUE (ASC-LIST-QUEUE-NAME)                                  
304500             INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
304600             ITEM  (PV-BLOCK-NUMBER)                                      
304700             RESP  (PV-CICS-RESP)                                         
304800     END-EXEC.                                                            
304900                                                                          
305000     IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
305100       OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
305200         CONTINUE                                                         
305300     ELSE                                                                 
305400         SET DP013-CICS-ABEND     TO TRUE                                 
305500         SET DP013-NO-ROLLBACK    TO TRUE                                 
305600         MOVE '4410-READ-TEMP-STORAGE'                                    
305700                                  TO DP013-PARAGRAPH                      
305800         MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
305900                                  TO DP013-MESSAGE-TEXT(1)                
306000         PERFORM DP013-0000-PROCESS-ABEND                                 
306100     END-IF.                                                              
306200                                                                          
306300     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
306400*                                                                         
306500     IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
306600         INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
306700     END-IF.                                                              
306800 EJECT                                                                    
306900*----------------------------------------------------------------*        
307000*    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
307100*----------------------------------------------------------------*        
307200                                                                          
307300 4420-FORMAT-LIST-LINES.                                                  
307400     IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
307500         SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
307600         MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA (PV-SUB)                   
307700         MOVE DP015-HL-OFF       TO MR-SELECTH (PV-SUB)                   
                                          MR-RQSTD-TICKETSH (PV-SUB)            
                                          MR-LINE-COUNTH (PV-SUB)               
307800         MOVE SPACE              TO MR-SELECTI (PV-SUB)                   
307900                                    MR-ACTION (PV-SUB)                    
308000                                    MR-PO-NUMBER (PV-SUB)                 
308100                                    MR-RQST-TYPE (PV-SUB)               86
308200                                    MR-RQST-STAT (PV-SUB)               86
308300                                    MR-VENDOR-NAME (PV-SUB)             86
308400                                    MR-REQUEST-DATE (PV-SUB)            86
308500                                    MR-VENDOR-DUE-DATE (PV-SUB)         86
308600                                    MR-PRINT-DUE-DATE (PV-SUB)          86
308700                                    MR-PRIORITY (PV-SUB)                86
308800                                    MR-LINE-COUNT (PV-SUB)              86
308900                                    MR-RQSTD-TICKETS-X (PV-SUB)         86
309000                                                                          
309100     ELSE                                                                 
309200         IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
309300                                                   > SPACE                
309400             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
309500                                 TO MR-SELECTI (PV-SUB)                   
309600         ELSE                                                             
309700             MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
309800                                 TO MR-SELECTI (PV-SUB)                   
309900         END-IF                                                           
310000*                                                                         
310100         IF  DP030-SET-CURSOR-TRAILER                                     
310200             IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)        
310300                 > SPACE)                                                 
310400               OR (PV-SUB = 1))                                           
310500                SET DP030-SET-CURSOR-APPL-1 TO TRUE                       
310600                MOVE -1          TO MR-SELECTL (PV-SUB)                   
310700             END-IF                                                       
310800         END-IF                                                           
310900*                                                                         
311000         IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
                   IF ASC-RQST-STAT(ASC-BLK-SUB ASC-ITEM-SUB)                   
                      EQUAL TO 'R' OR 'H'                                       
021300                 MOVE DP015-YELLOW                                        
021300                                   TO MR-PO-NUMBERC (PV-SUB)              
021300                                      MR-RQST-TYPEC (PV-SUB)              
021300                                      MR-RQST-STATC (PV-SUB)              
021300                                      MR-RQSTD-TICKETSC (PV-SUB)          
021300                                      MR-VENDOR-NAMEC (PV-SUB)            
021300                                      MR-REQUEST-DATEC (PV-SUB)           
021300                                      MR-VENDOR-DUE-DATEC (PV-SUB)        
021300                                      MR-PRINT-DUE-DATEC (PV-SUB)         
021300                                      MR-PRIORITYC (PV-SUB)               
021300                                      MR-LINE-COUNTC (PV-SUB)             
                   ELSE                                                         
021300                 MOVE DP015-BLUE  TO MR-PO-NUMBERC (PV-SUB)               
021300                                      MR-RQST-TYPEC (PV-SUB)              
021300                                      MR-RQST-STATC (PV-SUB)              
021300                                      MR-RQSTD-TICKETSC (PV-SUB)          
021300                                      MR-VENDOR-NAMEC (PV-SUB)            
021300                                      MR-REQUEST-DATEC (PV-SUB)           
021300                                      MR-VENDOR-DUE-DATEC (PV-SUB)        
021300                                      MR-PRINT-DUE-DATEC (PV-SUB)         
021300                                      MR-PRIORITYC (PV-SUB)               
021300                                      MR-LINE-COUNTC (PV-SUB)             
                   END-IF                                                       
                                                                                
311100             MOVE ASC-ACTION (ASC-BLK-SUB ASC-ITEM-SUB)                   
311200                  TO MR-ACTION (PV-SUB)                                   
311100             MOVE ASC-PO-NUMBER (ASC-BLK-SUB ASC-ITEM-SUB)                
311200                  TO MR-PO-NUMBER-N (PV-SUB)                              
311300              MOVE ASC-RQST-TYPE (ASC-BLK-SUB ASC-ITEM-SUB)               
311400                  TO  MR-RQST-TYPE (PV-SUB)                               
311500              MOVE ASC-RQST-STAT (ASC-BLK-SUB ASC-ITEM-SUB)               
311600                  TO  MR-RQST-STAT (PV-SUB)                               
311700              MOVE ASC-RQSTD-TICKETS (ASC-BLK-SUB ASC-ITEM-SUB)           
311800                  TO  MR-RQSTD-TICKETS (PV-SUB)                           
                                                                                
                    IF ASC-RQSTD-TICKETS (ASC-BLK-SUB ASC-ITEM-SUB)             
                        = +0                                                    
021300                  MOVE DP015-RED   TO MR-RQSTD-TICKETSC (PV-SUB)          
                    END-IF                                                      
                                                                                
311900              MOVE ASC-VENDOR-NAME (ASC-BLK-SUB ASC-ITEM-SUB)             
312000                  TO  MR-VENDOR-NAME (PV-SUB)                             
312100              MOVE ASC-RQST-DATE (ASC-BLK-SUB ASC-ITEM-SUB)               
312200                  TO  MR-REQUEST-DATE (PV-SUB)                            
312300              MOVE ASC-VENDOR-DUE-DATE (ASC-BLK-SUB ASC-ITEM-SUB)         
312400                  TO  MR-VENDOR-DUE-DATE (PV-SUB)                         
312500              MOVE ASC-PRINT-DUE-DATE (ASC-BLK-SUB ASC-ITEM-SUB)          
312600                  TO  MR-PRINT-DUE-DATE (PV-SUB)                          
312700              MOVE ASC-PRIORITY (ASC-BLK-SUB ASC-ITEM-SUB)                
312800                  TO  MR-PRIORITY (PV-SUB)                                
312900              MOVE ASC-LINE-COUNT-X (ASC-BLK-SUB ASC-ITEM-SUB)            
313000                  TO  MR-LINE-COUNT (PV-SUB)                              
                                                                                
312900              IF ASC-LINE-COUNT (ASC-BLK-SUB ASC-ITEM-SUB) = +0           
021300                  MOVE DP015-RED   TO MR-LINE-COUNTC (PV-SUB)             
                    END-IF                                                      
                                                                                
134200                                                                          
313200         END-IF                                                           
313300     END-IF.                                                              
313400     SUBTRACT +1 FROM ASC-ITEM-SUB                                        
313500     IF  ASC-ITEM-SUB < + 1                                               
313600         MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
313700         MOVE +1                 TO ASC-BLK-SUB                           
313800     END-IF.                                                              
313900******************************************************************        
314000** IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF   **        
314100** RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,      **        
314200** ISSUE A WARNING.                                             **        
314300******************************************************************        
314400 4430-CHECK-FOR-BEGIN-AND-END.                                            
314500     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
314600         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
314700*            -- NO END-OF-RANGE SPECFIED ---                              
314800             MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER                    
314900             SET DP020-MSG-WARNING TO TRUE                                
315000             PERFORM 4431-POSITION-AT-WARNING                             
315100         END-IF                                                           
315200     ELSE                                                                 
315300         IF  ASC-END-OF-SELECTED-RANGE > ZERO                             
315400*            -- NO BEGINNING-OF-RANGE SPECFIED ---                        
315500             MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER                    
315600             SET DP020-MSG-WARNING TO TRUE                                
315700             PERFORM 4431-POSITION-AT-WARNING                             
315800         END-IF                                                           
315900     END-IF.                                                              
316000******************************************************************        
316100** ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,  **        
316200** DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE   **        
316300** IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND        **        
316400** POSITION THE CURSOR THERE.                                   **        
316500******************************************************************        
316600 4431-POSITION-AT-WARNING.                                                
316700     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
316800                    - PV-TOP-ITEM + 1.                                    
316900     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
317000         CONTINUE                                                         
317100     ELSE                                                                 
317200         SET DP030-SET-CURSOR-APPL-1                                      
317300                                   TO TRUE                                
317400         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
317500         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
317600         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
317700     END-IF.                                                              
317800*                                                                         
317900     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
318000                    - PV-TOP-ITEM + 1.                                    
318100     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
318200         CONTINUE                                                         
318300     ELSE                                                                 
318400         SET DP030-SET-CURSOR-APPL-1                                      
318500                                   TO TRUE                                
318600         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
318700         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
318800         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
318900     END-IF.                                                              
319000 EJECT                                                                    
319100*----------------------------------------------------------------*        
319200* PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *        
319300* ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *        
319400* FOR THE SELECTED ITEMS QUEUE.                                  *        
319500*                                                                *        
319600* FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *        
319700* QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *        
319800* THE ARRAY IS FULL.                                             *        
319900*----------------------------------------------------------------*        
320000                                                                          
320100 5000-PREPARE-SEL-QUEUE.                                                  
320200                                                                          
320300     MOVE +1                   TO  ASC-BLK-SUB.                           
320400     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
320500     ADD +1                    TO  ASC-BLK-SUB.                           
320600     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
320700                                                                          
320800     MOVE ASC-SEL-QUEUE-NAME   TO  VL005-SEL-QUEUE-NAME.                  
320900     PERFORM 6010-DELETE-SEL-QUEUE.                                       
321000                                                                          
321100     SET ASC-UPDATE-TSQ        TO  TRUE.                                  
321200                                                                          
321300     SET VL006-IDX             TO  1.                                     
321400     MOVE ZERO                 TO VL005-NUMBER-OF-TS-ITEMS.               
321500     INITIALIZE VL006-SELECTED-ITEM-ARRAY.                                
321600                                                                          
321700     PERFORM 5200-FILL-SEL-QUEUE-RECORD                                   
321800             VARYING PV-ITEM                                              
321900             FROM 1 BY 1                                                  
322000             UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.             
322100                                                                          
322200     IF  VL006-IDX > 1                                                    
322300         PERFORM 5205-WRITE-SEL-QUEUE                                     
322400         ADD +1                TO VL005-NUMBER-OF-TS-ITEMS                
322500     END-IF.                                                              
322600 EJECT                                                                    
322700*----------------------------------------------------------------*        
322800*    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
322900*    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
323000*----------------------------------------------------------------*        
323100                                                                          
323200 5100-WRITE-TEMP-STORAGE.                                                 
323300     MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
323400                                   TO PV-LIST-ITEM-NUMBER.                
323500     COMPUTE PV-BLOCK-NUMBER =                                            
323600         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
323700                                                                          
323800     IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
323900     OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
324000         IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
324100             PERFORM 5105-REWRITE-ASC-LISTQ                               
324200         END-IF                                                           
324300     ELSE                                                                 
324400         COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
324500         ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
324600         PERFORM 5110-WRITE-ASC-LISTQ                                     
324700         ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
324800     END-IF.                                                              
324900                                                                          
325000     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
325100******************************************************************        
325200** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **        
325300** SPECIFIC COMMAREA.                                           **        
325400******************************************************************        
325500 5105-REWRITE-ASC-LISTQ.                                                  
325600     EXEC CICS                                                            
325700         WRITEQ TS                                                        
325800             QUEUE (ASC-LIST-QUEUE-NAME)                                  
325900             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
326000             ITEM  (PV-BLOCK-NUMBER)                                      
326100             REWRITE                                                      
326200             RESP  (PV-CICS-RESP)                                         
326300     END-EXEC.                                                            
326400                                                                          
326500     IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
326600         CONTINUE                                                         
326700     ELSE                                                                 
326800         SET DP013-CICS-ABEND            TO TRUE                          
326900         SET DP013-NO-ROLLBACK           TO TRUE                          
327000         MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH               
327100         MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
327200                                         TO DP013-MESSAGE-TEXT(1)         
327300         PERFORM DP013-0000-PROCESS-ABEND                                 
327400     END-IF.                                                              
327500                                                                          
327600******************************************************************        
327700** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **        
327800** SPECIFIC COMMAREA.                                           **        
327900******************************************************************        
328000 5110-WRITE-ASC-LISTQ.                                                    
328100     EXEC CICS                                                            
328200         WRITEQ TS                                                        
328300             QUEUE (ASC-LIST-QUEUE-NAME)                                  
328400             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
328500             ITEM  (PV-BLOCK-NUMBER)                                      
328600             RESP  (PV-CICS-RESP)                                         
328700     END-EXEC.                                                            
328800     IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
328900         SET DP013-CICS-ABEND      TO TRUE                                
329000         SET DP013-NO-ROLLBACK     TO TRUE                                
329100         MOVE '5110-WRITE-ASC-LISTQ'                                      
329200                                   TO DP013-PARAGRAPH                     
329300         MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
329400                                   TO DP013-MESSAGE-TEXT(1)               
329500         PERFORM DP013-0000-PROCESS-ABEND                                 
329600     END-IF.                                                              
329700 EJECT                                                                    
329800*----------------------------------------------------------------*        
329900* READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *        
330000* TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *        
330100*----------------------------------------------------------------*        
330200                                                                          
330300 5200-FILL-SEL-QUEUE-RECORD.                                              
330400                                                                          
330500     PERFORM 6112-READ-LIST-QUEUE.                                        
330600                                                                          
330700     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
330800         PERFORM                                                          
330900         VARYING LQW-IDX                                                  
331000         FROM 1 BY 1                                                      
331100         UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL                    
331200           OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO                  
331300                                                                          
331400             IF  LQW-SELECT (LQW-IDX)                                     
331500                 MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)                      
331600                                   TO VL006-ITEM (VL006-IDX)              
331700                 MOVE SPACE        TO VL006-ACTION-SW (VL006-IDX)         
331800                 MOVE LQW-RQST-NBR (LQW-IDX)                              
331900                                   TO VL006-PO-REQ-NBR                    
332000                                        (VL006-IDX)                       
332001                 MOVE LQW-PO-NUMBER (LQW-IDX)                             
332002                                    TO VL006-PO-NUMBER                  11
332003                                        (VL006-IDX)                       
332004                 MOVE LQW-VENDOR-NAME (LQW-IDX)                           
332005                                    TO VL006-VENDOR-NAME                11
332006                                        (VL006-IDX)                       
332007                 MOVE LQW-ENT-ID (LQW-IDX)                                
332008                                    TO VL006-ENT-ID                     11
332009                                        (VL006-IDX)                       
332010                 MOVE LQW-DEPT-NBR (LQW-IDX)                              
332020                                    TO VL006-DEPT-NBR                   11
332030                                        (VL006-IDX)                       
332040                 SET VL006-IDX UP BY 1                                    
332050                 IF  VL006-IDX > VL006-MAX-ITEMS                          
332060                     PERFORM 5205-WRITE-SEL-QUEUE                         
332070                     ADD +1        TO VL005-NUMBER-OF-TS-ITEMS            
332080                     INITIALIZE VL006-SELECTED-ITEM-ARRAY                 
332090                     SET VL006-IDX TO  1                                  
332100                 END-IF                                                   
332200             END-IF                                                       
332300         END-PERFORM                                                      
332400     END-IF.                                                              
332500                                                                          
332600*----------------------------------------------------------------*        
332700*    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *        
332800*----------------------------------------------------------------*        
332900                                                                          
333000 5205-WRITE-SEL-QUEUE.                                                    
333100                                                                          
333200     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
333300                                   TO VL006-SELECTED-ITEMS.               
333400                                                                          
333500     EXEC CICS                                                            
333600          WRITEQ TS                                                       
333700              QUEUE (ASC-SEL-QUEUE-NAME)                                  
333800              FROM  (VL006-LINE-SEL-REC)                                  
333900              ITEM  (PV-SEL-QUEUE-ITEM)                                   
334000              RESP  (PV-CICS-RESP)                                        
334100     END-EXEC.                                                            
334200                                                                          
334300     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
334400         CONTINUE                                                         
334500     ELSE                                                                 
334600         SET DP013-CICS-ABEND      TO TRUE                                
334700         SET DP013-NO-ROLLBACK     TO TRUE                                
334800         MOVE '5205-WRITE-SEL-QUEUE'                                      
334900                                   TO DP013-PARAGRAPH                     
335000         MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'                  
335100                                   TO DP013-MESSAGE-TEXT(1)               
335200         PERFORM DP013-0000-PROCESS-ABEND                                 
335300     END-IF.                                                              
335400                                                                          
335500 EJECT                                                                    
335600******************************************************************        
335700** WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, **        
335800** SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT **        
335900** THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO    **        
336000** THE USER.                                                    **        
336100******************************************************************        
336200 6000-REFRESH-PANEL-FROM-TEMP.                                            
336300     IF  ASC-UPDATE-TSQ                                                   
336400         SET ASC-NO-TSQ-UPDATE TO TRUE                                    
336500         MOVE ZERO                 TO PV-SAVE-ITEM                        
336600                                      PV-SEL-QUEUE-ITEM                   
336700         MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                
336800                                   TO PV-HOLD-SELECTED-ITEMS              
336900         MOVE +1                   TO PV-ITEMS-PROCESSED                  
337000         PERFORM 6100-UPDATE-TSQ                                          
337100             UNTIL PV-ITEMS-PROCESSED >                                   
337200                                     PV-HOLD-SELECTED-ITEMS               
337300         PERFORM 6010-DELETE-SEL-QUEUE                                    
337400         INITIALIZE ASC-BLOCK-ENTRIES (1)                                 
337500                    ASC-BLOCK-ENTRIES (2)                                 
337600     END-IF.                                                              
337700                                                                          
337800*----------------------------------------------------------------*        
337900*    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *        
338000*----------------------------------------------------------------*        
338100 6010-DELETE-SEL-QUEUE.                                                   
338200                                                                          
338300     EXEC CICS                                                            
338400         DELETEQ TS                                                       
338500             QUEUE (ASC-SEL-QUEUE-NAME)                                   
338600             RESP  (PV-CICS-RESP)                                         
338700     END-EXEC.                                                            
338800*                                                                         
338900     IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
339000        (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
339100         CONTINUE                                                         
339200     ELSE                                                                 
339300         SET DP013-CICS-ABEND TO TRUE                                     
339400         SET DP013-NO-ROLLBACK TO TRUE                                    
339500         MOVE '6010-DELETE-SEL-QUEUE'                                     
339600                        TO DP013-PARAGRAPH                                
339700         MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
339800                        TO DP013-MESSAGE-TEXT (1)                         
339900         PERFORM DP013-0000-PROCESS-ABEND                                 
340000     END-IF.                                                              
340100                                                                          
340200*----------------------------------------------------------------*        
340300*    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
340400*    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
340500*    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
340600*    THE TEMP QUEUE.                                             *        
340700*----------------------------------------------------------------*        
340800                                                                          
340900 6100-UPDATE-TSQ.                                                         
341000                                                                          
341100     ADD 1                     TO  PV-SEL-QUEUE-ITEM.                     
341200     PERFORM 6105-READ-SEL-QUEUE.                                         
341300     PERFORM                                                              
341400         VARYING VL006-IDX                                                
341500         FROM 1 BY 1                                                      
341600         UNTIL ((PV-ITEMS-PROCESSED >                                     
341700                              PV-HOLD-SELECTED-ITEMS)                     
341800         OR (VL006-IDX GREATER THAN VL006-MAX-ITEMS))                     
341900         IF  VL006-ACTION-SW (VL006-IDX) > SPACE                          
342000             PERFORM 6110-PROCESS-ITEMS                                   
342100         END-IF                                                           
342200         ADD +1                TO PV-ITEMS-PROCESSED                      
342300     END-PERFORM.                                                         
342400     PERFORM 6111-WRITE-LIST-QUEUE.                                       
342500*----------------------------------------------------------------*        
342600*    READ THE LIST QUEUE RECORD.                                 *        
342700*----------------------------------------------------------------*        
342800                                                                          
342900 6105-READ-SEL-QUEUE.                                                     
343000                                                                          
343100     EXEC CICS                                                            
343200          READQ TS                                                        
343300              QUEUE (ASC-SEL-QUEUE-NAME)                                  
343400              INTO  (VL006-LINE-SEL-REC)                                  
343500              ITEM  (PV-SEL-QUEUE-ITEM)                                   
343600              RESP  (PV-CICS-RESP)                                        
343700     END-EXEC.                                                            
343800*                                                                         
343900     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
344000         CONTINUE                                                         
344100     ELSE                                                                 
344200         SET DP013-CICS-ABEND      TO  TRUE                               
344300         SET DP013-NO-ROLLBACK     TO  TRUE                               
344400         MOVE '6105-READ-SEL-QUEUE'                                       
344500                                   TO  DP013-PARAGRAPH                    
344600         MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
344700                                   TO  DP013-MESSAGE-TEXT(1)              
344800         PERFORM DP013-0000-PROCESS-ABEND                                 
344900     END-IF.                                                              
345000 EJECT                                                                    
345100*----------------------------------------------------------------*        
345200*    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*        
345300*    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*        
345400*    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *        
345500*----------------------------------------------------------------*        
345600                                                                          
345700 6110-PROCESS-ITEMS.                                                      
345800                                                                          
345900     COMPUTE PV-ITEM = 1 + ((VL006-ITEM (VL006-IDX) - 1)                  
346000                     / PC-ITEMS-PER-PANEL).                               
346100                                                                          
346200     IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                                   
346300         IF  PV-SAVE-ITEM NOT EQUAL ZERO                                  
346400             PERFORM 6111-WRITE-LIST-QUEUE                                
346500         END-IF                                                           
346600         MOVE PV-ITEM          TO PV-SAVE-ITEM                            
346700                                                                          
346800         PERFORM 6112-READ-LIST-QUEUE                                     
346900         IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                       
347000             CONTINUE                                                     
347100         END-IF                                                           
347200     END-IF.                                                              
347300                                                                          
347400     COMPUTE PV-SUB = (VL006-ITEM (VL006-IDX)                             
347500                    - LQW-LIST-ITEM-NUMBER (1)) + 1.                      
347600     SET LQW-DESELECT (PV-SUB) TO    TRUE                                 
347700     MOVE LQW-SELECTED-SW (PV-SUB)                                        
347800                               TO LQW-TEMP-SELECTED-SW (PV-SUB).          
347900     EVALUATE TRUE                                                        
348000         WHEN VL006-INQ (VL006-IDX)                                       
348100              MOVE PC-INQ      TO LQW-ACTION (PV-SUB)                     
348200                                                                          
348300         WHEN VL006-UPD (VL006-IDX)                                       
348400              MOVE PC-UPD      TO LQW-ACTION (PV-SUB)                     
348500                                                                          
348600         WHEN VL006-DEL (VL006-IDX)                                       
348700              MOVE PC-DEL      TO LQW-ACTION (PV-SUB)                     
348800                                                                          
348900         WHEN VL006-ERR (VL006-IDX)                                       
349000              MOVE PC-ERR      TO LQW-ACTION (PV-SUB)                     
349100     END-EVALUATE.                                                        
349200                                                                          
349300     SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                        
349400                                                                          
349500*----------------------------------------------------------------*        
349600*    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *        
349700*----------------------------------------------------------------*        
349800                                                                          
349900 6111-WRITE-LIST-QUEUE.                                                   
350000                                                                          
350100     EXEC CICS                                                            
350200         WRITEQ TS                                                        
350300             QUEUE (ASC-LIST-QUEUE-NAME)                                  
350400             FROM  (LQW-LIST-QUEUE-WORK-AREA)                             
350500             ITEM  (PV-SAVE-ITEM)                                         
350600             REWRITE                                                      
350700             RESP  (PV-CICS-RESP)                                         
350800     END-EXEC.                                                            
350900*                                                                         
351000     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
351100         CONTINUE                                                         
351200     ELSE                                                                 
351300         SET DP013-CICS-ABEND  TO  TRUE                                   
351400         SET DP013-NO-ROLLBACK TO  TRUE                                   
351500         MOVE '6111-WRITE-LIST-QUEUE'                                     
351600                               TO  DP013-PARAGRAPH                        
351700         MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                          
351800                               TO  DP013-MESSAGE-TEXT(1)                  
351900         PERFORM DP013-0000-PROCESS-ABEND                                 
352000     END-IF.                                                              
352100                                                                          
352200 EJECT                                                                    
352300*----------------------------------------------------------------*        
352400*    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
352500*----------------------------------------------------------------*        
352600                                                                          
352700 6112-READ-LIST-QUEUE.                                                    
352800                                                                          
352900     EXEC CICS                                                            
353000         READQ TS                                                         
353100             QUEUE (ASC-LIST-QUEUE-NAME)                                  
353200             INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
353300             ITEM  (PV-ITEM)                                              
353400             RESP  (PV-CICS-RESP)                                         
353500     END-EXEC.                                                            
353600*                                                                         
353700     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
353800         CONTINUE                                                         
353900     ELSE                                                                 
354000         SET DP013-CICS-ABEND        TO TRUE                              
354100         SET DP013-NO-ROLLBACK       TO TRUE                              
354200         MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH                   
354300         MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
354400                                     TO DP013-MESSAGE-TEXT(1)             
354500         PERFORM DP013-0000-PROCESS-ABEND                                 
354600     END-IF.                                                              
354700                                                                          
354800 EJECT                                                                    
354900*----------------------------------------------------------------*        
355000* DELETE THE SELECTED ITEMS.                                              
355100*                                                                *        
355200*----------------------------------------------------------------*        
355300                                                                          
355400 7000-DELETE-SELECTED-REQUESTS.                                           
355500                                                                          
355600     MOVE +1                   TO  ASC-BLK-SUB.                           
355700     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
           MOVE PV-BLOCK-NUMBER      TO PV-SAVE-BLOCK-NUMBER.                   
                                                                                
355800     ADD +1                    TO  ASC-BLK-SUB.                           
355900     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
356000                                                                          
356100     PERFORM 7100-DELETE-SELECTED-ITEMS                                   
356200             VARYING PV-ITEM                                              
356300             FROM 1 BY 1                                                  
356400             UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.             
                                                                                
299000     MOVE PV-SAVE-BLOCK-NUMBER TO PV-BLOCK-NUMBER.                        
299600     MOVE +1                   TO ASC-BLK-SUB.                            
299700     PERFORM 4410-READ-TEMP-STORAGE.                                      
                                                                                
299800     ADD +1                    TO ASC-BLK-SUB                             
299900                                  PV-BLOCK-NUMBER.                        
300000     PERFORM 4410-READ-TEMP-STORAGE.                                      
356500                                                                          
356600*----------------------------------------------------------------*        
356700* READ A LIST QUEUE RECORD AND DELETE SELECTED OBJECTS FROM DB2  *        
356800*----------------------------------------------------------------*        
356900 7100-DELETE-SELECTED-ITEMS.                                              
357000                                                                          
357100     PERFORM 6112-READ-LIST-QUEUE.                                        
357200                                                                          
357300     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
357400         PERFORM                                                          
357500         VARYING LQW-IDX                                                  
357600         FROM 1 BY 1                                                      
357700         UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL                    
357800           OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO                  
357900                                                                          
358000             IF  LQW-SELECT (LQW-IDX)                                     
                       IF LQW-RQST-STAT (LQW-IDX) = 'I'                         
                           PERFORM 7300-CANCEL-REQUEST                          
                           SET LQW-DESELECT (LQW-IDX)                           
                                         TO TRUE                                
347700                     MOVE LQW-SELECTED-SW (LQW-IDX)                       
347800                               TO LQW-TEMP-SELECTED-SW (LQW-IDX)          
358200                     MOVE PC-CAN   TO LQW-ACTION (LQW-IDX)                
467800                     MOVE PC-TSYMSG-00522                                 
                                         TO DP020-MSG-NUMBER                    
467900                     SET DP020-MSG-INFORMATIONAL                          
                                         TO TRUE                                
                       ELSE                                                     
358100                     PERFORM 7200-DELETE-REQUEST                          
358200                     MOVE PC-DEL   TO LQW-ACTION (LQW-IDX)                
                           SET LQW-DESELECT (LQW-IDX)                           
                                         TO TRUE                                
347700                     MOVE LQW-SELECTED-SW (LQW-IDX)                       
347800                               TO LQW-TEMP-SELECTED-SW (LQW-IDX)          
467800                     MOVE PC-TSYMSG-00115                                 
                                         TO DP020-MSG-NUMBER                    
467900                     SET DP020-MSG-INFORMATIONAL                          
                                         TO TRUE                                
                       END-IF                                                   
358300             END-IF                                                       
358400         END-PERFORM                                                      
358500     END-IF.                                                              
358600                                                                          
358700     MOVE PV-ITEM TO PV-SAVE-ITEM.                                        
358800                                                                          
358900     PERFORM 6111-WRITE-LIST-QUEUE.                                       
359000                                                                          
466600                                                                          
466700*----------------------------------------------------------------*        
466800*  DELETE ALL ASSOCIATED INFORMATION FOR THIS REQUEST            *        
466900*----------------------------------------------------------------*        
467000                                                                          
467100 7200-DELETE-REQUEST.                                                     
467200                                                                          
467300     MOVE LQW-RQST-NBR (LQW-IDX)                                          
467400                               TO DK-PO-REQ-NBR.                          
467401                                                                          
467403     PERFORM 7220-DELETE-ALL-REQUEST-LINES.                               
467404                                                                          
467405     PERFORM 7230-DELETE-REQUEST-COMMENTS.                                
467404                                                                          
467406     PERFORM 7240-DELETE-REQUEST-REASONS.                                 
467404                                                                          
467402     PERFORM 7210-DELETE-REQUEST-HEADER.                                  
469000                                                                          
469100*----------------------------------------------------------------*        
469200*  DELETE A PRETICKET REQUEST HEADER RECORD FROM TPTKREQ         *        
469300*----------------------------------------------------------------*        
469400                                                                          
469500 7210-DELETE-REQUEST-HEADER.                                              
469600                                                                          
469700     PERFORM                                                              
469800         WITH TEST AFTER                                                  
469900         VARYING PV-RETRY-COUNTER                                         
470000         FROM 1 BY 1                                                      
470100         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
470200             OR (SQLCODE = +0)                                            
470210             OR (SQLCODE = +100))                                         
470300                                                                          
470400         EXEC SQL                                                         
470500              DELETE FROM TPTKREQ                                         
470600              WHERE                                                       
470700                  PO_REQ_NBR      = :DK-PO-REQ-NBR                        
470800         END-EXEC                                                         
470900                                                                          
471000         EVALUATE TRUE                                                    
471100             WHEN SQLCODE = +0                                            
471110             WHEN SQLCODE = +100                                          
471200                 CONTINUE                                                 
471300                                                                          
471400             WHEN SQLWARN0 NOT = SPACE                                    
471500             WHEN SQLCODE < +0                                            
471600             WHEN SQLCODE > +0                                            
471700                 MOVE '7210-DELETE-HEADER'                                
471800                                   TO DP013-PARAGRAPH                     
471900                 MOVE 'DELETE PRETICKET REQUEST HEADER TABLE'             
472000                                   TO DP013-MESSAGE-TEXT (1)              
472100                 MOVE DK-PO-REQ-NBR                                       
472200                                   TO DP013-MESSAGE-TEXT (2)              
472300                 MOVE SQLCA        TO DP013-SQLCA                         
472400                 MOVE 'TPTKREQ '   TO DP013-DB2-TABLE-NAME (1)            
472500                 SET DP013-DB2-ABEND                                      
472600                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
472700                 PERFORM DP013-0000-PROCESS-ABEND                         
472800                                                                          
472900         END-EVALUATE                                                     
473000     END-PERFORM.                                                         
473100                                                                          
473200*                                                                         
473300     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
473400         MOVE '7210-DELETE-HEADER' TO DP013-PARAGRAPH                     
473500         MOVE 'MAXIMUM RETRIES EXCEEDED DELETE HEADER TABLE'              
473600                                   TO DP013-MESSAGE-TEXT (1)              
473700         MOVE DK-PO-REQ-NBR        TO DP013-MESSAGE-TEXT (2)              
473800         MOVE SQLCA                TO DP013-SQLCA                         
473900         MOVE 'TPTKREQ '           TO DP013-DB2-TABLE-NAME (1)            
474000         SET DP013-DB2-ABEND                                              
474100             DP013-XCTL-DISPLAY-RESTART                                   
474200                                   TO TRUE                                
474300         PERFORM DP013-0000-PROCESS-ABEND                                 
474400     END-IF.                                                              
474500                                                                          
474600                                                                          
474700*----------------------------------------------------------------*        
474800*  DELETE PRETICKET REQUEST LINE RECORD DB2 TABLE TPTKLNE   *             
474900*----------------------------------------------------------------*        
475000                                                                          
475100 7220-DELETE-ALL-REQUEST-LINES.                                           
475200                                                                          
475300     PERFORM                                                              
475400         WITH TEST AFTER                                                  
475500         VARYING PV-RETRY-COUNTER                                         
475600         FROM 1 BY 1                                                      
475700         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
475800             OR (SQLCODE = +0))                                           
475810             OR (SQLCODE = +100)                                          
475900                                                                          
476000         EXEC SQL                                                         
476100              DELETE FROM TPTKLNE                                         
476200                  WHERE                                                   
476300                      PO_REQ_NBR     = :DK-PO-REQ-NBR                     
476400         END-EXEC                                                         
476500                                                                          
476600         EVALUATE TRUE                                                    
476700             WHEN SQLCODE = +0                                            
476710             WHEN SQLCODE = +100                                          
476800                 CONTINUE                                                 
476900                                                                          
477000             WHEN SQLWARN0 NOT = SPACE                                    
477100             WHEN SQLCODE < +0                                            
477200             WHEN SQLCODE > +0                                            
477300                 MOVE '7220-DELETE-ALL-REQUEST-LINES'                     
477400                                   TO DP013-PARAGRAPH                     
477500                 MOVE 'DELETE PRETICKET REQUEST LINE TABLE'               
477600                                   TO DP013-MESSAGE-TEXT (1)              
477700                 MOVE DK-PO-REQ-NBR                                       
477800                                   TO DP013-MESSAGE-TEXT (2)              
477900                 MOVE SQLCA        TO DP013-SQLCA                         
478000                 MOVE 'TPTKLNE '   TO DP013-DB2-TABLE-NAME (1)            
478100                 SET DP013-DB2-ABEND                                      
478200                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
478300                 PERFORM DP013-0000-PROCESS-ABEND                         
478400                                                                          
478500         END-EVALUATE                                                     
478600     END-PERFORM.                                                         
478700                                                                          
478800*                                                                         
478900     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
479000         MOVE '7220-DELETE-ALL-REQUEST-LINES'                             
479100                                   TO DP013-PARAGRAPH                     
479200         MOVE 'MAXIMUM RETRIES EXCEEDED DELETING DETAIL TABLE'            
479300                                   TO DP013-MESSAGE-TEXT (1)              
479400         MOVE DK-PO-REQ-NBR        TO DP013-MESSAGE-TEXT (2)              
479500         MOVE SQLCA                TO DP013-SQLCA                         
479600         MOVE 'TPTKLNE '           TO DP013-DB2-TABLE-NAME (1)            
479700         SET DP013-DB2-ABEND                                              
479800             DP013-XCTL-DISPLAY-RESTART                                   
479900                                   TO TRUE                                
480000         PERFORM DP013-0000-PROCESS-ABEND                                 
480100     END-IF.                                                              
480300                                                                          
480301                                                                          
480302*----------------------------------------------------------------*        
480303*  DELETE THE COMMENTS ASSOCIATED TO A REQUEST (TPTKCMT)         *        
480304*----------------------------------------------------------------*        
480305 7230-DELETE-REQUEST-COMMENTS.                                            
101170                                                                          
      * FIRST SELECT THE REQUEST COMMENT ID                                     
101180     PERFORM                                                              
101190         WITH TEST AFTER                                                  
101200         VARYING PV-RETRY-COUNTER                                         
101210         FROM 1 BY 1                                                      
101220         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
101230             OR (SQLCODE = +0))                                           
101250                                                                          
101260         EXEC SQL                                                         
101270              SELECT CMNT_ID                                              
                    INTO   :PV-REQUEST-CMNT-ID                                  
                    FROM   TPTKREQ                                              
101280              WHERE                                                       
101290                  PO_REQ_NBR      = :DK-PO-REQ-NBR                        
101300         END-EXEC                                                         
101310                                                                          
101320         EVALUATE TRUE                                                    
101330             WHEN SQLCODE = +0                                            
101350                 CONTINUE                                                 
101360                                                                          
101370             WHEN SQLWARN0 NOT = SPACE                                    
101380             WHEN SQLCODE < +0                                            
101390             WHEN SQLCODE > +0                                            
515905                 MOVE '7230-DELETE-REQUEST-COMMENTS'              51590500
515906                                   TO DP013-PARAGRAPH             51590600
515907                 MOVE 'DELETE PRETICKET REQUEST COMMENT TABLE'    51590700
515908                                   TO DP013-MESSAGE-TEXT (1)      51590800
101440                 MOVE DK-PO-REQ-NBR                                       
101450                                   TO DP013-MESSAGE-TEXT (2)              
101460                 MOVE SQLCA        TO DP013-SQLCA                         
101470                 MOVE 'TPTKCMT '   TO DP013-DB2-TABLE-NAME (1)            
101480                 SET DP013-DB2-ABEND                                      
101490                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
101500                 PERFORM DP013-0000-PROCESS-ABEND                         
101510                                                                          
101520         END-EVALUATE                                                     
101530     END-PERFORM.                                                         
101540                                                                          
101550                                                                          
101560     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
515922         MOVE '7230-DELETE-REQUEST-COMMENTS'                            00
515923                                   TO DP013-PARAGRAPH                   00
515924         MOVE 'MAXIMUM RETRIES EXCEEDED DELETE COMMENT TABLE'           00
515925                                   TO DP013-MESSAGE-TEXT (1)            00
101610         MOVE DK-PO-REQ-NBR                                               
101611                                   TO DP013-MESSAGE-TEXT (2)              
101620         MOVE SQLCA                TO DP013-SQLCA                         
101630         MOVE 'TPTKCMT '           TO DP013-DB2-TABLE-NAME (1)            
101640         SET DP013-DB2-ABEND                                              
101650             DP013-XCTL-DISPLAY-RESTART                                   
101660                                   TO TRUE                                
101670         PERFORM DP013-0000-PROCESS-ABEND                                 
101680     END-IF.                                                              
                                                                                
                                                                                
      * THEN SELECT THE SHIPMENT COMMENT ID                                     
101180     PERFORM                                                              
101190         WITH TEST AFTER                                                  
101200         VARYING PV-RETRY-COUNTER                                         
101210         FROM 1 BY 1                                                      
101220         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
101230             OR (SQLCODE = +0)                                            
101230             OR (SQLCODE = +100))                                         
101250                                                                          
101260         EXEC SQL                                                         
101270              SELECT A.CMNT_ID                                            
                    INTO   :PV-SHIPT-CMNT-ID                                    
                    FROM   TPTKSHP A                                            
                       ,   TREQSHP B                                            
101280              WHERE                                                       
101290                  B.PO_REQ_NBR  = :DK-PO-REQ-NBR                          
101290              AND B.SHIPT_NBR   = A.SHIPT_NBR                             
101300         END-EXEC                                                         
101310                                                                          
101320         EVALUATE TRUE                                                    
101330             WHEN SQLCODE = +0                                            
101350                 CONTINUE                                                 
101330             WHEN SQLCODE = +100                                          
101350                 MOVE +0 TO PV-SHIPT-CMNT-ID                              
101360                                                                          
101370             WHEN SQLWARN0 NOT = SPACE                                    
101380             WHEN SQLCODE < +0                                            
101390             WHEN SQLCODE > +0                                            
515905                 MOVE '7230-DELETE-REQUEST-COMMENTS'              51590500
515906                                   TO DP013-PARAGRAPH             51590600
515907                 MOVE 'DELETE PRETICKET REQUEST COMMENT TABLE'    51590700
515908                                   TO DP013-MESSAGE-TEXT (1)      51590800
101440                 MOVE DK-PO-REQ-NBR                                       
101450                                   TO DP013-MESSAGE-TEXT (2)              
101460                 MOVE SQLCA        TO DP013-SQLCA                         
101470                 MOVE 'TPTKCMT '   TO DP013-DB2-TABLE-NAME (1)            
101480                 SET DP013-DB2-ABEND                                      
101490                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
101500                 PERFORM DP013-0000-PROCESS-ABEND                         
101510                                                                          
101520         END-EVALUATE                                                     
101530     END-PERFORM.                                                         
101540                                                                          
101550                                                                          
101560     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
515922         MOVE '7230-DELETE-REQUEST-COMMENTS'                            00
515923                                   TO DP013-PARAGRAPH                   00
515924         MOVE 'MAXIMUM RETRIES EXCEEDED DELETE COMMENT TABLE'           00
515925                                   TO DP013-MESSAGE-TEXT (1)            00
101610         MOVE DK-PO-REQ-NBR                                               
101611                                   TO DP013-MESSAGE-TEXT (2)              
101620         MOVE SQLCA                TO DP013-SQLCA                         
101630         MOVE 'TPTKCMT '           TO DP013-DB2-TABLE-NAME (1)            
101640         SET DP013-DB2-ABEND                                              
101650             DP013-XCTL-DISPLAY-RESTART                                   
101660                                   TO TRUE                                
101670         PERFORM DP013-0000-PROCESS-ABEND                                 
101680     END-IF.                                                              
                                                                                
                                                                                
      * FINALLY, DELETE ALL THE COMMENTS ASSOCIATED TO THE REQUEST              
           IF PV-REQUEST-CMNT-ID    > 0                                         
           OR PV-SHIPT-CMNT-ID      > 0                                         
101180         PERFORM                                                          
101190             WITH TEST AFTER                                              
101200             VARYING PV-RETRY-COUNTER                                     
101210             FROM 1 BY 1                                                  
101220             UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                   
101230                 OR (SQLCODE = +0)                                        
101240                 OR (SQLCODE = +100))                                     
101250                                                                          
101260             EXEC SQL                                                     
101270                  DELETE FROM TPTKCMT                                     
101280                  WHERE                                                   
101290                      CMNT_ID  IN (:PV-REQUEST-CMNT-ID                    
                                        ,:PV-SHIPT-CMNT-ID )                    
101300             END-EXEC                                                     
101310                                                                          
101320             EVALUATE TRUE                                                
101330                 WHEN SQLCODE = +0                                        
101340                 WHEN SQLCODE = +100                                      
101350                     CONTINUE                                             
101360                                                                          
101370                 WHEN SQLWARN0 NOT = SPACE                                
101380                 WHEN SQLCODE < +0                                        
101390                 WHEN SQLCODE > +0                                        
515905                     MOVE '7230-DELETE-REQUEST-COMMENTS'          51590500
515906                                     TO DP013-PARAGRAPH           51590600
515907                     MOVE 'DELETE PRETICKET REQUEST COMMENT TABLE'51590700
515908                                     TO DP013-MESSAGE-TEXT (1)    51590800
101440                     MOVE PV-REQUEST-CMNT-ID                              
101450                                     TO DP013-MESSAGE-TEXT (2)            
101440                     MOVE PV-SHIPT-CMNT-ID                                
101450                                     TO DP013-MESSAGE-TEXT (3)            
101460                     MOVE SQLCA      TO DP013-SQLCA                       
101470                     MOVE 'TPTKCMT ' TO DP013-DB2-TABLE-NAME (1)          
101480                     SET DP013-DB2-ABEND                                  
101490                         DP013-XCTL-DISPLAY-RESTART TO TRUE               
101500                     PERFORM DP013-0000-PROCESS-ABEND                     
101510                                                                          
101520             END-EVALUATE                                                 
101530         END-PERFORM                                                      
101540                                                                          
101550                                                                          
101560         IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                            
515922             MOVE '7230-DELETE-REQUEST-COMMENTS'                        00
515923                                     TO DP013-PARAGRAPH                 00
515924             MOVE 'MAXIMUM RETRIES EXCEEDED DELETE COMMENT TABLE'       00
515925                                     TO DP013-MESSAGE-TEXT (1)          00
                   MOVE PV-REQUEST-CMNT-ID TO DP013-MESSAGE-TEXT (2)            
101450             MOVE PV-SHIPT-CMNT-ID   TO DP013-MESSAGE-TEXT (3)            
101620             MOVE SQLCA              TO DP013-SQLCA                       
101630             MOVE 'TPTKCMT '         TO DP013-DB2-TABLE-NAME (1)          
101640             SET DP013-DB2-ABEND                                          
101650                 DP013-XCTL-DISPLAY-RESTART                               
101660                                       TO TRUE                            
101670             PERFORM DP013-0000-PROCESS-ABEND                             
101680         END-IF                                                           
101680     END-IF.                                                              
101690                                                                          
101700                                                                          
480359                                                                          
480360*----------------------------------------------------------------*        
480361*  DELETE THE REASONS ASSOCIATED TO A REQUEST (TPTKRSN)         *         
480362*----------------------------------------------------------------*        
480363 7240-DELETE-REQUEST-REASONS.                                             
480364                                                                          
480365     PERFORM                                                              
480366         WITH TEST AFTER                                                  
480367         VARYING PV-RETRY-COUNTER                                         
480368         FROM 1 BY 1                                                      
480369         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
480370             OR (SQLCODE = +0)                                            
480371             OR (SQLCODE = +100))                                         
480372                                                                          
480373         EXEC SQL                                                         
480374              DELETE FROM TPTKRSN                                         
480375              WHERE                                                       
480376                  PO_REQ_NBR      = :DK-PO-REQ-NBR                        
480377         END-EXEC                                                         
480378                                                                          
480379         EVALUATE TRUE                                                    
480380             WHEN SQLCODE = +0                                            
480381             WHEN SQLCODE = +100                                          
480382                 CONTINUE                                                 
480383                                                                          
480384             WHEN SQLWARN0 NOT = SPACE                                    
480385             WHEN SQLCODE < +0                                            
480386             WHEN SQLCODE > +0                                            
480387                 MOVE '7240-DELETE-REQUEST-REASONS'                       
480388                                   TO DP013-PARAGRAPH                     
480389                 MOVE 'DELETE PRETICKET REQUEST REASON TABLE'             
480390                                   TO DP013-MESSAGE-TEXT (1)              
480391                 MOVE DK-PO-REQ-NBR                                       
480392                                   TO DP013-MESSAGE-TEXT (2)              
480393                 MOVE SQLCA        TO DP013-SQLCA                         
480394                 MOVE 'TPTKRSN '   TO DP013-DB2-TABLE-NAME (1)            
480395                 SET DP013-DB2-ABEND                                      
480396                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
480397                 PERFORM DP013-0000-PROCESS-ABEND                         
480398                                                                          
480399         END-EVALUATE                                                     
480400     END-PERFORM.                                                         
480401                                                                          
480402*                                                                         
480403     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
480404         MOVE '7230-DELETE-REQUEST-REASONS'                               
480405                                   TO DP013-PARAGRAPH                     
480406         MOVE 'MAXIMUM RETRIES EXCEEDED DELETE REASON TABLE'              
480407                                   TO DP013-MESSAGE-TEXT (1)              
480408         MOVE DK-PO-REQ-NBR        TO DP013-MESSAGE-TEXT (2)              
480409         MOVE SQLCA                TO DP013-SQLCA                         
480410         MOVE 'TPTKRSN '           TO DP013-DB2-TABLE-NAME (1)            
480411         SET DP013-DB2-ABEND                                              
480412             DP013-XCTL-DISPLAY-RESTART                                   
480413                                   TO TRUE                                
480414         PERFORM DP013-0000-PROCESS-ABEND                                 
480415     END-IF.                                                              
480416                                                                          
469000                                                                          
469100*----------------------------------------------------------------*        
469200*  DELETE A PRETICKET REQUEST HEADER RECORD FROM TPTKREQ         *        
469300*----------------------------------------------------------------*        
469400                                                                          
469500 7300-CANCEL-REQUEST.                                                     
469600                                                                          
467300     MOVE LQW-RQST-NBR (LQW-IDX)                                          
467400                               TO DK-PO-REQ-NBR.                          
469700     PERFORM                                                              
469800         WITH TEST AFTER                                                  
469900         VARYING PV-RETRY-COUNTER                                         
470000         FROM 1 BY 1                                                      
470100         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
470200             OR (SQLCODE = +0)                                            
470210             OR (SQLCODE = +100))                                         
470300                                                                          
470400         EXEC SQL                                                         
470500              UPDATE TPTKREQ                                              
                    SET REQ_STAT_CDE = 'C'                                      
470600              WHERE                                                       
470700                  PO_REQ_NBR      = :DK-PO-REQ-NBR                        
470800         END-EXEC                                                         
470900                                                                          
471000         EVALUATE TRUE                                                    
471100             WHEN SQLCODE = +0                                            
471110             WHEN SQLCODE = +100                                          
471200                 CONTINUE                                                 
471300                                                                          
471400             WHEN SQLWARN0 NOT = SPACE                                    
471500             WHEN SQLCODE < +0                                            
471600             WHEN SQLCODE > +0                                            
471700                 MOVE '7300-CANCEL-HEADER'                                
471800                                   TO DP013-PARAGRAPH                     
471900                 MOVE 'CANCEL-PRETICKET REQUEST HEADER'                   
472000                                   TO DP013-MESSAGE-TEXT (1)              
472100                 MOVE DK-PO-REQ-NBR                                       
472200                                   TO DP013-MESSAGE-TEXT (2)              
472300                 MOVE SQLCA        TO DP013-SQLCA                         
472400                 MOVE 'TPTKREQ '   TO DP013-DB2-TABLE-NAME (1)            
472500                 SET DP013-DB2-ABEND                                      
472600                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
472700                 PERFORM DP013-0000-PROCESS-ABEND                         
472800                                                                          
472900         END-EVALUATE                                                     
473000     END-PERFORM.                                                         
473100                                                                          
473200*                                                                         
473300     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
473400         MOVE '7300-CANCEL-HEADER' TO DP013-PARAGRAPH                     
473500         MOVE 'MAXIMUM RETRIES EXCEEDED CANCEL HEADER TABLE'              
473600                                   TO DP013-MESSAGE-TEXT (1)              
473700         MOVE DK-PO-REQ-NBR        TO DP013-MESSAGE-TEXT (2)              
473800         MOVE SQLCA                TO DP013-SQLCA                         
473900         MOVE 'TPTKREQ '           TO DP013-DB2-TABLE-NAME (1)            
474000         SET DP013-DB2-ABEND                                              
474100             DP013-XCTL-DISPLAY-RESTART                                   
474200                                   TO TRUE                                
474300         PERFORM DP013-0000-PROCESS-ABEND                                 
474400     END-IF.                                                              
474500                                                                          
474600                                                                          
480417*----------------------------------------------------------------*        
480418*    ABEND PROCESSOR MODULE                                      *        
480419*----------------------------------------------------------------*        
480420                                                                          
480430     COPY DPPD013.                                                        
