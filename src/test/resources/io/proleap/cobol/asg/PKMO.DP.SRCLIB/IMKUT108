       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    IMKUT108.                                         00020004
       AUTHOR.        PAUL KEBER.                                       00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  07-16-2008.                                       00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      *     IMKUT108 - WRITE TO THE RMS ITEM MESSAGE MQSERIES QUEUE     00080000
      ******************************************************************00090000
      *     THIS PROGRAM WRITES INPUT FILE RECORDS TO THE ITEM MESSAGE  00092000
      *     BRIDGE MQSERIES QUEUE (IM.VSM.ITEM.TO.RMS.Q).               00092000
      ******************************************************************00093000
      *  CHANGED:                                                      *00200094
      *                                                                *00210094
      *    WR/PITS#     DATE        DESCRIPTION                        *00220094
      *    --------   ----------   ---------------------------------   *00230094
      *    WR35095    07-16-2008    INITIAL IMPLEMENTATION.            *00250094
      ******************************************************************00272094
      *                                                                         
       ENVIRONMENT DIVISION.                                            00120000
       CONFIGURATION SECTION.                                           00130000
       INPUT-OUTPUT SECTION.                                            00150000
       FILE-CONTROL.                                                    00160000
           SELECT CONTROL-CARD-1   ASSIGN TO UT-S-INPUT01.                      
           SELECT BACKUP-FILE      ASSIGN TO UT-S-INPUT02.                      
      *                                                                 00190000
       DATA DIVISION.                                                   00200000
       FILE SECTION.                                                    00210000
       FD  CONTROL-CARD-1                                                       
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  CONTROL-CARD-1-LINE           PIC  X(80).                            
                                                                                
       FD  BACKUP-FILE                                                          
           RECORDING MODE F                                                     
           LABEL RECORDS OMITTED                                                
           BLOCK CONTAINS  0 RECORDS.                                           
       01  BACKUP-RECORD                 PIC X(450).                            
                                                                        00350000
      *                                                                 00430000
      **  HOUSEKEEPING AND WORK VARIABLES                               00440000
      *                                                                 00450000
       WORKING-STORAGE SECTION.                                         00460000
      *                                                                 00470000
       77  INPUT-SWITCH        PIC X     VALUE 'Y'.                     00480000
           88  NO-MORE-INPUT             VALUE 'N'.                     00490000
       77  LINE-NUM            PIC S9(4) COMP VALUE +60.                00500000
       77  RECORD-CNT          PIC S9(4) COMP VALUE ZERO.               00520000
       77  REJECT-CNT          PIC S9(4) COMP VALUE ZERO.               00530000
      *                                                                 00770000
      ** MESSAGE AND INFORMATION LINE                                   00780000
      *                                                                 00790000
       01  PC-PROGRAM-CONSTANTS.                                        00800000
           05  PC-PGM-NAME               PIC X(09) VALUE 'IMKUT108 '.           
           05  PC-FIFTY-STARS            PIC X(50)  VALUE ALL '*'.              
           05  PC-QNAME                PIC X(48)    VALUE               01090058
                                               'IM.VSM.ITEM.TO.RMS.Q'.  01100058
           05  PC-QMGR-NAME            PIC X(48)    VALUE SPACES.       01110074
      *                                                      QKSP       01120074
               88  PC-PROD-QMGR-NAME                VALUE 'ISWMQ04P'.   01130078
      *                                                      QKSL       01170074
               88  PC-LEARN-QMGR-NAME               VALUE 'ISWMQ04L'.   01180078
      *                                                      QKSQ       01170074
               88  PC-QA-QMGR-NAME                  VALUE 'ISWMQ04Q'.   01180078
      *                                                      QKST       01140074
               88  PC-TEST-QMGR-NAME                VALUE 'ISWMQ04T'.   01150099
                                                                        01190080
           05  PC-CORREL-ID            PIC X(48)    VALUE SPACES.       01110074
      *                                                      QKSP       01120074
               88  PC-PROD-CORREL-ID                VALUE 'ISWMQ04P'.   01130078
      *                                                      QKSL       01170074
               88  PC-LEARN-CORREL-ID               VALUE 'ISWMQ04L'.   01180078
      *                                                      QKSQ       01170074
               88  PC-QA-CORREL-ID                  VALUE 'ISWMQ04Q'.   01180078
      *                                                      QKST       01140074
               88  PC-TEST-CORREL-ID                VALUE 'ISWMQ04T'.   01150099
                                                                        01190080
      *                                                                 00790000
       01  PS-PROGRAM-SWITCHES.                                         00800000
           05  PS-CONTROL-CARD-1-SW      PIC X(01) VALUE 'N'.                   
               88  PS-CONTROL-CARD-1-END           VALUE 'Y'.                   
               88  PS-CONTROL-CARD-1-START         VALUE 'N'.                   
      *                                                                 00790000
       01  MESSAGE-LINE.                                                00800000
           05 FILLER                 PIC X(10) VALUE ' COMPCODE='.      00810000
           05 MESSAGE-LINE-CC        PIC -ZZZ9.                         00820000
           05 FILLER                 PIC X(8) VALUE ' REASON='.         00830000
           05 MESSAGE-LINE-REASON    PIC -ZZZZZZZZ9.                    00840000
           05 FILLER                 PIC X(11) VALUE ' CALL TYPE='.     00850000
           05 MESSAGE-LINE-TYPE      PIC X(8).                          00860000
           05 FILLER                 PIC X(12) VALUE ' ERROR MSG.='.    00870000
           05 MESSAGE-LINE-INFO      PIC X(50).                         00880000
       01  TOTAL-LINE.                                                  01000000
           05 FILLER                 PIC X(24)                          01010000
              VALUE 'TOTAL RECORDS PROCESSED='.                         01020000
           05 TOTAL-RECORDS          PIC ZZZ9.                          01030000
           05 FILLER                 PIC X(29)                          01040000
              VALUE '      TOTAL RECORDS REJECTED='.                    01050000
           05 TOTAL-REJECTS          PIC ZZZ9.                          01060000
           05 FILLER                 PIC X(71) VALUE SPACES.            01070000
      /                                                                 01080000
      *                                                                 01090000
      ** THE MQSERIES API REQUIRES THAT YOU PROVIDE SOME VARIABLES      01100000
      ** TO BE UTILIZED ON THE VARIOUS CALLS.   THESE ALSO INCLUDE      01110000
      ** NAMES OF ANY OBJECTS TO BE ACCESSED.                           01120000
      ** PLACE THE NECESSARY CODE HERE.                                 01130000
      *                                                                 01140000
       01  PV-VARIABLES.                                                01180001
           05  PV-MQM               PIC X(48)  VALUE SPACES.            01190001
           05  PV-HCONN             PIC S9(09) COMP VALUE 0.                    
           05  PV-CC                PIC S9(09) COMP VALUE 0.                    
           05  PV-REASON            PIC S9(09) COMP VALUE 0.                    
           05  PV-OPTIONS           PIC S9(09) COMP VALUE 0.                    
           05  PV-HOBJ-TRANS        PIC S9(09) COMP VALUE 0.                    
           05  PV-BUFFER-LEN        PIC S9(09) COMP VALUE 450.                  
                                                                        01210001
      *----------------------------------------------------------------*        
      *    ABEND CODES THE ARE APPLICATION DEFINED                              
      *----------------------------------------------------------------*        
       01  ABEND-CODE                  PIC S9(4)    VALUE ZEROS                 
                                                    COMP SYNC.                  
           88  AC-CONTROL-CARD-1-MISSING            VALUE +4001.                
           88  AC-CONTROL-CARD-1-ERROR1             VALUE +4002.                
           88  AC-CONTROL-CARD-1-ERROR2             VALUE +4003.                
           88  AC-MQ-ERROR                          VALUE +4020.                
                                                                        01210001
      *----------------------------------------------------------------*        
      *    CONTROL CARD                                                         
      *----------------------------------------------------------------*        
       01  CC-CONTROL-CARD-1.                                                   
           05  CC-QMGR-NAME              PIC X(48).                             
               88  CC-KOHLS-TEST-SYSTEM             VALUE 'QKST'.       01210073
               88  CC-KOHLS-QA-SYSTEM               VALUE 'QKSQ'.       01210073
               88  CC-KOHLS-LEARN-SYSTEM            VALUE 'QKSL'.       01210073
               88  CC-KOHLS-PROD-SYSTEM             VALUE 'QKSP'.       01210073
           05  FILLER                    PIC X(01)  VALUE SPACE.                
           05  CC-MQ-PRIORITY-LABEL      PIC X(14)  VALUE SPACES.               
      **       'MQMD PRIORITY='                                                 
           05  CC-MQ-PRIORITY            PIC 9(01)  VALUE ZERO.                 
           05  FILLER                    PIC X(16)  VALUE SPACES.               
                                                                                
                                                                        01270001
      *****************************************************             01280000
      * STRUCTURE FOR OUTPUT QUEUE                        *             01290000
      *****************************************************             01300000
       01  MQOUT-BUFFER             PIC X(450) VALUE SPACES.            01310002
      /                                                                 01360000
      *                                                                 01370000
      ** THE MQSERIES COPY BOOKS FOR THE OBJECT DESCRIPTOR (MQOD),      01380000
      ** MESSAGE DESCRIPTOR (MQMD), PUT MESSAGE OPTIONS (MQPMO) AND     01390000
      ** MQ CONSTANT VALUES (MQV) ARE REQUIRED.                         01400000
      ** PLACE THE NECESSARY CODE HERE.                                 01410000
      *                                                                 01420000
                                                                        01430001
       01  CMQV.                                                        01150001
           COPY CMQV.                                                   01160001
                                                                        01170001
       01  CMQODV.                                                      01150001
           COPY CMQODV.                                                 01160001
                                                                        01170001
       01  CMQMDV.                                                      01150001
           COPY CMQMDV.                                                 01160001
                                                                        01170001
       01  CMQPMOV.                                                     01150001
           COPY CMQPMOV.                                                01160001
                                                                        01550001
                                                                        01550001
       PROCEDURE DIVISION.                                              01560000
      *                                                                 01570000
           PERFORM 050-HOUSE-KEEPING.                                   01620000
           PERFORM 100-GET-INPUT THROUGH 100-GET-INPUT-EXIT             01630000
             UNTIL NO-MORE-INPUT.                                       01640000
           PERFORM 950-CLOSE-MQSERIES-QUEUE.                            01650000
           PERFORM 990-PROGRAM-COMPLETION.                                      
           GOBACK.                                                      01660000
                                                                        01670000
                                                                        01680000
                                                                        01690000
       050-HOUSE-KEEPING.                                               01700000
      *                                                                 01710000
      ** INITIALIZATION ROUTINE WILL OPEN: OUTPUT FILES AN INPUT QUEUES.01720000
      *                                                                 01730000
           SET PS-CONTROL-CARD-1-START TO TRUE.                                 
           OPEN INPUT CONTROL-CARD-1                                            
                      BACKUP-FILE.                                              
                                                                                
           PERFORM 8000-READ-CONTROL-CARD1.                                     
                                                                                
           IF PS-CONTROL-CARD-1-END                                             
               SET AC-CONTROL-CARD-1-MISSING TO TRUE                            
               DISPLAY '* * * * * * * * * * * * * * * * *'                      
               DISPLAY '*    ***IMKUT108 ABEND***       *'                      
               DISPLAY '*                               *'                      
               DISPLAY '*   CONTROL CARD MISSING -      *'                      
               DISPLAY '*   FOR QMGR NAME               *'                      
               DISPLAY '*                               *'                      
               DISPLAY '*   ABEND CODE: ' ABEND-CODE                            
                           '             *'                                     
               DISPLAY '* * * * * * * * * * * * * * * * *'                      
               CALL 'ILBOABN0'    USING  ABEND-CODE                             
           ELSE                                                                 
               DISPLAY PC-PGM-NAME                                              
                       '- '                                                     
                       PC-FIFTY-STARS                                           
               DISPLAY PC-PGM-NAME                                              
                       '- CONTROL CARD: '                                       
                       CC-CONTROL-CARD-1                                        
           END-IF.                                                              
                                                                                
           IF CC-MQ-PRIORITY-LABEL NOT = 'MQMD PRIORITY='                       
               SET AC-CONTROL-CARD-1-ERROR1 TO TRUE                             
               DISPLAY '* * * * * * * * * * * * * * * * *'                      
               DISPLAY '*    ***IMKUT108 ABEND***       *'                      
               DISPLAY '*                               *'                      
               DISPLAY '*   CONTROL CARD MISSING THE    *'                      
               DISPLAY '*   MQMD PRIORITY LABEL         *'                      
               DISPLAY '*                               *'                      
               DISPLAY '*   ABEND CODE: ' ABEND-CODE                            
                           '             *'                                     
               DISPLAY '* * * * * * * * * * * * * * * * *'                      
               CALL 'ILBOABN0'    USING  ABEND-CODE                             
                                                                                
           IF CC-MQ-PRIORITY NOT NUMERIC                                        
               SET AC-CONTROL-CARD-1-ERROR2 TO TRUE                             
               DISPLAY '* * * * * * * * * * * * * * * * *'                      
               DISPLAY '*    ***IMKUT108 ABEND***       *'                      
               DISPLAY '*                               *'                      
               DISPLAY '*   CONTROL CARD MQMD PRIORITY  *'                      
               DISPLAY '*   IS NOT NUMERIC              *'                      
               DISPLAY '*                               *'                      
               DISPLAY '*   ABEND CODE: ' ABEND-CODE                            
                           '             *'                                     
               DISPLAY '* * * * * * * * * * * * * * * * *'                      
               CALL 'ILBOABN0'    USING  ABEND-CODE                             
                                                                                
           CLOSE CONTROL-CARD-1.                                                
                                                                        01790000
           IF CC-KOHLS-PROD-SYSTEM                                      074     
               SET PC-PROD-QMGR-NAME TO TRUE                            074     
               SET PC-PROD-CORREL-ID TO TRUE                            074     
           ELSE                                                         074     
               IF CC-KOHLS-LEARN-SYSTEM                                     074 
                   SET PC-LEARN-QMGR-NAME   TO TRUE                         074 
                   SET PC-LEARN-CORREL-ID   TO TRUE                         074 
               ELSE                                                         074 
                   IF CC-KOHLS-QA-SYSTEM                                        
                       SET PC-QA-QMGR-NAME  TO TRUE                             
                       SET PC-QA-CORREL-ID  TO TRUE                             
                   ELSE                                                         
                       SET PC-TEST-QMGR-NAME TO TRUE                            
                       SET PC-TEST-CORREL-ID TO TRUE                            
                   END-IF                                                       
               END-IF                                                       0227
           END-IF.                                                      02270074
      *                                                                 01800000
      ** THE MQSERIES API MUST BE INVOKED TO CONNECT TO A QUEUE MANAGER 01810000
      ** PLACE THE NECESSARY CODE HERE.                                 01820000
      *                                                                 01830000
           MOVE CC-QMGR-NAME TO PV-MQM.                                         
                                                                                
           CALL 'CSQBCONN' USING                                                
                         PV-MQM,                                                
                         PV-HCONN,                                              
                         PV-CC,                                                 
                         PV-REASON                                              
                                                                                
           MOVE 'MQCONN'   TO MESSAGE-LINE-TYPE.                        01840000
                                                                        01930000
           EVALUATE PV-CC                                               01940002
             WHEN MQCC-OK PERFORM 920-INFOMSG THROUGH 920-INFOMSG-EXIT  01950000
             WHEN MQCC-WARNING PERFORM 900-WARNING                      01960000
             WHEN MQCC-FAILED GO TO 910-ERROR.                          01970000
                                                                        01980000
      *                                                                 01990000
      ** THE MQSERIES API MUST BE INVOKED TO OPEN A SPECIFIC QUEUE      02000000
      ** PLACE THE NECESSARY CODE HERE.                                 02010000
      *                                                                 02020000
           MOVE PC-QNAME                TO MQOD-OBJECTNAME.                     
           MOVE PC-QMGR-NAME            TO MQOD-OBJECTQMGRNAME.         02360074
           COMPUTE PV-OPTIONS = MQOO-OUTPUT                                     
                              + MQOO-FAIL-IF-QUIESCING                          
                              + MQOO-SET-IDENTITY-CONTEXT.                      
                                                                                
           CALL 'CSQBOPEN' USING                                                
                           PV-HCONN,                                            
                           MQOD,                                                
                           PV-OPTIONS,                                          
                           PV-HOBJ-TRANS,                                       
                           PV-CC,                                               
                           PV-REASON                                            
                                                                                
           MOVE 'MQOPEN'   TO MESSAGE-LINE-TYPE.                        02030000
                                                                        02160001
                                                                        02170001
           EVALUATE PV-CC                                               02180002
             WHEN MQCC-OK PERFORM 920-INFOMSG THROUGH 920-INFOMSG-EXIT  02190000
             WHEN MQCC-WARNING PERFORM 900-WARNING                      02200000
             WHEN MQCC-FAILED GO TO 910-ERROR.                          02210000
                                                                        02220000
                                                                        02230000
                                                                        02240000
       100-GET-INPUT.                                                   02250000
      *                                                                 02260000
           READ BACKUP-FILE RECORD                                      02270000
               AT END MOVE 'N' TO INPUT-SWITCH.                         02280000
           IF NO-MORE-INPUT  GO TO 100-GET-INPUT-EXIT.                  02290000
           MOVE BACKUP-RECORD TO MQOUT-BUFFER.                          02310002
           ADD +1 TO RECORD-CNT.                                        02490000
                                                                        02500000
      *                                                                 02510000
      ** THE MQSERIES API MUST BE INVOKED TO PUT DATA TO A QUEUE        02520000
      ** PLACE THE NECESSARY CODE HERE.                                 02530000
      *                                                                 02540000
           MOVE MQMI-NONE TO MQMD-MSGID.                                        
           MOVE PC-CORREL-ID TO MQMD-CORRELID.                                  
           MOVE MQFMT-STRING           TO MQMD-FORMAT.                  05940074
           MOVE MQPER-PERSISTENT       TO MQMD-PERSISTENCE.             05950074
           MOVE CC-MQ-PRIORITY         TO MQMD-PRIORITY.                        
           COMPUTE MQPMO-OPTIONS = MQPMO-NONE                                   
                                 + MQPMO-NO-SYNCPOINT.                          
                                                                                
           CALL 'CSQBPUT' USING                                                 
                          PV-HCONN,                                             
                          PV-HOBJ-TRANS,                                        
                          MQMD,                                                 
                          MQPMO,                                                
                          PV-BUFFER-LEN,                                        
                          MQOUT-BUFFER,                                         
                          PV-CC,                                                
                          PV-REASON                                             
                                                                                
           MOVE 'MQPUT'    TO MESSAGE-LINE-TYPE.                        02550000
                                                                        02710001
                                                                        02720000
           EVALUATE PV-CC                                               02730002
             WHEN MQCC-OK PERFORM 920-INFOMSG THROUGH 920-INFOMSG-EXIT  02740000
             WHEN MQCC-WARNING PERFORM 900-WARNING                      02750000
             WHEN MQCC-FAILED GO TO 910-ERROR.                          02760000
                                                                        02770000
       100-GET-INPUT-EXIT.                                              02780000
           EXIT.                                                        02790000
                                                                        02800000
       900-WARNING.                                                     03130000
      *                                                                 03140000
           MOVE PV-CC TO MESSAGE-LINE-CC.                               03150002
           MOVE PV-REASON TO MESSAGE-LINE-REASON.                       03160002
           MOVE 'CALL WARNING STATUS SET, PROGRAM WILL CONTINUE' TO     03170000
             MESSAGE-LINE-INFO.                                         03180000
           DISPLAY MESSAGE-LINE.                                        03190000
                                                                        03200000
       900-ERROR-ROUTINE-EXIT.                                          03210000
           EXIT.                                                        03220000
                                                                        03230000
       910-ERROR.                                                       03240000
      *                                                                 03250000
           MOVE PV-CC TO MESSAGE-LINE-CC.                               03260002
           MOVE PV-REASON TO MESSAGE-LINE-REASON.                       03270002
           MOVE 'CALL ERROR STATUS SET, PROGRAM ENDING' TO              03280000
             MESSAGE-LINE-INFO.                                         03290000
           DISPLAY MESSAGE-LINE.                                        03300000
           PERFORM 990-PROGRAM-COMPLETION.                              03310000
           GOBACK.                                                      03320000
                                                                        03350000
       920-INFOMSG.                                                     03360000
      *                                                                 03370000
           MOVE PV-CC TO MESSAGE-LINE-CC.                               03380002
           MOVE PV-REASON TO MESSAGE-LINE-REASON.                       03390002
           MOVE 'CALL COMPLETE (STATUS), PROGRAM WILL CONTINUE' TO      03400000
             MESSAGE-LINE-INFO.                                         03410000
           DISPLAY MESSAGE-LINE.                                        03420000
                                                                        03430000
       920-INFOMSG-EXIT.                                                03440000
           EXIT.                                                        03450000
                                                                        03460000
                                                                        03470000
       950-CLOSE-MQSERIES-QUEUE.                                        03480000
                                                                        03490000
      *                                                                 03520000
      ** THE MQSERIES API MUST BE INVOKED TO CLOSE THE QUEUE IN USE     03530000
      ** PLACE THE NECESSARY CODE HERE.                                 03540000
      *                                                                 03550000
           COMPUTE PV-OPTIONS = MQCO-NONE.                                      
                                                                                
           CALL 'CSQBCLOS' USING                                                
                           PV-HCONN,                                            
                           PV-HOBJ-TRANS,                                       
                           PV-OPTIONS,                                          
                           PV-CC,                                               
                           PV-REASON                                            
                                                                                
           MOVE 'MQCLOSE'  TO MESSAGE-LINE-TYPE.                        03560000
                                                                        03680000
           EVALUATE PV-CC                                               03690002
             WHEN MQCC-OK PERFORM 920-INFOMSG THROUGH 920-INFOMSG-EXIT  03700000
             WHEN MQCC-WARNING PERFORM 900-WARNING                      03710000
             WHEN MQCC-FAILED GO TO 910-ERROR.                          03720000
                                                                        03730000
      *                                                                 03740000
      ** THE MQSERIES API MUST BE INVOKED TO DISCONNECT FROM THE MQM    03750000
      ** PLACE THE NECESSARY CODE HERE.                                 03760000
      *                                                                 03770000
           CALL 'CSQBDISC' USING                                                
                         PV-HCONN,                                              
                         PV-CC,                                                 
                         PV-REASON                                              
                                                                                
           MOVE 'MQDISC'   TO MESSAGE-LINE-TYPE.                        03780000
                                                                        03870000
           EVALUATE PV-CC                                               03880002
             WHEN MQCC-OK PERFORM 920-INFOMSG THROUGH 920-INFOMSG-EXIT  03890000
             WHEN MQCC-WARNING PERFORM 900-WARNING                      03900000
             WHEN MQCC-FAILED GO TO 910-ERROR.                          03910000
                                                                        03920000
       990-PROGRAM-COMPLETION.                                          03930000
      *                                                                 03940000
      **  END OF PROGRAM PROCESSING ROUTINE.                            03950000
      *                                                                 03960000
           MOVE RECORD-CNT TO TOTAL-RECORDS.                            03970000
           MOVE REJECT-CNT TO TOTAL-REJECTS.                            03980000
           DISPLAY TOTAL-LINE.                                          04000000
           CLOSE BACKUP-FILE.                                           04010000
                                                                        03920000
      * ------------------------------------------------------------- *         
      * READ CONTROL CARD 1 TO OBTAIN THE INPUT QUEUE MGR NAME                  
      * ------------------------------------------------------------- *         
       8000-READ-CONTROL-CARD1.                                                 
            READ CONTROL-CARD-1 INTO CC-CONTROL-CARD-1                          
                AT END                                                          
                    SET PS-CONTROL-CARD-1-END   TO TRUE.                        
                                                                                
