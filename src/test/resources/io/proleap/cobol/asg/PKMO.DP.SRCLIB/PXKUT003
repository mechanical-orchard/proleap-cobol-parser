       IDENTIFICATION DIVISION.                                         00010021
       PROGRAM-ID.    PXKUT003.                                         00020052
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030021
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040021
       DATE-WRITTEN.  03/31/2005.                                       00050048
       DATE-COMPILED.                                                   00060021
      ******************************************************************00070021
      **THIS PROGRAM WILL COMPARE THE TIMESTAMP ON THE RECORD TO THE    00080051
      **     LAST RUN TIMESTAMP TO SEE IF ANY CHANGES HAVE BEEN MADE.   00090051
      ******************************************************************00100021
      **DATE:04/12/2005 MADE BY:JULIE TEWES INITIAL PROGRAM WRITE       00110051
      ******************************************************************00120021
050405**DATE:05/04/2005 MADE BY:JULIE TEWES CHANGE TO REVERSE LOGIC     00130063
050405**                ON TIMESTAMP COMPARISON                         00140063
      ******************************************************************00150063
091305**DATE:09/13/2005 MADE BY:JULIE TEWES PRICE OPTIMIZATION          00160064
091305******************************************************************00170064
       ENVIRONMENT DIVISION.                                            00180021
       CONFIGURATION SECTION.                                           00190021
       SOURCE-COMPUTER.    IBM-3090.                                    00200021
       OBJECT-COMPUTER.    IBM-3090.                                    00210021
       INPUT-OUTPUT SECTION.                                            00220021
       FILE-CONTROL.                                                    00230021
                                                                        00240021
           SELECT INPUT-FILE                                            00250021
                  ASSIGN TO INP01.                                      00260021
           SELECT OUTPUT-FILE                                           00270048
                  ASSIGN TO OUT01.                                      00280021
                                                                        00290021
       DATA DIVISION.                                                   00300021
       FILE SECTION.                                                    00310021
                                                                        00320021
       FD  INPUT-FILE                                                   00330021
           RECORDING MODE IS F                                          00340021
           LABEL RECORDS OMITTED                                        00350021
           BLOCK CONTAINS 0 RECORDS                                     00360021
           DATA RECORD IS INPUT-RECORD.                                 00370021
091305 01  INPUT-RECORD                        PIC X(200).              00380065
                                                                        00390021
       FD  OUTPUT-FILE                                                  00400048
           RECORDING MODE IS F                                          00410045
           LABEL RECORDS OMITTED                                        00420045
           BLOCK CONTAINS 0 RECORDS                                     00430045
           DATA RECORD IS OUTPUT-RECORD.                                00440048
091305 01  OUTPUT-RECORD                       PIC X(200).              00450065
                                                                        00460045
       WORKING-STORAGE SECTION.                                         00470021
      ******************************************************************00480021
      **PROGRAM SWITCHES                                                00490021
      ******************************************************************00500021
       01  PS-SWITCH-AREAS.                                             00510021
           05  PS-END-OF-INPUT-FILE-SW         PIC X(01)  VALUE 'N'.    00520021
               88  PS-END-OF-INPUT-FILE                   VALUE 'Y'.    00530021
               88  PS-NOT-END-OF-INPUT-FILE               VALUE 'N'.    00540021
           05  PS-USE-PREV-TIMESTAMP-SW        PIC X(01)  VALUE 'N'.    00550052
               88  PS-USE-PREV-TIMESTAMP                  VALUE 'Y'.    00560052
               88  PS-DO-NOT-USE-PREV-TIMESTAMP           VALUE 'N'.    00570052
      ******************************************************************00580024
      **DATE TIME FIELDS                                                00590024
      ******************************************************************00600024
       01 DF-DATE-TIME-FIELDS.                                          00610024
          05 DF-ACCEPT-DATE.                                            00620057
             10 DF-ACCEPT-YY                   PIC 9(02) VALUE ZEROS.   00630057
             10 DF-ACCEPT-MM                   PIC 9(02) VALUE ZEROS.   00640057
             10 DF-ACCEPT-DD                   PIC 9(02) VALUE ZEROS.   00650057
          05 DF-ACCEPT-TIME.                                            00660057
             10 DF-ACCEPT-HH                   PIC 9(02) VALUE ZEROS.   00670057
             10 DF-ACCEPT-MIN                  PIC 9(02) VALUE ZEROS.   00680057
             10 DF-ACCEPT-SEC                  PIC 9(02) VALUE ZEROS.   00690057
             10 DF-ACCEPT-HSEC                 PIC 9(02) VALUE ZEROS.   00700057
       01 DF-FORMAT-DATE-TIME.                                          00710057
          05 DF-FORMAT-DATE-CC                 PIC 9(02) VALUE 20.      00720057
          05 DF-FORMAT-DATE-YY                 PIC 9(02) VALUE ZEROS.   00730057
          05 FILLER                            PIC X(01) VALUE '-'.     00740057
          05 DF-FORMAT-DATE-MM                 PIC 9(02) VALUE ZEROS.   00750057
          05 FILLER                            PIC X(01) VALUE '-'.     00760057
          05 DF-FORMAT-DATE-DD                 PIC 9(02) VALUE ZEROS.   00770057
          05 FILLER                            PIC X(01) VALUE '-'.     00780057
          05 DF-FORMAT-DATE-HH                 PIC 9(02) VALUE ZEROS.   00790057
          05 FILLER                            PIC X(01) VALUE '.'.     00800057
          05 DF-FORMAT-DATE-MI                 PIC 9(02) VALUE ZEROS.   00810057
          05 FILLER                            PIC X(01) VALUE '.'.     00820057
          05 DF-FORMAT-DATE-SS                 PIC 9(02) VALUE ZEROS.   00830057
          05 FILLER                            PIC X(01) VALUE '.'.     00840057
          05 DF-FORMAT-DATE-MS                 PIC 9(06) VALUE ZEROS.   00850057
      ******************************************************************00860023
      **PROGRAM COUNTERS                                                00870023
      ******************************************************************00880023
       01  PC-PROGRAM-COUNTERS.                                         00890023
           05  PC-INPUT-RECORDS-READ           PIC 9(06) VALUE ZEROS.   00900043
           05  PC-OUTPUT-RECORDS-WRITTEN       PIC 9(06) VALUE ZEROS.   00910048
      ****************************************************************  00920021
      **ABEND INFORMATION                                               00930021
      ****************************************************************  00940021
       01  AC-ABEND-FIELDS.                                             00950021
           05  AC-ABEND-CODE                   PIC S9(04) VALUE ZERO    00960021
                                                           COMP SYNC.   00970021
               88  AC-TABLE-ERROR                        VALUE +4004.   00980021
               88  AC-DB2-ERROR                          VALUE +4013.   00990021
       01  PV-MISC-ABEND-FIELDS.                                        01000021
           05  PV-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   01010021
           05  PV-OPERATION-MSG-1              PIC X(40) VALUE SPACE.   01020021
           05  PV-OPERATION-MSG-2              PIC X(40) VALUE SPACE.   01030021
           05  PV-DB2-OPERATION                PIC X(30) VALUE SPACE.   01040021
      ******************************************************************01050021
      **COPYBOOKS                                                       01060021
      ******************************************************************01070021
      ****INPUT RECORD LAYOUT                                           01080021
           COPY PXRD005.                                                01090051
      ****OUTPUT RECORD LAYOUT                                          01100021
           COPY PXRD001.                                                01110052
      ****ABEND ROUTINE INFORMATION                                     01120021
           COPY SQLCA2.                                                 01130021
      ****ABEND ROUTINE INFORMATION                                     01140021
           COPY DPWS004.                                                01150021
      ******************************************************************01160021
      **TABLE INCLUSIONS                                                01170021
      ******************************************************************01180021
      ***PRICE CHANGE REPORTING TABLE                                   01190051
           EXEC SQL                                                     01200021
                INCLUDE PXT00001                                        01210051
           END-EXEC.                                                    01220021
      *****DB2 TABLE INCLUSION                                          01230021
           EXEC SQL                                                     01240021
                INCLUDE SQLCA                                           01250021
           END-EXEC.                                                    01260021
                                                                        01270029
       PROCEDURE DIVISION.                                              01280021
      ******************************************************************01290021
       000-MAINLINE-PROCESSING.                                         01300021
                                                                        01310021
           PERFORM 1000-INITIALIZATION.                                 01320021
           PERFORM 2000-PROCESS-RECORDS                                 01330021
                   UNTIL PS-END-OF-INPUT-FILE.                          01340021
           PERFORM 9999-WRAPUP.                                         01350021
           GOBACK.                                                      01360021
                                                                        01370021
       EJECT.                                                           01380021
      ******************************************************************01390021
      **INITIALIZATION ROUTINE                                          01400021
      ******************************************************************01410021
       1000-INITIALIZATION.                                             01420021
                                                                        01430021
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             01440021
                                                                        01450021
           OPEN INPUT  INPUT-FILE                                       01460024
                OUTPUT OUTPUT-FILE.                                     01470048
                                                                        01480021
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             01490021
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             01500021
           MOVE DF-ACCEPT-YY          TO DF-FORMAT-DATE-YY.             01510057
           MOVE DF-ACCEPT-MM          TO DF-FORMAT-DATE-MM.             01520057
           MOVE DF-ACCEPT-DD          TO DF-FORMAT-DATE-DD.             01530057
           MOVE DF-ACCEPT-HH          TO DF-FORMAT-DATE-HH.             01540057
           MOVE DF-ACCEPT-MIN         TO DF-FORMAT-DATE-MI.             01550057
           MOVE DF-ACCEPT-SEC         TO DF-FORMAT-DATE-SS.             01560057
                                                                        01570021
           PERFORM 9999-READ-INPUT-RECORD.                              01580052
                                                                        01590052
           IF  PS-NOT-END-OF-INPUT-FILE                                 01600059
               MOVE PXRD005-EFF-DATE  TO PXT00001-PRCHG-EFF-DTE         01610059
               PERFORM 1100-LOOKUP-PREVIOUS-RUN                         01620059
           END-IF.                                                      01630059
                                                                        01640021
       EJECT.                                                           01650021
      ******************************************************************01660021
      **RETRIEVE PREVIOUS RUN'S TIMESTAMP                               01670052
      ******************************************************************01680021
       1100-LOOKUP-PREVIOUS-RUN.                                        01690052
                                                                        01700021
           MOVE '1100-LOOKUP-PREVIOUS-RUN' TO PV-PARAGRAPH-NAME.        01710052
                                                                        01720021
           EXEC SQL                                                     01730052
                SELECT PRCG_TMST                                        01740052
                INTO   :PXT00001-PRCG-TMST                              01750052
                FROM   PXT_PRCHG_RPT_PRCG                               01760052
                WHERE  PRCHG_EFF_DTE = :PXT00001-PRCHG-EFF-DTE          01770052
                  AND  TKT_CLOR_CDE = 'Y'                               01780060
                WITH UR                                                 01790052
           END-EXEC.                                                    01800052
                                                                        01810052
           EVALUATE TRUE                                                01820052
              WHEN SQLCODE = +0                                         01830052
                   SET PS-USE-PREV-TIMESTAMP                            01840052
                                          TO TRUE                       01850052
              WHEN SQLCODE = +100                                       01860052
                   SET PS-DO-NOT-USE-PREV-TIMESTAMP                     01870052
                                          TO TRUE                       01880052
              WHEN OTHER                                                01890052
                   DISPLAY 'LOOKUP PRCHG PROCESSING TIME....'           01900052
                   DISPLAY 'DATE ' PXT00001-PRCHG-EFF-DTE               01910058
                   PERFORM 9999-DISPLAY-STATISTICS                      01920052
                   PERFORM 9999-SQL-ERROR                               01930052
           END-EVALUATE.                                                01940052
                                                                        01950052
       EJECT.                                                           01960052
      ******************************************************************01970052
      **PROCESS INPUT RECORD TO CHECK ROWS                              01980052
      ******************************************************************01990052
       2000-PROCESS-RECORDS.                                            02000052
                                                                        02010052
           MOVE '2000-PROCESS-RECORDS'   TO PV-PARAGRAPH-NAME.          02020052
                                                                        02030052
           INITIALIZE PXRD001-YELLOW-TICKET-REPORT.                     02040052
                                                                        02050043
           EVALUATE TRUE                                                02060052
              WHEN PS-DO-NOT-USE-PREV-TIMESTAMP                         02070052
                   SET PXRD001-RECORD-CHANGE-NO                         02080052
                                         TO TRUE                        02090052
              WHEN PS-USE-PREV-TIMESTAMP                                02100052
050405             IF  PXRD005-LAST-UPD-TMST < PXT00001-PRCG-TMST       02110063
                       SET PXRD001-RECORD-CHANGE-NO                     02120057
                                         TO TRUE                        02130052
                   ELSE                                                 02140052
                       SET PXRD001-RECORD-CHANGE-YES                    02150057
                                         TO TRUE                        02160052
                   END-IF                                               02170052
           END-EVALUATE.                                                02180052
                                                                        02190052
           MOVE PXRD005-PRCHG-ID         TO PXRD001-PRCHG-ID.           02200052
           MOVE PXRD005-EFF-DATE         TO PXRD001-EFF-DATE.           02210052
           MOVE PXRD005-STR-NBR          TO PXRD001-STR-NBR.            02220052
           MOVE PXRD005-LOC-NM           TO PXRD001-LOC-NM.             02230052
           MOVE PXRD005-BUS-AREA-ID      TO PXRD001-BUS-AREA-ID.        02240052
           MOVE PXRD005-BUS-AREA-DESC    TO PXRD001-BUS-AREA-DESC.      02250052
           MOVE PXRD005-DEPT-NBR         TO PXRD001-DEPT-NBR.           02260052
           MOVE PXRD005-VS-NBR           TO PXRD001-VS-NBR.             02270052
           MOVE PXRD005-LBL-NM           TO PXRD001-LBL-NM.             02280052
           MOVE PXRD005-VS-DESC          TO PXRD001-VS-DESC.            02290052
           MOVE PXRD005-STKR-NBR         TO PXRD001-STKR-NBR.           02300052
           MOVE PXRD005-SUB-CL-NBR       TO PXRD001-SUB-CL-NBR.         02310052
           MOVE PXRD005-STR-EXPTED-QTY   TO PXRD001-STR-EXPTED-QTY.     02320052
           MOVE PXRD005-IN-TRNST-QTY     TO PXRD001-IN-TRNST-QTY.       02330052
           MOVE PXRD005-UNT-RTL-AMT      TO PXRD001-UNT-RTL-AMT.        02340052
           MOVE PXRD005-FULL-STYLE-MKDWN TO PXRD001-FULL-STYLE-MKDWN.   02350052
091305     MOVE PXRD005-LAST-NCLR-RTL-AMT                               02360064
091305                                   TO PXRD001-LAST-NCLR-RTL-AMT.  02370064
                                                                        02380043
           PERFORM 9999-WRITE-OUTPUT-RECORD.                            02390048
           PERFORM 9999-READ-INPUT-RECORD.                              02400021
                                                                        02410021
       EJECT.                                                           02420021
      ******************************************************************02430043
      **READ INPUT RECORD                                               02440043
      ******************************************************************02450043
       9999-READ-INPUT-RECORD.                                          02460043
                                                                        02470043
           READ INPUT-FILE INTO PXRD005-YELLOW-TICKET-UNLOAD            02480052
                AT END                                                  02490021
                SET PS-END-OF-INPUT-FILE TO TRUE                        02500021
           END-READ.                                                    02510021
                                                                        02520021
           IF  PS-NOT-END-OF-INPUT-FILE                                 02530021
               ADD +1                      TO PC-INPUT-RECORDS-READ     02540021
           END-IF.                                                      02550021
                                                                        02560021
       EJECT.                                                           02570021
      ****************************************************************  02580045
      **WRITE OUTPUT RECORD                                             02590048
      ****************************************************************  02600045
       9999-WRITE-OUTPUT-RECORD.                                        02610048
                                                                        02620045
           WRITE OUTPUT-RECORD FROM PXRD001-YELLOW-TICKET-REPORT.       02630052
                                                                        02640045
           ADD +1              TO PC-OUTPUT-RECORDS-WRITTEN.            02650048
                                                                        02660045
       EJECT.                                                           02670045
      ******************************************************************02680021
      **CLOSE INPUT FILES.                                              02690021
      ******************************************************************02700021
       9999-WRAPUP.                                                     02710021
                                                                        02720021
           CLOSE INPUT-FILE                                             02730024
                 OUTPUT-FILE.                                           02740048
                                                                        02750021
           IF  PC-OUTPUT-RECORDS-WRITTEN > 0                            02760059
               PERFORM 9999-UPDATE-PROCESS-TABLE                        02770059
           END-IF.                                                      02780059
           PERFORM 9999-DISPLAY-STATISTICS.                             02790056
                                                                        02800021
       EJECT.                                                           02810021
      ******************************************************************02820053
      **UPDATE PX PROCESSING TABLE                                      02830056
      ******************************************************************02840053
       9999-UPDATE-PROCESS-TABLE.                                       02850056
                                                                        02860053
           MOVE '9999-UPDATE-PROCESS-TABLE' TO PV-PARAGRAPH-NAME.       02870056
                                                                        02880056
           MOVE DF-FORMAT-DATE-TIME         TO PXT00001-PRCG-TMST.      02890057
                                                                        02900057
           EXEC SQL                                                     02910056
                UPDATE PXT_PRCHG_RPT_PRCG                               02920056
                SET    PRCG_TMST     = :PXT00001-PRCG-TMST              02930061
                WHERE  PRCHG_EFF_DTE = :PXT00001-PRCHG-EFF-DTE          02940056
                  AND  TKT_CLOR_CDE  = 'Y'                              02950061
           END-EXEC.                                                    02960056
                                                                        02970056
           EVALUATE TRUE                                                02980056
              WHEN SQLCODE = +0                                         02990056
                   SET PS-USE-PREV-TIMESTAMP                            03000056
                                          TO TRUE                       03010056
              WHEN SQLCODE = +100                                       03020056
                   PERFORM 9999-INSERT-PROCESS-TABLE                    03030061
              WHEN OTHER                                                03040056
                   DISPLAY 'UPDATE PRCHG PROCESSING TIME....'           03050061
                   PERFORM 9999-DISPLAY-STATISTICS                      03060056
                   PERFORM 9999-SQL-ERROR                               03070056
           END-EVALUATE.                                                03080056
                                                                        03090056
       EJECT.                                                           03100056
      ******************************************************************03110061
      **INSERT PX PROCESSING TABLE                                      03120061
      ******************************************************************03130061
       9999-INSERT-PROCESS-TABLE.                                       03140061
                                                                        03150061
           MOVE '9999-INSERT-PROCESS-TABLE' TO PV-PARAGRAPH-NAME.       03160061
                                                                        03170061
           MOVE DF-FORMAT-DATE-TIME         TO PXT00001-PRCG-TMST.      03180061
                                                                        03190061
           EXEC SQL                                                     03200061
                INSERT INTO PXT_PRCHG_RPT_PRCG                          03210061
                                       (PRCHG_EFF_DTE                   03220061
                                       ,PRCG_TMST                       03230061
                                       ,TKT_CLOR_CDE)                   03240061
                VALUES       (:PXT00001-PRCHG-EFF-DTE                   03250061
                             ,:PXT00001-PRCG-TMST                       03260061
                             ,'Y')                                      03270061
           END-EXEC.                                                    03280061
                                                                        03290061
           EVALUATE TRUE                                                03300061
              WHEN SQLCODE = +0                                         03310061
                   CONTINUE                                             03320061
              WHEN OTHER                                                03330061
                   DISPLAY 'UPDATE PRCHG PROCESSING TIME....'           03340061
                   PERFORM 9999-DISPLAY-STATISTICS                      03350061
                   PERFORM 9999-SQL-ERROR                               03360061
           END-EVALUATE.                                                03370061
                                                                        03380061
       EJECT.                                                           03390061
      ******************************************************************03400061
      **DISPLAY PROGRAM STATS                                           03410056
      ******************************************************************03420056
       9999-DISPLAY-STATISTICS.                                         03430056
                                                                        03440056
           DISPLAY '*************************************************'. 03450021
           DISPLAY '* PROGRAM...........: PXKUT003'.                    03460052
           DISPLAY '* DATE..............: ' DF-ACCEPT-DATE.             03470021
           DISPLAY '* TIME..............: ' DF-ACCEPT-TIME.             03480021
           DISPLAY '* RECORDS READ......: ' PC-INPUT-RECORDS-READ.      03490021
           DISPLAY '* OUTPUT RECORDS WRITTEN...: '                      03500048
                                              PC-OUTPUT-RECORDS-WRITTEN.03510048
           DISPLAY '*************************************************'. 03520021
                                                                        03530021
       EJECT.                                                           03540021
      ******************************************************************03550021
      * ABEND ROUTINE FOR BAD SQL CALLS                                *03560021
      ******************************************************************03570021
       9999-SQL-ERROR.                                                  03580021
                                                                        03590021
           DISPLAY '** ABEND     **'.                                   03600021
           DISPLAY '** PROGRAM   ** = PXKUT003'.                        03610052
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              03620021
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               03630021
      ***************************************************************** 03640021
      *    COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED  03650021
      *    TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.   03660021
      ***************************************************************** 03670021
           COPY DPPD004.                                                03680021
           SET AC-DB2-ERROR TO TRUE.                                    03690021
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         03700021
                                                                        03710021
       EJECT.                                                           03720021
