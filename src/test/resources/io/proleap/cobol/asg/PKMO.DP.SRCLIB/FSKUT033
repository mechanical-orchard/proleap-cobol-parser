      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.              FSKUT033.                                       
       AUTHOR.                  BOB FORD.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            09/01/04.                                       
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * PROGRAM DESCRIPTION:                                                    
      * --------------------                                                    
      * REGIONAL CLEARANCE PRICED SKUS SALES EXTRACT                            
      *                                                                         
      * PROGRAM OBJECTIVE:                                                      
      * ------------------                                                      
      * SEQUENTIALLY READ THE EXTRACT FILE OF REGIONAL CLEARANCE                
      * PRICED SKU/STORES AND WRITE OUT A FILE THAT CONTAINS THE INPUT          
      * INFORMATION AND THE DAILY SALES INFO FROM MHT_DLY_SLS FOR THE           
      * THE CURRENT WEEK-TO-DATE AND THE PREVIOUS WEEK.  IF THE SALES           
      * INFO IS NOT FOUND A RECORD THAT CONTAINS ZERO SALES IS STILL            
      * WRITTEN.                                                                
      *                                                                         
      * JOB - FSK008 AND PROC - FSK0081.                                        
      *                                                                         
      ******************************************************************        
      * CHANGE LOG:                                                             
      * -----------                                                             
      * WR27494 09/01/2004 BOB FORD    NEW PROGRAM -                            
      *                                FALL04 PERFORMANCE TUNING                
      *                                                                         
      * WR27494 09-17-2004 M JOHNSON   THE INPUT RECORD LAYOUT WAS              
      *                                CHANGED AND READ INTO FSRD066.           
      *                                THE INPUT RECORD LENGTH WAS              
      *                                REDUCED AND ADDED MOVES FROM THE         
      *                                INPUT TO THE OUTPUT.                     
      *                                ALSO CHANGED THE READ OF THE             
      *                                DAILY SALE MHT_DLY_SLS TO THE            
      *                                WEEKLY SALES TSKSTWKS TO GET THE         
      *                                SALES BUCKETS.                           
      * WR27494 10-27-2004 B FORD      ADDED RTL-CHG-DTE AND STAT-CHG-DT        
      *                                AND CHANGED LRECLS.                      
      * WR26354 03/29/05 - A KINTOPF   CHANGED FOR STORE NBR EXPANSION  00310011
      *                                AND TO HANDLE NEW NUMERIC STR    00320011
      *                                COLUMN ON TSKSTWKS               00320011
KP1005* WR29725 10-27-2005 K PETERSON  CHANGES REQUIRED FOR MARKDOWN            
KP1005*                                OPTIMIZATION.  INCREASED LENGTH          
KP1005*                                OF INPUT FILE.                           
      *                                                                         
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
      ******************************************************************        
       CONFIGURATION SECTION.                                                   
                                                                                
      ******************************************************************        
       INPUT-OUTPUT SECTION.                                                    
                                                                                
      ******************************************************************        
       FILE-CONTROL.                                                            
                                                                                
           SELECT RCP-SKU-FILE                                                  
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT RCP-SUMMARY-FILE-OUT                                          
               ASSIGN TO OUT01.                                                 
                                                                                
      /*****************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
      ******************************************************************        
       FILE SECTION.                                                            
                                                                                
       FD  RCP-SKU-FILE                                                         
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
KP1005 01  RCP-SKU-REC                 PIC  X(71).                              
                                                                                
       FD  RCP-SUMMARY-FILE-OUT                                                 
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  RCP-SUMMARY-REC             PIC  X(250).                             
                                                                                
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-RCP-SKU-EOF-SW       PIC  X(01)          VALUE 'N'.           
               88  PS-RCP-SKU-EOF                          VALUE 'Y'.           
                                                                                
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME         PIC  X(08)          VALUE                
               'FSKUT033'.                                                      
                                                                                
      ******************************************************************        
       01  PT-PROGRAM-TOTALS.                                                   
           05  PT-SKUS-READ-CNT        PIC S9(09)          VALUE ZEROS          
                                                           COMP-3.              
           05  PT-TSKSTWKS-RCD-FND     PIC S9(09)          VALUE ZEROS          
                                                           COMP-3.              
           05  PT-TSKSTWKS-RCD-NOT-FND PIC S9(09)          VALUE ZEROS          
                                                           COMP-3.              
           05  PT-RCD-WRTTN            PIC S9(09)          VALUE ZEROS          
                                                           COMP-3.              
      *    05  PT-DISPLAY-CNTR         PIC S9(09)          VALUE +0             
      *                                                    COMP-3.              
      *        88  PT-DISPLAY-CNTR-CNT                     VALUE                
      *            5000000.                                                     
      *            5000.                                                        
                                                                                
      ******************************************************************        
       01  CC-DATES.                                                            
           05  CC-PROCESS-DATE         PIC X(10)           VALUE SPACE.         
                                                                                
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-SUB                  PIC S9(4) COMP  VALUE ZERO.              
           05  PV-SUB-MAX              PIC S9(4) COMP  VALUE +14.               
           05  PV-SUB-START            PIC S9(4) COMP  VALUE ZERO.              
      *    05  PV-STR-NBR              PIC  X(3)           VALUE SPACE.         
      *    05  PV-9-5                  PIC  9(5).                               
      *    05  PV-X-5 REDEFINES PV-9-5.                                         
      *        10 FILLER               PIC  X(02).                              
      *        10 PV-X-3               PIC  X(03).                              
           05  PV-DTATTR-DATE          PIC  X(10)          VALUE SPACE.         
           05  PV-PARAGRAPH-NAME       PIC  X(25)          VALUE SPACE.         
           05  PV-ABEND-MESSAGE        PIC  X(50)          VALUE SPACE.         
           05  PV-OPER-ATTEMPTED       PIC  X(30)          VALUE SPACE.         
           05  PV-FSCL-BOW-DTE         PIC  X(10)          VALUE SPACE.         
           05  PV-FSCL-BOW-DTE-THISW   PIC  X(10)          VALUE SPACE.         
           05  PV-FSCL-BOW-DTE-LASTW   PIC  X(10)          VALUE SPACE.         
                                                                                
      ******************************************************************        
       01  ABEND-CODE                  PIC S9(04)          VALUE +4003          
                                                           COMP SYNC.           
                                                                                
      ******************************************************************        
       01  NI-NULL-INDICATORS.                                                  
           05  NI-SUN-SLS-UNIT-QTY     PIC S9(04)          VALUE +0             
                                                           COMP.                
           05  NI-MON-SLS-UNIT-QTY     PIC S9(04)          VALUE +0             
                                                           COMP.                
           05  NI-TUE-SLS-UNIT-QTY     PIC S9(04)          VALUE +0             
                                                           COMP.                
           05  NI-WED-SLS-UNIT-QTY     PIC S9(04)          VALUE +0             
                                                           COMP.                
           05  NI-THU-SLS-UNIT-QTY     PIC S9(04)          VALUE +0             
                                                           COMP.                
           05  NI-FRI-SLS-UNIT-QTY     PIC S9(04)          VALUE +0             
                                                           COMP.                
           05  NI-SAT-SLS-UNIT-QTY     PIC S9(04)          VALUE +0             
                                                           COMP.                
                                                                                
      ******************************************************************        
       01  SDT-SYSTEM-DATE-TIME.                                                
           05  SDT-YYYYMMDD            PIC  X(10)          VALUE SPACE.         
           05  FILLER REDEFINES SDT-YYYYMMDD.                                   
               10  SDT-YYYYMMDD-CCYY.                                           
                   15  SDT-YYYYMMDD-CC PIC  X(02).                              
                   15  SDT-YYYYMMDD-YY PIC  X(02).                              
               10  FILLER              PIC  X(01).                              
               10  SDT-YYYYMMDD-MM     PIC  X(02).                              
               10  FILLER              PIC  X(01).                              
               10  SDT-YYYYMMDD-DD     PIC  X(02).                              
           05  SDT-HHMMSS              PIC  X(08)          VALUE SPACE.         
           05  FILLER REDEFINES SDT-HHMMSS.                                     
               10  SDT-HHMMSS-HH       PIC  X(02).                              
               10  FILLER              PIC  X(01).                              
               10  SDT-HHMMSS-MM       PIC  X(02).                              
               10  FILLER              PIC  X(01).                              
               10  SDT-HHMMSS-SS       PIC  X(02).                              
                                                                                
      ******************************************************************        
      * CALENDAR ROUTINE WORK AREA.                                             
      ******************************************************************        
           COPY DPWS500.                                                        
                                                                                
      ******************************************************************        
      *  INVENTORY MANAGEMENT CLEARANCE ITEM INFO - INPUT RECORD                
      ******************************************************************        
           COPY FSRD066.                                                        
                                                                                
      ******************************************************************        
      *  FASHION SALES CLEARANCE ITEM INFO - OUTPUT RECORD                      
      ******************************************************************        
           COPY FSRD065.                                                        
                                                                                
      *****************************************************************         
      * DB2 DATE TABLE                                                          
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 DAILY SALES TABLE                                                   
      *****************************************************************         
      *    EXEC SQL                                                             
      *        INCLUDE MHT00000                                                 
      *    END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 WEEKLY SALES TABLE                                                  
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TSKSTWKS                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 STANDARD COPYBOOK                                                   
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  EXTENSION TO THE SQL COMMUNICATIONS AREA FOR EDITING                   
      *  SQL CODES FOR DISPLAY                                                  
      *****************************************************************         
           COPY SQLCA2.                                                         
                                                                                
      ******************************************************************        
       LINKAGE SECTION.                                                         
                                                                                
      /*****************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '* START OF FSKUT033               '.                        
           DISPLAY '**********************************'.                        
                                                                                
           PERFORM 0100-INITIALIZE.                                             
                                                                                
           PERFORM 8000-OPEN-FILES.                                             
                                                                                
           PERFORM 8100-READ-RCP-SKU-FILE.                                      
           IF PS-RCP-SKU-EOF                                                    
             DISPLAY '***********************************************'          
             DISPLAY '*** NO REGIONAL CLEARANCE PRICED SKUS FOUND ***'          
             DISPLAY '***********************************************'          
           ELSE                                                                 
             PERFORM 1000-PROCESS-RCP-SKUS                                      
               UNTIL PS-RCP-SKU-EOF                                             
           END-IF.                                                              
                                                                                
           PERFORM 8900-CLOSE-FILES.                                            
                                                                                
           PERFORM 0900-DISPLAY-PROGRAM-TOTALS.                                 
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       0100-INITIALIZE.                                                         
                                                                                
           PERFORM 7600-SELECT-DATE-TIME.                                       
                                                                                
           DISPLAY 'FSKUT033 RUN DATE: ' SDT-YYYYMMDD ' - '                     
              SDT-HHMMSS.                                                       
                                                                                
      *  ACCEPT CONTROL CARD TO GET DATES FOR DB2 READS                         
           ACCEPT CC-PROCESS-DATE.                                              
           DISPLAY 'FSKUT033 - PROCESS DATE ' CC-PROCESS-DATE.                  
           PERFORM 7100-SELECT-TDTATTR-DTL.                                     
           DISPLAY 'FSKUT033 - THIS WEEK BEGIN '                                
               PV-FSCL-BOW-DTE-THISW                                            
             ' LAST WEEK BEGIN ' PV-FSCL-BOW-DTE-LASTW.                         
                                                                                
      ******************************************************************        
       0900-DISPLAY-PROGRAM-TOTALS.                                             
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '* FSKUT033 PROGRAM STATISTICS:    '.                        
           DISPLAY '**********************************'.                        
           DISPLAY '* TOTAL SKUS READ: ' PT-SKUS-READ-CNT.                      
           DISPLAY '* TOTAL SALES FND: ' PT-TSKSTWKS-RCD-FND.                   
           DISPLAY '* TOTAL SALES NOT FND: '                                    
              PT-TSKSTWKS-RCD-NOT-FND.                                          
           DISPLAY '* TOTAL RCDS WRITTEN: '                                     
              PT-RCD-WRTTN.                                                     
           DISPLAY '**********************************'.                        
           DISPLAY '**********************************'.                        
           DISPLAY '* END OF FSKUT033                 '.                        
           DISPLAY '**********************************'.                        
                                                                                
      /*****************************************************************        
      *    PROCESS ALL REGIONAL CLEARANCE PRICED SKUS.                          
      ******************************************************************        
       1000-PROCESS-RCP-SKUS.                                                   
                                                                                
           INITIALIZE                  FS065-RCP-SKU-LIST-REC.                  
           MOVE FS066-ITM-NBR          TO FS065-ITM-NBR.                        
           MOVE FS066-STR-LVL-STAT-CDE TO FS065-STR-LVL-STAT-CDE.               
           MOVE FS066-SUM-LVL-CDE      TO FS065-SUM-LVL-CDE.                    
           MOVE FS066-SKU              TO FS065-SKU.                            
           MOVE FS066-INTR-UPC-ID      TO FS065-INTR-UPC-ID.                    
           MOVE FS066-UNT-RTL-AMT      TO FS065-UNT-RTL-AMT.                    
           MOVE FS066-STR-NBR          TO FS065-STR-NBR.                        
      *                                   PV-9-5.                               
      *    MOVE PV-X-3                 TO PV-STR-NBR                            
           MOVE FS066-UNT-RTL-CHG-DTE  TO FS065-UNT-RTL-CHG-DTE.                
           MOVE FS066-STAT-CHG-DTE     TO FS065-STAT-CHG-DTE.                   
                                                                                
           EVALUATE TRUE                                                        
             WHEN FS066-TICKET-COLOR = 'W'                                      
               MOVE 'WHITE'            TO FS065-TICKET-COLOR                    
             WHEN FS066-TICKET-COLOR = 'Y'                                      
               MOVE 'YELLOW'           TO FS065-TICKET-COLOR                    
             WHEN FS066-TICKET-COLOR = 'R'                                      
               MOVE 'RED'              TO FS065-TICKET-COLOR                    
             WHEN OTHER                                                         
               MOVE 'UNKNOWN'          TO FS065-TICKET-COLOR                    
           END-EVALUATE.                                                        
                                                                                
           MOVE FS066-SKU-LVL-STAT-CDE TO FS065-SKU-LVL-STAT-CDE.               
                                                                                
      ******************************************************************        
      *    GET THIS WEEKS SALES                                                 
      ******************************************************************        
           MOVE PV-FSCL-BOW-DTE-THISW  TO PV-FSCL-BOW-DTE.                      
           PERFORM 7200-SELECT-TSKSTWKS-ROW.                                    
                                                                                
           MOVE SKSTWKS-SUN-SLS-UNIT-QTY                                        
                                       TO FS065-SUN-SIS-QTY.                    
           COMPUTE FS065-SUN-SIS-AMT =                                          
             FS065-SUN-SIS-QTY * FS065-UNT-RTL-AMT.                             
                                                                                
           MOVE SKSTWKS-MON-SLS-UNIT-QTY                                        
                                       TO FS065-MON-SIS-QTY.                    
           COMPUTE FS065-MON-SIS-AMT =                                          
             FS065-MON-SIS-QTY * FS065-UNT-RTL-AMT.                             
                                                                                
           MOVE SKSTWKS-TUE-SLS-UNIT-QTY                                        
                                       TO FS065-TUE-SIS-QTY.                    
           COMPUTE FS065-TUE-SIS-AMT =                                          
             FS065-TUE-SIS-QTY * FS065-UNT-RTL-AMT.                             
                                                                                
           MOVE SKSTWKS-WED-SLS-UNIT-QTY                                        
                                       TO FS065-WED-SIS-QTY                     
           COMPUTE FS065-WED-SIS-AMT =                                          
             FS065-WED-SIS-QTY * FS065-UNT-RTL-AMT.                             
                                                                                
           MOVE SKSTWKS-THU-SLS-UNIT-QTY                                        
                                       TO FS065-THU-SIS-QTY.                    
           COMPUTE FS065-THU-SIS-AMT =                                          
             FS065-THU-SIS-QTY * FS065-UNT-RTL-AMT.                             
                                                                                
           MOVE SKSTWKS-FRI-SLS-UNIT-QTY                                        
                                       TO FS065-FRI-SIS-QTY.                    
           COMPUTE FS065-FRI-SIS-AMT =                                          
             FS065-FRI-SIS-QTY * FS065-UNT-RTL-AMT.                             
                                                                                
           MOVE SKSTWKS-SAT-SLS-UNIT-QTY                                        
                                       TO FS065-SAT-SIS-QTY.                    
           COMPUTE FS065-SAT-SIS-AMT =                                          
             FS065-SAT-SIS-QTY * FS065-UNT-RTL-AMT.                             
                                                                                
                                                                                
      ******************************************************************        
      *    GET LAST WEEKS SALES                                                 
      ******************************************************************        
           MOVE PV-FSCL-BOW-DTE-LASTW  TO PV-FSCL-BOW-DTE.                      
           PERFORM 7200-SELECT-TSKSTWKS-ROW.                                    
                                                                                
           MOVE SKSTWKS-SUN-SLS-UNIT-QTY                                        
                                       TO FS065-PWK-SUN-SIS-QTY.                
           COMPUTE FS065-PWK-SUN-SIS-AMT =                                      
             FS065-PWK-SUN-SIS-QTY * FS065-UNT-RTL-AMT.                         
                                                                                
           MOVE SKSTWKS-MON-SLS-UNIT-QTY                                        
                                       TO FS065-PWK-MON-SIS-QTY.                
           COMPUTE FS065-PWK-MON-SIS-AMT =                                      
             FS065-PWK-MON-SIS-QTY * FS065-UNT-RTL-AMT.                         
                                                                                
           MOVE SKSTWKS-TUE-SLS-UNIT-QTY                                        
                                       TO FS065-PWK-TUE-SIS-QTY.                
           COMPUTE FS065-PWK-TUE-SIS-AMT =                                      
             FS065-PWK-TUE-SIS-QTY * FS065-UNT-RTL-AMT.                         
                                                                                
           MOVE SKSTWKS-WED-SLS-UNIT-QTY                                        
                                       TO FS065-PWK-WED-SIS-QTY.                
           COMPUTE FS065-PWK-WED-SIS-AMT =                                      
             FS065-PWK-WED-SIS-QTY * FS065-UNT-RTL-AMT.                         
                                                                                
           MOVE SKSTWKS-THU-SLS-UNIT-QTY                                        
                                       TO FS065-PWK-THU-SIS-QTY.                
           COMPUTE FS065-PWK-THU-SIS-AMT =                                      
             FS065-PWK-THU-SIS-QTY * FS065-UNT-RTL-AMT.                         
                                                                                
           MOVE SKSTWKS-FRI-SLS-UNIT-QTY                                        
                                       TO FS065-PWK-FRI-SIS-QTY.                
           COMPUTE FS065-PWK-FRI-SIS-AMT =                                      
             FS065-PWK-FRI-SIS-QTY * FS065-UNT-RTL-AMT.                         
                                                                                
           MOVE SKSTWKS-SAT-SLS-UNIT-QTY                                        
                                       TO FS065-PWK-SAT-SIS-QTY.                
           COMPUTE FS065-PWK-SAT-SIS-AMT =                                      
             FS065-PWK-SAT-SIS-QTY * FS065-UNT-RTL-AMT.                         
           IF  FS065-SUN-SIS-QTY      = ZERO                                    
           AND FS065-MON-SIS-QTY      = ZERO                                    
           AND FS065-TUE-SIS-QTY      = ZERO                                    
           AND FS065-WED-SIS-QTY      = ZERO                                    
           AND FS065-THU-SIS-QTY      = ZERO                                    
           AND FS065-FRI-SIS-QTY      = ZERO                                    
           AND FS065-SAT-SIS-QTY      = ZERO                                    
           AND FS065-PWK-SUN-SIS-QTY  = ZERO                                    
           AND FS065-PWK-MON-SIS-QTY  = ZERO                                    
           AND FS065-PWK-TUE-SIS-QTY  = ZERO                                    
           AND FS065-PWK-WED-SIS-QTY  = ZERO                                    
           AND FS065-PWK-THU-SIS-QTY  = ZERO                                    
           AND FS065-PWK-FRI-SIS-QTY  = ZERO                                    
           AND FS065-PWK-SAT-SIS-QTY  = ZERO                                    
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 8200-WRITE-OUTPUT-REC.                                   
           PERFORM 8100-READ-RCP-SKU-FILE.                                      
                                                                                
      ******************************************************************        
       7100-SELECT-TDTATTR-DTL.                                                 
                                                                                
           EXEC SQL                                                             
               SELECT KY_DTE,                                                   
                      FSCL_WK_BEG_DTE,                                          
                      FSCL_WK_BEG_DTE - 7 DAYS                                  
               INTO :DTATTR-KY-DTE,                                             
                    :PV-FSCL-BOW-DTE-THISW,                                     
                    :PV-FSCL-BOW-DTE-LASTW                                      
               FROM TDTATTR                                                     
               WHERE KY_DTE = :CC-PROCESS-DATE                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
       7200-SELECT-TSKSTWKS-ROW.                                                
                                                                                
           INITIALIZE                  DCLTSKSTWKS.                             
                                                                                
           EXEC SQL                                                             
               SELECT ITEM_NMBR,                                                
                      STR_NBR,                                                  
                      FSCL_BOW_DTE,                                             
                      SUM(SUN_SLS_UNIT_QTY),                                    
                      SUM(MON_SLS_UNIT_QTY),                                    
                      SUM(TUE_SLS_UNIT_QTY),                                    
                      SUM(WED_SLS_UNIT_QTY),                                    
                      SUM(THU_SLS_UNIT_QTY),                                    
                      SUM(FRI_SLS_UNIT_QTY),                                    
                      SUM(SAT_SLS_UNIT_QTY)                                     
               INTO :SKSTWKS-ITEM-NMBR,                                         
                    :SKSTWKS-STR-NBR,                                           
                    :SKSTWKS-FSCL-BOW-DTE,                                      
                    :SKSTWKS-SUN-SLS-UNIT-QTY:NI-SUN-SLS-UNIT-QTY,              
                    :SKSTWKS-MON-SLS-UNIT-QTY:NI-MON-SLS-UNIT-QTY,              
                    :SKSTWKS-TUE-SLS-UNIT-QTY:NI-TUE-SLS-UNIT-QTY,              
                    :SKSTWKS-WED-SLS-UNIT-QTY:NI-WED-SLS-UNIT-QTY,              
                    :SKSTWKS-THU-SLS-UNIT-QTY:NI-THU-SLS-UNIT-QTY,              
                    :SKSTWKS-FRI-SLS-UNIT-QTY:NI-FRI-SLS-UNIT-QTY,              
                    :SKSTWKS-SAT-SLS-UNIT-QTY:NI-SAT-SLS-UNIT-QTY               
               FROM TSKSTWKS                                                    
               GROUP BY ITEM_NMBR, STR_NBR, FSCL_BOW_DTE                        
               HAVING ITEM_NMBR  = :FS065-ITM-NBR                               
               AND STR_NBR       = :FS065-STR-NBR                               
               AND FSCL_BOW_DTE  = :PV-FSCL-BOW-DTE                             
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
               ADD +1                  TO PT-TSKSTWKS-RCD-FND                   
      *        CONTINUE                                                         
             WHEN SQLCODE = +100                                                
               ADD +1                  TO PT-TSKSTWKS-RCD-NOT-FND               
               MOVE ZERO               TO SKSTWKS-SUN-SLS-UNIT-QTY              
               MOVE ZERO               TO SKSTWKS-MON-SLS-UNIT-QTY              
               MOVE ZERO               TO SKSTWKS-TUE-SLS-UNIT-QTY              
               MOVE ZERO               TO SKSTWKS-WED-SLS-UNIT-QTY              
               MOVE ZERO               TO SKSTWKS-THU-SLS-UNIT-QTY              
               MOVE ZERO               TO SKSTWKS-FRI-SLS-UNIT-QTY              
               MOVE ZERO               TO SKSTWKS-SAT-SLS-UNIT-QTY              
             WHEN OTHER                                                         
               MOVE '7300-SELECT-TSKSTWKS-ROW'                                  
                                       TO PV-PARAGRAPH-NAME                     
               MOVE 'SELECT TSKSTWKS'  TO PV-OPER-ATTEMPTED                     
               PERFORM 9998-DB2-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
       7600-SELECT-DATE-TIME.                                                   
                                                                                
           EXEC SQL                                                             
             SELECT CURRENT DATE,                                               
                    CURRENT TIME                                                
             INTO :SDT-YYYYMMDD,                                                
                  :SDT-HHMMSS                                                   
             FROM SYSIBM.SYSDUMMY1                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
               CONTINUE                                                         
             WHEN OTHER                                                         
               MOVE '7600-SELECT-DATE-TIME'                                     
                                       TO PV-PARAGRAPH-NAME                     
               MOVE 'SELECT SYSDUMMY1' TO PV-OPER-ATTEMPTED                     
               PERFORM 9998-DB2-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      /*****************************************************************        
      *                                                                         
      ******************************************************************        
       8000-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  RCP-SKU-FILE                                             
                OUTPUT RCP-SUMMARY-FILE-OUT.                                    
                                                                                
      ******************************************************************        
      * READ THE NEXT RECORD ON THE RCP-SKU-FILE.                               
      ******************************************************************        
       8100-READ-RCP-SKU-FILE.                                                  
                                                                                
           READ RCP-SKU-FILE                                                    
             INTO FS066-RCP-SKU-LIST-REC                                        
               AT END                                                           
                 SET PS-RCP-SKU-EOF    TO TRUE                                  
               NOT AT END                                                       
                   ADD +1              TO PT-SKUS-READ-CNT                      
      *                                   PT-DISPLAY-CNTR                       
      *            IF PT-DISPLAY-CNTR-CNT                                       
      *              DISPLAY 'FSKUT033 - '                                      
      *                ' NBR RCDS READ '                                        
      *                PT-SKUS-READ-CNT ' ITEM NBR '                            
      *                FS065-ITM-NBR ' LOC NBR ' FS065-STR-NBR                  
      *              MOVE +0           TO PT-DISPLAY-CNTR                       
      *            END-IF                                                       
           END-READ.                                                            
                                                                                
      ******************************************************************        
      *    WRITE SUMMARY RECORDS - ONE RECORD FOR ALL STORES                    
      ******************************************************************        
       8200-WRITE-OUTPUT-REC.                                                   
                                                                                
           WRITE RCP-SUMMARY-REC                                                
             FROM FS065-RCP-SKU-LIST-REC.                                       
                                                                                
           ADD +1                      TO PT-RCD-WRTTN.                         
                                                                                
      ******************************************************************        
      *    CLOSE THE FILES.                                                     
      ******************************************************************        
       8900-CLOSE-FILES.                                                        
                                                                                
           CLOSE RCP-SKU-FILE                                                   
                 RCP-SUMMARY-FILE-OUT.                                          
                                                                                
      /*****************************************************************        
      *  FORMAT A DB2 ERROR MESSAGE & ABEND THE PROGRAM                *        
      ******************************************************************        
       9998-DB2-ABEND.                                                          
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '** ABEND **'.                                               
           DISPLAY '** PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                     
           DISPLAY '** OPERATION **  = ' PV-OPER-ATTEMPTED.                     
           DISPLAY '** FSKUT033 - SQL ERROR'.                                   
           MOVE SQLCODE TO SQLCODE2.                                            
           MOVE SQLERRD(3) TO SQLERRD3.                                         
           DISPLAY '**       SQLCODE = ' SQLCODE2.                              
           DISPLAY '**      SQLWARN0 = ' SQLWARN0.                              
           DISPLAY '**      SQLWARN1 = ' SQLWARN1.                              
           DISPLAY '**      SQLWARN2 = ' SQLWARN2.                              
           DISPLAY '**      SQLWARN3 = ' SQLWARN3.                              
           DISPLAY '**      SQLWARN4 = ' SQLWARN4.                              
           DISPLAY '**      SQLWARN5 = ' SQLWARN5.                              
           DISPLAY '**      SQLWARN6 = ' SQLWARN6.                              
           DISPLAY '**      SQLWARN7 = ' SQLWARN7.                              
           DISPLAY '**********************************'.                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      *  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM.         
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-OPER-ATTEMPTED.                    
           DISPLAY '**********************************'.                        
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
