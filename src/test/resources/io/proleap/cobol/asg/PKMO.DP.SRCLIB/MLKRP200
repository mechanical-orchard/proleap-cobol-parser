000100 IDENTIFICATION DIVISION.                                                 
000200                                                                          
000300 PROGRAM-ID.    MLKRP200.                                                 
000400 AUTHOR.        TESS BESTE.                                               
000500 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000600 DATE-WRITTEN.  04/15/98.                                                 
000700 DATE-COMPILED.                                                           
000800                                                                          
000900******************************************************************        
001000*     MLKRP200 - DEPARTMENT-LEVEL MERCHANDISE LEDGER REPORT               
001100*                                                                         
001200* THIS PROGRAM READS AN EXTRACT FROM THE DEPARTMENT-LEVEL MTD             
001300* MERCHANDISE LEDGER TABLE AND PRODUCES THE DEPARTMENT-LEVEL              
001400* MERCHANDISE LEDGER REPORT.                                              
001500*                                                                         
001600* TOTALS ARE REPORTED FOR THE FOLLOWING LEVELS:                           
001700*       DEPARTMENT                                                        
001800*       BUYER                                                             
001900*       MANAGER  (DMM)                                                    
002000*       DIVISION (GMM)                                                    
002100*       COMPANY                                                           
002200*                                                                         
002300* AN ADJUSTMENT LINE IS ALSO PRINTED TO SHOW THE INVENTORY                
002400* ADJUSTMENT TO BOOK INVENTORY AFTER A PHYSICAL INVENTORY.                
002500*                                                                         
002600* THIS PROGRAM WAS CREATED FROM MLKRP015, WHICH PRODUCED THE              
002700* DEPARTMENT-LEVEL MERCHANDISE LEDGER REPORT FROM AN EXTRACT              
002800* FROM THE MERCHANDISE LEDGER IMS DATABASES.                              
002900*                                                                         
WR5701******************************************************************        
WR5701*   DATE : 12/10/98       CHANGE ID : WR5701     WHO :TKMA372             
WR5701*   REASON FOR CHANGE : 'PURCHASES' COLUMN OBSOLETED.                     
WR5701*    REPLACED WITH 'RECEIPTS' AND 'PURCHASE ADJ' COST & RETAIL.           
WR5701*    'B.I.' LINE ONLY DISPLAYED FOR INITIAL MONTH AS PREV END-INV.        
WR5701******************************************************************        
003000*   12/28/99             WR#5701               JIM SHIFF         *        
      *   REMOVE REFERENCE TO LASER PRINTING FUNCTION                  *        
      *                                                                *        
W26603******************************************************************        
W26603* CHG ID   DATE        DESCRIPTION                               *        
W26603* ------   ----------  ----------------------------------------- *        
W26603* W26603   03-17-2004  STORE EXPANSION.  EXPANDED LRECL OF OUT01 *        
W26603*                      FILE BECAUSE OF CHANGES TO MLRD102.       *        
W26603*                      EXPANDED WORKING STORAGE VARIABLES.       *        
W26603*                      ADD 'WITH UR' TO DB2 SELECTS.             *        
W26603*                      AT THIS TIME WE ARE NOT EXPANDING THE     *        
W26603*                      REPORT FIELDS.                            *        
W26603*                      - RONNA MCDONALD                          *        
C14666* CRQ000000014666   07-10-2012     RAVIKUMAR GUNTHA              *        
C14666*                      CHANGES MADE TO SHOW SHORTAGE SHRINK      *        
C14666*                      PERCENTAGE WITH 2 POSITIONS TO THE RIGHT  *        
C14666*                      OF THE DECIMAL POINT AND TO SHIFT         *        
C14666*                      EVERYTHING IN THE REPORT AFTER 'SHK%' BY 1*        
C14666*                      POSITION.                                 *        
222725* CHG0222725        04-29-2019                                   *        
222725*                      CHANGES MADE TO PICK ONLY THE BUYER       *        
222725*                      CREATED  UNDER 'DPT' IN TMDSEAR TABLE     *        
      ******************************************************************        
003100                                                                          
003200 ENVIRONMENT DIVISION.                                                    
003300                                                                          
003400                                                                          
003500 CONFIGURATION SECTION.                                                   
003600                                                                          
003700 SOURCE-COMPUTER.        IBM-3090.                                        
003800 OBJECT-COMPUTER.        IBM-3090.                                        
003900                                                                          
004000                                                                          
004100 INPUT-OUTPUT SECTION.                                                    
004200                                                                          
004300 FILE-CONTROL.                                                            
004400                                                                          
004500     SELECT LEDGER-EXTRACT-FILE                                           
004600         ASSIGN TO INP01.                                                 
004700                                                                          
004800     SELECT LEDGER-EXTRACT-DATE-FILE                                      
004900         ASSIGN TO INP02.                                                 
005000                                                                          
005100     SELECT LEDGER-REPORT-FILE                                            
005200         ASSIGN RPT01.                                                    
005300                                                                          
005400                                                                          
005500                                                                          
005600 DATA DIVISION.                                                           
005700                                                                          
005800                                                                          
005900 FILE SECTION.                                                            
006000                                                                          
006100 FD  LEDGER-EXTRACT-FILE                                                  
006200     RECORDING MODE IS F                                                  
006300     LABEL RECORDS ARE STANDARD                                           
006400     BLOCK CONTAINS 0 RECORDS.                                            
006500                                                                          
W26603 01  LEDGER-EXTRACT-RECORD            PIC  X(1288).                       
006700                                                                          
006800 FD  LEDGER-EXTRACT-DATE-FILE                                             
006900     RECORDING MODE IS F                                                  
007000     LABEL RECORDS ARE STANDARD                                           
007100     BLOCK CONTAINS 0 RECORDS.                                            
007200                                                                          
007300 01  LEDGER-EXTRACT-DATE-RECORD       PIC X(80).                          
007400                                                                          
007500 FD  LEDGER-REPORT-FILE                                                   
007600     RECORDING MODE IS F                                                  
007700     LABEL RECORDS ARE STANDARD                                           
007800     BLOCK CONTAINS 0 RECORDS.                                            
007900                                                                          
008000 01  LEDGER-REPORT-RECORD             PIC  X(175).                        
008100                                                                          
008200                                                                          
008300 WORKING-STORAGE SECTION.                                                 
008400                                                                          
008500*****************************************************************         
008600* RECORD LAYOUT FOR LEDGER EXTRACT FILE                                   
008700*****************************************************************         
008800                                                                          
008900     COPY MLRD102.                                                        
009000                                                                          
009100****************************************************************          
009200* RECORD LAYOUT FOR LEDGER EXTRACT DATE RECORD                            
009300****************************************************************          
009400                                                                          
009500     COPY MLRD114.                                                        
009600                                                                          
009700*****************************************************************         
WR5701* 150 CHARACTER REPORT LAYOUT                                             
009900*****************************************************************         
010000                                                                          
WR5701*    COPY DPWS150O.                                                       
010200                                                                          
010300*****************************************************************         
010400*    WORKING STORAGE AREA FOR ROUTINE TO FORMAT NAME                      
010500*****************************************************************         
010600                                                                          
010700     COPY DPWS024.                                                        
010800                                                                          
010900******************************************************************        
011000*  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004                
011100******************************************************************        
011200                                                                          
011300     COPY DPWS004.                                                        
011400                                                                          
011500 01  PC-PROGRAM-CONSTANTS.                                                
011600     05  PC-COMPANY-TOTALS            PIC  X(22)     VALUE                
011700                                      '*** COMPANY TOTALS ***'.           
011800     05  PC-DIVISION-TOTALS           PIC  X(23)     VALUE                
011900                                      '*** DIVISION TOTALS ***'.          
012000     05  PC-MANAGER-TOTALS            PIC  X(22)     VALUE                
012100                                      '*** MANAGER TOTALS ***'.           
012200     05  PC-BUYER-TOTALS              PIC  X(20)     VALUE                
012300                                      '*** BUYER TOTALS ***'.             
012400     05  PC-DEPT-TOTALS               PIC  X(19)     VALUE                
012500                                      '*** DEPT TOTALS ***'.              
012600                                                                          
012700     05  PC-DEPT-LVL                  PIC S9(04)     COMP SYNC            
012800                                                     VALUE +1.            
012900     05  PC-BYR-LVL                   PIC S9(04)     COMP SYNC            
013000                                                     VALUE +2.            
013100     05  PC-DMA-LVL                   PIC S9(04)     COMP SYNC            
013200                                                     VALUE +3.            
013300     05  PC-GMA-LVL                   PIC S9(04)     COMP SYNC            
013400                                                     VALUE +4.            
013500     05  PC-OPERCO-LVL                PIC S9(04)     COMP SYNC            
013600                                                     VALUE +5.            
013700                                                                          
013800     05  PC-MDSELV-GMA                PIC  X(03)     VALUE 'GMA'.         
013900     05  PC-MDSELV-DMA                PIC  X(03)     VALUE 'DMA'.         
014000     05  PC-MDSELV-DPT                PIC  X(03)     VALUE 'DPT'.         
014100                                                                          
014200     05  PC-RSPLVL-CDE-GMM            PIC  X(03)     VALUE 'GMM'.         
014300     05  PC-RSPLVL-CDE-DMM            PIC  X(03)     VALUE 'DMM'.         
014400     05  PC-RSPLVL-CDE-BYR            PIC  X(03)     VALUE 'BYR'.         
014500                                                                          
014600     05  PC-THOUSAND                  PIC S9(04)V99  COMP-3               
014700                                                     VALUE +1000.         
014800     05  PC-DEPT-NBR-000              PIC  X(03)     VALUE '000'.         
014900     05  PC-OPERCO-NBR                PIC  X(02)     VALUE '03'.          
015000     05  PC-APLSYS-CDE                PIC  X(02)     VALUE 'ML'.          
015100     05  PC-2-SPACES-LIT              PIC  X(02)     VALUE '  '.          
015200     05  PC-PAREN-SPACE-LIT           PIC  X(02)     VALUE ') '.          
015300                                                                          
015400 01  PS-PROGRAM-SWITCHES.                                                 
015500     05  PS-END-OF-FSCL-MN-NM-CSR-SW   PIC X     VALUE 'N'.               
015600         88 PS-END-OF-FSCL-MN-NM-CSR             VALUE 'Y'.               
015700     05  PS-END-OF-LEDGER-EXTRACT-SW   PIC X     VALUE 'N'.               
015800         88 PS-END-OF-LEDGER-EXTRACT             VALUE 'Y'.               
015900     05  PS-END-OF-LEDGER-DATE-FILE-SW PIC X     VALUE 'N'.               
016000         88 PS-END-OF-LEDGER-DATE-FILE           VALUE 'Y'.               
016100                                                                          
016200     05  PS-BREAK-LEVEL-SW             PIC X     VALUE SPACE.             
016300         88  PS-BREAK-LEVEL-SW-INIT              VALUE ' '.               
016400         88  PS-BREAK-IS-OPERCO                  VALUE 'C'.               
016500         88  PS-BREAK-IS-GMA                     VALUE 'G'.               
016600         88  PS-BREAK-IS-DMA                     VALUE 'V'.               
016700         88  PS-BREAK-IS-BYR                     VALUE 'B'.               
016800         88  PS-BREAK-IS-DEPT                    VALUE 'D'.               
016900                                                                          
017000 01  PV-PROGRAM-VARIABLES.                                                
017100     05  PV-AMT-RNDED                 PIC S9(11)V9   COMP-3               
017200                                                     VALUE ZERO.          
017300     05  PV-PCT                       PIC S9(04)V9   COMP-3               
017400                                                     VALUE ZERO.          
C14666     05  PV-SHNK-PCT                  PIC S9(03)V99  COMP-3               
C14666                                                     VALUE ZERO.          
017500     05  PV-BEG-INV-COST-AMT          PIC S9(11)V99  COMP-3               
017600                                                     VALUE ZERO.          
017700     05  PV-BEG-INV-RTL-AMT           PIC S9(11)V99  COMP-3               
017800                                                     VALUE ZERO.          
017900     05  PV-DEPT-NBR                  PIC  X(03)     VALUE SPACE.         
018000     05  PV-GMA-NBR                   PIC  X(02)     VALUE SPACE.         
018100     05  PV-DMA-NBR                   PIC  X(02)     VALUE SPACE.         
018200     05  PV-BYR-NBR                   PIC  X(03)     VALUE SPACE.         
018300     05  PV-MDSELV-CDE                PIC  X(03)     VALUE SPACE.         
018400     05  PV-NAME-WITH-PARENS.                                             
018500         10  PV-NAME-PAREN-1          PIC  X(02)     VALUE ' ('.          
018600         10  PV-ENTIRE-NAME           PIC  X(20).                         
018700         10  PV-NAME-PAREN-2          PIC  X(02)     VALUE SPACE.         
018800     05  PV-DESC                      PIC  X(25)     VALUE SPACE.         
018900     05  PV-NAME                      PIC  X(24)     VALUE SPACE.         
019000     05  PV-PAGE-CNT                  PIC S9(05)     COMP-3               
019100                                                     VALUE ZERO.          
019200     05  PV-PARAGRAPH-NAME            PIC  X(30)     VALUE SPACE.         
019300     05  PV-DB2-OPER-ATTEMPTED        PIC  X(30)     VALUE SPACE.         
019400     05  PV-ABEND-CODE                PIC S9(04)     COMP                 
019500                                                     VALUE ZERO.          
019600                                                                          
019700 01  SS-SUBSCRIPTS.                                                       
019800     05  SS-OPEN-MN                   PIC S9(04)     COMP SYNC            
019900                                                     VALUE ZERO.          
020000     05  SS-CURR-MN                   PIC S9(04)     COMP SYNC            
020100                                                     VALUE ZERO.          
020200     05  SS-SEASON                    PIC S9(04)     COMP SYNC            
020300                                                     VALUE ZERO.          
020400     05  SS-CURR-LVL                  PIC S9(04)     COMP SYNC            
020500                                                     VALUE ZERO.          
020600     05  SS-NEXT-LVL                  PIC S9(04)     COMP SYNC            
020700                                                     VALUE ZERO.          
020800                                                                          
020900                                                                          
021000 01  AT-ACCUMULATION-TABLE.                                               
021100     05  AT-BUSINESS-LEVEL-TOTALS     OCCURS  5.                          
W26603         10  AT-BEG-INV-COST-AMT      PIC S9(13)V99  COMP-3.              
W26603         10  AT-BEG-INV-RTL-AMT       PIC S9(13)V99                       
021400                                                     COMP-3.              
021500         10  AT-MONTH-TOTALS          OCCURS 12.                          
W26603             15  AT-PRCH-NET-COST-AMT PIC S9(13)V99  COMP-3.              
W26603             15  AT-PRCH-RTL-AMT      PIC S9(13)V99  COMP-3.              
W26603             15  AT-RCPT-NET-COST-AMT PIC S9(13)V99  COMP-3.              
W26603             15  AT-RCPT-RTL-AMT      PIC S9(13)V99  COMP-3.              
W26603             15  AT-PADJ-NET-COST-AMT PIC S9(13)V99  COMP-3.              
W26603             15  AT-PADJ-RTL-AMT      PIC S9(13)V99  COMP-3.              
W26603             15  AT-SLS-RTL-AMT       PIC S9(13)V99  COMP-3.              
W26603             15  AT-SHNK-RTL-AMT      PIC S9(13)V99  COMP-3.              
W26603             15  AT-INV-ADJ-RTL-AMT   PIC S9(13)V99  COMP-3.              
W26603             15  AT-MKDN-RTL-AMT      PIC S9(13)V99  COMP-3.              
W26603             15  AT-END-INV-COST-AMT  PIC S9(13)V99  COMP-3.              
W26603             15  AT-END-INV-RTL-AMT   PIC S9(13)V99  COMP-3.              
W26603             15  AT-SLS-COST-AMT      PIC S9(13)V99  COMP-3.              
W26603             15  AT-GRO-MRGN-AMT      PIC S9(13)V99  COMP-3.              
022600                                                                          
022700         10  AT-SEASON-TOTALS         OCCURS  2.                          
022800             15  AT-SEAS-PRCH-NET-COST-AMT                                
W26603                                      PIC S9(13)V99  COMP-3.              
W26603             15  AT-SEAS-PRCH-RTL-AMT PIC S9(13)V99  COMP-3.              
WR5701             15  AT-SEAS-RCPT-NET-COST-AMT                                
W26603                                      PIC S9(13)V99  COMP-3.              
W26603             15  AT-SEAS-RCPT-RTL-AMT PIC S9(13)V99  COMP-3.              
WR5701             15  AT-SEAS-PADJ-NET-COST-AMT                                
W26603                                      PIC S9(13)V99  COMP-3.              
W26603             15  AT-SEAS-PADJ-RTL-AMT PIC S9(13)V99  COMP-3.              
W26603             15  AT-SEAS-SLS-RTL-AMT  PIC S9(14)V99  COMP-3.              
W26603             15  AT-SEAS-SHNK-RTL-AMT PIC S9(12)V99  COMP-3.              
W26603             15  AT-SEAS-MKDN-RTL-AMT PIC S9(13)V99  COMP-3.              
W26603             15  AT-SEAS-SLS-COST-AMT PIC S9(13)V99  COMP-3.              
W26603             15  AT-SEAS-GRO-MRGN-AMT PIC S9(13)V99  COMP-3.              
023600                                                                          
023700 01  YTD-YEAR-TO-DATE-TOTALS.                                             
W26603     05  YTD-SLS-RTL-AMT              PIC S9(13)V99  COMP-3               
023900                                                     VALUE ZERO.          
W26603     05  YTD-SHNK-RTL-AMT             PIC S9(13)V99  COMP-3               
024100                                                     VALUE ZERO.          
W26603     05  YTD-MKDN-RTL-AMT             PIC S9(13)V99  COMP-3               
024300                                                     VALUE ZERO.          
W26603     05  YTD-SLS-COST-AMT             PIC S9(13)V99  COMP-3               
024500                                                     VALUE ZERO.          
W26603     05  YTD-GRO-MRGN-AMT             PIC S9(13)V99  COMP-3               
024700                                                     VALUE ZERO.          
W26603     05  YTD-PRCH-NET-COST-AMT        PIC S9(13)V99  COMP-3               
024900                                                     VALUE ZERO.          
W26603     05  YTD-PRCH-RTL-AMT             PIC S9(13)V99  COMP-3               
025100                                                     VALUE ZERO.          
W26603     05  YTD-RCPT-NET-COST-AMT        PIC S9(13)V99  COMP-3               
WR5701                                                     VALUE ZERO.          
W26603     05  YTD-RCPT-RTL-AMT             PIC S9(13)V99  COMP-3               
WR5701                                                     VALUE ZERO.          
W26603     05  YTD-PADJ-NET-COST-AMT        PIC S9(13)V99  COMP-3               
WR5701                                                     VALUE ZERO.          
W26603     05  YTD-PADJ-RTL-AMT             PIC S9(13)V99  COMP-3               
WR5701                                                     VALUE ZERO.          
025200                                                                          
025300 01  MT-MONTH-NAME-TABLE.                                                 
025400     05  MT-FSCL-MN-NM-ENTRY          OCCURS 12.                          
025500         10  MT-FSCL-MN-NM.                                               
025600             15  MT-FSCL-MN-NM-ABBREV PIC  X(03)     VALUE SPACE.         
025700             15  FILLER               PIC  X(06)     VALUE SPACE.         
025800                                                                          
025900 01  HN-HOLD-NUMBERS.                                                     
026000     05  HN-GMA-NBR                   PIC  X(02)     VALUE ZERO.          
026100     05  HN-DMA-NBR                   PIC  X(02)     VALUE ZERO.          
026200     05  HN-BYR-NBR                   PIC  X(03)     VALUE ZERO.          
026300     05  HN-DEPT-NBR                  PIC  X(03)     VALUE ZERO.          
026400                                                                          
026500*01  LC-LASER-CONTROL-SWITCHES.                                           
026600*    05  LC-LASER-CONTROL-FF.                                             
026700*        10  FILLER                   PIC S9(04)     COMP                 
026800*                                                    VALUE -1.            
026900*    05  LC-LASER-CONTROL-FD.                                             
027000*        10  FILLER                   PIC S9(04)     COMP                 
027100*                                                    VALUE -513.          
027200*        10  FILLER                   PIC  X(79)     VALUE SPACE.         
027300*    05  LC-LASER-BREAK-SW            PIC  X         VALUE 'N'.           
027400*        88  LC-LASER-BREAK                          VALUE 'Y'.           
027500                                                                          
027600 01  TD-TODAYS-DATE.                                                      
027700     05  TD-YEAR                      PIC  9(02)     VALUE ZERO.          
027800     05  TD-MONTH                     PIC  9(02)     VALUE ZERO.          
027900     05  TD-DAY                       PIC  9(02)     VALUE ZERO.          
028000                                                                          
028100 01  TT-TODAYS-TIME.                                                      
028200     05  TT-HOUR                      PIC  9(02)     VALUE ZERO.          
028300     05  TT-MINUTE                    PIC  9(02)     VALUE ZERO.          
028400     05  FILLER                       PIC  9(04)     VALUE ZERO.          
028500                                                                          
WR5701 01  DP175O-STANDRD-HEADER-175-COLS.                              12/03/92
00002      05  DP175O-LASER-NEW-PAGE-INDICATR    PIC X(02) VALUE SPACE. DPWS150O
00003      05  DP175O-RUN-DATE.                                            LV001
00004          10  DP175O-RUN-MONTH              PIC 9(02) VALUE ZERO.  DPWS150O
00005          10  FILLER                        PIC X(01) VALUE '/'.   DPWS150O
00006          10  DP175O-RUN-DAY                PIC 9(02) VALUE ZERO.  DPWS150O
00007          10  FILLER                        PIC X(01) VALUE '/'.   DPWS150O
00008          10  DP175O-RUN-YEAR               PIC 9(02) VALUE ZERO.  DPWS150O
00009      05  FILLER                            PIC X(02) VALUE SPACE. DPWS150O
00010      05  DP175O-RUN-TIME.                                         DPWS150O
00011          10  DP175O-RUN-HOUR               PIC 9(02) VALUE ZERO.  DPWS150O
00012          10  FILLER                        PIC X(01) VALUE ':'.   DPWS150O
00013          10  DP175O-RUN-MINUTE             PIC 9(02) VALUE ZERO.  DPWS150O
00014      05  FILLER                            PIC X(47) VALUE SPACE. DPWS150O
00015      05  FILLER                            PIC X(45)              DPWS150O
00016          VALUE 'K O H L S   D E P A R T M E N T   S T O R E S'.   DPWS150O
00017      05  FILLER                            PIC X(42) VALUE SPACE. DPWS150O
00018      05  DP175O-PROGRAM-NAME               PIC X(08) VALUE SPACE. DPWS150O
00019      05  FILLER                            PIC X(01) VALUE '-'.   DPWS150O
00020      05  DP175O-REPORT-NUMBER              PIC 9(01) VALUE ZERO.  DPWS150O
00021      05  FILLER                            PIC X(02) VALUE SPACE. DPWS150O
00022      05  FILLER                            PIC X(05) VALUE 'PAGE'.DPWS150O
00023      05  DP175O-PAGE-NUMBER                PIC ZZZZ9 VALUE ZERO.  DPWS150O
00024                                                                   DPWS150O
028600 01  PH2-PAGE-HEADING-2.                                                  
WR5701     05  FILLER                       PIC X(62) VALUE SPACE.              
028800     05  FILLER                       PIC X(08) VALUE                     
028900                                                   'MONTHLY '.            
029000     05  FILLER                       PIC X(12) VALUE                     
029100                                                   'MERCHANDISE '.        
029200     05  FILLER                       PIC X(07) VALUE                     
029300                                                   'LEDGER '.             
029400     05  FILLER                       PIC X(11) VALUE                     
029500                                                   'FOR FISCAL '.         
029600     05  PH2-FSCL-MN-NM               PIC X(09) VALUE SPACE.              
029700     05  FILLER                       PIC X(01) VALUE SPACE.              
029800     05  PH2-FSCL-YR-NBR              PIC X(04) VALUE SPACE.              
WR5701     05  FILLER                       PIC X(61) VALUE SPACE.              
030000                                                                          
030100 01  PH3-PAGE-HEADING-3.                                                  
WR5701     05  FILLER                       PIC X(84) VALUE SPACE.              
030300     05  FILLER                       PIC X(05) VALUE    '($000'.         
030400     05  FILLER                       PIC X(01) VALUE QUOTE.              
030500     05  FILLER                       PIC X(02) VALUE 'S)'.               
WR5701     05  FILLER                       PIC X(83) VALUE SPACE.              
030700                                                                          
030800 01  CH-COMPANY-HEADING.                                                  
030900     05  FILLER                       PIC X(33) VALUE                     
031000                              ' COMPANY: KOHLS DEPARTMENT STORES'.        
WR5701     05  FILLER                       PIC X(142) VALUE SPACE.             
031200                                                                          
031300 01  GH-GMA-HEADING.                                                      
031400     05  FILLER                       PIC X(06) VALUE '  GMM:'.           
031500     05  FILLER                       PIC X(02) VALUE SPACE.              
031600     05  GH-GMA-NBR                   PIC X(02) VALUE SPACE.              
031700     05  FILLER                       PIC X(01) VALUE '-'.                
031800     05  GH-GMA-DESC-GMM-NAME         PIC X(50) VALUE SPACE.              
WR5701     05  FILLER                       PIC X(89) VALUE SPACE.              
032000                                                                          
032100 01  DH-DMA-HEADING.                                                      
032200     05  FILLER                       PIC X(06) VALUE '  DMM:'.           
032300     05  FILLER                       PIC X(02) VALUE SPACE.              
032400     05  DH-DMA-NBR                   PIC X(02) VALUE SPACE.              
032500     05  FILLER                       PIC X(01) VALUE '-'.                
032600     05  DH-DMA-DESC-DMM-NAME         PIC X(50) VALUE SPACE.              
WR5701     05  FILLER                       PIC X(89) VALUE SPACE.              
032800                                                                          
032900 01  BH-BUYER-HEADING.                                                    
033000     05  FILLER                       PIC X(06) VALUE 'BUYER:'.           
WR5701     05  FILLER                       PIC X(01) VALUE SPACE.              
WR5701     05  BH-BYR-NBR                   PIC X(03) VALUE SPACE.              
033300     05  FILLER                       PIC X(01) VALUE '-'.                
033400     05  BH-BYR-NAME                  PIC X(31) VALUE SPACE.              
WR5701     05  FILLER                       PIC X(108) VALUE SPACE.             
033600                                                                          
033700 01  DH-DEPT-HEADING.                                                     
033800     05  FILLER                       PIC X(06) VALUE ' DEPT:'.           
033900     05  FILLER                       PIC X(01) VALUE SPACE.              
034000     05  DH-DEPT-NBR                  PIC 9(03) VALUE ZERO.               
034100     05  FILLER                       PIC X(01) VALUE '-'.                
034200     05  DH-DEPT-NAME                 PIC X(20) VALUE SPACE.              
WR5701     05  FILLER                       PIC X(116) VALUE SPACE.             
034400                                                                          
034500 01  DH1-DETAIL-HEADING-1.                                                
034600     05  FILLER                       PIC X(04) VALUE SPACE.              
034700     05  FILLER                       PIC X(09) VALUE                     
034800                                                     '<------- '.         
034900     05  FILLER                       PIC X(09) VALUE                     
035000                                                    'RECEIPTS '.          
035100     05  FILLER                       PIC X(08) VALUE                     
035200                                                     '------> '.          
034700     05  FILLER                       PIC X(07) VALUE                     
034800                                                     '<----- '.           
034900     05  FILLER                       PIC X(09) VALUE                     
035000                                                    'PURCHASE '.          
034900     05  FILLER                       PIC X(04) VALUE                     
035000                                                    'ADJ '.               
035100     05  FILLER                       PIC X(07) VALUE                     
035200                                                     '-----> '.           
035100     05  FILLER                       PIC X(06) VALUE SPACES.             
035400     05  FILLER                       PIC X(15) VALUE                     
035500                                                '<--------------'.        
035600     05  FILLER                       PIC X(19) VALUE                     
035700                                            ' RETAIL REDUCTIONS '.        
C14666     05  FILLER                       PIC X(16) VALUE                     
C14666                                               '--------------->'.        
036000     05  FILLER                       PIC X(01) VALUE SPACE.              
036100     05  FILLER                       PIC X(03) VALUE '<--'.              
036200     05  FILLER                       PIC X(18) VALUE                     
036300                                             ' ENDING INVENTORY '.        
036400     05  FILLER                       PIC X(04) VALUE '--->'.             
036500     05  FILLER                       PIC X(03) VALUE SPACE.              
036600     05  FILLER                       PIC X(07) VALUE 'COST OF'.          
036700     05  FILLER                       PIC X(08) VALUE SPACE.              
036800                                                                          
036900 01  DH2-DETAIL-HEADING-2.                                                
037000     05  FILLER                       PIC X(04) VALUE 'MNTH'.             
037100     05  FILLER                       PIC X(04) VALUE SPACE.              
WR5701     05  FILLER                       PIC X(04) VALUE 'COST'.             
WR5701     05  FILLER                       PIC X(07) VALUE SPACE.              
WR5701     05  FILLER                       PIC X(06) VALUE 'RETAIL'.           
WR5701     05  FILLER                       PIC X(09) VALUE SPACE.              
WR5701     05  FILLER                       PIC X(04) VALUE 'COST'.             
WR5701     05  FILLER                       PIC X(08) VALUE SPACE.              
WR5701     05  FILLER                       PIC X(06) VALUE 'RETAIL'.           
WR5701     05  FILLER                       PIC X(04) VALUE SPACE.              
WR5701     05  FILLER                       PIC X(05) VALUE 'MKON%'.            
WR5701     05  FILLER                       PIC X(06) VALUE SPACE.              
037700     05  FILLER                       PIC X(05) VALUE 'SALES'.            
037800     05  FILLER                       PIC X(05) VALUE SPACE.              
037900     05  FILLER                       PIC X(09) VALUE 'SHRINKAGE'.        
038000     05  FILLER                       PIC X(02) VALUE SPACE.              
038100     05  FILLER                       PIC X(05) VALUE 'SHK% '.            
038200     05  FILLER                       PIC X(03) VALUE SPACE.              
038300     05  FILLER                       PIC X(09) VALUE 'MARKDOWNS'.        
038400     05  FILLER                       PIC X(03) VALUE SPACE.              
038500     05  FILLER                       PIC X(05) VALUE 'MKDN%'.            
038600     05  FILLER                       PIC X(05) VALUE SPACE.              
038700     05  FILLER                       PIC X(04) VALUE 'COST'.             
038800     05  FILLER                       PIC X(08) VALUE SPACE.              
038900     05  FILLER                       PIC X(06) VALUE 'RETAIL'.           
039000     05  FILLER                       PIC X(07) VALUE SPACE.              
039100     05  FILLER                       PIC X(05) VALUE 'SALES'.            
039200     05  FILLER                       PIC X(08) VALUE SPACE.              
039300     05  FILLER                       PIC X(07) VALUE 'GM DLRS'.          
039400     05  FILLER                       PIC X(05) VALUE SPACE.              
039500     05  FILLER                       PIC X(03) VALUE 'GM%'.              
039600     05  FILLER                       PIC X(01) VALUE SPACE.              
039700                                                                          
039800 01  DL-DETAIL-LINE.                                                      
039900     05  DL-MONTH-ABBREV              PIC X(03) VALUE SPACES.             
040000     05  FILLER                       PIC X(01) VALUE SPACES.             
040100*    05  DL-PURCHASES-COST            PIC ZZ,ZZZ,ZZZ.Z-                   
040200*                                               VALUE ZEROES.             
040300*    05  DL-PURCHASES-RETAIL          PIC ZZ,ZZZ,ZZZ.Z-                   
040400*                                               VALUE ZEROES.             
WR5701     05  DL-RECEIPTS-COST             PIC ZZ,ZZZ,ZZZ.Z-                   
WR5701                                                VALUE ZEROES.             
WR5701     05  DL-RECEIPTS-RETAIL           PIC ZZ,ZZZ,ZZZ.Z-                   
WR5701                                                VALUE ZEROES.             
WR5701     05  DL-PURCHASE-ADJ-COST         PIC ZZ,ZZZ,ZZZ.Z-                   
WR5701                                                VALUE ZEROES.             
WR5701     05  DL-PURCHASE-ADJ-RETAIL       PIC ZZ,ZZZ,ZZZ.Z-                   
WR5701                                                VALUE ZEROES.             
WR5701     05  DL-MKON-PCT                  PIC ZZ9.9-                          
WR5701                                                VALUE ZEROES.             
040700     05  DL-SALES                     PIC ZZ,ZZZ,ZZZ.Z-                   
040800                                                VALUE ZEROES.             
040900     05  DL-SHRINKAGE                 PIC Z,ZZZ,ZZZ.Z-                    
041000                                                VALUE ZEROES.             
C14666     05  DL-SHNK-PCT                  PIC ZZ9.99-                         
041200                                                VALUE ZEROES.             
041300     05  DL-MARKDOWNS                 PIC ZZ,ZZZ,ZZZ.Z-                   
041400                                                VALUE ZEROES.             
041500     05  DL-MKDN-PCT                  PIC ZZ9.9-                          
041600                                                VALUE ZEROES.             
041700     05  DL-END-INV-COST              PIC ZZ,ZZZ,ZZZ.Z-                   
041800                                                VALUE ZEROES.             
041900     05  DL-END-INV-RETAIL            PIC ZZ,ZZZ,ZZZ.Z-                   
042000                                                VALUE ZEROES.             
042100     05  DL-COST-OF-SALES             PIC ZZ,ZZZ,ZZZ.Z-                   
042200                                                VALUE ZEROES.             
042300     05  DL-GM-DLRS                   PIC ZZ,ZZZ,ZZZ.Z-                   
042400                                                VALUE ZEROES.             
042500     05  FILLER                       PIC X(01) VALUE SPACES.             
042600     05  DL-GM-PCT                    PIC ZZ9.9-                          
042700                                                VALUE ZEROES.             
042800                                                                          
042900 01  UL-UNDER-LINE.                                                       
043000     05  FILLER                       PIC X(05) VALUE SPACES.             
043100     05  FILLER                       PIC X(12) VALUE                     
043100     '------------'.                                                      
043200     05  FILLER                       PIC X(01) VALUE SPACES.             
043200     05  FILLER                       PIC X(12) VALUE                     
043300     '------------'.                                                      
043300     05  FILLER                       PIC X(01) VALUE SPACES.             
043400     05  FILLER                       PIC X(12) VALUE                     
044400     '------------'.                                                      
044500     05  FILLER                       PIC X(01) VALUE SPACES.             
044500     05  FILLER                       PIC X(12) VALUE                     
044600     '------------'.                                                      
044600     05  FILLER                       PIC X(01) VALUE SPACES.             
043900     05  FILLER                       PIC X(05) VALUE                     
043800     '-----'.                                                             
043900     05  FILLER                       PIC X(01) VALUE SPACES.             
044000     05  FILLER                       PIC X(12) VALUE                     
044100     '------------'.                                                      
044200     05  FILLER                       PIC X(01) VALUE SPACES.             
044300     05  FILLER                       PIC X(11) VALUE                     
044400     '-----------'.                                                       
044500     05  FILLER                       PIC X(01) VALUE SPACES.             
C14666     05  FILLER                       PIC X(06) VALUE                     
C14666     '------'.                                                            
044800     05  FILLER                       PIC X(01) VALUE SPACES.             
044900     05  FILLER                       PIC X(12) VALUE                     
045000     '------------'.                                                      
045100     05  FILLER                       PIC X(01) VALUE SPACES.             
045200     05  FILLER                       PIC X(05) VALUE                     
045300     '-----'.                                                             
045400     05  FILLER                       PIC X(01) VALUE SPACES.             
045500     05  FILLER                       PIC X(12) VALUE                     
045600     '------------'.                                                      
045700     05  FILLER                       PIC X(01) VALUE SPACES.             
045800     05  FILLER                       PIC X(12) VALUE                     
045900     '------------'.                                                      
046000     05  FILLER                       PIC X(01) VALUE SPACES.             
046100     05  FILLER                       PIC X(12) VALUE                     
046200     '------------'.                                                      
046300     05  FILLER                       PIC X(01) VALUE SPACES.             
046400     05  FILLER                       PIC X(12) VALUE                     
046500     '------------'.                                                      
046600     05  FILLER                       PIC X(02) VALUE SPACES.             
046700     05  FILLER                       PIC X(05) VALUE                     
046800     '-----'.                                                             
046900                                                                          
047000                                                                          
047100****************************************************************          
047200* DATE ATTRIBUTE TABLE                                                    
047300****************************************************************          
047400                                                                          
047500     EXEC SQL                                                             
047600         INCLUDE TDTATTR                                                  
047700     END-EXEC.                                                            
047800                                                                          
047900******************************************************************        
048000*  MERCHANDISE AREA TABLE (TMDSEAR)                                       
048100******************************************************************        
048200                                                                          
048300     EXEC SQL                                                             
048400         INCLUDE TMDSEAR                                                  
048500     END-EXEC.                                                            
048600                                                                          
048700******************************************************************        
048800*  APPLICATION AVAILABILITY TABLE (TAPLAVL)                               
048900******************************************************************        
049000                                                                          
049100     EXEC SQL                                                             
049200         INCLUDE TAPLAVL                                                  
049300     END-EXEC.                                                            
049400                                                                          
049500******************************************************************        
049600*  RESPONSIBILITY AREA TABLE (TRSPBAR)                           *        
049700******************************************************************        
049800                                                                          
049900     EXEC SQL                                                             
050000         INCLUDE TRSPBAR                                                  
050100     END-EXEC.                                                            
050200                                                                          
050300                                                                          
050400******************************************************************        
050500*    DB2 COMMUNICATION AREA                                               
050600******************************************************************        
050700                                                                          
050800     EXEC SQL                                                             
050900         INCLUDE SQLCA                                                    
051000     END-EXEC.                                                            
051100                                                                          
051200****************************************************************          
051300*  DATE TABLE CURSOR                                                      
051400****************************************************************          
051500                                                                          
051600     EXEC SQL                                                             
051700         DECLARE FSCL-MN-NM-CSR CURSOR FOR                                
051800             SELECT DISTINCT                                              
051900                  FSCL_MN_NBR                                             
052000                 ,FSCL_MN_NM                                              
052100             FROM                                                         
052200                  TDTATTR                                                 
052300             WHERE KY_DTE BETWEEN :DTATTR-FSCL-YR-BEG-DTE                 
052400                              AND :ML114-EXTRACT-DTE                      
W26603             WITH UR                                                      
052500     END-EXEC.                                                            
052600                                                                          
052700                                                                          
052800                                                                          
052900 LINKAGE SECTION.                                                         
053000                                                                          
053100                                                                          
053200                                                                          
053300 PROCEDURE DIVISION.                                                      
053400                                                                          
053500                                                                          
053600 1000-MAIN-MODULE.                                                        
053700                                                                          
053800     OPEN INPUT  LEDGER-EXTRACT-FILE                                      
053900                 LEDGER-EXTRACT-DATE-FILE                                 
054000          OUTPUT LEDGER-REPORT-FILE.                                      
054100                                                                          
054200     PERFORM 1150-INIT.                                                   
054300                                                                          
054400     PERFORM 7000-READ-LEDGER-EXTRACT.                                    
054500                                                                          
054600     PERFORM 2000-PROCESS-EXTRACT                                         
054700         UNTIL PS-END-OF-LEDGER-EXTRACT.                                  
054800                                                                          
054900     SET PS-BREAK-IS-OPERCO TO TRUE.                                      
055000*    MOVE 'Y' TO LC-LASER-BREAK-SW.                                       
055100                                                                          
055200     PERFORM 2500-PROCESS-BREAKS.                                         
055300                                                                          
055400     CLOSE LEDGER-EXTRACT-FILE                                            
055500           LEDGER-EXTRACT-DATE-FILE                                       
055600           LEDGER-REPORT-FILE.                                            
055700                                                                          
055800     STOP RUN.                                                            
055900                                                                          
056000                                                                          
056100 1150-INIT.                                                               
056200                                                                          
056300     PERFORM 7100-READ-LEDGER-EXTRACT-DATE.                               
056400                                                                          
056500     IF PS-END-OF-LEDGER-DATE-FILE                                        
056600         DISPLAY '** ABEND **'                                            
056700         DISPLAY '** PROGRAM MLKRP200 **'                                 
056800         DISPLAY '** PARAGRAPH 1150-INIT'                                 
056900         DISPLAY '** LEDGER EXTRACT DATE FILE IS EMPTY.'                  
057000         MOVE +4003 TO PV-ABEND-CODE                                      
057100         PERFORM 9999-ABEND                                               
057200     END-IF.                                                              
057300                                                                          
057400     MOVE ML114-EXTRACT-MN-NBR       TO SS-OPEN-MN.                       
057500     MOVE ML114-EXTRACT-DTE          TO DTATTR-KY-DTE.                    
057600                                                                          
057700     PERFORM 8050-GET-DATE-ATTRIBUTES.                                    
057800                                                                          
057900     MOVE DTATTR-FSCL-YR-NBR         TO PH2-FSCL-YR-NBR.                  
058000     MOVE DTATTR-FSCL-MN-NM          TO PH2-FSCL-MN-NM.                   
058100                                                                          
058200*    TABLE THE MONTH NAMES OF THE MONTHS IN THE OPEN FISCAL               
058300*    YEAR.                                                                
058400                                                                          
058500     PERFORM 8500-OPEN-FSCL-MN-NM-CSR.                                    
058600                                                                          
058700     PERFORM 8600-FETCH-FSCL-MN-NM-CSR.                                   
058800                                                                          
058900     PERFORM UNTIL PS-END-OF-FSCL-MN-NM-CSR                               
059000         MOVE DTATTR-FSCL-MN-NBR TO SS-CURR-MN                            
059100         MOVE DTATTR-FSCL-MN-NM  TO MT-FSCL-MN-NM (SS-CURR-MN)            
059200         PERFORM 8600-FETCH-FSCL-MN-NM-CSR                                
059300     END-PERFORM.                                                         
059400                                                                          
059500     PERFORM 8700-CLOSE-FSCL-MN-NM-CSR.                                   
059600                                                                          
059700*    BUILD STANDARD REPORT HEADING.                                       
059800                                                                          
059900     ACCEPT TD-TODAYS-DATE FROM DATE.                                     
WR5701     MOVE TD-YEAR    TO DP175O-RUN-YEAR.                                  
WR5701     MOVE TD-MONTH   TO DP175O-RUN-MONTH.                                 
WR5701     MOVE TD-DAY     TO DP175O-RUN-DAY.                                   
060300                                                                          
060400     ACCEPT TT-TODAYS-TIME FROM TIME.                                     
WR5701     MOVE TT-HOUR    TO DP175O-RUN-HOUR.                                  
WR5701     MOVE TT-MINUTE  TO DP175O-RUN-MINUTE.                                
060700                                                                          
WR5701     MOVE 'MLKRP200' TO DP175O-PROGRAM-NAME.                              
WR5701     MOVE 1          TO DP175O-REPORT-NUMBER.                             
061000                                                                          
061100     INITIALIZE AT-BUSINESS-LEVEL-TOTALS (PC-OPERCO-LVL).                 
061200                                                                          
061300     MOVE +20        TO DP024-FLNAME-LENGTH.                              
061400                                                                          
061500                                                                          
061600 2000-PROCESS-EXTRACT.                                                    
061700                                                                          
061800******************************************************************        
061900*    PROCESS ALL GMAS IN THE COMPANY.  WHEN A NEW GMA IS                  
062000*    ENCOUNTERED, PRINT THE INFO FOR THE PREVIOUS GMA.                    
062100******************************************************************        
062200                                                                          
062300     MOVE ML102-GMA-NBR TO HN-GMA-NBR.                                    
062400     INITIALIZE AT-BUSINESS-LEVEL-TOTALS (PC-GMA-LVL).                    
062500                                                                          
062600     PERFORM 2100-PROCESS-GMA                                             
062700         UNTIL (ML102-GMA-NBR NOT = HN-GMA-NBR)                           
062800            OR (PS-END-OF-LEDGER-EXTRACT).                                
062900                                                                          
063000     SET PS-BREAK-IS-GMA TO TRUE.                                         
063100                                                                          
063200     PERFORM 2500-PROCESS-BREAKS.                                         
063300*    MOVE 'Y' TO LC-LASER-BREAK-SW.                                       
063400                                                                          
063500                                                                          
063600 2100-PROCESS-GMA.                                                        
063700                                                                          
063800******************************************************************        
063900*    PROCESS ALL DMAS WITHIN THIS GMA.  WHEN A NEW GMA OR DMA IS          
064000*    ENCOUNTERED, PRINT THE INFO FOR THE PREVIOUS DMA.                    
064100******************************************************************        
064200                                                                          
064300     MOVE ML102-DMA-NBR   TO HN-DMA-NBR.                                  
064400     INITIALIZE AT-BUSINESS-LEVEL-TOTALS (PC-DMA-LVL).                    
064500                                                                          
064600     PERFORM 2200-PROCESS-DMA                                             
064700         UNTIL (ML102-GMA-NBR NOT = HN-GMA-NBR)                           
064800            OR (ML102-DMA-NBR NOT = HN-DMA-NBR)                           
064900            OR  PS-END-OF-LEDGER-EXTRACT.                                 
065000                                                                          
065100     SET PS-BREAK-IS-DMA TO TRUE.                                         
065200                                                                          
065300     PERFORM 2500-PROCESS-BREAKS.                                         
065400*    MOVE 'Y' TO LC-LASER-BREAK-SW.                                       
065500                                                                          
065600                                                                          
065700 2200-PROCESS-DMA.                                                        
065800                                                                          
065900******************************************************************        
066000*    PROCESS ALL BYRS WITHIN THIS DMA.  WHEN A NEW GMA, DMA, OR           
066100*    BYR IS ENCOUNTERED, PRINT THE INFO FOR THE PREVIOUS BYR.             
066200******************************************************************        
066300                                                                          
066400     MOVE ML102-BYR-NBR TO HN-BYR-NBR.                                    
066500     INITIALIZE AT-BUSINESS-LEVEL-TOTALS (PC-BYR-LVL).                    
066600                                                                          
066700     PERFORM 2300-PROCESS-BYR                                             
066800         UNTIL (ML102-GMA-NBR NOT = HN-GMA-NBR)                           
066900            OR (ML102-DMA-NBR NOT = HN-DMA-NBR)                           
067000            OR (ML102-BYR-NBR NOT = HN-BYR-NBR)                           
067100            OR (PS-END-OF-LEDGER-EXTRACT).                                
067200                                                                          
067300     SET PS-BREAK-IS-BYR TO TRUE.                                         
067400                                                                          
067500     PERFORM 2500-PROCESS-BREAKS.                                         
067600*    MOVE 'Y' TO LC-LASER-BREAK-SW.                                       
067700                                                                          
067800                                                                          
067900 2300-PROCESS-BYR.                                                        
068000                                                                          
068100******************************************************************        
068200*    PROCESS ALL DEPTS WITHIN THIS BYR.  WHEN A NEW GMA, DMA,             
068300*    BYR, OR DEPT IS ENCOUNTERED, PRINT THE INFO FOR THE                  
068400*    PREVIOUS DEPT.                                                       
068500******************************************************************        
068600                                                                          
068700     MOVE ML102-DEPT-NBR TO HN-DEPT-NBR.                                  
068800     INITIALIZE AT-BUSINESS-LEVEL-TOTALS (PC-DEPT-LVL).                   
068900                                                                          
069000     PERFORM 3000-FILL-HEADINGS.                                          
069100                                                                          
069200     PERFORM 2400-PROCESS-DEPT                                            
069300         UNTIL (ML102-GMA-NBR  NOT = HN-GMA-NBR)                          
069400            OR (ML102-DMA-NBR  NOT = HN-DMA-NBR)                          
069500            OR (ML102-BYR-NBR  NOT = HN-BYR-NBR)                          
069600            OR (ML102-DEPT-NBR NOT = HN-DEPT-NBR)                         
069700            OR (PS-END-OF-LEDGER-EXTRACT).                                
069800                                                                          
069900     SET PS-BREAK-IS-DEPT TO TRUE.                                        
070000                                                                          
070100     PERFORM 2500-PROCESS-BREAKS.                                         
070200                                                                          
070300                                                                          
070400 2400-PROCESS-DEPT.                                                       
070500                                                                          
070600******************************************************************        
070700*    PROCESS THE MERCHANDISE LEDGER RECORD.  UPDATE THE                   
070800*    APPROPRIATE BUCKETS USING THE DEPARTMENT INFORMATION.                
070900******************************************************************        
071000                                                                          
071100     MOVE ML102-BEG-INV-COST-AMT                                          
071200          TO AT-BEG-INV-COST-AMT (PC-DEPT-LVL).                           
071300     MOVE ML102-BEG-INV-RTL-AMT                                           
071400          TO AT-BEG-INV-RTL-AMT  (PC-DEPT-LVL).                           
071500                                                                          
071600     PERFORM VARYING SS-CURR-MN FROM 1 BY 1                               
071700       UNTIL SS-CURR-MN > SS-OPEN-MN                                      
071800                                                                          
071900         COMPUTE AT-PRCH-NET-COST-AMT    (PC-DEPT-LVL SS-CURR-MN)         
072000               = ML102-RCPT-NET-COST-AMT (SS-CURR-MN)                     
072100               + ML102-PADJ-NET-COST-AMT (SS-CURR-MN)                     
072200         COMPUTE AT-PRCH-RTL-AMT         (PC-DEPT-LVL SS-CURR-MN)         
072300               = ML102-RCPT-RTL-AMT      (SS-CURR-MN)                     
072400               + ML102-PADJ-RTL-AMT      (SS-CURR-MN)                     
072500               + ML102-MKUP-RTL-AMT      (SS-CURR-MN)                     
072600               + ML102-XFER-IN-RTL-AMT   (SS-CURR-MN)                     
072700               - ML102-XFER-OUT-RTL-AMT  (SS-CURR-MN)                     
WR5701                                                                          
WR5701         MOVE ML102-RCPT-NET-COST-AMT    (SS-CURR-MN)                     
WR5701              TO AT-RCPT-NET-COST-AMT    (PC-DEPT-LVL SS-CURR-MN)         
WR5701         MOVE ML102-RCPT-RTL-AMT         (SS-CURR-MN)                     
WR5701              TO AT-RCPT-RTL-AMT         (PC-DEPT-LVL SS-CURR-MN)         
WR5701         MOVE ML102-PADJ-NET-COST-AMT    (SS-CURR-MN)                     
WR5701              TO AT-PADJ-NET-COST-AMT    (PC-DEPT-LVL SS-CURR-MN)         
WR5701         COMPUTE AT-PADJ-RTL-AMT         (PC-DEPT-LVL SS-CURR-MN)         
WR5701               = ML102-PADJ-RTL-AMT      (SS-CURR-MN)                     
WR5701               + ML102-MKUP-RTL-AMT      (SS-CURR-MN)                     
WR5701               + ML102-XFER-IN-RTL-AMT   (SS-CURR-MN)                     
WR5701               - ML102-XFER-OUT-RTL-AMT  (SS-CURR-MN)                     
WR5701                                                                          
072800         MOVE ML102-SLS-RTL-AMT          (SS-CURR-MN)                     
072900              TO AT-SLS-RTL-AMT          (PC-DEPT-LVL SS-CURR-MN)         
073000         MOVE ML102-SHNK-RTL-AMT         (SS-CURR-MN)                     
073100              TO AT-SHNK-RTL-AMT         (PC-DEPT-LVL SS-CURR-MN)         
073200         MOVE ML102-INV-ADJ-RTL-AMT      (SS-CURR-MN)                     
073300              TO AT-INV-ADJ-RTL-AMT      (PC-DEPT-LVL SS-CURR-MN)         
073400         MOVE ML102-MKDN-RTL-AMT         (SS-CURR-MN)                     
073500              TO AT-MKDN-RTL-AMT         (PC-DEPT-LVL SS-CURR-MN)         
073600         MOVE ML102-END-INV-RTL-AMT      (SS-CURR-MN)                     
073700              TO AT-END-INV-RTL-AMT      (PC-DEPT-LVL SS-CURR-MN)         
073800         MOVE ML102-END-INV-COST-AMT     (SS-CURR-MN)                     
073900              TO AT-END-INV-COST-AMT     (PC-DEPT-LVL SS-CURR-MN)         
074000         MOVE ML102-SLS-COST-AMT         (SS-CURR-MN)                     
074100              TO AT-SLS-COST-AMT         (PC-DEPT-LVL SS-CURR-MN)         
074200         MOVE ML102-GRO-MRGN-AMT         (SS-CURR-MN)                     
074300              TO AT-GRO-MRGN-AMT         (PC-DEPT-LVL SS-CURR-MN)         
074400     END-PERFORM.                                                         
074500                                                                          
074600     PERFORM 3200-ADD-TO-DEPT-SEASON-TOTS.                                
074700                                                                          
074800     PERFORM 7000-READ-LEDGER-EXTRACT.                                    
074900                                                                          
075000                                                                          
075100 2500-PROCESS-BREAKS.                                                     
075200                                                                          
075300*    DETERMINE WHICH BUSINES LEVEL THE BREAK IS OCCURRING ON AND          
075400*    SET THE LEVEL SUBSCRIPT ACCORDINGLY.                                 
075500                                                                          
075600     EVALUATE  TRUE                                                       
075700         WHEN PS-BREAK-IS-DEPT                                            
075800             MOVE PC-DEPT-LVL   TO SS-CURR-LVL                            
075900         WHEN PS-BREAK-IS-BYR                                             
076000             MOVE PC-BYR-LVL    TO SS-CURR-LVL                            
076100         WHEN PS-BREAK-IS-DMA                                             
076200             MOVE PC-DMA-LVL    TO SS-CURR-LVL                            
076300         WHEN PS-BREAK-IS-GMA                                             
076400             MOVE PC-GMA-LVL    TO SS-CURR-LVL                            
076500         WHEN PS-BREAK-IS-OPERCO                                          
076600             MOVE PC-OPERCO-LVL TO SS-CURR-LVL                            
076700     END-EVALUATE.                                                        
076800                                                                          
076900*    PRINT THE HEADINGS                                                   
077000                                                                          
077100     PERFORM 7200-PRINT-PAGE-HEADINGS.                                    
077200     PERFORM 7300-PRINT-TOTAL-HEADING.                                    
077300     PERFORM 7400-PRINT-DETAIL-HEADINGS.                                  
077400                                                                          
077500*    PRINT THE DETAIL                                                     
077600                                                                          
077700     MOVE AT-BEG-INV-COST-AMT (SS-CURR-LVL)                               
077800                                           TO PV-BEG-INV-COST-AMT.        
077900     MOVE AT-BEG-INV-RTL-AMT  (SS-CURR-LVL)                               
078000                                           TO PV-BEG-INV-RTL-AMT.         
078100     PERFORM 3600-PROCESS-BEGIN-INV-LINE.                                 
078200                                                                          
078300     PERFORM 3300-PROCESS-MONTH-DET-LINES                                 
078400         VARYING SS-CURR-MN FROM 1 BY 1                                   
078500         UNTIL   SS-CURR-MN > SS-OPEN-MN.                                 
078600                                                                          
078700*    PRINT THE YEAR-TO-DATE TOTALS                                        
078800                                                                          
078900     IF SS-OPEN-MN > 6                                                    
079000         PERFORM 3400-PROCESS-YTD-TOTAL-LINE                              
079100     END-IF.                                                              
079200                                                                          
079300     IF PS-BREAK-IS-OPERCO                                                
079400         NEXT SENTENCE                                                    
079500     ELSE                                                                 
079600         PERFORM 3100-ADD-TO-NEXT-HIGHER-LEVEL                            
079700     END-IF.                                                              
079800                                                                          
079900     SET PS-BREAK-LEVEL-SW-INIT TO TRUE.                                  
080000                                                                          
080100                                                                          
080200 3000-FILL-HEADINGS.                                                      
080300                                                                          
080400******************************************************************        
080500*    FILL IN THE SUB HEADING INFORMATION.                                 
080600******************************************************************        
080700                                                                          
080800     IF NOT PS-END-OF-LEDGER-EXTRACT                                      
080900*        GET GMM NAME                                                     
081000         MOVE ML102-GMA-NBR           TO PV-GMA-NBR                       
081100         PERFORM 8100-GET-GMM-NAME                                        
081200         MOVE RSPBAR-WORKER-FIR-NAME  TO DP024-FNAME                      
081300         MOVE RSPBAR-WORKER-LAST-NAME TO DP024-LNAME                      
081400         PERFORM DP024-0000-FORMAT-NAME                                   
081500                                                                          
081600*        PUT PARENTHESES AROUND GMM NAME                                  
081700         MOVE SPACES                  TO PV-ENTIRE-NAME                   
081800                                         PV-NAME-PAREN-2                  
081900                                         PV-NAME                          
082000         MOVE DP024-FLNAME            TO PV-ENTIRE-NAME                   
082100         INSPECT PV-NAME-WITH-PARENS REPLACING                            
082200             FIRST PC-2-SPACES-LIT                                        
082300                BY PC-PAREN-SPACE-LIT                                     
082400         MOVE PV-NAME-WITH-PARENS     TO PV-NAME                          
082500                                                                          
082600*        GET GMA DESCRIPTION                                              
082700         MOVE '??'                    TO PV-DMA-NBR                       
082800                                         PV-BYR-NBR                       
082900         MOVE PC-DEPT-NBR-000         TO PV-DEPT-NBR                      
083000         MOVE PC-MDSELV-GMA           TO PV-MDSELV-CDE                    
083100         PERFORM 8400-GET-MDSEAR-DESC                                     
083200                                                                          
083300*        BUILD GMA HEADING LINE                                           
083400         MOVE ML102-GMA-NBR           TO GH-GMA-NBR                       
083500         MOVE SPACES                  TO GH-GMA-DESC-GMM-NAME             
083600         MOVE MDSEAR-DESC             TO PV-DESC                          
083700         STRING PV-DESC  PV-NAME                                          
083800             DELIMITED BY PC-2-SPACES-LIT                                 
083900                                    INTO GH-GMA-DESC-GMM-NAME             
084000                                                                          
084100*        GET DMM NAME                                                     
084200         MOVE ML102-DMA-NBR           TO PV-DMA-NBR                       
084300         PERFORM 8200-GET-DMM-NAME                                        
084400         MOVE RSPBAR-WORKER-FIR-NAME  TO DP024-FNAME                      
084500         MOVE RSPBAR-WORKER-LAST-NAME TO DP024-LNAME                      
084600         PERFORM DP024-0000-FORMAT-NAME                                   
084700                                                                          
084800*        PUT PARENTHESES AROUND DMM NAME                                  
084900         MOVE SPACES                  TO PV-ENTIRE-NAME                   
085000                                         PV-NAME-PAREN-2                  
085100                                         PV-NAME                          
085200         MOVE DP024-FLNAME            TO PV-ENTIRE-NAME                   
085300         INSPECT PV-NAME-WITH-PARENS REPLACING                            
085400             FIRST PC-2-SPACES-LIT   BY                                   
085500                   PC-PAREN-SPACE-LIT                                     
085600         MOVE PV-NAME-WITH-PARENS     TO PV-NAME                          
085700                                                                          
085800*        GET DMA DESCRIPTION                                              
085900         MOVE '??'                    TO  PV-GMA-NBR                      
086000                                          PV-BYR-NBR                      
086100         MOVE PC-DEPT-NBR-000         TO  PV-DEPT-NBR                     
086200         MOVE PC-MDSELV-DMA           TO  PV-MDSELV-CDE                   
086300         PERFORM 8400-GET-MDSEAR-DESC                                     
086400                                                                          
086500*        BUILD DMA HEADING LINE                                           
086600         MOVE ML102-DMA-NBR           TO DH-DMA-NBR                       
086700         MOVE SPACES                  TO DH-DMA-DESC-DMM-NAME             
086800         MOVE MDSEAR-DESC             TO PV-DESC                          
086900         STRING PV-DESC  PV-NAME                                          
087000         DELIMITED BY PC-2-SPACES-LIT                                     
087100                                    INTO DH-DMA-DESC-DMM-NAME             
087200                                                                          
087300*        GET BUYER NAME                                                   
087400         MOVE ML102-BYR-NBR           TO PV-BYR-NBR                       
087500         MOVE ML102-DEPT-NBR          TO PV-DEPT-NBR                      
087600         PERFORM 8300-GET-BYR-NAME                                        
087700         MOVE RSPBAR-WORKER-FIR-NAME  TO DP024-FNAME                      
087800         MOVE RSPBAR-WORKER-LAST-NAME TO DP024-LNAME                      
087900         PERFORM DP024-0000-FORMAT-NAME                                   
088000                                                                          
088100*        BUILD BUYER HEADING LINE                                         
WR5701         MOVE ML102-BYR-NBR           TO BH-BYR-NBR                       
088300         MOVE DP024-FLNAME            TO BH-BYR-NAME                      
088400                                                                          
088500*        GET DEPT DESCRIPTION                                             
088600         MOVE '??'                    TO PV-GMA-NBR                       
088700                                         PV-DMA-NBR                       
088800         MOVE ML102-DEPT-NBR          TO PV-DEPT-NBR                      
088900         MOVE PC-MDSELV-DPT           TO PV-MDSELV-CDE                    
089000         PERFORM 8400-GET-MDSEAR-DESC                                     
089100                                                                          
089200*        BUILD DEPT HEADING LINE                                          
089300         MOVE ML102-DEPT-NBR          TO DH-DEPT-NBR                      
089400         MOVE MDSEAR-DESC             TO DH-DEPT-NAME                     
089500     END-IF.                                                              
089600                                                                          
089700                                                                          
089800 3100-ADD-TO-NEXT-HIGHER-LEVEL.                                           
089900                                                                          
090000******************************************************************        
090100* ADD THE BEGINNING INVENTORY, MONTHLY, AND SEASON TOTALS                 
090200* FOR THE CURRENT LEVEL TO THE NEXT HIGHER LEVEL.                         
090300******************************************************************        
090400                                                                          
090500     ADD 1 SS-CURR-LVL GIVING SS-NEXT-LVL.                                
090600                                                                          
090700     ADD AT-BEG-INV-COST-AMT (SS-CURR-LVL) TO                             
090800         AT-BEG-INV-COST-AMT (SS-NEXT-LVL).                               
090900     ADD AT-BEG-INV-RTL-AMT  (SS-CURR-LVL) TO                             
091000         AT-BEG-INV-RTL-AMT  (SS-NEXT-LVL).                               
091100                                                                          
091200     PERFORM VARYING SS-CURR-MN FROM 1 BY 1                               
091300       UNTIL SS-CURR-MN > SS-OPEN-MN                                      
091400         ADD AT-PRCH-NET-COST-AMT (SS-CURR-LVL SS-CURR-MN) TO             
091500             AT-PRCH-NET-COST-AMT (SS-NEXT-LVL SS-CURR-MN)                
091600         ADD AT-PRCH-RTL-AMT      (SS-CURR-LVL SS-CURR-MN) TO             
091700             AT-PRCH-RTL-AMT      (SS-NEXT-LVL SS-CURR-MN)                
WR5701         ADD AT-RCPT-NET-COST-AMT (SS-CURR-LVL SS-CURR-MN) TO             
WR5701             AT-RCPT-NET-COST-AMT (SS-NEXT-LVL SS-CURR-MN)                
WR5701         ADD AT-RCPT-RTL-AMT      (SS-CURR-LVL SS-CURR-MN) TO             
WR5701             AT-RCPT-RTL-AMT      (SS-NEXT-LVL SS-CURR-MN)                
WR5701         ADD AT-PADJ-NET-COST-AMT (SS-CURR-LVL SS-CURR-MN) TO             
WR5701             AT-PADJ-NET-COST-AMT (SS-NEXT-LVL SS-CURR-MN)                
WR5701         ADD AT-PADJ-RTL-AMT      (SS-CURR-LVL SS-CURR-MN) TO             
WR5701             AT-PADJ-RTL-AMT      (SS-NEXT-LVL SS-CURR-MN)                
091800         ADD AT-SLS-RTL-AMT       (SS-CURR-LVL SS-CURR-MN) TO             
091900             AT-SLS-RTL-AMT       (SS-NEXT-LVL SS-CURR-MN)                
092000         ADD AT-SHNK-RTL-AMT      (SS-CURR-LVL SS-CURR-MN) TO             
092100             AT-SHNK-RTL-AMT      (SS-NEXT-LVL SS-CURR-MN)                
092200         ADD AT-INV-ADJ-RTL-AMT   (SS-CURR-LVL SS-CURR-MN) TO             
092300             AT-INV-ADJ-RTL-AMT   (SS-NEXT-LVL SS-CURR-MN)                
092400         ADD AT-MKDN-RTL-AMT      (SS-CURR-LVL SS-CURR-MN) TO             
092500             AT-MKDN-RTL-AMT      (SS-NEXT-LVL SS-CURR-MN)                
092600         ADD AT-END-INV-COST-AMT  (SS-CURR-LVL SS-CURR-MN) TO             
092700             AT-END-INV-COST-AMT  (SS-NEXT-LVL SS-CURR-MN)                
092800         ADD AT-END-INV-RTL-AMT   (SS-CURR-LVL SS-CURR-MN) TO             
092900             AT-END-INV-RTL-AMT   (SS-NEXT-LVL SS-CURR-MN)                
093000         ADD AT-SLS-COST-AMT      (SS-CURR-LVL SS-CURR-MN) TO             
093100             AT-SLS-COST-AMT      (SS-NEXT-LVL SS-CURR-MN)                
093200         ADD AT-GRO-MRGN-AMT      (SS-CURR-LVL SS-CURR-MN) TO             
093300             AT-GRO-MRGN-AMT      (SS-NEXT-LVL SS-CURR-MN)                
093400     END-PERFORM.                                                         
093500                                                                          
093600     PERFORM VARYING SS-SEASON FROM 1 BY 1                                
093700       UNTIL SS-SEASON > 2                                                
093800         ADD AT-SEAS-PRCH-NET-COST-AMT (SS-CURR-LVL SS-SEASON) TO         
093900             AT-SEAS-PRCH-NET-COST-AMT (SS-NEXT-LVL SS-SEASON)            
094000         ADD AT-SEAS-PRCH-RTL-AMT      (SS-CURR-LVL SS-SEASON) TO         
094100             AT-SEAS-PRCH-RTL-AMT      (SS-NEXT-LVL SS-SEASON)            
WR5701         ADD AT-SEAS-RCPT-NET-COST-AMT (SS-CURR-LVL SS-SEASON) TO         
WR5701             AT-SEAS-RCPT-NET-COST-AMT (SS-NEXT-LVL SS-SEASON)            
WR5701         ADD AT-SEAS-RCPT-RTL-AMT      (SS-CURR-LVL SS-SEASON) TO         
WR5701             AT-SEAS-RCPT-RTL-AMT      (SS-NEXT-LVL SS-SEASON)            
WR5701         ADD AT-SEAS-PADJ-NET-COST-AMT (SS-CURR-LVL SS-SEASON) TO         
WR5701             AT-SEAS-PADJ-NET-COST-AMT (SS-NEXT-LVL SS-SEASON)            
WR5701         ADD AT-SEAS-PADJ-RTL-AMT      (SS-CURR-LVL SS-SEASON) TO         
WR5701             AT-SEAS-PADJ-RTL-AMT      (SS-NEXT-LVL SS-SEASON)            
094200         ADD AT-SEAS-SLS-RTL-AMT       (SS-CURR-LVL SS-SEASON) TO         
094300             AT-SEAS-SLS-RTL-AMT       (SS-NEXT-LVL SS-SEASON)            
094400         ADD AT-SEAS-SHNK-RTL-AMT      (SS-CURR-LVL SS-SEASON) TO         
094500             AT-SEAS-SHNK-RTL-AMT      (SS-NEXT-LVL SS-SEASON)            
094600         ADD AT-SEAS-MKDN-RTL-AMT      (SS-CURR-LVL SS-SEASON) TO         
094700             AT-SEAS-MKDN-RTL-AMT      (SS-NEXT-LVL SS-SEASON)            
094800         ADD AT-SEAS-SLS-COST-AMT      (SS-CURR-LVL SS-SEASON) TO         
094900             AT-SEAS-SLS-COST-AMT      (SS-NEXT-LVL SS-SEASON)            
095000         ADD AT-SEAS-GRO-MRGN-AMT      (SS-CURR-LVL SS-SEASON) TO         
095100             AT-SEAS-GRO-MRGN-AMT      (SS-NEXT-LVL SS-SEASON)            
095200     END-PERFORM.                                                         
095300                                                                          
095400                                                                          
095500 3200-ADD-TO-DEPT-SEASON-TOTS.                                            
095600                                                                          
095700******************************************************************        
095800* ADD THE DEPARTMENT'S DOLLARS TO THE SEASON TOTALS FORTHE                
095900* DEPARTMENT.                                                             
096000******************************************************************        
096100                                                                          
096200*    ADD THE BEGINNING INVENTORY TO THE SEASON PURCHASE TOTAL             
096300*    FOR THE DEPARTMENT.                                                  
096400     MOVE 1 TO SS-SEASON.                                                 
096500     MOVE AT-BEG-INV-COST-AMT       (PC-DEPT-LVL)                         
096600       TO AT-SEAS-PRCH-NET-COST-AMT (PC-DEPT-LVL SS-SEASON).              
096700     MOVE AT-BEG-INV-RTL-AMT        (PC-DEPT-LVL)                         
096800       TO AT-SEAS-PRCH-RTL-AMT      (PC-DEPT-LVL SS-SEASON).              
096900                                                                          
097000     MOVE 2 TO SS-SEASON.                                                 
097100     IF SS-OPEN-MN > 6                                                    
097200         MOVE AT-END-INV-COST-AMT       (PC-DEPT-LVL 6)                   
097300           TO AT-SEAS-PRCH-NET-COST-AMT (PC-DEPT-LVL SS-SEASON)           
097400         MOVE AT-END-INV-RTL-AMT        (PC-DEPT-LVL 6)                   
097500           TO AT-SEAS-PRCH-RTL-AMT      (PC-DEPT-LVL SS-SEASON)           
097600     END-IF.                                                              
097700                                                                          
097800*    ADD THE MONTHLY DOLLARS TO THE SEASON TOTALS FOR THE                 
097900*    DEPARTMENT. MONTHS 1-6 BELONG TO THE SPRING SEASON.                  
098000*    MONTHS 7-12 BELONG TO THE FALL SEASON.                               
098100                                                                          
098200     PERFORM VARYING SS-CURR-MN FROM 1 BY 1                               
098300       UNTIL (SS-CURR-MN > SS-OPEN-MN) OR (SS-CURR-MN > 12)               
098400                                                                          
098500         IF SS-CURR-MN < 7                                                
098600             MOVE 1 TO SS-SEASON                                          
098700         ELSE                                                             
098800             MOVE 2 TO SS-SEASON                                          
098900         END-IF                                                           
099000                                                                          
099100         ADD AT-PRCH-NET-COST-AMT      (PC-DEPT-LVL SS-CURR-MN) TO        
099200             AT-SEAS-PRCH-NET-COST-AMT (PC-DEPT-LVL SS-SEASON)            
099300         ADD AT-PRCH-RTL-AMT           (PC-DEPT-LVL SS-CURR-MN) TO        
099400             AT-SEAS-PRCH-RTL-AMT      (PC-DEPT-LVL SS-SEASON)            
WR5701         ADD AT-RCPT-NET-COST-AMT      (PC-DEPT-LVL SS-CURR-MN) TO        
WR5701             AT-SEAS-RCPT-NET-COST-AMT (PC-DEPT-LVL SS-SEASON)            
WR5701         ADD AT-RCPT-RTL-AMT           (PC-DEPT-LVL SS-CURR-MN) TO        
WR5701             AT-SEAS-RCPT-RTL-AMT      (PC-DEPT-LVL SS-SEASON)            
WR5701         ADD AT-PADJ-NET-COST-AMT      (PC-DEPT-LVL SS-CURR-MN) TO        
WR5701             AT-SEAS-PADJ-NET-COST-AMT (PC-DEPT-LVL SS-SEASON)            
WR5701         ADD AT-PADJ-RTL-AMT           (PC-DEPT-LVL SS-CURR-MN) TO        
WR5701             AT-SEAS-PADJ-RTL-AMT      (PC-DEPT-LVL SS-SEASON)            
099500         ADD AT-SLS-RTL-AMT            (PC-DEPT-LVL SS-CURR-MN) TO        
099600             AT-SEAS-SLS-RTL-AMT       (PC-DEPT-LVL SS-SEASON)            
099700         ADD AT-SHNK-RTL-AMT           (PC-DEPT-LVL SS-CURR-MN) TO        
099800             AT-SEAS-SHNK-RTL-AMT      (PC-DEPT-LVL SS-SEASON)            
099900         ADD AT-INV-ADJ-RTL-AMT        (PC-DEPT-LVL SS-CURR-MN) TO        
100000             AT-SEAS-SHNK-RTL-AMT      (PC-DEPT-LVL SS-SEASON)            
100100         ADD AT-MKDN-RTL-AMT           (PC-DEPT-LVL SS-CURR-MN) TO        
100200             AT-SEAS-MKDN-RTL-AMT      (PC-DEPT-LVL SS-SEASON)            
100300         ADD AT-SLS-COST-AMT           (PC-DEPT-LVL SS-CURR-MN) TO        
100400             AT-SEAS-SLS-COST-AMT      (PC-DEPT-LVL SS-SEASON)            
100500         ADD AT-GRO-MRGN-AMT           (PC-DEPT-LVL SS-CURR-MN) TO        
100600             AT-SEAS-GRO-MRGN-AMT      (PC-DEPT-LVL SS-SEASON)            
100700     END-PERFORM.                                                         
100800                                                                          
100900                                                                          
101000 3300-PROCESS-MONTH-DET-LINES.                                            
101100                                                                          
101200******************************************************************        
101300* BUILD THE DETAIL LINE FOR THE CURRENT MONTH.                            
101400******************************************************************        
101500                                                                          
101600     MOVE MT-FSCL-MN-NM-ABBREV (SS-CURR-MN) TO DL-MONTH-ABBREV.           
WR5701                                                                          
WR5701     COMPUTE PV-AMT-RNDED ROUNDED                                         
WR5701           = AT-RCPT-NET-COST-AMT (SS-CURR-LVL SS-CURR-MN)                
WR5701           / PC-THOUSAND.                                                 
WR5701     MOVE PV-AMT-RNDED     TO DL-RECEIPTS-COST.                           
WR5701                                                                          
WR5701     COMPUTE PV-AMT-RNDED ROUNDED                                         
WR5701           = AT-RCPT-RTL-AMT (SS-CURR-LVL SS-CURR-MN)                     
WR5701           / PC-THOUSAND.                                                 
WR5701     MOVE PV-AMT-RNDED     TO DL-RECEIPTS-RETAIL.                         
WR5701                                                                          
WR5701     COMPUTE PV-AMT-RNDED ROUNDED                                         
WR5701           = AT-PADJ-NET-COST-AMT (SS-CURR-LVL SS-CURR-MN)                
WR5701           / PC-THOUSAND.                                                 
WR5701     MOVE PV-AMT-RNDED     TO DL-PURCHASE-ADJ-COST.                       
WR5701                                                                          
WR5701     COMPUTE PV-AMT-RNDED ROUNDED                                         
WR5701           = AT-PADJ-RTL-AMT (SS-CURR-LVL SS-CURR-MN)                     
WR5701           / PC-THOUSAND.                                                 
WR5701     MOVE PV-AMT-RNDED     TO DL-PURCHASE-ADJ-RETAIL.                     
102700                                                                          
102800     IF AT-PRCH-RTL-AMT (SS-CURR-LVL SS-CURR-MN) NOT = ZERO               
102900         COMPUTE PV-PCT ROUNDED                                           
103000               = ((AT-PRCH-RTL-AMT    (SS-CURR-LVL SS-CURR-MN)            
103100               - AT-PRCH-NET-COST-AMT (SS-CURR-LVL SS-CURR-MN))           
103200               * 100)                                                     
103300               / AT-PRCH-RTL-AMT      (SS-CURR-LVL SS-CURR-MN)            
103400         MOVE PV-PCT       TO DL-MKON-PCT                                 
103500     ELSE                                                                 
103600         MOVE ZERO         TO DL-MKON-PCT                                 
103700     END-IF.                                                              
103800                                                                          
103900     COMPUTE PV-AMT-RNDED ROUNDED                                         
104000           = AT-SLS-RTL-AMT (SS-CURR-LVL SS-CURR-MN)                      
104100           / PC-THOUSAND.                                                 
104200     MOVE PV-AMT-RNDED     TO DL-SALES.                                   
104300                                                                          
104400     COMPUTE PV-AMT-RNDED ROUNDED                                         
104500           = AT-SHNK-RTL-AMT (SS-CURR-LVL SS-CURR-MN)                     
104600           / PC-THOUSAND.                                                 
104700     MOVE PV-AMT-RNDED     TO DL-SHRINKAGE.                               
104800                                                                          
104900     COMPUTE PV-AMT-RNDED ROUNDED                                         
105000           = AT-MKDN-RTL-AMT (SS-CURR-LVL SS-CURR-MN)                     
105100           / PC-THOUSAND.                                                 
105200     MOVE PV-AMT-RNDED     TO DL-MARKDOWNS.                               
105300                                                                          
105400     COMPUTE PV-AMT-RNDED ROUNDED                                         
105500           = AT-END-INV-COST-AMT (SS-CURR-LVL SS-CURR-MN)                 
105600           / PC-THOUSAND.                                                 
105700     MOVE PV-AMT-RNDED     TO DL-END-INV-COST.                            
105800                                                                          
105900     COMPUTE PV-AMT-RNDED ROUNDED                                         
106000           = AT-END-INV-RTL-AMT (SS-CURR-LVL SS-CURR-MN)                  
106100           / PC-THOUSAND.                                                 
106200     MOVE PV-AMT-RNDED     TO DL-END-INV-RETAIL.                          
106300                                                                          
106400     COMPUTE PV-AMT-RNDED ROUNDED                                         
106500           = AT-SLS-COST-AMT (SS-CURR-LVL SS-CURR-MN)                     
106600           / PC-THOUSAND.                                                 
106700     MOVE PV-AMT-RNDED     TO DL-COST-OF-SALES.                           
106800                                                                          
106900     COMPUTE PV-AMT-RNDED ROUNDED                                         
107000           = AT-GRO-MRGN-AMT (SS-CURR-LVL SS-CURR-MN)                     
107100           / PC-THOUSAND.                                                 
107200     MOVE PV-AMT-RNDED     TO DL-GM-DLRS.                                 
107300                                                                          
107400     IF AT-SLS-RTL-AMT (SS-CURR-LVL SS-CURR-MN) NOT = ZERO                
C14666         COMPUTE PV-SHNK-PCT ROUNDED                                      
107600               = (AT-SHNK-RTL-AMT (SS-CURR-LVL SS-CURR-MN) * 100)         
107700               / AT-SLS-RTL-AMT   (SS-CURR-LVL SS-CURR-MN)                
C14666         MOVE PV-SHNK-PCT  TO DL-SHNK-PCT                                 
107900                                                                          
108000         COMPUTE PV-PCT ROUNDED                                           
108100               = (AT-MKDN-RTL-AMT (SS-CURR-LVL SS-CURR-MN) * 100)         
108200               / AT-SLS-RTL-AMT   (SS-CURR-LVL SS-CURR-MN)                
108300         MOVE PV-PCT       TO DL-MKDN-PCT                                 
108400                                                                          
108500         COMPUTE PV-PCT ROUNDED                                           
108600               = (AT-GRO-MRGN-AMT (SS-CURR-LVL SS-CURR-MN) * 100)         
108700               / AT-SLS-RTL-AMT   (SS-CURR-LVL SS-CURR-MN)                
108800         MOVE PV-PCT       TO DL-GM-PCT                                   
108900     ELSE                                                                 
109000         MOVE ZEROES       TO DL-SHNK-PCT                                 
109100                              DL-MKDN-PCT                                 
109200                              DL-GM-PCT                                   
109300     END-IF.                                                              
109400                                                                          
109500     PERFORM 7500-PRINT-DETAIL-LINE.                                      
109600                                                                          
109700     IF AT-INV-ADJ-RTL-AMT (SS-CURR-LVL SS-CURR-MN) NOT = ZERO            
109800         MOVE SPACES TO DL-DETAIL-LINE                                    
109900         MOVE 'ADJ'  TO DL-MONTH-ABBREV                                   
110000         COMPUTE PV-AMT-RNDED ROUNDED                                     
110100               = AT-INV-ADJ-RTL-AMT (SS-CURR-LVL SS-CURR-MN)              
110200               / PC-THOUSAND                                              
110300         MOVE PV-AMT-RNDED TO DL-SHRINKAGE                                
110400                                                                          
110500         PERFORM 7500-PRINT-DETAIL-LINE                                   
110600                                                                          
110700         MOVE SPACES TO DL-MONTH-ABBREV                                   
110800     END-IF.                                                              
110900                                                                          
111000     IF (SS-CURR-MN = SS-OPEN-MN) OR                                      
111100        (SS-CURR-MN = 6)                                                  
111200         PERFORM 3500-PROCESS-SEASON-TOTALS                               
111300     END-IF.                                                              
111400                                                                          
111500     IF (SS-CURR-MN NOT = SS-OPEN-MN) AND                                 
111600        (SS-CURR-MN     = 6)                                              
111700         MOVE AT-END-INV-COST-AMT (SS-CURR-LVL SS-CURR-MN)                
111800              TO PV-BEG-INV-COST-AMT                                      
111900         MOVE AT-END-INV-RTL-AMT (SS-CURR-LVL SS-CURR-MN)                 
112000              TO PV-BEG-INV-RTL-AMT                                       
112100         PERFORM 3600-PROCESS-BEGIN-INV-LINE                              
112200     END-IF.                                                              
112300                                                                          
112400                                                                          
112500 3400-PROCESS-YTD-TOTAL-LINE.                                             
112600                                                                          
112700******************************************************************        
112800*    SET THE DETAIL LINE FOR THE YEAR TO DATE INFORMATION.                
112900******************************************************************        
113000                                                                          
113100     INITIALIZE     DL-DETAIL-LINE                                        
113200     MOVE 'YTD'  TO DL-MONTH-ABBREV.                                      
113300                                                                          
113400     COMPUTE YTD-PRCH-NET-COST-AMT                                        
113500           = AT-SEAS-PRCH-NET-COST-AMT (SS-CURR-LVL 1)                    
113600           + AT-SEAS-PRCH-NET-COST-AMT (SS-CURR-LVL 2)                    
113700           - AT-END-INV-COST-AMT       (SS-CURR-LVL 6).                   
113800                                                                          
113900     COMPUTE YTD-PRCH-RTL-AMT                                             
114000           = AT-SEAS-PRCH-RTL-AMT(SS-CURR-LVL 1)                          
114100           + AT-SEAS-PRCH-RTL-AMT(SS-CURR-LVL 2)                          
114200           - AT-END-INV-RTL-AMT(SS-CURR-LVL 6).                           
114300                                                                          
115100                                                                          
WR5701     COMPUTE YTD-RCPT-NET-COST-AMT                                        
WR5701           = AT-SEAS-RCPT-NET-COST-AMT (SS-CURR-LVL 1)                    
WR5701           + AT-SEAS-RCPT-NET-COST-AMT (SS-CURR-LVL 2).                   
WR5701                                                                          
WR5701     COMPUTE YTD-RCPT-RTL-AMT                                             
WR5701           = AT-SEAS-RCPT-RTL-AMT(SS-CURR-LVL 1)                          
WR5701           + AT-SEAS-RCPT-RTL-AMT(SS-CURR-LVL 2).                         
WR5701                                                                          
WR5701     COMPUTE PV-AMT-RNDED ROUNDED                                         
WR5701           = YTD-RCPT-NET-COST-AMT / PC-THOUSAND.                         
WR5701     MOVE PV-AMT-RNDED TO DL-RECEIPTS-COST.                               
WR5701                                                                          
WR5701     COMPUTE PV-AMT-RNDED ROUNDED                                         
WR5701           = YTD-RCPT-RTL-AMT / PC-THOUSAND.                              
WR5701     MOVE PV-AMT-RNDED TO DL-RECEIPTS-RETAIL.                             
WR5701                                                                          
WR5701     COMPUTE YTD-PADJ-NET-COST-AMT                                        
WR5701           = AT-SEAS-PADJ-NET-COST-AMT (SS-CURR-LVL 1)                    
WR5701           + AT-SEAS-PADJ-NET-COST-AMT (SS-CURR-LVL 2).                   
WR5701                                                                          
WR5701     COMPUTE YTD-PADJ-RTL-AMT                                             
WR5701           = AT-SEAS-PADJ-RTL-AMT(SS-CURR-LVL 1)                          
WR5701           + AT-SEAS-PADJ-RTL-AMT(SS-CURR-LVL 2).                         
WR5701                                                                          
WR5701     COMPUTE PV-AMT-RNDED ROUNDED                                         
WR5701           = YTD-PADJ-NET-COST-AMT / PC-THOUSAND.                         
WR5701     MOVE PV-AMT-RNDED TO DL-PURCHASE-ADJ-COST.                           
WR5701                                                                          
WR5701     COMPUTE PV-AMT-RNDED ROUNDED                                         
WR5701           = YTD-PADJ-RTL-AMT / PC-THOUSAND.                              
WR5701     MOVE PV-AMT-RNDED TO DL-PURCHASE-ADJ-RETAIL.                         
WR5701                                                                          
WR5701     IF YTD-PRCH-RTL-AMT  NOT = ZERO                                      
WR5701         COMPUTE PV-PCT ROUNDED =                                         
WR5701           (((  YTD-PRCH-RTL-AMT                                          
WR5701             -  YTD-PRCH-NET-COST-AMT ) * 100 )                           
WR5701             /  YTD-PRCH-RTL-AMT )                                        
WR5701         MOVE PV-PCT   TO DL-MKON-PCT                                     
WR5701     ELSE                                                                 
WR5701         MOVE ZERO     TO DL-MKON-PCT                                     
WR5701     END-IF.                                                              
116100                                                                          
116200     COMPUTE YTD-SLS-RTL-AMT                                              
116300           = AT-SEAS-SLS-RTL-AMT (SS-CURR-LVL 1)                          
116400           + AT-SEAS-SLS-RTL-AMT (SS-CURR-LVL 2).                         
116500                                                                          
116600     COMPUTE YTD-SHNK-RTL-AMT                                             
116700           = AT-SEAS-SHNK-RTL-AMT (SS-CURR-LVL 1)                         
116800           + AT-SEAS-SHNK-RTL-AMT (SS-CURR-LVL 2).                        
116900                                                                          
117000     COMPUTE YTD-MKDN-RTL-AMT                                             
117100           = AT-SEAS-MKDN-RTL-AMT (SS-CURR-LVL 1)                         
117200           + AT-SEAS-MKDN-RTL-AMT (SS-CURR-LVL 2).                        
117300                                                                          
117400     COMPUTE YTD-SLS-COST-AMT                                             
117500           = AT-SEAS-SLS-COST-AMT (SS-CURR-LVL 1)                         
117600           + AT-SEAS-SLS-COST-AMT (SS-CURR-LVL 2).                        
117700                                                                          
117800     COMPUTE YTD-GRO-MRGN-AMT                                             
117900           = AT-SEAS-GRO-MRGN-AMT (SS-CURR-LVL 1)                         
118000           + AT-SEAS-GRO-MRGN-AMT (SS-CURR-LVL 2).                        
118100                                                                          
118200     COMPUTE PV-AMT-RNDED ROUNDED                                         
118300           = YTD-SLS-RTL-AMT / PC-THOUSAND.                               
118400     MOVE PV-AMT-RNDED TO DL-SALES.                                       
118500                                                                          
118600     COMPUTE PV-AMT-RNDED ROUNDED                                         
118700           = YTD-SHNK-RTL-AMT / PC-THOUSAND.                              
118800     MOVE PV-AMT-RNDED TO DL-SHRINKAGE.                                   
118900                                                                          
119000     COMPUTE PV-AMT-RNDED ROUNDED                                         
119100           = YTD-MKDN-RTL-AMT / PC-THOUSAND.                              
119200     MOVE PV-AMT-RNDED TO DL-MARKDOWNS.                                   
119300                                                                          
119400     COMPUTE PV-AMT-RNDED ROUNDED                                         
119500           = YTD-SLS-COST-AMT / PC-THOUSAND.                              
119600     MOVE PV-AMT-RNDED TO DL-COST-OF-SALES.                               
119700                                                                          
119800     COMPUTE PV-AMT-RNDED ROUNDED                                         
119900           = YTD-GRO-MRGN-AMT / PC-THOUSAND.                              
120000     MOVE PV-AMT-RNDED TO DL-GM-DLRS.                                     
120100                                                                          
120200     IF YTD-SLS-RTL-AMT NOT = ZERO                                        
C14666         COMPUTE PV-SHNK-PCT ROUNDED                                      
120400               = (YTD-SHNK-RTL-AMT * 100) / YTD-SLS-RTL-AMT               
C14666         MOVE PV-SHNK-PCT TO DL-SHNK-PCT                                  
120600                                                                          
120700         COMPUTE PV-PCT ROUNDED                                           
120800               = (YTD-MKDN-RTL-AMT * 100) / YTD-SLS-RTL-AMT               
120900         MOVE PV-PCT   TO DL-MKDN-PCT                                     
121000                                                                          
121100         COMPUTE PV-PCT ROUNDED                                           
121200               = (YTD-GRO-MRGN-AMT * 100) / YTD-SLS-RTL-AMT               
121300         MOVE PV-PCT   TO DL-GM-PCT                                       
121400     ELSE                                                                 
121500         MOVE ZEROES   TO DL-SHNK-PCT                                     
121600                          DL-MKDN-PCT                                     
121700                          DL-GM-PCT                                       
121800     END-IF.                                                              
121900                                                                          
122000     PERFORM 7600-PRINT-TOTAL-LINE.                                       
122100                                                                          
122200                                                                          
122300 3500-PROCESS-SEASON-TOTALS.                                              
122400                                                                          
122500******************************************************************        
122600* SET UP THE DETAIL LINE FOR THE END OF SEASON INFO.                      
122700******************************************************************        
122800                                                                          
122900     INITIALIZE DL-DETAIL-LINE.                                           
123000                                                                          
123100     IF SS-CURR-MN < 7                                                    
123200         MOVE 1        TO SS-SEASON                                       
123300         MOVE 'SPR'    TO DL-MONTH-ABBREV                                 
123400     ELSE                                                                 
123500         MOVE 2        TO SS-SEASON                                       
123600         MOVE 'FAL'    TO DL-MONTH-ABBREV                                 
123700     END-IF.                                                              
123800                                                                          
WR5701     COMPUTE PV-AMT-RNDED ROUNDED                                         
124000           = AT-SEAS-RCPT-NET-COST-AMT (SS-CURR-LVL SS-SEASON)            
124100           / PC-THOUSAND.                                                 
124200     MOVE PV-AMT-RNDED TO DL-RECEIPTS-COST.                               
124300                                                                          
124400     COMPUTE PV-AMT-RNDED ROUNDED                                         
124500           = AT-SEAS-RCPT-RTL-AMT (SS-CURR-LVL SS-SEASON)                 
124600           / PC-THOUSAND.                                                 
124700     MOVE PV-AMT-RNDED TO DL-RECEIPTS-RETAIL.                             
124800                                                                          
123900     COMPUTE PV-AMT-RNDED ROUNDED                                         
124000           = AT-SEAS-PADJ-NET-COST-AMT (SS-CURR-LVL SS-SEASON)            
124100           / PC-THOUSAND.                                                 
124200     MOVE PV-AMT-RNDED TO DL-PURCHASE-ADJ-COST.                           
124300                                                                          
124400     COMPUTE PV-AMT-RNDED ROUNDED                                         
124500           = AT-SEAS-PADJ-RTL-AMT (SS-CURR-LVL SS-SEASON)                 
124600           / PC-THOUSAND.                                                 
124700     MOVE PV-AMT-RNDED TO DL-PURCHASE-ADJ-RETAIL.                         
124800                                                                          
WR5701     IF AT-SEAS-RCPT-RTL-AMT (SS-CURR-LVL SS-SEASON) NOT = ZERO           
WR5701         COMPUTE AT-SEAS-PRCH-RTL-AMT (SS-CURR-LVL SS-SEASON)             
WR5701             =  (AT-SEAS-RCPT-RTL-AMT (SS-CURR-LVL SS-SEASON)             
WR5701               + AT-SEAS-PADJ-RTL-AMT (SS-CURR-LVL SS-SEASON)             
WR5701               + PV-BEG-INV-RTL-AMT  )                                    
WR5701         COMPUTE AT-SEAS-PRCH-NET-COST-AMT (SS-CURR-LVL SS-SEASON)        
WR5701             =  (AT-SEAS-RCPT-NET-COST-AMT (SS-CURR-LVL SS-SEASON)        
WR5701               + AT-SEAS-PADJ-NET-COST-AMT (SS-CURR-LVL SS-SEASON)        
WR5701               + PV-BEG-INV-COST-AMT  )                                   
WR5701         IF AT-SEAS-PRCH-RTL-AMT (SS-CURR-LVL SS-SEASON) NOT = 0          
WR5701             COMPUTE PV-PCT ROUNDED =                                     
WR5701                 (((AT-SEAS-PRCH-RTL-AMT (SS-CURR-LVL SS-SEASON)          
WR5701                  - AT-SEAS-PRCH-NET-COST-AMT                             
WR5701                                         (SS-CURR-LVL SS-SEASON))         
WR5701                  * 100)                                                  
WR5701                 /  AT-SEAS-PRCH-RTL-AMT (SS-CURR-LVL SS-SEASON))         
125600             MOVE PV-PCT   TO DL-MKON-PCT                                 
WR5701         ELSE                                                             
WR5701             MOVE ZERO     TO DL-MKON-PCT                                 
125700     ELSE                                                                 
125800         MOVE ZERO     TO DL-MKON-PCT                                     
125900     END-IF.                                                              
126000                                                                          
126100     COMPUTE PV-AMT-RNDED ROUNDED                                         
126200           = AT-SEAS-SLS-RTL-AMT (SS-CURR-LVL SS-SEASON)                  
126300           / PC-THOUSAND.                                                 
126400     MOVE PV-AMT-RNDED TO DL-SALES.                                       
126500                                                                          
126600     COMPUTE PV-AMT-RNDED ROUNDED                                         
126700           = AT-SEAS-SHNK-RTL-AMT (SS-CURR-LVL SS-SEASON)                 
126800           / PC-THOUSAND.                                                 
126900     MOVE PV-AMT-RNDED TO DL-SHRINKAGE.                                   
127000                                                                          
127100     COMPUTE PV-AMT-RNDED ROUNDED                                         
127200           = AT-SEAS-MKDN-RTL-AMT (SS-CURR-LVL SS-SEASON)                 
127300           / PC-THOUSAND.                                                 
127400     MOVE PV-AMT-RNDED TO DL-MARKDOWNS.                                   
127500                                                                          
127600     COMPUTE PV-AMT-RNDED ROUNDED                                         
127700           = AT-SEAS-SLS-COST-AMT (SS-CURR-LVL SS-SEASON)                 
127800           / PC-THOUSAND.                                                 
127900     MOVE PV-AMT-RNDED TO DL-COST-OF-SALES.                               
128000                                                                          
128100     COMPUTE PV-AMT-RNDED ROUNDED                                         
128200           = AT-SEAS-GRO-MRGN-AMT (SS-CURR-LVL SS-SEASON)                 
128300           / PC-THOUSAND.                                                 
128400     MOVE PV-AMT-RNDED TO DL-GM-DLRS.                                     
128500                                                                          
128600     IF AT-SEAS-SLS-RTL-AMT (SS-CURR-LVL SS-SEASON) NOT = ZERO            
C14666         COMPUTE PV-SHNK-PCT ROUNDED                                      
128800               = (AT-SEAS-SHNK-RTL-AMT (SS-CURR-LVL SS-SEASON)            
128900               * 100)                                                     
129000               / AT-SEAS-SLS-RTL-AMT   (SS-CURR-LVL SS-SEASON)            
C14666         MOVE PV-SHNK-PCT TO DL-SHNK-PCT                                  
129200                                                                          
129300         COMPUTE PV-PCT ROUNDED                                           
129400               = (AT-SEAS-MKDN-RTL-AMT (SS-CURR-LVL SS-SEASON)            
129500               * 100)                                                     
129600               / AT-SEAS-SLS-RTL-AMT   (SS-CURR-LVL SS-SEASON)            
129700         MOVE PV-PCT   TO DL-MKDN-PCT                                     
129800                                                                          
129900         COMPUTE PV-PCT ROUNDED                                           
130000               = (AT-SEAS-GRO-MRGN-AMT (SS-CURR-LVL SS-SEASON)            
130100               * 100)                                                     
130200               / AT-SEAS-SLS-RTL-AMT   (SS-CURR-LVL SS-SEASON)            
130300         MOVE PV-PCT   TO DL-GM-PCT                                       
130400                                                                          
130500     ELSE                                                                 
130600         MOVE ZEROES   TO DL-SHNK-PCT                                     
130700                          DL-MKDN-PCT                                     
130800                          DL-GM-PCT                                       
130900     END-IF.                                                              
131000                                                                          
131100     PERFORM 7600-PRINT-TOTAL-LINE.                                       
131200     PERFORM 7700-PRINT-BLANK-LINE.                                       
131300                                                                          
131400                                                                          
131500 3600-PROCESS-BEGIN-INV-LINE.                                             
131600                                                                          
131700******************************************************************        
131800* BUILD THE DETAIL LINE FOR THE BEGINNING INVENTORY INFO.                 
131900******************************************************************        
132000                                                                          
132100     MOVE SPACES       TO DL-DETAIL-LINE.                                 
132200     MOVE 'B.I'        TO DL-MONTH-ABBREV.                                
132300                                                                          
132400     COMPUTE PV-AMT-RNDED ROUNDED                                         
132500           = PV-BEG-INV-COST-AMT / PC-THOUSAND.                           
WR5701     MOVE PV-AMT-RNDED TO DL-END-INV-COST.                                
132700                                                                          
132800     COMPUTE PV-AMT-RNDED ROUNDED                                         
132900           = PV-BEG-INV-RTL-AMT / PC-THOUSAND.                            
WR5701     MOVE PV-AMT-RNDED TO DL-END-INV-RETAIL.                              
134000                                                                          
WR5701***  IF SS-CURR-MN NOT = 6                                                
WR5701***      PERFORM 7500-PRINT-DETAIL-LINE                                   
WR5701***  END-IF.                                                              
133100                                                                          
133200     IF PV-BEG-INV-RTL-AMT NOT = ZERO                                     
133300         COMPUTE PV-PCT ROUNDED                                           
133400             = ((PV-BEG-INV-RTL-AMT - PV-BEG-INV-COST-AMT) * 100)         
133500             /   PV-BEG-INV-RTL-AMT                                       
133600         MOVE PV-PCT   TO DL-MKON-PCT                                     
133700     ELSE                                                                 
133800         MOVE ZERO     TO DL-MKON-PCT                                     
133900     END-IF.                                                              
134200                                                                          
******     PERFORM 7500-PRINT-DETAIL-LINE.                                      
134300                                                                          
134400 7000-READ-LEDGER-EXTRACT.                                                
134500                                                                          
134600     READ LEDGER-EXTRACT-FILE INTO ML102-LEDGER-EXTRACT-REC               
134700         AT END SET PS-END-OF-LEDGER-EXTRACT TO TRUE.                     
134800                                                                          
134900                                                                          
135000 7100-READ-LEDGER-EXTRACT-DATE.                                           
135100                                                                          
135200     READ LEDGER-EXTRACT-DATE-FILE                                        
135300         INTO ML114-LEDGER-EXTRACT-DATE-REC                               
135400         AT END SET PS-END-OF-LEDGER-DATE-FILE TO TRUE.                   
135500                                                                          
135600                                                                          
135700 7200-PRINT-PAGE-HEADINGS.                                                
135800                                                                          
135900******************************************************************        
136000* PRINT THE PAGE HEADINGS.                                                
136100* PRINT THE SUB HEADINGS BASED ON THE VALUE OF THE SUBSCRIPT.             
136200******************************************************************        
136300                                                                          
136400*    IF LC-LASER-BREAK                                                    
136500*        MOVE LC-LASER-CONTROL-FF TO                                      
136600*            DP175O-LASER-NEW-PAGE-INDICATR                               
136700*        WRITE LEDGER-REPORT-RECORD FROM                                  
136800*            LC-LASER-CONTROL-FD                                          
136900*                AFTER ADVANCING PAGE                                     
137000*    ELSE                                                                 
137100*        MOVE SPACES TO DP175O-LASER-NEW-PAGE-INDICATR  .                 
137200*    END-IF.                                                              
137300*    MOVE 'N' TO LC-LASER-BREAK-SW.                                       
137400                                                                          
137500     ADD  1           TO PV-PAGE-CNT.                                     
137600     MOVE PV-PAGE-CNT TO DP175O-PAGE-NUMBER.                              
137700                                                                          
137800     WRITE LEDGER-REPORT-RECORD                                           
137900         FROM DP175O-STANDRD-HEADER-175-COLS                              
138000         AFTER ADVANCING PAGE.                                            
138100                                                                          
138200     WRITE LEDGER-REPORT-RECORD                                           
138300         FROM PH2-PAGE-HEADING-2                                          
138400         AFTER ADVANCING 1 LINE.                                          
138500                                                                          
138600     WRITE LEDGER-REPORT-RECORD                                           
138700         FROM PH3-PAGE-HEADING-3                                          
138800         AFTER ADVANCING 1 LINE.                                          
138900                                                                          
139000     IF SS-CURR-LVL = PC-OPERCO-LVL                                       
139100         WRITE LEDGER-REPORT-RECORD  FROM CH-COMPANY-HEADING              
139200             AFTER ADVANCING 1 LINE                                       
139300     END-IF.                                                              
139400                                                                          
139500     IF SS-CURR-LVL < PC-OPERCO-LVL                                       
139600         WRITE LEDGER-REPORT-RECORD  FROM GH-GMA-HEADING                  
139700             AFTER ADVANCING 1 LINE                                       
139800     END-IF.                                                              
139900                                                                          
140000     IF SS-CURR-LVL < PC-GMA-LVL                                          
140100         WRITE LEDGER-REPORT-RECORD  FROM DH-DMA-HEADING                  
140200             AFTER ADVANCING 1 LINE                                       
140300     END-IF.                                                              
140400                                                                          
140500     IF SS-CURR-LVL < PC-DMA-LVL                                          
140600         WRITE LEDGER-REPORT-RECORD  FROM BH-BUYER-HEADING                
140700             AFTER ADVANCING 1 LINE                                       
140800     END-IF.                                                              
140900                                                                          
141000     IF SS-CURR-LVL < PC-BYR-LVL                                          
141100         WRITE LEDGER-REPORT-RECORD  FROM DH-DEPT-HEADING                 
141200             AFTER ADVANCING 1 LINE                                       
141300     END-IF.                                                              
141400                                                                          
141500                                                                          
141600 7300-PRINT-TOTAL-HEADING.                                                
141700                                                                          
141800     EVALUATE TRUE                                                        
141900         WHEN PS-BREAK-IS-OPERCO                                          
142000             WRITE LEDGER-REPORT-RECORD FROM PC-COMPANY-TOTALS            
142100             AFTER ADVANCING 1 LINE                                       
142200         WHEN PS-BREAK-IS-GMA                                             
142300             WRITE LEDGER-REPORT-RECORD FROM PC-DIVISION-TOTALS           
142400         WHEN PS-BREAK-IS-DMA                                             
142500             WRITE LEDGER-REPORT-RECORD FROM PC-MANAGER-TOTALS            
142600         WHEN PS-BREAK-IS-BYR                                             
142700             WRITE LEDGER-REPORT-RECORD FROM PC-BUYER-TOTALS              
142800             AFTER ADVANCING 1 LINE                                       
WR5701*        WHEN PS-BREAK-IS-DEPT                                            
143000*            WRITE LEDGER-REPORT-RECORD FROM PC-DEPT-TOTALS               
143100*            AFTER ADVANCING 1 LINE                                       
143200     END-EVALUATE.                                                        
143300                                                                          
143400                                                                          
143500 7400-PRINT-DETAIL-HEADINGS.                                              
143600                                                                          
143700     WRITE LEDGER-REPORT-RECORD                                           
143800         FROM DH1-DETAIL-HEADING-1                                        
143900         AFTER ADVANCING 2 LINES.                                         
144000                                                                          
144100     WRITE LEDGER-REPORT-RECORD                                           
144200         FROM DH2-DETAIL-HEADING-2                                        
144300         AFTER ADVANCING 1 LINE.                                          
144400                                                                          
144500                                                                          
144600 7500-PRINT-DETAIL-LINE.                                                  
144700                                                                          
144800     WRITE LEDGER-REPORT-RECORD                                           
144900         FROM DL-DETAIL-LINE                                              
145000         AFTER ADVANCING 1 LINE.                                          
145100                                                                          
145200                                                                          
145300 7600-PRINT-TOTAL-LINE.                                                   
145400                                                                          
145500     WRITE LEDGER-REPORT-RECORD                                           
145600         FROM UL-UNDER-LINE                                               
145700         AFTER ADVANCING 1 LINE.                                          
145800                                                                          
145900     WRITE LEDGER-REPORT-RECORD                                           
146000         FROM DL-DETAIL-LINE                                              
146100         AFTER ADVANCING 1 LINE.                                          
146200                                                                          
146300                                                                          
146400 7700-PRINT-BLANK-LINE.                                                   
146500                                                                          
146600     INITIALIZE LEDGER-REPORT-RECORD.                                     
146700     WRITE LEDGER-REPORT-RECORD                                           
146800        AFTER ADVANCING 1 LINE.                                           
146900                                                                          
147000                                                                          
147100 8050-GET-DATE-ATTRIBUTES.                                                
147200                                                                          
147300     EXEC SQL                                                             
147400          SELECT                                                          
147500              FSCL_YR_NBR                                                 
147600             ,FSCL_MN_NM                                                  
147700             ,FSCL_YR_BEG_DTE                                             
147800          INTO                                                            
147900              :DTATTR-FSCL-YR-NBR                                         
148000             ,:DTATTR-FSCL-MN-NM                                          
148100             ,:DTATTR-FSCL-YR-BEG-DTE                                     
148200          FROM                                                            
148300              TDTATTR                                                     
148400          WHERE                                                           
148500              KY_DTE = :DTATTR-KY-DTE                                     
W26603          WITH UR                                                         
148600     END-EXEC.                                                            
148700                                                                          
148800     EVALUATE TRUE                                                        
148900         WHEN SQLCODE = ZERO                                              
149000             CONTINUE                                                     
149100         WHEN OTHER                                                       
149200             DISPLAY 'DTATTR-KY-DTE: ' DTATTR-KY-DTE                      
149300             MOVE    '8050-GET-DATE-ATTRIBUTES'                           
149400                                          TO PV-PARAGRAPH-NAME            
149500             MOVE    'SELECT ROW FROM TDTATTR'                            
149600                                          TO PV-DB2-OPER-ATTEMPTED        
149700             PERFORM 9998-SQL-ABEND                                       
149800     END-EVALUATE.                                                        
149900                                                                          
150000                                                                          
150100 8100-GET-GMM-NAME.                                                       
150200                                                                          
150300     EXEC SQL                                                             
150400          SELECT      B.WORKER_LAST_NAME                                  
150500               ,      B.WORKER_FIR_NAME                                   
150600            INTO     :RSPBAR-WORKER-LAST-NAME                             
150700               ,     :RSPBAR-WORKER-FIR-NAME                              
150800            FROM      TMDSEAR A                                           
150900               ,      TRSPBAR B                                           
151000               ,      TAPLAVL C                                           
151100           WHERE     (A.GMA_NBR     = :PV-GMA-NBR)                        
151200             AND     (B.RSPLVL_CDE  = :PC-RSPLVL-CDE-GMM)                 
151300             AND     (A.OPERCO_NBR  = :PC-OPERCO-NBR)                     
151400             AND     (C.APLSYS_CDE  = :PC-APLSYS-CDE)                     
151500             AND     (A.MDSEAR_DKEY =  B.MDSEAR_DKEY)                     
151600             AND     (A.MDSEAR_DKEY =  C.MDSEAR_DKEY)                     
151700             AND     (:ML114-EXTRACT-DTE                                  
151800                      BETWEEN B.WORKER_STRT_DTE                           
151900                      AND     B.WORKER_END_DTE)                           
152000             AND     (:ML114-EXTRACT-DTE                                  
152100                      BETWEEN C.MDSEAR_STRT_DTE                           
152200                      AND     C.MDSEAR_END_DTE)                           
W26603          WITH UR                                                         
152300     END-EXEC.                                                            
152400                                                                          
152500     EVALUATE TRUE                                                        
152600         WHEN SQLCODE = ZERO                                              
152700             CONTINUE                                                     
152800         WHEN SQLCODE = +100                                              
152900             MOVE 'OPEN' TO  RSPBAR-WORKER-FIR-NAME                       
153000             MOVE SPACE  TO  RSPBAR-WORKER-LAST-NAME                      
153100         WHEN OTHER                                                       
153200             MOVE '8100-GET-GMM-NAME'     TO PV-PARAGRAPH-NAME            
153300             MOVE 'SELECT ROW FROM TRSPBAR'                               
153400                                          TO PV-DB2-OPER-ATTEMPTED        
153500             PERFORM 9998-SQL-ABEND                                       
153600     END-EVALUATE.                                                        
153700                                                                          
153800                                                                          
153900 8200-GET-DMM-NAME.                                                       
154000                                                                          
154100     EXEC SQL                                                             
154200          SELECT      B.WORKER_LAST_NAME                                  
154300               ,      B.WORKER_FIR_NAME                                   
154400            INTO     :RSPBAR-WORKER-LAST-NAME                             
154500               ,     :RSPBAR-WORKER-FIR-NAME                              
154600            FROM      TMDSEAR A                                           
154700               ,      TRSPBAR B                                           
154800               ,      TAPLAVL C                                           
154900           WHERE     (A.DMA_NBR     = :PV-DMA-NBR)                        
155000             AND     (B.RSPLVL_CDE  = :PC-RSPLVL-CDE-DMM)                 
155100             AND     (A.OPERCO_NBR  = :PC-OPERCO-NBR)                     
155200             AND     (C.APLSYS_CDE  = :PC-APLSYS-CDE)                     
155300             AND     (A.MDSEAR_DKEY =  B.MDSEAR_DKEY)                     
155400             AND     (A.MDSEAR_DKEY =  C.MDSEAR_DKEY)                     
155500             AND     (:ML114-EXTRACT-DTE                                  
155600                      BETWEEN B.WORKER_STRT_DTE                           
155700                      AND     B.WORKER_END_DTE)                           
155800             AND     (:ML114-EXTRACT-DTE                                  
155900                      BETWEEN C.MDSEAR_STRT_DTE                           
156000                      AND     C.MDSEAR_END_DTE)                           
W26603          WITH UR                                                         
156100     END-EXEC.                                                            
156200                                                                          
156300     EVALUATE TRUE                                                        
156400         WHEN SQLCODE = ZERO                                              
156500             CONTINUE                                                     
156600         WHEN SQLCODE = +100                                              
156700             MOVE 'OPEN' TO  RSPBAR-WORKER-FIR-NAME                       
156800             MOVE SPACE  TO  RSPBAR-WORKER-LAST-NAME                      
156900         WHEN OTHER                                                       
157000             MOVE '8200-GET-DMM-NAME'     TO PV-PARAGRAPH-NAME            
157100             MOVE 'SELECT ROW FROM TRSPBAR'                               
157200                                          TO PV-DB2-OPER-ATTEMPTED        
157300             PERFORM 9998-SQL-ABEND                                       
157400     END-EVALUATE.                                                        
157500                                                                          
157600                                                                          
157700 8300-GET-BYR-NAME.                                                       
157800                                                                          
157900     EXEC SQL                                                             
158000          SELECT      DISTINCT                                            
158100                      A.BYR_NBR                                           
158200               ,      B.WORKER_LAST_NAME                                  
158300               ,      B.WORKER_FIR_NAME                                   
158400            INTO     :MDSEAR-BYR-NBR                                      
158500               ,     :RSPBAR-WORKER-LAST-NAME                             
158600               ,     :RSPBAR-WORKER-FIR-NAME                              
158700            FROM      TMDSEAR A                                           
158800               ,      TRSPBAR B                                           
158900               ,      TAPLAVL C                                           
159000           WHERE     (A.BYR_NBR     = :PV-BYR-NBR)                        
159100             AND     (A.DEPT_NBR    = :PV-DEPT-NBR)                       
222725             AND     (A.MDSELV_CDE  = 'DPT')                              
159200             AND     (B.RSPLVL_CDE  = :PC-RSPLVL-CDE-BYR)                 
159300             AND     (A.OPERCO_NBR  = :PC-OPERCO-NBR)                     
159400             AND     (C.APLSYS_CDE  = :PC-APLSYS-CDE)                     
159500             AND     (A.MDSEAR_DKEY =  B.MDSEAR_DKEY)                     
159600             AND     (A.MDSEAR_DKEY =  C.MDSEAR_DKEY)                     
159700             AND     (:ML114-EXTRACT-DTE                                  
159800                      BETWEEN B.WORKER_STRT_DTE                           
159900                      AND     B.WORKER_END_DTE)                           
160000             AND     (:ML114-EXTRACT-DTE                                  
160100                      BETWEEN C.MDSEAR_STRT_DTE                           
160200                      AND     C.MDSEAR_END_DTE)                           
W26603          WITH UR                                                         
160300     END-EXEC.                                                            
160400                                                                          
160500     EVALUATE TRUE                                                        
160600         WHEN SQLCODE = ZERO                                              
160700             CONTINUE                                                     
160800         WHEN SQLCODE = +100                                              
160900             MOVE 'OPEN' TO  RSPBAR-WORKER-FIR-NAME                       
161000             MOVE SPACE  TO  RSPBAR-WORKER-LAST-NAME                      
161100         WHEN OTHER                                                       
161200             MOVE '8300-GET-BYR-NAME'     TO PV-PARAGRAPH-NAME            
161300             MOVE 'SELECT ROW FROM TRSPBAR'                               
161400                                          TO PV-DB2-OPER-ATTEMPTED        
161500             PERFORM 9998-SQL-ABEND                                       
161600     END-EVALUATE.                                                        
161700                                                                          
161800                                                                          
161900 8400-GET-MDSEAR-DESC.                                                    
162000                                                                          
162100     EXEC SQL                                                             
162200         SELECT     A.MDSEAR_DESC                                         
162300           INTO    :MDSEAR-DESC                                           
162400           FROM     TMDSEAR A                                             
162500              ,     TAPLAVL B                                             
162600          WHERE    (A.GMA_NBR     = :PV-GMA-NBR                           
162700             OR     A.DMA_NBR     = :PV-DMA-NBR                           
162800             OR     A.BYR_NBR     = :PV-BYR-NBR)                          
162900            AND    (A.DEPT_NBR    = :PV-DEPT-NBR)                         
163000            AND    (A.MDSELV_CDE  = :PV-MDSELV-CDE)                       
163100            AND    (A.OPERCO_NBR  = :PC-OPERCO-NBR)                       
163200            AND    (B.APLSYS_CDE  = :PC-APLSYS-CDE)                       
163300            AND    (A.MDSEAR_DKEY =  B.MDSEAR_DKEY)                       
163400            AND    (:ML114-EXTRACT-DTE                                    
163500                    BETWEEN B.MDSEAR_STRT_DTE                             
163600                    AND     B.MDSEAR_END_DTE)                             
163700            AND    (:ML114-EXTRACT-DTE                                    
163800                    BETWEEN A.STRT_DTE                                    
163900                    AND     A.END_DTE)                                    
W26603          WITH UR                                                         
164000     END-EXEC.                                                            
164100                                                                          
164200     EVALUATE TRUE                                                        
164300         WHEN SQLCODE = ZERO                                              
164400             CONTINUE                                                     
164500         WHEN SQLCODE = +100                                              
164600             MOVE 'NOT FOUND' TO MDSEAR-DESC                              
164700         WHEN OTHER                                                       
164800             MOVE '8400-GET-MDSEAR-DESC'  TO PV-PARAGRAPH-NAME            
164900             MOVE 'SELECT DEPARTMENT ROW ON TMDSEAR'                      
165000                                          TO PV-DB2-OPER-ATTEMPTED        
165100             PERFORM 9998-SQL-ABEND                                       
165200     END-EVALUATE.                                                        
165300                                                                          
165400                                                                          
165500 8500-OPEN-FSCL-MN-NM-CSR.                                                
165600                                                                          
165700     EXEC SQL                                                             
165800          OPEN FSCL-MN-NM-CSR                                             
165900     END-EXEC.                                                            
166000                                                                          
166100     EVALUATE TRUE                                                        
166200         WHEN SQLCODE = ZERO                                              
166300             CONTINUE                                                     
166400         WHEN OTHER                                                       
166500             MOVE '8500-OPEN-FSCL-MN-NM-CSR'                              
166600                                          TO PV-PARAGRAPH-NAME            
166700             MOVE 'OPEN FSCL-MN-NM-CSR'   TO PV-DB2-OPER-ATTEMPTED        
166800             PERFORM 9998-SQL-ABEND                                       
166900     END-EVALUATE.                                                        
167000                                                                          
167100                                                                          
167200 8600-FETCH-FSCL-MN-NM-CSR.                                               
167300                                                                          
167400     EXEC SQL                                                             
167500         FETCH                                                            
167600             FSCL-MN-NM-CSR                                               
167700         INTO                                                             
167800             :DTATTR-FSCL-MN-NBR                                          
167900            ,:DTATTR-FSCL-MN-NM                                           
168000     END-EXEC.                                                            
168100                                                                          
168200     EVALUATE TRUE                                                        
168300         WHEN SQLCODE = ZERO                                              
168400             CONTINUE                                                     
168500         WHEN SQLCODE = +100                                              
168600             SET PS-END-OF-FSCL-MN-NM-CSR TO TRUE                         
168700         WHEN OTHER                                                       
168800             MOVE '8600-FETCH-FSCL-MN-NM-CSR'                             
168900                                          TO PV-PARAGRAPH-NAME            
169000             MOVE 'FETCH FSCL-MN-NM-CSR'  TO PV-DB2-OPER-ATTEMPTED        
169100             PERFORM 9998-SQL-ABEND                                       
169200     END-EVALUATE.                                                        
169300                                                                          
169400                                                                          
169500 8700-CLOSE-FSCL-MN-NM-CSR.                                               
169600                                                                          
169700     EXEC SQL                                                             
169800         CLOSE FSCL-MN-NM-CSR                                             
169900     END-EXEC.                                                            
170000                                                                          
170100     EVALUATE TRUE                                                        
170200         WHEN SQLCODE = ZERO                                              
170300             CONTINUE                                                     
170400         WHEN OTHER                                                       
170500             MOVE '8700-CLOSE-FSCL-MN-NM-CSR'                             
170600                                          TO PV-PARAGRAPH-NAME            
170700             MOVE 'CLOSE FSCL-MN-NM-CSR'  TO PV-DB2-OPER-ATTEMPTED        
170800             PERFORM 9998-SQL-ABEND                                       
170900     END-EVALUATE.                                                        
171000                                                                          
171100                                                                          
171200* DPPD024 COMBINES SEPARATE FIRST AND LAST NAME FIELDS INTO ONE  *        
171300* FIRST/LAST NAME FIELD.                                         *        
171400                                                                          
171500     COPY DPPD024.                                                        
171600                                                                          
171700 9998-SQL-ABEND.                                                          
171800                                                                          
171900     MOVE +4013 TO PV-ABEND-CODE.                                         
172000     DISPLAY '** ABEND **         ** = SQLERROR'.                         
172100     DISPLAY '** PROGRAM          ** = MLKRP200'.                         
172200     DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
172300     DISPLAY '** OPERATION        ** = ' PV-DB2-OPER-ATTEMPTED.           
172400                                                                          
172500*    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
172600*    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
172700     COPY DPPD004.                                                        
172800                                                                          
172900     PERFORM 9999-ABEND.                                                  
173000                                                                          
173100                                                                          
173200 9999-ABEND.                                                              
173300                                                                          
173400     CALL 'ILBOABN0'  USING PV-ABEND-CODE.                                
