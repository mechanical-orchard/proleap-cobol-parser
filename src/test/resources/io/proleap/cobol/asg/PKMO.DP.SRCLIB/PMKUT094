      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             PMKUT094.                                        
       AUTHOR.                 AMANDA NEWHART                                   
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           APRIL 2003.                                      
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *    PMKUT094 -                                                           
      *                                                                *        
      *    LOG ONTO ORACLE. OPEN THE SALES DATE FILE AND READ IT. FIND *        
      *    THE SIMPLE RECORDS FROM THE PMTT_PROMO_ITM_SLS_RSLT_LOD     *        
      *    TABLE AND LOAD INTO THE PMT_PROMO_ITM_MDSE_SELN_FCT TABLE.  *        
      *    WHEN ALL THE SIMPLE RECORDS HAVE BEEN SELECTED AND INSERTED *        
      *    LOOK FOR THE UNAD SIMPLE RECORDS FROM THE PMTT_PROMO_ITM_   *        
      *    SLS_RSLT_LOD TABLE.  INSERT THESE RECORDS INTO THE          *        
      *    PMT_PROMO_ITM_MDSE_SELN_FCT TABLE.  WHEN ALL THE UNAD       *        
      *    SIMPLE RECORDS HAVE BEEN FOUND LOOK FOR THE TIERED RECORDS  *        
      *    ON THE PMTT_PROMO_ITM_SLS_RSLT_LOD TABLE.  THEN INSERT THE  *        
      *    TIERED RECORDS INTO PMT_PROMO_ITM_INTFC_SUB_ID_FCT TABLE.   *        
      *    WHEN ALL THOSE ARE FOUND LOOK FOR THE UNAD TIERED RECORDS   *        
      *    AND INSERT THOSE INTO THE PMT_PROMO_ITM_INTFC_SUB_ID_FCT.   *        
      *    WHEN ALL UNAD TIERED RECORDS ARE FOUND AND INSERTED DO A    *        
      *    COMMIT TO KEEP ALL OF THE INSERTS JUST DONE.  CLOSE THE     *        
      *    SALES DATE FILE AND DISPLAY TOTALS IN THE OUTPUT FILE.      *        
      *                                                                *        
      ******************************************************************        
      **   CHANGE LOG:                                                 *        
      ******************************************************************        
      *    07/17/2003 - AMANDA NEWHART - WR21770                       *        
      *     - DO NOT ABEND WHEN THE SALES DATE FILE IS EMPTY.  JUST    *        
      *       DISPLAY A MESSAGE INSTEAD.                               *        
      *                                                                *        
      *    07/26/2003 - COLLEEN CUMMINGS - ABEND                       *        
      *     - U0000 - ORA-01476: DIVISOR IS EQUAL TO ZERO.  ADDED      *        
      *       DIVIDE BY ZERO DECODE STATEMENT INTO CURSORS FOR         *        
      *       TOT-MKUP-PCT CALCULATION.                                *        
      *                                                                *        
      *    10/13/2003 - COLLEEN CUMMINGS - P000596550                  *        
      *     - ADDED MDSE_SELN_ID TO THE JOIN TO PMTT_STR_SELN_STR_ASSOC*        
      *       TABLE FOR UNADVERTISED PROMOTION SALES.                  *        
      *       THIS WAS DONE TO UNIQUELY ROLL UP SALES BY STORE.        *        
      *       WE WERE OVER-STATING SALES BECAUSE THE SAME STORE WAS    *        
      *       IN THIS TABLE FOR THE SAME PROMO ITEM (DUE TO HAVING     *        
      *       A 'CORPORATE' ROW ON PMT_PROMO_ITM_ASSN AND HAVING MORE  *        
      *                                                                *        
      *    09/02/2006 - DARYL WOELFEL - P000920228                     *        
      *     - ADDED "HAVING > -999" TO SELECT STATEMENTS TO INSULATE   *        
      *       US AGAINST TYPO COSTS CAUSING THE JOB TO ABEND.          *        
      *                                                                *        
      *    07/30/2018 - COGNIZANT  - CHG0204126                        *        
      *     - ORACLE LOGON STANDARDS PROGRAM CHANGE                    *        
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SALES-DATE-FILE                                               
               ASSIGN                       TO INP01.                           
                                                                                
070941     SELECT ORACLE-LOGON-FILE                                             
070941         ASSIGN                       TO ORALOGON.                        
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  SALES-DATE-FILE                                                      
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  SALES-DATE-LINE                PIC  X(080).                          
                                                                                
070941 FD  ORACLE-LOGON-FILE                                                    
070941     RECORDING MODE IS F                                                  
070941     LABEL RECORDS ARE STANDARD                                           
070941     BLOCK CONTAINS 0 RECORDS                                             
070941     RECORD CONTAINS 80 CHARACTERS                                        
070941     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
070941 01  OL-ORACLE-LOGON-RECORD         PIC X(80).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0.         
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.           
           88  ABEND-4002-READ-ERROR                     VALUE +4002.           
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.           
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.           
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.           
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.           
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.           
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.           
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
           88  ABEND-4014-ORACLE-LOGON-ERROR             VALUE +4014.           
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.           
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.           
                                                                                
       01  CC-SALES-DATE-FILE.                                                  
           05  CC-SALES-DATE                PIC X(10).                          
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
070941     05  PS-END-OF-LOGON-FILE-SW      PIC X(01) VALUE 'N'.                
070941         88  PS-END-OF-LOGON-FILE               VALUE 'Y'.                
070941     05  PS-ORA-SRC-CTL-DONE-SW       PIC X(01) VALUE 'N'.                
070941         88  PS-ORA-SRC-EMPTY                   VALUE 'Y'.                
           05  PS-SALES-DATE-EXIST-SWITCH   PIC X(01) VALUE 'N'.                
               88  PS-SALES-DATE-EXIST                VALUE 'Y'.                
               88  PS-SALES-DATE-NOT-EXIST            VALUE 'N'.                
           05  PS-SALES-DATE-FILE-SWITCH    PIC X(01) VALUE 'N'.                
               88  PS-SALES-DATE-FILE-END             VALUE 'Y'.                
               88  PS-SALES-DATE-FILE-START           VALUE 'N'.                
           05  PS-SIMP-PROMO-CSR-SWITCH     PIC X(01) VALUE 'N'.                
               88  PS-END-SIMP-PROMO-CSR              VALUE 'Y'.                
               88  PS-NOT-END-SIMP-PROMO-CSR          VALUE 'N'.                
           05  PS-UNADSIMPPROMO-CSR-SWITCH  PIC X(01) VALUE 'N'.                
               88  PS-END-UNADSIMPPROMO-CSR           VALUE 'Y'.                
               88  PS-NOT-END-UNADSIMPPROMO-CSR       VALUE 'N'.                
           05  PS-TIER-PROMO-CSR-SWITCH     PIC X(01) VALUE 'N'.                
               88  PS-END-TIER-PROMO-CSR              VALUE 'Y'.                
               88  PS-NOT-END-TIER-PROMO-CSR          VALUE 'N'.                
           05  PS-UNADTIERPROMO-CSR-SWITCH  PIC X(01) VALUE 'N'.                
               88  PS-END-UNADTIERPROMO-CSR           VALUE 'Y'.                
               88  PS-NOT-END-UNADTIERPROMO-CSR       VALUE 'N'.                
                                                                                
070941 01  CC-ORACLE-CONNECTION.                                                
070941     05  CC-ORACLE-USERID             PIC X(40).                          
070941         88 CC-AUTO-LOGON                        VALUE '/'.               
070941     05  CC-ORACLE-PASSWORD           PIC X(40).                          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
070941*    05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)  VALUE                   
070941     05  PC-AUTO-LOGON                PIC  X(1)  VALUE '/'.               
070941     05  PC-PROGRAM-NAME              PIC  X(08) VALUE                    
                                                      'PMKUT094'.               
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
070941     05  PV-ORACLE-USERID-LEN         PIC S9(4)   VALUE ZEROS             
070941                                                  COMP SYNC.              
070941     05  PV-ORACLE-PASSWORD-LEN       PIC S9(4)   VALUE ZEROS             
070941                                                  COMP SYNC.              
070941     05  PC-AUTO-LOGON                PIC  X(01)  VALUE '/'.              
           05  PV-SQL                       PIC S9(04)  VALUE ZEROS.            
           05  PV-TABLE-NAME                PIC  X(12)  VALUE SPACES.           
           05  PV-OPERATION-MSG             PIC  X(60)  VALUE SPACES.           
           05  PV-OPERATION-MSG-1           PIC  X(40)  VALUE SPACES.           
           05  PV-PARAGRAPH-NAME            PIC  X(30)  VALUE SPACES.           
                                                                                
       01  PV-PROMO-VARIABLES.                                                  
           05  PV-TM-DIM-KY-DTE             PIC  X(10)   VALUE SPACES.          
           05  PV-STR-SELN-ID               PIC  S9(05)   COMP-3                
                                                VALUE ZEROS.                    
           05  PV-PROMO-ID                  PIC  S9(10)   COMP-3                
                                                VALUE ZEROS.                    
           05  PV-PROMO-ITM-ID              PIC  S9(10)   COMP-3                
                                                VALUE ZEROS.                    
           05  PV-PROMO-INTFC-SUB-ID        PIC  S9(10)   COMP-3                
                                                VALUE ZEROS.                    
           05  PV-MDSE-SELN-ID              PIC  S9(10)   COMP-3                
                                                VALUE ZEROS.                    
           05  PV-UNT-MKDN-MIN-RTL-AMT      PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-UNT-MKDN-MAX-RTL-AMT      PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-UNT-MKDN-MIN-RTL-PCT      PIC S9(03)V999 COMP-3               
                                                VALUE ZEROS.                    
           05  PV-UNT-MKDN-MAX-RTL-PCT      PIC S9(03)V999 COMP-3               
                                                VALUE ZEROS.                    
           05  PV-UNT-MKUP-MIN-RTL-PCT      PIC S9(03)V999 COMP-3               
                                                VALUE ZEROS.                    
           05  PV-UNT-MKUP-MAX-RTL-PCT      PIC S9(03)V999 COMP-3               
                                                VALUE ZEROS.                    
           05  PV-MIN-UNT-REG-RTL-AMT       PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-MAX-UNT-REG-RTL-AMT       PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-MIN-UNT-PROMO-RTL-AMT     PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-MAX-UNT-PROMO-RTL-AMT     PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-MIN-UNT-REG-COST-AMT      PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-MAX-UNT-REG-COST-AMT      PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-SLS-UNT-QTY               PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-SLS-COST-AMT              PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-SLS-RTL-AMT               PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-MKDN-RTL-AMT              PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-TOT-MKUP-PCT              PIC S9(03)V999 COMP-3               
                                                VALUE ZEROS.                    
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-SIMP-PROMO-CNT            PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-UNAD-SIMP-PROMO-CNT       PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-TIER-PROMO-CNT            PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-UNAD-TIER-PROMO-CNT       PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-SIMP-INSERT               PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-TIER-INSERT               PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-COMMIT-COUNTER            PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
                                                                                
      *****************************************************************         
      *                      O  R  A  C  L  E                                   
      *================================================================         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *****************************************************************         
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *---------------------------------------------------------------*         
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *---------------------------------------------------------------*         
       01      PC-ENT01-TYPE-CDE          PIC X(02)      VALUE '01'.            
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
                                                                                
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
                                                                                
       01      ORACLE-RETURN              PIC X(200)     VALUE SPACES.          
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      ********************                                                      
       EJECT                                                                    
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      * ==> PROCESSES:                                                *         
      *  - SET UP ORACLE                                              *         
      *  - RUN PARAGRAPHS TO LOAD THE PMT_PROMO_ITM_MDSE_SELN_FCT     *         
      *    TABLE.  THIS IS FOR THE SIMPLE PROMOS WITH PROMO ITEMS.    *         
      *  - RUN PARAGRAPHS TO LOAD THE PMT_PROMO_ITM_INTFC_SUB_ID_FCT  *         
      *    TABLE.  THIS IS FOR THE TIERED PROMOS WITH PROMO ITEMS.    *         
      *  - COMMIT WHAT WAS JUST INSERTED.                             *         
      *  - DISPLAY TOTALS.                                            *         
      * ==> PERFORMED FROM: START                                     *         
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
070941*    PERFORM PM700-A000-ORACLE-SETUP.                                     
           PERFORM B100-OPEN-FILES.                                             
           PERFORM B200-READ-SALES-DATE-FILE.                                   
                                                                                
           IF PS-SALES-DATE-EXIST                                               
               PERFORM C200-DECLARE-SIMP-PROMO-CSR                              
               PERFORM C300-OPEN-SIMP-PROMO-CSR                                 
               PERFORM C400-FETCH-SIMP-PROMO-CSR                                
               PERFORM C500-PROCESS-SIMP-PROMO-CSR                              
                   UNTIL PS-END-SIMP-PROMO-CSR                                  
               PERFORM C600-CLOSE-SIMP-PROMO-CSR                                
                                                                                
               PERFORM C205-DECLARE-UNADSIMPPROMO-CSR                           
               PERFORM C305-OPEN-UNADSIMPPROMO-CSR                              
               PERFORM C405-FETCH-UNADSIMPPROMO-CSR                             
               PERFORM C505-PROCESS-UNADSIMPPROMO-CSR                           
                   UNTIL PS-END-UNADSIMPPROMO-CSR                               
               PERFORM C605-CLOSE-UNADSIMPPROMO-CSR                             
                                                                                
               PERFORM C210-DECLARE-TIER-PROMO-CSR                              
               PERFORM C310-OPEN-TIER-PROMO-CSR                                 
               PERFORM C410-FETCH-TIER-PROMO-CSR                                
               PERFORM C510-PROCESS-TIER-PROMO-CSR                              
                   UNTIL PS-END-TIER-PROMO-CSR                                  
               PERFORM C610-CLOSE-TIER-PROMO-CSR                                
                                                                                
               PERFORM C215-DECLARE-UNADTIERPROMO-CSR                           
               PERFORM C315-OPEN-UNADTIERPROMO-CSR                              
               PERFORM C415-FETCH-UNADTIERPROMO-CSR                             
               PERFORM C515-PROCESS-UNADTIERPROMO-CSR                           
                   UNTIL PS-END-UNADTIERPROMO-CSR                               
               PERFORM C615-CLOSE-UNADTIERPROMO-CSR                             
                                                                                
               PERFORM C120-COMMIT                                              
                                                                                
               DISPLAY 'FINAL SIMP PROMO COUNT  = ' PA-SIMP-PROMO-CNT           
               DISPLAY 'FINAL UNAD SIMP PROMO COUNT  = '                        
                                                PA-UNAD-SIMP-PROMO-CNT          
               DISPLAY 'FINAL TIER PROMO COUNT  = ' PA-TIER-PROMO-CNT           
               DISPLAY 'FINAL UNAD TIER PROMO COUNT  = '                        
                                                PA-UNAD-TIER-PROMO-CNT          
               DISPLAY 'FINAL SIMP INSERT COUNT = ' PA-SIMP-INSERT              
               DISPLAY 'FINAL TIER INSERT COUNT = ' PA-TIER-INSERT              
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM B900-CLOSE-FILES.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * B100-OPEN-FILES.                                              *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE SALES DATE CONTROL CARD                           *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B100-OPEN-FILES.                                                         
                                                                                
070941     DISPLAY 'ORACLE FILE OPEN'.                                          
070941     OPEN INPUT ORACLE-LOGON-FILE.                                        
070941                                                                          
070941     DISPLAY 'ORACLE FILE READ'.                                          
070941     PERFORM 7000-READ-ORACLE-LOGON.                                      
070941                                                                          
070941     DISPLAY 'ORACLE FILE CLOSE'.                                         
070941     CLOSE ORACLE-LOGON-FILE.                                             
070941                                                                          
070941     IF ((PS-END-OF-LOGON-FILE)                                           
070941             OR (CC-ORACLE-CONNECTION = SPACES))                          
070941         DISPLAY '** ABEND **'                                            
070941         DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
070941                   'NOT PROVIDED'                                         
070941         SET PS-ORA-SRC-EMPTY TO TRUE                                     
070941         CALL 'ILBOABN0' USING ABEND-CODE                                 
070941     END-IF.                                                              
070941                                                                          
070941     IF CC-AUTO-LOGON                                                     
070941         DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
070941         EXEC SQL                                                         
070941              CONNECT :USERNAME                                           
070941         END-EXEC                                                         
070941     ELSE                                                                 
070941         INITIALIZE PV-ORACLE-USERID-LEN                                  
070941                    PV-ORACLE-PASSWORD-LEN                                
070941         INSPECT CC-ORACLE-USERID                                         
070941             TALLYING PV-ORACLE-USERID-LEN                                
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      INSPECT CC-ORACLE-PASSWORD                                          
070941          TALLYING PV-ORACLE-PASSWORD-LEN                                 
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
070941                USERNAME-ARR                                              
070941      MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
070941      MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
070941                PASSWD-ARR                                                
070941      MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
070941      DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '               
070941                USERNAME-ARR                                              
070941      EXEC SQL                                                            
070941        CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
070941      END-EXEC                                                            
070941      INITIALIZE CC-ORACLE-PASSWORD                                       
070941                 PASSWD                                                   
070941      END-IF.                                                             
070941                                                                          
070941     IF (SQLCODE NOT = 0)                                                 
070941         DISPLAY PV-PARAGRAPH-NAME                                        
070941         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
070941         PERFORM 9900-ORACLE-ABEND                                        
070941     END-IF.                                                              
                                                                                
           OPEN INPUT  SALES-DATE-FILE.                                         
                                                                                
      *****************************************************************         
      * B200-READ-SALES-DATE-FILE.                                    *         
      * ==> PROCESSES:                                                *         
      *  - READ IN THE SALES DATE FILE                                *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B200-READ-SALES-DATE-FILE.                                               
                                                                                
           READ SALES-DATE-FILE INTO CC-SALES-DATE-FILE                         
              AT END                                                            
                 SET PS-SALES-DATE-FILE-END TO TRUE                             
           END-READ.                                                            
                                                                                
           IF (PS-SALES-DATE-FILE-END) OR (CC-SALES-DATE-FILE = SPACES)         
               DISPLAY 'SALES DATE HAS NOTHING TO PROCESS'                      
               DISPLAY '**** END OF PROGRAM ****'                               
               SET PS-SALES-DATE-NOT-EXIST TO TRUE                              
           ELSE                                                                 
               DISPLAY 'CC-SALES-DATE = ' CC-SALES-DATE                         
               SET PS-SALES-DATE-EXIST TO TRUE                                  
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B900-CLOSE-FILES                                              *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE SALES DATE CONTROL CARD                          *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B900-CLOSE-FILES.                                                        
                                                                                
           CLOSE  SALES-DATE-FILE.                                              
                                                                                
      *****************************************************************         
      * C100-INSERT-SIMP:                                             *         
      * ==> PROCESSES:                                                *         
      *  - INSERT THE SIMPLE ROW THAT WAS SELECTED FROM THE           *         
      *    PMT_PROMO_ITM_SLS_RSLT_LOD TABLE.                          *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = INSERT WAS SUCCEFULL                                   *         
      * ==> PERFORMED FROM: C500-PROCESS-SIMP-PROMO-CSR,              *         
      *       C505-PROCESS-UNAD-SIMP-PROMO-CSR                        *         
      *****************************************************************         
       C100-INSERT-SIMP.                                                        
                                                                                
           EXEC SQL                                                             
               INSERT INTO PMT_PROMO_ITM_MDSE_SELN_FCT                          
                   (PROMO_ID                                                    
                   ,PROMO_ITM_ID                                                
                   ,MDSE_SELN_ID                                                
                   ,TM_DIM_KY_DTE                                               
                   ,STR_SELN_ID                                                 
                   ,UNT_MKDN_MIN_RTL_AMT                                        
                   ,UNT_MKDN_MAX_RTL_AMT                                        
                   ,UNT_MKDN_MIN_RTL_PCT                                        
                   ,UNT_MKDN_MAX_RTL_PCT                                        
                   ,UNT_MKUP_MIN_RTL_PCT                                        
                   ,UNT_MKUP_MAX_RTL_PCT                                        
                   ,MIN_UNT_REG_RTL_AMT                                         
                   ,MAX_UNT_REG_RTL_AMT                                         
                   ,MIN_UNT_PROMO_RTL_AMT                                       
                   ,MAX_UNT_PROMO_RTL_AMT                                       
                   ,MIN_UNT_REG_COST_AMT                                        
                   ,MAX_UNT_REG_COST_AMT                                        
                   ,SLS_UNT_QTY                                                 
                   ,SLS_COST_AMT                                                
                   ,SLS_RTL_AMT                                                 
                   ,MKDN_RTL_AMT                                                
                   ,TOT_MKUP_PCT)                                               
               VALUES                                                           
                   (:PV-PROMO-ID                                                
                   ,:PV-PROMO-ITM-ID                                            
                   ,:PV-MDSE-SELN-ID                                            
                   ,:PV-TM-DIM-KY-DTE                                           
                   ,:PV-STR-SELN-ID                                             
                   ,:PV-UNT-MKDN-MIN-RTL-AMT                                    
                   ,:PV-UNT-MKDN-MAX-RTL-AMT                                    
                   ,:PV-UNT-MKDN-MIN-RTL-PCT                                    
                   ,:PV-UNT-MKDN-MAX-RTL-PCT                                    
                   ,:PV-UNT-MKUP-MIN-RTL-PCT                                    
                   ,:PV-UNT-MKUP-MAX-RTL-PCT                                    
                   ,:PV-MIN-UNT-REG-RTL-AMT                                     
                   ,:PV-MAX-UNT-REG-RTL-AMT                                     
                   ,:PV-MIN-UNT-PROMO-RTL-AMT                                   
                   ,:PV-MAX-UNT-PROMO-RTL-AMT                                   
                   ,:PV-MIN-UNT-REG-COST-AMT                                    
                   ,:PV-MAX-UNT-REG-COST-AMT                                    
                   ,:PV-SLS-UNT-QTY                                             
                   ,:PV-SLS-COST-AMT                                            
                   ,:PV-SLS-RTL-AMT                                             
                   ,:PV-MKDN-RTL-AMT                                            
                   ,:PV-TOT-MKUP-PCT)                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
                 ADD 1 TO PA-SIMP-INSERT                                        
                 CONTINUE                                                       
             WHEN OTHER                                                         
               DISPLAY 'PROMO ID         = ' PV-PROMO-ID                        
               DISPLAY 'PV-PROMO-ITM-ID  = ' PV-PROMO-ITM-ID                    
               DISPLAY 'PV-MDSE-SELN-ID  = ' PV-MDSE-SELN-ID                    
               DISPLAY 'PV-TM-DIM-KY-DTE = ' PV-TM-DIM-KY-DTE                   
               DISPLAY 'PV-STR-SELN-ID   = ' PV-STR-SELN-ID                     
               MOVE SQLCODE               TO PV-SQL                             
               MOVE 'DUAL'                TO PV-TABLE-NAME                      
               MOVE 'C100-INSERT-SIMP'    TO PV-PARAGRAPH-NAME                  
               PERFORM 9900-ORACLE-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C110-INSERT-TIER                                              *         
      * ==> PROCESSES:                                                *         
      *  - INSERT THE TIERED ROW THAT WAS SELECTED FROM THE           *         
      *    PMT_PROMO_ITM_SLS_RSLT_LOD TABLE.                          *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = INSERT WAS SUCCEFULL                                   *         
      * ==> PERFORMED FROM: C510-PROCESS-TIER-PROMO-CSR,              *         
      *       C515-PROCESS-UNAD-TIER-PROMO-CSR                        *         
      *****************************************************************         
       C110-INSERT-TIER.                                                        
           EXEC SQL                                                             
               INSERT INTO PMT_PROMO_ITM_INTFC_SUB_ID_FCT                       
                   (PROMO_ID                                                    
                   ,PROMO_ITM_ID                                                
                   ,PROMO_INTFC_SUB_ID                                          
                   ,TM_DIM_KY_DTE                                               
                   ,STR_SELN_ID                                                 
                   ,SLS_RTL_AMT                                                 
                   ,SLS_UNT_QTY                                                 
                   ,MKDN_RTL_AMT                                                
                   ,SLS_COST_AMT)                                               
               VALUES                                                           
                   (:PV-PROMO-ID                                                
                   ,:PV-PROMO-ITM-ID                                            
                   ,:PV-PROMO-INTFC-SUB-ID                                      
                   ,:PV-TM-DIM-KY-DTE                                           
                   ,:PV-STR-SELN-ID                                             
                   ,:PV-SLS-RTL-AMT                                             
                   ,:PV-SLS-UNT-QTY                                             
                   ,:PV-MKDN-RTL-AMT                                            
                   ,:PV-SLS-COST-AMT)                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
                 ADD 1 TO PA-TIER-INSERT                                        
                 CONTINUE                                                       
             WHEN OTHER                                                         
               MOVE SQLCODE               TO PV-SQL                             
               MOVE 'DUAL'                TO PV-TABLE-NAME                      
               MOVE 'C100-INSERT-SIMP'    TO PV-PARAGRAPH-NAME                  
               PERFORM 9900-ORACLE-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C120-COMMIT                                                   *         
      * ==> PROCESSES:                                                *         
      *  - SAVE WHAT HAS BEEN DONE TO THE DATABASE SO FAR             *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = COMMIT WAS SUCCEFULL                                   *         
      * ==> PERFORMED FROM:  A100-MAINLINE                            *         
      *****************************************************************         
       C120-COMMIT.                                                             
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
067400                                                                          
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
               ADD 1    TO PA-COMMIT-COUNTER                                    
067600         CONTINUE                                                         
             WHEN OTHER                                                         
067800         MOVE 'PGM ABEND DURING A COMMIT' TO PV-OPERATION-MSG             
067900         MOVE 'C120-COMMIT'               TO PV-PARAGRAPH-NAME            
068000         PERFORM Z910-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C200-DECLARE-SIMP-PROMO-CSR                                   *         
      * ==> PROCESSES:                                                *         
      *  - DECLARE THE CURSOR USED TO FIND THE SIMPLE PROMO ITEMS.    *         
      *  - IN THE CURSOR USE MAX TO FIND THE MAX NUMBER IN THE        *         
      *    FIELD, MIN TO FIND THE MIN NUMBER IN THE FIELD, AND SUM    *         
      *    TO ADD UP ALL THE ROWS THAT ARE GROUPED TOGETHER.          *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C200-DECLARE-SIMP-PROMO-CSR.                                             
                                                                                
           EXEC SQL                                                             
               DECLARE SIMPPROMOCSR                                             
               CURSOR FOR                                                       
                   SELECT DISTINCT                                              
                        A.TRN_SLS_DTE                                           
                       ,A.PROMO_ID                                              
                       ,A.PROMO_ITM_ID                                          
                       ,A.MDSE_SELN_ID                                          
                       ,B.STR_SELN_ID                                           
                       ,MIN(A.UNT_MKDN_MIN_RTL_AMT)                             
                       ,MAX(A.UNT_MKDN_MAX_RTL_AMT)                             
                       ,MIN(A.UNT_MKDN_MIN_RTL_PCT)                             
                       ,MAX(A.UNT_MKDN_MAX_RTL_PCT)                             
                       ,MIN(A.UNT_MKUP_MIN_RTL_PCT)                             
                       ,MAX(A.UNT_MKUP_MAX_RTL_PCT)                             
                       ,MIN(A.UNT_REG_RTL_AMT)                                  
                       ,MAX(A.UNT_REG_RTL_AMT)                                  
                       ,MIN(A.MIN_UNT_PROMO_RTL_AMT)                            
                       ,MAX(A.MAX_UNT_PROMO_RTL_AMT)                            
                       ,MIN(A.UNT_REG_COST_AMT)                                 
                       ,MAX(A.UNT_REG_COST_AMT)                                 
                       ,SUM(A.SLS_UNT_QTY)                                      
                       ,SUM(A.SLS_COST_AMT)                                     
                       ,SUM(A.SLS_RTL_AMT)                                      
                       ,SUM(A.MKDN_RTL_AMT)                                     
                       ,DECODE (SUM(A.SLS_RTL_AMT), 0, 0,                       
                          ROUND(((SUM(A.SLS_RTL_AMT) -                          
                                  SUM(A.SLS_COST_AMT))                          
                                / SUM(A.SLS_RTL_AMT)) * 100,3))                 
                   FROM                                                         
                        PMTT_PROMO_ITM_SLS_RSLT_LOD A                           
                       ,PMTT_STR_SELN_STR_ASSOC B                               
                       ,PMT_PROMO_REG C                                         
                   WHERE                                                        
                        A.PROMO_ID = B.PROMO_ID                                 
                    AND A.STR_NBR = B.STR_NBR                                   
                    AND B.PROMO_ID = C.PROMO_ID                                 
                    AND A.PROMO_SLS_TYP_CDE = 'S'                               
                    AND A.TRN_SLS_DTE =                                         
                                TO_DATE(:CC-SALES-DATE, 'YYYY-MM-DD')           
                    AND C.TYP_CDE = 'A'                                         
                   GROUP BY                                                     
                        A.TRN_SLS_DTE                                           
                       ,A.PROMO_ID                                              
                       ,A.PROMO_ITM_ID                                          
                       ,A.MDSE_SELN_ID                                          
                       ,B.STR_SELN_ID                                           
                   HAVING                                                       
                       DECODE (SUM(A.SLS_RTL_AMT), 0, 0,                        
                          ROUND(((SUM(A.SLS_RTL_AMT) -                          
                                  SUM(A.SLS_COST_AMT))                          
                                / SUM(A.SLS_RTL_AMT)) * 100,3)) > -999          
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C205-DECLARE-UNADSIMPPROMO-CSR                                *         
      * ==> PROCESSES:                                                *         
      *  - DECLARE THE CURSOR USED TO FIND THE SIMPLE PROMO ITEMS.    *         
      *  - IN THE CURSOR USE MAX TO FIND THE MAX NUMBER IN THE        *         
      *    FIELD, MIN TO FIND THE MIN NUMBER IN THE FIELD, AND SUM    *         
      *    TO ADD UP ALL THE ROWS THAT ARE GROUPED TOGETHER.          *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C205-DECLARE-UNADSIMPPROMO-CSR.                                          
                                                                                
           EXEC SQL                                                             
               DECLARE UNADSIMPPROMOCSR                                         
               CURSOR FOR                                                       
                   SELECT DISTINCT                                              
                        A.TRN_SLS_DTE                                           
                       ,A.PROMO_ID                                              
                       ,A.PROMO_ITM_ID                                          
                       ,A.MDSE_SELN_ID                                          
                       ,B.STR_SELN_ID                                           
                       ,MIN(A.UNT_MKDN_MIN_RTL_AMT)                             
                       ,MAX(A.UNT_MKDN_MAX_RTL_AMT)                             
                       ,MIN(A.UNT_MKDN_MIN_RTL_PCT)                             
                       ,MAX(A.UNT_MKDN_MAX_RTL_PCT)                             
                       ,MIN(A.UNT_MKUP_MIN_RTL_PCT)                             
                       ,MAX(A.UNT_MKUP_MAX_RTL_PCT)                             
                       ,MIN(A.UNT_REG_RTL_AMT)                                  
                       ,MAX(A.UNT_REG_RTL_AMT)                                  
                       ,MIN(A.MIN_UNT_PROMO_RTL_AMT)                            
                       ,MAX(A.MAX_UNT_PROMO_RTL_AMT)                            
                       ,MIN(A.UNT_REG_COST_AMT)                                 
                       ,MAX(A.UNT_REG_COST_AMT)                                 
                       ,SUM(A.SLS_UNT_QTY)                                      
                       ,SUM(A.SLS_COST_AMT)                                     
                       ,SUM(A.SLS_RTL_AMT)                                      
                       ,SUM(A.MKDN_RTL_AMT)                                     
                       ,DECODE (SUM(A.SLS_RTL_AMT), 0, 0,                       
                          ROUND(((SUM(A.SLS_RTL_AMT) -                          
                                  SUM(A.SLS_COST_AMT))                          
                                / SUM(A.SLS_RTL_AMT)) * 100,3))                 
                   FROM                                                         
                        PMTT_PROMO_ITM_SLS_RSLT_LOD A                           
                       ,PMTT_STR_SELN_STR_ASSOC B                               
                       ,PMT_PROMO_REG C                                         
                   WHERE                                                        
                        A.PROMO_ID = B.PROMO_ID                                 
                    AND A.PROMO_ITM_ID = B.PROMO_ITM_ID                         
                    AND A.MDSE_SELN_ID = B.MDSE_SELN_ID                         
                    AND A.STR_NBR = B.STR_NBR                                   
                    AND B.PROMO_ID = C.PROMO_ID                                 
                    AND A.PROMO_SLS_TYP_CDE = 'S'                               
                    AND A.TRN_SLS_DTE =                                         
                                TO_DATE(:CC-SALES-DATE, 'YYYY-MM-DD')           
                    AND C.TYP_CDE = 'U'                                         
                   GROUP BY                                                     
                        A.TRN_SLS_DTE                                           
                       ,A.PROMO_ID                                              
                       ,A.PROMO_ITM_ID                                          
                       ,A.MDSE_SELN_ID                                          
                       ,B.STR_SELN_ID                                           
                   HAVING                                                       
                       DECODE (SUM(A.SLS_RTL_AMT), 0, 0,                        
                          ROUND(((SUM(A.SLS_RTL_AMT) -                          
                                  SUM(A.SLS_COST_AMT))                          
                                / SUM(A.SLS_RTL_AMT)) * 100,3)) > -999          
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C210-DECLARE-TIER-PROMO-CSR                                   *         
      * ==> PROCESSES:                                                *         
      *  - DECLARE THE CURSOR USED TO FIND THE TIERED PROMO ITEMS.    *         
      *  - IN THE CURSOR USE MAX TO FIND THE MAX NUMBER IN THE        *         
      *    FIELD, MIN TO FIND THE MIN NUMBER IN THE FIELD, AND SUM    *         
      *    TO ADD UP ALL THE ROWS THAT ARE GROUPED TOGETHER.          *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C210-DECLARE-TIER-PROMO-CSR.                                             
                                                                                
           EXEC SQL                                                             
               DECLARE TIERPROMOCSR                                             
               CURSOR FOR                                                       
                   SELECT DISTINCT                                              
                        A.TRN_SLS_DTE                                           
                       ,A.PROMO_ID                                              
                       ,A.PROMO_ITM_ID                                          
                       ,B.STR_SELN_ID                                           
                       ,A.PROMO_INTFC_SUB_ID                                    
                       ,SUM(A.SLS_UNT_QTY)                                      
                       ,SUM(A.SLS_COST_AMT)                                     
                       ,SUM(A.SLS_RTL_AMT)                                      
                       ,SUM(A.MKDN_RTL_AMT)                                     
                   FROM                                                         
                        PMTT_PROMO_ITM_SLS_RSLT_LOD A                           
                       ,PMTT_STR_SELN_STR_ASSOC B                               
                       ,PMT_PROMO_REG C                                         
                   WHERE                                                        
                        A.PROMO_ID = B.PROMO_ID                                 
                    AND A.STR_NBR = B.STR_NBR                                   
                    AND B.PROMO_ID = C.PROMO_ID                                 
                    AND A.PROMO_SLS_TYP_CDE = 'T'                               
                    AND A.TRN_SLS_DTE =                                         
                                TO_DATE(:CC-SALES-DATE, 'YYYY-MM-DD')           
                    AND C.TYP_CDE = 'A'                                         
                   GROUP BY                                                     
                        A.TRN_SLS_DTE                                           
                       ,A.PROMO_ID                                              
                       ,A.PROMO_ITM_ID                                          
                       ,B.STR_SELN_ID                                           
                       ,A.PROMO_INTFC_SUB_ID                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C215-DECLARE-UNADTIERPROMO-CSR                                *         
      * ==> PROCESSES:                                                *         
      *  - DECLARE THE CURSOR USED TO FIND THE TIERED PROMO ITEMS.    *         
      *  - IN THE CURSOR USE MAX TO FIND THE MAX NUMBER IN THE        *         
      *    FIELD, MIN TO FIND THE MIN NUMBER IN THE FIELD, AND SUM    *         
      *    TO ADD UP ALL THE ROWS THAT ARE GROUPED TOGETHER.          *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C215-DECLARE-UNADTIERPROMO-CSR.                                          
                                                                                
           EXEC SQL                                                             
               DECLARE UNADTIERPROMOCSR                                         
               CURSOR FOR                                                       
                   SELECT DISTINCT                                              
                        A.TRN_SLS_DTE                                           
                       ,A.PROMO_ID                                              
                       ,A.PROMO_ITM_ID                                          
                       ,B.STR_SELN_ID                                           
                       ,A.PROMO_INTFC_SUB_ID                                    
                       ,SUM(A.SLS_UNT_QTY)                                      
                       ,SUM(A.SLS_COST_AMT)                                     
                       ,SUM(A.SLS_RTL_AMT)                                      
                       ,SUM(A.MKDN_RTL_AMT)                                     
                   FROM                                                         
                        PMTT_PROMO_ITM_SLS_RSLT_LOD A                           
                       ,PMTT_STR_SELN_STR_ASSOC B                               
                       ,PMT_PROMO_REG C                                         
                   WHERE                                                        
                        A.PROMO_ID = B.PROMO_ID                                 
                    AND A.PROMO_ITM_ID = B.PROMO_ITM_ID                         
                    AND A.MDSE_SELN_ID = B.MDSE_SELN_ID                         
                    AND A.STR_NBR = B.STR_NBR                                   
                    AND B.PROMO_ID = C.PROMO_ID                                 
                    AND A.PROMO_SLS_TYP_CDE = 'T'                               
                    AND A.TRN_SLS_DTE =                                         
                                TO_DATE(:CC-SALES-DATE, 'YYYY-MM-DD')           
                    AND C.TYP_CDE = 'U'                                         
                   GROUP BY                                                     
                        A.TRN_SLS_DTE                                           
                       ,A.PROMO_ID                                              
                       ,A.PROMO_ITM_ID                                          
                       ,B.STR_SELN_ID                                           
                       ,A.PROMO_INTFC_SUB_ID                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C300-OPEN-SIMP-PROMO-CSR.                                     *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE SIMPLE CURSOR.                                    *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = WAS OPENED SUCCESSFULLY.                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C300-OPEN-SIMP-PROMO-CSR.                                                
                                                                                
           EXEC SQL                                                             
                 OPEN SIMPPROMOCSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C300-OPEN-SIMP-PROMO-CSR   '                           
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON SIMPPROMOCSR '                           
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C305-OPEN-UNADSIMPPROMO-CSR.                                  *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE UNAD SIMPLE CURSOR.                               *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = WAS OPENED SUCCESSFULLY.                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C305-OPEN-UNADSIMPPROMO-CSR.                                             
                                                                                
           EXEC SQL                                                             
                 OPEN UNADSIMPPROMOCSR                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C305-OPEN-UNADSIMPPROMO-CSR '                          
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON UNADSIMPPROMOCSR '                       
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C310-OPEN-TIER-PROMO-CSR.                                     *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE TIERED CURSOR.                                    *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = WAS OPENED SUCCESSFULLY.                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C310-OPEN-TIER-PROMO-CSR.                                                
                                                                                
           EXEC SQL                                                             
                 OPEN TIERPROMOCSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C310-OPEN-TIER-PROMO-CSR   '                           
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON TIERPROMOCSR '                           
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C315-OPEN-UANDTIERPROMO-CSR.                                  *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE UNAD TIERED CURSOR.                               *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = WAS OPENED SUCCESSFULLY.                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C315-OPEN-UNADTIERPROMO-CSR.                                             
                                                                                
           EXEC SQL                                                             
                 OPEN UNADTIERPROMOCSR                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C315-OPEN-UNADTIERPROMO-CSR '                          
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON UNADTIERPROMOCSR '                       
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C400-FETCH-SIMP-PROMO-CSR.                                    *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE, C500-PROCESS-SIMP-PROMO-CSR*         
      *****************************************************************         
       C400-FETCH-SIMP-PROMO-CSR.                                               
                                                                                
           EXEC SQL                                                             
               FETCH SIMPPROMOCSR                                               
                INTO :PV-TM-DIM-KY-DTE                                          
                    ,:PV-PROMO-ID                                               
                    ,:PV-PROMO-ITM-ID                                           
                    ,:PV-MDSE-SELN-ID                                           
                    ,:PV-STR-SELN-ID                                            
                    ,:PV-UNT-MKDN-MIN-RTL-AMT                                   
                    ,:PV-UNT-MKDN-MAX-RTL-AMT                                   
                    ,:PV-UNT-MKDN-MIN-RTL-PCT                                   
                    ,:PV-UNT-MKDN-MAX-RTL-PCT                                   
                    ,:PV-UNT-MKUP-MIN-RTL-PCT                                   
                    ,:PV-UNT-MKUP-MAX-RTL-PCT                                   
                    ,:PV-MIN-UNT-REG-RTL-AMT                                    
                    ,:PV-MAX-UNT-REG-RTL-AMT                                    
                    ,:PV-MIN-UNT-PROMO-RTL-AMT                                  
                    ,:PV-MAX-UNT-PROMO-RTL-AMT                                  
                    ,:PV-MIN-UNT-REG-COST-AMT                                   
                    ,:PV-MAX-UNT-REG-COST-AMT                                   
                    ,:PV-SLS-UNT-QTY                                            
                    ,:PV-SLS-COST-AMT                                           
                    ,:PV-SLS-RTL-AMT                                            
                    ,:PV-MKDN-RTL-AMT                                           
                    ,:PV-TOT-MKUP-PCT                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-SIMP-PROMO-CNT                                   
                                                                                
               WHEN SQLCODE = 1403                                              
                   SET PS-END-SIMP-PROMO-CSR TO TRUE                            
                                                                                
               WHEN OTHER                                                       
                   DISPLAY 'PROMO ID = ' PV-PROMO-ID                            
                   DISPLAY 'PV-PROMO-ITM-ID = ' PV-PROMO-ITM-ID                 
                   DISPLAY 'PV-MDSE-SELN-ID = ' PV-MDSE-SELN-ID                 
                   DISPLAY 'PV-STR-SELN-ID  = ' PV-STR-SELN-ID                  
                   MOVE 'C400-FETCH-SIMP-PROMO-CSR     '                        
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON SIMPPROMOCSR   '                        
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C405-FETCH-UNADSIMPPROMO-CSR.                                 *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM:                                           *         
      *              A100-MAINLINE, C505-PROCESS-UNAD-SIMP-PROMO-CSR  *         
      *****************************************************************         
       C405-FETCH-UNADSIMPPROMO-CSR.                                            
                                                                                
           EXEC SQL                                                             
               FETCH UNADSIMPPROMOCSR                                           
                INTO :PV-TM-DIM-KY-DTE                                          
                    ,:PV-PROMO-ID                                               
                    ,:PV-PROMO-ITM-ID                                           
                    ,:PV-MDSE-SELN-ID                                           
                    ,:PV-STR-SELN-ID                                            
                    ,:PV-UNT-MKDN-MIN-RTL-AMT                                   
                    ,:PV-UNT-MKDN-MAX-RTL-AMT                                   
                    ,:PV-UNT-MKDN-MIN-RTL-PCT                                   
                    ,:PV-UNT-MKDN-MAX-RTL-PCT                                   
                    ,:PV-UNT-MKUP-MIN-RTL-PCT                                   
                    ,:PV-UNT-MKUP-MAX-RTL-PCT                                   
                    ,:PV-MIN-UNT-REG-RTL-AMT                                    
                    ,:PV-MAX-UNT-REG-RTL-AMT                                    
                    ,:PV-MIN-UNT-PROMO-RTL-AMT                                  
                    ,:PV-MAX-UNT-PROMO-RTL-AMT                                  
                    ,:PV-MIN-UNT-REG-COST-AMT                                   
                    ,:PV-MAX-UNT-REG-COST-AMT                                   
                    ,:PV-SLS-UNT-QTY                                            
                    ,:PV-SLS-COST-AMT                                           
                    ,:PV-SLS-RTL-AMT                                            
                    ,:PV-MKDN-RTL-AMT                                           
                    ,:PV-TOT-MKUP-PCT                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-UNAD-SIMP-PROMO-CNT                              
                                                                                
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'UNAD SIMP PROMO COUNT FROM FETCH = '                
                                                PA-UNAD-SIMP-PROMO-CNT          
                   DISPLAY 'END OF UNAD SIMP PROMO CSR'                         
                   SET PS-END-UNADSIMPPROMO-CSR TO TRUE                         
                                                                                
               WHEN OTHER                                                       
                   DISPLAY 'PROMO ID = ' PV-PROMO-ID                            
                   MOVE 'C405-FETCH-UNADSIMPPROMO-CSR '                         
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON UNADSIMPPROMOCSR   '                    
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C410-FETCH-TIER-PROMO-CSR.                                    *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE, C510-PROCESS-TIER-PROMO-CSR*         
      *****************************************************************         
       C410-FETCH-TIER-PROMO-CSR.                                               
                                                                                
           EXEC SQL                                                             
               FETCH TIERPROMOCSR                                               
                INTO :PV-TM-DIM-KY-DTE                                          
                    ,:PV-PROMO-ID                                               
                    ,:PV-PROMO-ITM-ID                                           
                    ,:PV-STR-SELN-ID                                            
                    ,:PV-PROMO-INTFC-SUB-ID                                     
                    ,:PV-SLS-UNT-QTY                                            
                    ,:PV-SLS-COST-AMT                                           
                    ,:PV-SLS-RTL-AMT                                            
                    ,:PV-MKDN-RTL-AMT                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-TIER-PROMO-CNT                                   
                                                                                
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'TIER PROMO COUNT FROM FETCH = '                     
                                                    PA-TIER-PROMO-CNT           
                   DISPLAY 'END OF TIER PROMO CSR'                              
                   SET PS-END-TIER-PROMO-CSR TO TRUE                            
                                                                                
               WHEN OTHER                                                       
                   DISPLAY 'PROMO ID = ' PV-PROMO-ID                            
                   MOVE 'C410-FETCH-TIER-PROMO-CSR     '                        
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON TIERPROMOCSR   '                        
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C415-FETCH-UNADTIERPROMO-CSR.                                 *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM:                                           *         
      *            A100-MAINLINE, C515-PROCESS-UNAD-TIER-PROMO-CSR    *         
      *****************************************************************         
       C415-FETCH-UNADTIERPROMO-CSR.                                            
                                                                                
           EXEC SQL                                                             
               FETCH UNADTIERPROMOCSR                                           
                INTO :PV-TM-DIM-KY-DTE                                          
                    ,:PV-PROMO-ID                                               
                    ,:PV-PROMO-ITM-ID                                           
                    ,:PV-STR-SELN-ID                                            
                    ,:PV-PROMO-INTFC-SUB-ID                                     
                    ,:PV-SLS-UNT-QTY                                            
                    ,:PV-SLS-COST-AMT                                           
                    ,:PV-SLS-RTL-AMT                                            
                    ,:PV-MKDN-RTL-AMT                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-UNAD-TIER-PROMO-CNT                              
                                                                                
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'UNAD TIER PROMO COUNT FROM FETCH = '                
                                                 PA-UNAD-TIER-PROMO-CNT         
                   DISPLAY 'END OF UNAD TIER PROMO CSR'                         
                   SET PS-END-UNADTIERPROMO-CSR TO TRUE                         
                                                                                
               WHEN OTHER                                                       
                   DISPLAY 'PROMO ID = ' PV-PROMO-ID                            
                   MOVE 'C415-FETCH-UNADTIERPROMO-CSR     '                     
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON UNADTIERPROMOCSR   '                    
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C500-PROCESS-SIMP-PROMO-CSR.                                  *         
      * ==> PROCESSES:                                                *         
      *  - CALL THE INSERT PARAGRAPH                                  *         
      *  - INITIALIZE THE VARIABLES TO MAKE SURE NO DATA IS LEFT OVER *         
      *    FROM THE PREVIOUS FETCH.                                   *         
      *  - CALL FETCH PARAGRAPH TO GET THE NEXT ROW FROM THE CURSOR   *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C500-PROCESS-SIMP-PROMO-CSR.                                             
                                                                                
           PERFORM C100-INSERT-SIMP.                                            
                                                                                
           INITIALIZE PV-PROMO-VARIABLES.                                       
           PERFORM C400-FETCH-SIMP-PROMO-CSR.                                   
                                                                                
      *****************************************************************         
      * C505-PROCESS-UNADSIMPPROMO-CSR.                               *         
      * ==> PROCESSES:                                                *         
      *  - CALL THE INSERT PARAGRAPH                                  *         
      *  - INITIALIZE THE VARIABLES TO MAKE SURE NO DATA IS LEFT OVER *         
      *    FROM THE PREVIOUS FETCH.                                   *         
      *  - CALL FETCH PARAGRAPH TO GET THE NEXT ROW FROM THE CURSOR   *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C505-PROCESS-UNADSIMPPROMO-CSR.                                          
                                                                                
           PERFORM C100-INSERT-SIMP.                                            
                                                                                
           INITIALIZE PV-PROMO-VARIABLES.                                       
           PERFORM C405-FETCH-UNADSIMPPROMO-CSR.                                
                                                                                
      *****************************************************************         
      * C510-PROCESS-TIER-PROMO-CSR.                                  *         
      * ==> PROCESSES:                                                *         
      *  - CALL THE INSERT PARAGRAPH                                  *         
      *  - INITIALIZE THE VARIABLES TO MAKE SURE NO DATA IS LEFT OVER *         
      *    FROM THE PREVIOUS FETCH.                                   *         
      *  - CALL FETCH PARAGRAPH TO GET THE NEXT ROW FROM THE CURSOR   *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C510-PROCESS-TIER-PROMO-CSR.                                             
                                                                                
           PERFORM C110-INSERT-TIER.                                            
                                                                                
           INITIALIZE PV-PROMO-VARIABLES.                                       
           PERFORM C410-FETCH-TIER-PROMO-CSR.                                   
                                                                                
      *****************************************************************         
      * C515-PROCESS-UNADTIERPROMO-CSR.                               *         
      * ==> PROCESSES:                                                *         
      *  - CALL THE INSERT PARAGRAPH                                  *         
      *  - INITIALIZE THE VARIABLES TO MAKE SURE NO DATA IS LEFT OVER *         
      *    FROM THE PREVIOUS FETCH.                                   *         
      *  - CALL FETCH PARAGRAPH TO GET THE NEXT ROW FROM THE CURSOR   *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C515-PROCESS-UNADTIERPROMO-CSR.                                          
                                                                                
           PERFORM C110-INSERT-TIER.                                            
                                                                                
           INITIALIZE PV-PROMO-VARIABLES.                                       
           PERFORM C415-FETCH-UNADTIERPROMO-CSR.                                
                                                                                
      *****************************************************************         
      * C600-CLOSE-SIMP-PROMO-CSR.                                    *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE SIMPLE CURSOR                                    *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = THE CLOSE WAS SUCCESSFUL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C600-CLOSE-SIMP-PROMO-CSR.                                               
                                                                                
           EXEC SQL                                                             
                CLOSE SIMPPROMOCSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C600-CLOSE-SIMP-PROMO-CSR   '                           
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON SIMPPROMOCSR     '                       
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C605-CLOSE-UNADSIMPPROMO-CSR.                                 *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE UNAD SIMPLE CURSOR                               *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = THE CLOSE WAS SUCCESSFUL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C605-CLOSE-UNADSIMPPROMO-CSR.                                            
                                                                                
           EXEC SQL                                                             
                CLOSE UNADSIMPPROMOCSR                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C605-CLOSE-UNADSIMPPROMO-CSR   '                        
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON UNADSIMPPROMOCSR     '                   
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C610-CLOSE-TIER-PROMO-CSR.                                    *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE TIERED CURSOR                                    *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = THE CLOSE WAS SUCCESSFUL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C610-CLOSE-TIER-PROMO-CSR.                                               
                                                                                
           EXEC SQL                                                             
                CLOSE TIERPROMOCSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C610-CLOSE-TIER-PROMO-CSR   '                           
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON PROMO-CSR     '                          
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C615-CLOSE-UNADTIERPROMO-CSR.                                 *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE UNAD TIERED CURSOR                               *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = THE CLOSE WAS SUCCESSFUL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C615-CLOSE-UNADTIERPROMO-CSR.                                            
                                                                                
           EXEC SQL                                                             
                CLOSE UNADTIERPROMOCSR                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C615-CLOSE-UNADTIERPROMO-CSR  '                         
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON UNADTIERPROMO-CSR     '                  
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
070941****************************************************************          
070941** READ THE ORACLE LOGON CONTROL CARD.                        **          
070941****************************************************************          
070941 7000-READ-ORACLE-LOGON.                                                  
070941                                                                          
070941     READ ORACLE-LOGON-FILE                                               
070941         INTO CC-ORACLE-CONNECTION                                        
070941         AT END                                                           
070941             SET PS-END-OF-LOGON-FILE TO TRUE                             
070941     END-READ.                                                            
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT     *        
      * LOGONS TO ORACLE.                                              *        
      ******************************************************************        
070941*    COPY PMPD700.                                                        
                                                                                
      *----------------------------------------------------------               
      * 9900-ORACLE-ABEND.                                                      
      *     PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE.                    
      *----------------------------------------------------------               
       9900-ORACLE-ABEND.                                                       
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
070941*    DISPLAY '** PROGRAM        ** = ' PC-CURRENT-PROGRAM-NAME.           
070941     DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' PV-SQL.                            
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *        
      ******************************************************************        
       Z910-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
070941*    DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME .                 
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
