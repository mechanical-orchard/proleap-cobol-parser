      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.    MXKUP003.                                                 
       AUTHOR.        ROB SWAGER KFORCE.                                        
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN   02/27/12.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * CHANGE LOG                                                              
      ******************************************************************        
      * 03/27/2012  ROB SWAGER INITIAL IMPLEMENTATION  WR/IR #: WR37507         
      ******************************************************************        
      * INSERT/DELETE/UPDT SKU'S(ITEMS)/STORES ON MR ITEM/STORES TABLES         
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT MPX-INPUT-FILE                                                
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT COMMIT-CONTROL-FILE                                           
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT DATE-CONTROL-FILE                                             
               ASSIGN TO INP03.                                                 
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  MPX-INPUT-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 85 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  MPX-INPUT-RECORD         PIC X(85).                                  
                                                                                
       FD  COMMIT-CONTROL-FILE                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  COMMIT-CONTROL-RECORD    PIC X(80).                                  
                                                                                
       FD  DATE-CONTROL-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  DATE-CONTROL-RECORD    PIC X(80).                                    
                                                                                
       FD  OUTPUT-FILE                                                          
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE F                                                     
           RECORD CONTAINS 85 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS OUTPUT-ERROR-RECORD.                                  
                                                                                
       01  OUTPUT-ERROR-RECORD         PIC X(85).                               
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  ABEND-CODE                  PIC S9(4) COMP SYNC VALUE ZERO.          
           88  ABEND-4001-WRITE-ERROR                      VALUE +4001.         
           88  ABEND-4002-READ-ERROR                       VALUE +4002.         
           88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003.         
           88  ABEND-4004-TABLE-ERROR                      VALUE +4004.         
           88  ABEND-4005-OPEN-ERROR                       VALUE +4005.         
           88  ABEND-4006-CLOSE-ERROR                      VALUE +4006.         
           88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007.         
           88  ABEND-4008-DATA-ERROR                       VALUE +4008.         
           88  ABEND-4009-KEY-DATA-ERROR                   VALUE +4009.         
           88  ABEND-4013-DB2-ERROR                        VALUE +4013.         
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PGM-NAME             PIC  X(8) VALUE 'MXKUP003'.              
           05  PC-MAX-RETRIES          PIC S9(3) VALUE 3   COMP-3.              
           05  PC-MAX-BEFORE-COMMIT    PIC S9(04) COMP SYNC                     
                                                  VALUE +0.                     
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-EOF-SWITCH           PIC X     VALUE 'N'.                     
               88  PS-NOT-EOF                    VALUE 'N'.                     
               88  PS-EOF                        VALUE 'Y'.                     
014700     05  PS-PS-END-OF-DATE-SW    PIC X(1)  VALUE 'N'.                     
014800         88  PS-END-OF-DATE-FILE           VALUE 'Y'.                     
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
010900     05  PV-DB2-DATE-TIMESTAMP.                                           
               10  PV-DB2-CCYY-MM-DD PIC  X(10)   VALUE SPACE.                  
               10  PV-TIME-STAMP     PIC  X(16)                                 
                   VALUE '-19.00.00.000000'.                                    
010900     05  PV-DB2-DATE-TMSTMP-DEL.                                          
               10  PV-DB2-CCYY-MM-DD-DEL PIC  X(10)  VALUE SPACE.               
               10  PV-TIME-STAMP-DEL     PIC  X(16)                             
                   VALUE '-19.00.00.000001'.                                    
           05  PV-COMMIT-CNT           PIC S9(04) COMP SYNC                     
                                                  VALUE 0.                      
           05  PV-INPUT-CNT            PIC  9(9)  VALUE 0.                      
           05  PV-UPDATE-CNT           PIC  9(9)  VALUE 0.                      
           05  PV-ERROR-REC-CNT        PIC  9(9)  VALUE 0.                      
           05  PV-TMRITST-INSERT-CNT   PIC  9(9)  VALUE 0.                      
           05  PV-MRTAUDT-INSERT-CNT   PIC  9(9)  VALUE 0.                      
           05  PV-TMRITST-DELETE-CNT   PIC  9(9)  VALUE 0.                      
                                                                                
           05  PV-PARAGRAPH-NAME       PIC  X(30) VALUE SPACES.                 
           05  PV-DB2-OPER-ATTEMPTED   PIC  X(30) VALUE SPACES.                 
                                                                                
           05  PV-DELAY-INTERVAL.                                               
               10  FILLER                 PIC X(02)  VALUE SPACES.              
               10  PV-DELAY-TIME          PIC 9(08)  VALUE 6000.                
           05  WS-SYSWAIT                 PIC X(08)  VALUE 'SYSWAIT'.           
                                                                                
           05  PV-COMMIT-CONTROL-RECORD.                                        
               10  PV-COMMIT-CONTROL-CNT  PIC 9(09).                            
               10  FILLER                 PIC X(71)  VALUE SPACES.              
                                                                                
020100******************************************************************        
020200* RECORD LAYOUT FOR INPUT DATE CARD                                       
020300******************************************************************        
020400 01  CC-CONTROL-CARD.                                                     
020500     05  CC-PROCDT-CCYY-MM-DD.                                            
020600         10 CC-PROCESS-DT-CCYY       PIC X(04)  VALUE SPACES.             
020700         10 FILLER                   PIC X(01)  VALUE SPACES.             
020800         10 CC-PROCESS-DT-MM         PIC X(02)  VALUE SPACES.             
020900         10 FILLER                   PIC X(01)  VALUE SPACES.             
021000         10 CC-PROCESS-DT-DD         PIC X(02)  VALUE SPACES.             
021100     05  FILLER                      PIC X(70)  VALUE SPACES.             
021200                                                                          
      ******************************************************************        
      *   INPUT MPX FILE LAYOUT                                                 
      ******************************************************************        
           COPY MXRD005.                                                        
                                                                                
      ******************************************************************        
      *   ERROR MESSAGE AREA FOR DB2                                            
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
       EJECT                                                                    
      ************************                                                  
      * DB2 TABLES                                                              
      ************************                                                  
                                                                                
      *****************************************************************         
      *  TMRITST - MR ITEM/STORE TABLE                                          
      *****************************************************************         
           EXEC SQL                                                        CL**2
               INCLUDE TMRITST                                             CL**2
           END-EXEC.                                                       CL**2
                                                                                
      *****************************************************************         
      *  MRT00001 - MR ITEM/STORE AUDIT                                         
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE MRT00001                                                
           END-EXEC.                                                            
                                                                                
      ************************                                                  
      * DB2 COMMUNICATION AREA                                                  
      ************************                                                  
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-PROGRAM-MAINLINE.                                                   
                                                                                
           PERFORM 1000-PRE-PROCESSING.                                         
                                                                                
           PERFORM 2000-PROCESS-INFILE                                          
                   UNTIL PS-EOF.                                                
                                                                                
           PERFORM 9000-POST-PROCESSING.                                        
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
      *   OPEN FILES                                                            
      *   READ FIRST INPUT RECORD                                               
      ******************************************************************        
       1000-PRE-PROCESSING.                                                     
                                                                                
           OPEN INPUT  MPX-INPUT-FILE                                           
                       COMMIT-CONTROL-FILE                                      
                       DATE-CONTROL-FILE                                        
                OUTPUT OUTPUT-FILE.                                             
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '** PROGRAM: ' PC-PGM-NAME.                                  
           DISPLAY SPACES.                                                      
                                                                                
           PERFORM 7000-READ-COMMIT-CONTROL-FILE.                               
                                                                                
           PERFORM 7010-READ-DATE-CONTROL-FILE.                                 
                                                                                
           PERFORM 7050-READ-MPX-INPUT-FILE.                                    
                                                                                
                                                                                
      ******************************************************************        
      *   COMMIT CHANGES AFTER SPECIFIED NUMBER OF UPDATES                      
      *   READ THE NEXT INPUT RECORD                                            
      ******************************************************************        
       2000-PROCESS-INFILE.                                                     
                                                                                
           PERFORM 2100-PROCESS-INPUT-MPXREC.                                   
                                                                                
           IF  PV-COMMIT-CNT = PC-MAX-BEFORE-COMMIT                             
               PERFORM 7600-COMMIT-UPDATE                                       
           END-IF.                                                              
                                                                                
           PERFORM 7050-READ-MPX-INPUT-FILE.                                    
                                                                                
      ******************************************************************        
      *   PROCESS THE INPUT RECORD DEPENDING ON VALUE OF                        
      *   MX005-ISRT-DEL-IND                                                    
      *   FROM THE INPUT RECORD.                                                
      ******************************************************************        
       2100-PROCESS-INPUT-MPXREC.                                               
                                                                                
           MOVE '2100-PROCESS-INPUT-MPXREC' TO PV-PARAGRAPH-NAME.               
                                                                                
           IF MX005-ISRT-DEL-IND = 'I'                                          
              PERFORM 4000-FORMAT-INSERT-TMRITST                                
              PERFORM 6000-INSERT-TMRITST                                       
           ELSE                                                                 
           IF MX005-ISRT-DEL-IND = 'U'                                          
              PERFORM 4010-FORMAT-UPDATE-TMRITST                                
              PERFORM 7200-UPDATE-TMRITST                                       
           ELSE                                                                 
           IF MX005-ISRT-DEL-IND = 'D'                                          
              PERFORM 6050-DELETE-TMRITST                                       
           ELSE                                                                 
              DISPLAY '** ABEND     **'                                         
              DISPLAY '** PROGRAM   ** = ' PC-PGM-NAME                          
              DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME                    
              DISPLAY '** INVALID ISRT DEL IND IN INPUT FILE **'                
              SET ABEND-4008-DATA-ERROR TO TRUE                                 
              CALL 'ILBOABN0' USING ABEND-CODE                                  
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 4000-FORMAT-INSERT-TMRITST.                                    *        
      *  ==> PROCESSES:                                                *        
      *    - FORMATS A ROW INTO TMRITST TO APPLY DAILY SKU'S           *        
      ******************************************************************        
       4000-FORMAT-INSERT-TMRITST.                                              
                                                                                
           MOVE MX005-ITM-NBR      TO MRITST-ITEM-NBR.                          
           MOVE MX005-STR-NBR      TO MRITST-STR-NBR.                           
           MOVE MX005-FT-CDE       TO MRITST-FT-CDE.                            
           MOVE MX005-ORD-STGY-QTY TO MRITST-OSTRAT-QTY.                        
           MOVE MX005-MIN-PRSTN-UNT-QTY TO MRITST-CSTOCK-QTY.                   
           MOVE MX005-MIN-STK-UNT-QTY TO MRITST-USERMIN-QTY.                    
           MOVE MX005-MAX-STK-UNT-QTY TO MRITST-USERMAX-QTY.                    
           MOVE MX005-DSEA-DMND-UNT-QTY TO MRITST-DD-FCTR.                      
           MOVE MX005-PTAG-ID      TO MRITST-PTAG-ID.                           
           MOVE 0                  TO MRITST-COMSTK-QTY                         
                                      MRITST-SPOQ-QTY                           
                                      MRITST-MINOUTL-QTY                        
                                      MRITST-MAXOUTL-QTY                        
                                      MRITST-DDI-FCTR                           
                                      MRITST-LDDI-FCTR                          
                                      MRITST-LPP-QTY                            
                                      MRITST-MADSS-QTY                          
                                      MRITST-FWMAD-QTY                          
                                      MRITST-TRNDP-PCT                          
                                      MRITST-MSDTS-QTY                          
                                      MRITST-MADTS-QTY                          
                                      MRITST-PCTZ-PCT                           
                                      MRITST-LDD-FCTR                           
                                      MRITST-LMADSS-QTY                         
                                      MRITST-LFWMAD-QTY                         
                                      MRITST-LCPS-QTY                           
                                      MRITST-LRTFCST-NBR                        
                                      MRITST-MINDD-QTY                          
                                      MRITST-ON-ORDER-QTY                       
                                      MRITST-MAXOQP-PCT                         
                                      MRITST-OUTL-QTY                           
                                      MRITST-OP-QTY                             
                                      MRITST-SSTOCK-QTY.                        
           MOVE 'N'                TO MRITST-SPOQSW-CDE                         
                                      MRITST-MADSTAT-CDE                        
                                      MRITST-OUTSTAT-CDE                        
                                      MRITST-FTJOPF-CDE.                        
           MOVE 1                  TO MRITST-FOQ-QTY                            
                                      MRITST-LTVFACT-FCTR                       
                                      MRITST-CSTKMINT1-PCT                      
                                      MRITST-CSTKMINT2-PCT                      
                                      MRITST-CSTKMINT3-PCT                      
                                      MRITST-CSTKMINT4-PCT                      
                                      MRITST-CSTKMINT5-PCT                      
                                      MRITST-CSTKMINT6-PCT                      
                                      MRITST-CSTKMAXT1-PCT                      
                                      MRITST-CSTKMAXT2-PCT                      
                                      MRITST-CSTKMAXT3-PCT                      
                                      MRITST-CSTKMAXT4-PCT                      
                                      MRITST-CSTKMAXT5-PCT                      
                                      MRITST-CSTKMAXT6-PCT.                     
           MOVE 28                 TO MRITST-TMIN-QTY.                          
           MOVE  '.............'   TO MRITST-DFLREG-CDE                         
                                      MRITST-TSREG-CDE                          
                                      MRITST-DIREG-CDE                          
                                      MRITST-BYPREG-CDE.                        
           MOVE SPACES             TO MRITST-CHAR-STR-NBR.                      
           MOVE PV-DB2-CCYY-MM-DD  TO MRITST-PTAG-LAST-UPD-DTE.                 
           MOVE 'R'                TO MRITST-ORDSTAT-CDE.                       
           MOVE 'P'                TO MRITST-STAT-CDE.                          
                                                                                
      ******************************************************************        
      * 4010-FORMAT-UPDATE-TMRITST.                                    *        
      *  ==> PROCESSES:                                                *        
      *    - FORMATS A ROW INTO TMRITST TO UPDATE DAILY SKU'S          *        
      ******************************************************************        
       4010-FORMAT-UPDATE-TMRITST.                                              
                                                                                
           MOVE MX005-ITM-NBR      TO MRITST-ITEM-NBR.                          
           MOVE MX005-STR-NBR      TO MRITST-STR-NBR.                           
           MOVE MX005-FT-CDE       TO MRITST-FT-CDE.                            
           MOVE MX005-ORD-STGY-QTY TO MRITST-OSTRAT-QTY.                        
           MOVE MX005-MIN-PRSTN-UNT-QTY TO MRITST-CSTOCK-QTY.                   
           MOVE MX005-MIN-STK-UNT-QTY TO MRITST-USERMIN-QTY.                    
           MOVE MX005-MAX-STK-UNT-QTY TO MRITST-USERMAX-QTY.                    
           MOVE MX005-PTAG-ID      TO MRITST-PTAG-ID.                           
                                                                                
      ******************************************************************        
      * 6000-INSERT-TMRITST.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - INSERTS A ROW INTO TMRITST TO APPLY DAILY SKU'S           *        
      ******************************************************************        
       6000-INSERT-TMRITST.                                                     
           MOVE '6000-INSERT-TMRITST' TO PV-PARAGRAPH-NAME.                     
                                                                                
           EXEC SQL                                                             
               INSERT INTO TMRITST                                              
                 ( ITEM_NBR                                                     
                  ,STR_NBR                                                      
                  ,COMSTK_QTY                                                   
                  ,SPOQSW_CDE                                                   
                  ,SPOQ_QTY                                                     
                  ,FT_CDE                                                       
                  ,OSTRAT_QTY                                                   
                  ,CSTOCK_QTY                                                   
                  ,MINOUTL_QTY                                                  
                  ,MAXOUTL_QTY                                                  
                  ,FOQ_QTY                                                      
                  ,TMIN_QTY                                                     
                  ,USERMIN_QTY                                                  
                  ,USERMAX_QTY                                                  
                  ,DD_FCTR                                                      
                  ,DDI_FCTR                                                     
                  ,LPP_QTY                                                      
                  ,MADSS_QTY                                                    
                  ,FWMAD_QTY                                                    
                  ,TRNDP_PCT                                                    
                  ,MSDTS_QTY                                                    
                  ,MADTS_QTY                                                    
                  ,LTVFACT_FCTR                                                 
                  ,PCTZ_PCT                                                     
                  ,MADSTAT_CDE                                                  
                  ,DFLREG_CDE                                                   
                  ,TSREG_CDE                                                    
                  ,DIREG_CDE                                                    
                  ,BYPREG_CDE                                                   
                  ,LDD_FCTR                                                     
                  ,LDDI_FCTR                                                    
                  ,LMADSS_QTY                                                   
                  ,LFWMAD_QTY                                                   
                  ,LCPS_QTY                                                     
                  ,OUTSTAT_CDE                                                  
                  ,LRTFCST_NBR                                                  
                  ,ORDSTAT_CDE                                                  
                  ,PTAG_ID                                                      
                  ,MINDD_QTY                                                    
                  ,ON_ORDER_QTY                                                 
                  ,MAXOQP_PCT                                                   
                  ,OUTL_QTY                                                     
                  ,OP_QTY                                                       
                  ,FTJOPF_CDE                                                   
                  ,CSTKMINT1_PCT                                                
                  ,CSTKMINT2_PCT                                                
                  ,CSTKMINT3_PCT                                                
                  ,CSTKMINT4_PCT                                                
                  ,CSTKMINT5_PCT                                                
                  ,CSTKMINT6_PCT                                                
                  ,CSTKMAXT1_PCT                                                
                  ,CSTKMAXT2_PCT                                                
                  ,CSTKMAXT3_PCT                                                
                  ,CSTKMAXT4_PCT                                                
                  ,CSTKMAXT5_PCT                                                
                  ,CSTKMAXT6_PCT                                                
                  ,SSTOCK_QTY                                                   
                  ,PTAG_LAST_UPD_DTE                                            
                  ,STAT_CDE                                                     
                  ,CHAR_STR_NBR )                                               
          VALUES                                                                
                 (:MRITST-ITEM-NBR,                                             
                  :MRITST-STR-NBR,                                              
                  :MRITST-COMSTK-QTY,                                           
                  :MRITST-SPOQSW-CDE,                                           
                  :MRITST-SPOQ-QTY,                                             
                  :MRITST-FT-CDE,                                               
                  :MRITST-OSTRAT-QTY,                                           
                  :MRITST-CSTOCK-QTY,                                           
                  :MRITST-MINOUTL-QTY,                                          
                  :MRITST-MAXOUTL-QTY,                                          
                  :MRITST-FOQ-QTY,                                              
                  :MRITST-TMIN-QTY,                                             
                  :MRITST-USERMIN-QTY,                                          
                  :MRITST-USERMAX-QTY,                                          
                  :MRITST-DD-FCTR,                                              
                  :MRITST-DDI-FCTR,                                             
                  :MRITST-LPP-QTY,                                              
                  :MRITST-MADSS-QTY,                                            
                  :MRITST-FWMAD-QTY,                                            
                  :MRITST-TRNDP-PCT,                                            
                  :MRITST-MSDTS-QTY,                                            
                  :MRITST-MADTS-QTY,                                            
                  :MRITST-LTVFACT-FCTR,                                         
                  :MRITST-PCTZ-PCT,                                             
                  :MRITST-MADSTAT-CDE,                                          
                  :MRITST-DFLREG-CDE,                                           
                  :MRITST-TSREG-CDE,                                            
                  :MRITST-DIREG-CDE,                                            
                  :MRITST-BYPREG-CDE,                                           
                  :MRITST-LDD-FCTR,                                             
                  :MRITST-LDDI-FCTR,                                            
                  :MRITST-LMADSS-QTY,                                           
                  :MRITST-LFWMAD-QTY,                                           
                  :MRITST-LCPS-QTY,                                             
                  :MRITST-OUTSTAT-CDE,                                          
                  :MRITST-LRTFCST-NBR,                                          
                  :MRITST-ORDSTAT-CDE,                                          
                  :MRITST-PTAG-ID,                                              
                  :MRITST-MINDD-QTY,                                            
                  :MRITST-ON-ORDER-QTY,                                         
                  :MRITST-MAXOQP-PCT,                                           
                  :MRITST-OUTL-QTY,                                             
                  :MRITST-OP-QTY,                                               
                  :MRITST-FTJOPF-CDE,                                           
                  :MRITST-CSTKMINT1-PCT,                                        
                  :MRITST-CSTKMINT2-PCT,                                        
                  :MRITST-CSTKMINT3-PCT,                                        
                  :MRITST-CSTKMINT4-PCT,                                        
                  :MRITST-CSTKMINT5-PCT,                                        
                  :MRITST-CSTKMINT6-PCT,                                        
                  :MRITST-CSTKMAXT1-PCT,                                        
                  :MRITST-CSTKMAXT2-PCT,                                        
                  :MRITST-CSTKMAXT3-PCT,                                        
                  :MRITST-CSTKMAXT4-PCT,                                        
                  :MRITST-CSTKMAXT5-PCT,                                        
                  :MRITST-CSTKMAXT6-PCT,                                        
                  :MRITST-SSTOCK-QTY,                                           
                  :MRITST-PTAG-LAST-UPD-DTE,                                    
                  :MRITST-STAT-CDE,                                             
                  :MRITST-CHAR-STR-NBR )                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD +1      TO PV-TMRITST-INSERT-CNT                         
                              PV-COMMIT-CNT                                     
                   PERFORM 8010-INSERT-AUDIT-RECORD                             
                                                                                
               WHEN SQLCODE = -530                                              
                   DISPLAY 'TMRITST INSERT ROW NO PARENT ROW '                  
                           'ITM NBR ' MX005-ITM-NBR                             
                           ' AND STR NBR ' MX005-STR-NBR                        
                   PERFORM 8000-WRITE-ERROR-RECORD                              
                                                                                
               WHEN SQLCODE = -803                                              
                   PERFORM 4010-FORMAT-UPDATE-TMRITST                           
                   PERFORM 7210-UPDATE-TMRITST                                  
                                                                                
               WHEN OTHER                                                       
                   MOVE 'INSERT TMRITST TABLE'                                  
                                    TO PV-DB2-OPER-ATTEMPTED                    
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * 6050-DELETE-TMRITST.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - DELETE THE INPUT MX005-ITM-NBR FROM TMRITST.              *        
      *       THE DELETE WILL CASCADE TO TMRISFE AND TMRISHS.          *        
      *    - IF DELETE WAS NOT SUCCESSFUL WRITE DISPLAY OF ITM-NBR     *        
      *       WRITE INPUT RECORD TO THE OUTPUT ERROR FILE.             *        
      *    - IF DELETE WAS SUCCESSFUL FORMAT A DELETE AUDIT RECORD AND *        
      *       INSERT DELETE AUDIT RECORD TO MR_ITM_STR_AUD.                     
      ******************************************************************        
       6050-DELETE-TMRITST.                                                     
                                                                                
           MOVE '6050-DELETE-TMRITST' TO PV-PARAGRAPH-NAME.                     
           MOVE MX005-ITM-NBR         TO MRITST-ITEM-NBR.                       
           MOVE MX005-STR-NBR         TO MRITST-STR-NBR.                        
                                                                                
           EXEC SQL                                                             
              DELETE FROM TMRITST                                               
              WHERE       ITEM_NBR   = :MRITST-ITEM-NBR                         
              AND         STR_NBR    = :MRITST-STR-NBR                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = ZERO                                               
                  ADD +1       TO PV-TMRITST-DELETE-CNT                         
                             PV-COMMIT-CNT                                      
                  PERFORM 8020-DELETE-AUDIT-RECORD                              
              WHEN SQLCODE = +100                                               
                  DISPLAY 'TMRITST DELETE ROW NOT FOUND FOR '                   
                          'ITM NBR ' MX005-ITM-NBR                              
                          ' AND STR NBR ' MX005-STR-NBR                         
              WHEN OTHER                                                        
                  MOVE 'DELETE TMRITST TABLE'                                   
                                   TO PV-DB2-OPER-ATTEMPTED                     
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *   READ COMMIT CONTROL RECORD TO GET COMMIT COUNT MAX.                   
      ******************************************************************        
       7000-READ-COMMIT-CONTROL-FILE.                                           
                                                                                
           MOVE '7000-READ-COMMIT-CONTROL-FILE' TO PV-PARAGRAPH-NAME.           
                                                                                
           READ COMMIT-CONTROL-FILE                                             
               INTO PV-COMMIT-CONTROL-RECORD                                    
               AT END                                                           
                   DISPLAY '** ABEND     **'                                    
                   DISPLAY '** PROGRAM   ** = ' PC-PGM-NAME                     
                   DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME               
                   DISPLAY '** EMPTY COMMIT CONTROL CARD **'                    
                   SET ABEND-4003-CTRL-CARD-ERROR TO TRUE                       
                   CALL 'ILBOABN0' USING ABEND-CODE.                            
                                                                                
           MOVE PV-COMMIT-CONTROL-CNT TO PC-MAX-BEFORE-COMMIT.                  
                                                                                
      ******************************************************************        
      *   READ DATE CONTROL RECORD.                                             
      ******************************************************************        
       7010-READ-DATE-CONTROL-FILE.                                             
                                                                                
           MOVE '7010-READ-DATE-CONTROL-FILE' TO PV-PARAGRAPH-NAME.             
055000                                                                          
054700     READ DATE-CONTROL-FILE INTO CC-CONTROL-CARD                          
054800         AT END                                                           
054900             SET PS-END-OF-DATE-FILE TO TRUE.                             
055000                                                                          
055100     IF PS-END-OF-DATE-FILE                                               
              DISPLAY '** ABEND     **'                                         
              DISPLAY '** PROGRAM   ** = ' PC-PGM-NAME                          
              DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME                    
              DISPLAY '** EMPTY DATE CONTROL CARD **'                           
              SET ABEND-4003-CTRL-CARD-ERROR TO TRUE                            
              CALL 'ILBOABN0' USING ABEND-CODE                                  
055600     END-IF.                                                              
055700                                                                          
055800     DISPLAY PC-PGM-NAME                                                  
055900             ' - CONTROL CARD = (' CC-PROCDT-CCYY-MM-DD ')'.              
056000                                                                          
056100     MOVE CC-PROCDT-CCYY-MM-DD TO PV-DB2-CCYY-MM-DD                       
056100                                  PV-DB2-CCYY-MM-DD-DEL.                  
056200                                                                          
056300     CLOSE DATE-CONTROL-FILE.                                             
056400                                                                          
      ******************************************************************        
      *   READ MPX INPUT RECORD.                                                
      ******************************************************************        
       7050-READ-MPX-INPUT-FILE.                                                
                                                                                
           READ MPX-INPUT-FILE                                                  
               INTO MX005-ITM-STR-RECORD                                        
               AT END                                                           
                   SET PS-EOF TO TRUE.                                          
                                                                                
           IF PS-NOT-EOF                                                        
              ADD 1 TO PV-INPUT-CNT                                             
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH PERFORMS THE UPDATE OF THE TMRITST TABLE        *        
      ******************************************************************        
       7200-UPDATE-TMRITST.                                                     
                                                                                
           MOVE '7200-UPDATE-TMRITST'  TO PV-PARAGRAPH-NAME.                    
           MOVE 'UPDATE TMRITST'       TO PV-DB2-OPER-ATTEMPTED.                
                                                                                
           EXEC SQL                                                             
               UPDATE TMRITST                                                   
                  SET  FT_CDE     = :MRITST-FT-CDE                              
                      ,OSTRAT_QTY = :MRITST-OSTRAT-QTY                          
                      ,CSTOCK_QTY = :MRITST-CSTOCK-QTY                          
                      ,USERMIN_QTY = :MRITST-USERMIN-QTY                        
                      ,USERMAX_QTY = :MRITST-USERMAX-QTY                        
                      ,PTAG_ID    = :MRITST-PTAG-ID                             
                WHERE ITEM_NBR    = :MRITST-ITEM-NBR                            
                  AND STR_NBR     = :MRITST-STR-NBR                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PV-UPDATE-CNT                                       
                            PV-COMMIT-CNT                                       
                   DISPLAY 'TMRITST UPDATED FOR ITM NBR: ' MX005-ITM-NBR        
                   DISPLAY ' AND STORE NBR: ' MX005-STR-NBR                     
                                                                                
               WHEN SQLCODE = +100                                              
                   DISPLAY 'TMRITST ROW NOT UPDATED, ROW NOT FOUND'             
                   DISPLAY ' MX005-ITEM NBR = ' MX005-ITM-NBR                   
                   DISPLAY ' MX005-STR  NBR = ' MX005-STR-NBR                   
                   PERFORM 8000-WRITE-ERROR-RECORD                              
                                                                                
               WHEN OTHER                                                       
                    PERFORM 9999-SQL-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH PERFORMS THE UPDATE OF THE TMRITST TABLE WHEN AN*        
      * INSERT FAILED BECAUSE ROW WAS ALREADY THERE.                   *        
      ******************************************************************        
       7210-UPDATE-TMRITST.                                                     
                                                                                
           MOVE '7210-UPDATE-TMRITST'  TO PV-PARAGRAPH-NAME.                    
           MOVE 'UPDATE TMRITST'       TO PV-DB2-OPER-ATTEMPTED.                
                                                                                
           EXEC SQL                                                             
               UPDATE TMRITST                                                   
                  SET  FT_CDE     = :MRITST-FT-CDE                              
                      ,OSTRAT_QTY = :MRITST-OSTRAT-QTY                          
                      ,CSTOCK_QTY = :MRITST-CSTOCK-QTY                          
                      ,USERMIN_QTY = :MRITST-USERMIN-QTY                        
                      ,USERMAX_QTY = :MRITST-USERMAX-QTY                        
                      ,PTAG_ID    = :MRITST-PTAG-ID                             
                WHERE ITEM_NBR    = :MRITST-ITEM-NBR                            
                AND   STR_NBR     = :MRITST-STR-NBR                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PV-UPDATE-CNT                                       
                            PV-COMMIT-CNT                                       
                   DISPLAY 'INSERT CONVERTED TO UPDATE FOR ITM NBR: '           
                           MX005-ITM-NBR                                        
                           ' AND STORE NBR: ' MX005-STR-NBR                     
                                                                                
               WHEN SQLCODE = +100                                              
                   DISPLAY 'TMRITST ROW NOT UPDATED, ROW NOT FOUND'             
                   DISPLAY ' FOR '                                              
                   DISPLAY ' MX005-ITEM NBR = ' MX005-ITM-NBR                   
                   DISPLAY ' AND '                                              
                   DISPLAY ' MX005-STORE NBR = ' MX005-STR-NBR                  
                   PERFORM 8000-WRITE-ERROR-RECORD                              
                                                                                
               WHEN OTHER                                                       
                    PERFORM 9999-SQL-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *    COMMIT THE UPDATES AND RESET COMMIT COUNTER TO 0.                    
      ******************************************************************        
       7600-COMMIT-UPDATE.                                                      
                                                                                
           EXEC SQL                                                             
              COMMIT                                                            
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +000                                                    
               DISPLAY '** ' PV-COMMIT-CNT ' RECORDS COMMITTED.'                
               MOVE 0 TO PV-COMMIT-CNT                                          
           ELSE                                                                 
               MOVE '7600-COMMIT-UPDATE' TO PV-PARAGRAPH-NAME                   
               MOVE 'COMMIT'             TO PV-DB2-OPER-ATTEMPTED               
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *   WRITE ERROR RECORD TO OUTPUT FILE (FROM INPUT FILE).                  
      ******************************************************************        
       8000-WRITE-ERROR-RECORD.                                                 
                                                                                
           WRITE OUTPUT-ERROR-RECORD                                            
               FROM MPX-INPUT-RECORD.                                           
                                                                                
           ADD 1 TO PV-ERROR-REC-CNT.                                           
                                                                                
      ******************************************************************        
      *   PERFORM INSERT OF ADD RECORD TO MR ITEM AUDIT RECORD TABLE.           
      ******************************************************************        
       8010-INSERT-AUDIT-RECORD.                                                
                                                                                
           MOVE '8010-INSERT-AUDIT-RECORD' TO PV-PARAGRAPH-NAME.                
                                                                                
           MOVE PV-DB2-DATE-TIMESTAMP TO MRT00001-EFF-TMST                      
                                         MRT00001-CRE-TMST.                     
           MOVE 'A'                   TO MRT00001-ACTN-CDE.                     
           MOVE 'MXKUP003'            TO MRT00001-CRE-BY-USR-ID.                
           MOVE MX005-ITM-NBR         TO MRT00001-ITM-NBR.                      
           MOVE MX005-STR-NBR         TO MRT00001-STR-NBR.                      
                                                                                
           EXEC SQL                                                             
               INSERT INTO MRT_ITM_STR_AUD                                      
                 ( EFF_TMST                                                     
                  ,ITM_NBR                                                      
                  ,STR_NBR                                                      
                  ,ACTN_CDE                                                     
                  ,CRE_TMST                                                     
                  ,CRE_BY_USR_ID )                                              
          VALUES (:MRT00001-EFF-TMST,                                           
                  :MRT00001-ITM-NBR,                                            
                  :MRT00001-STR-NBR,                                            
                  :MRT00001-ACTN-CDE,                                           
                  :MRT00001-CRE-TMST,                                           
                  :MRT00001-CRE-BY-USR-ID )                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD +1      TO PV-MRTAUDT-INSERT-CNT                         
                                                                                
               WHEN SQLCODE = -803                                              
                   DISPLAY 'INSERT TO MRT AUDIT IT/ST- DUP INSERT'              
                   DISPLAY ',BY PASSING INSERT FOR '                            
                   DISPLAY ' MX005 ITEM NBR = ' MX005-ITM-NBR                   
                   DISPLAY ' AND '                                              
                   DISPLAY ' MX005 STORE NBR = ' MX005-STR-NBR                  
                   DISPLAY ' IN 8010-INSERT-AUDIT-RECORD'                       
                   MOVE 0 TO SQLCODE                                            
                                                                                
               WHEN OTHER                                                       
                   MOVE 'INSERT AUDIT ITEM/STORE TABLE'                         
                                    TO PV-DB2-OPER-ATTEMPTED                    
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *   PERFORM INSERT OF DELETE RECORD TO MR ITEM AUDIT RECORD TABLE.        
      ******************************************************************        
       8020-DELETE-AUDIT-RECORD.                                                
                                                                                
           MOVE '8020-DELETE-AUDIT-RECORD' TO PV-PARAGRAPH-NAME.                
                                                                                
           MOVE PV-DB2-DATE-TMSTMP-DEL TO MRT00001-EFF-TMST                     
                                          MRT00001-CRE-TMST.                    
           MOVE 'D'                   TO MRT00001-ACTN-CDE.                     
           MOVE 'MXKUP003'            TO MRT00001-CRE-BY-USR-ID.                
           MOVE MX005-ITM-NBR         TO MRT00001-ITM-NBR.                      
           MOVE MX005-STR-NBR         TO MRT00001-STR-NBR.                      
                                                                                
           EXEC SQL                                                             
               INSERT INTO MRT_ITM_STR_AUD                                      
                 ( EFF_TMST                                                     
                  ,ITM_NBR                                                      
                  ,STR_NBR                                                      
                  ,ACTN_CDE                                                     
                  ,CRE_TMST                                                     
                  ,CRE_BY_USR_ID )                                              
          VALUES (:MRT00001-EFF-TMST,                                           
                  :MRT00001-ITM-NBR,                                            
                  :MRT00001-STR-NBR,                                            
                  :MRT00001-ACTN-CDE,                                           
                  :MRT00001-CRE-TMST,                                           
                  :MRT00001-CRE-BY-USR-ID )                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD +1      TO PV-MRTAUDT-INSERT-CNT                         
                                                                                
               WHEN SQLCODE = -803                                              
                   DISPLAY 'INSERT TO MRT AUDIT ITM/ST - DUP INSERT'            
                   DISPLAY ',BY PASSING INSERT FOR '                            
                   DISPLAY ' MX005 ITEM NBR = ' MX005-ITM-NBR                   
                   DISPLAY ' AND '                                              
                   DISPLAY ' MX005 STORE NBR = ' MX005-STR-NBR                  
                   DISPLAY ' IN 8020-DELETE-AUDIT-RECORD'                       
                   MOVE 0 TO SQLCODE                                            
                                                                                
               WHEN OTHER                                                       
                   MOVE 'INSERT AUDIT ITEM/STORE TABLE FOR DELETE'              
                                    TO PV-DB2-OPER-ATTEMPTED                    
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *   PERFORM FINAL COMMIT, DISPLAY END OF PGM MESSAGES,                    
      *   AND CLOSE FILES                                                       
      ******************************************************************        
       9000-POST-PROCESSING.                                                    
                                                                                
           PERFORM 7600-COMMIT-UPDATE.                                          
                                                                                
           DISPLAY 'MPX SKU/STR INPUT RECORDS READ    = ' PV-INPUT-CNT.         
           DISPLAY 'MPX SKU/STR ERROR RECS WRTN  = ' PV-ERROR-REC-CNT.          
           DISPLAY 'TMRITST RECORDS UPDATED  = ' PV-UPDATE-CNT.                 
           DISPLAY 'TMRITST ROWS INSERTED = ' PV-TMRITST-INSERT-CNT.            
           DISPLAY 'MRT AUD ROWS INSERTED = ' PV-MRTAUDT-INSERT-CNT.            
           DISPLAY 'TMRITST ROWS DELETED  = ' PV-TMRITST-DELETE-CNT.            
                                                                                
           CLOSE MPX-INPUT-FILE                                                 
                 COMMIT-CONTROL-FILE                                            
                 OUTPUT-FILE.                                                   
                                                                                
                                                                                
      ******************************************************************        
      *   PERFORM SQL ABEND ROUTINE                                             
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           SET ABEND-4013-DB2-ERROR  TO TRUE.                                   
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PC-PGM-NAME.                          
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
           DISPLAY ' '.                                                         
           DISPLAY ' MX005 ITEM NBR   = ' MX005-ITM-NBR.                        
           DISPLAY ' MX005 STORE NBR   = ' MX005-STR-NBR.                       
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
      *****************************************************************         
      ***********************END OF PROGRAM****************************         
