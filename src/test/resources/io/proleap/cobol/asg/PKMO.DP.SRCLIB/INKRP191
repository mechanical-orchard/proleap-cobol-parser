       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKRP191.                                       
       AUTHOR.                  LEE YANG.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            07/12/2001.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  INVENTORY STORE-EXPECTED ADJUSTMENT DETAIL/SUMMARY REPORT     *        
      *                                                                *        
      *  THIS PROGRAM WILL GENERATE A DETAIL LISTING DOWN TO SKU LEVEL *        
      *  OF THE UNBALANCED STORE-EXPECTED TRANSACTIONS FROM RECEIVING  *        
      *  FOR MANIFEST PROCESSING.                                      *        
      *                                                                *        
      *  AN OPTIONAL CONTROL CARD WILL INDICATE WHETHER THE REPORT     *        
      *  SHOULD BE GENERATED FOR STORE/DEPT OR DEPT/STORE.  THE DATA   *        
      *  SHOULD BE SORTED IN THE ORDER OF THE OPTIONAL CONTROL CARD.   *        
      *  BY DEFAULT, THE REPORT WILL BE GENERATED BY STORE THEN DEPT.  *        
      *                                                                *        
      *  THIS PROGRAM HAS BEEN REWRITTEN FOR THE NEW MANIFEST PROJECT. *        
      *                                                                *        
      *CHANGE HISTORY:                                                 *        
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES          *        
      * ------- ---------- ----------- --------------------------------*        
W26600* W26600  04-05-2004 ROD JANSSEN INV LEDGER STORE EXPANSION               
W27808* W27808  04-29-2005 VLY - REWRITTEN FOR NEW MANIFEST PROJECT             
P28545* W28545 03-24-2006 ROD JANSSEN CYCLE INVENTORY PRICE MODULE SETUP        
P28545*                               (TINCNTL/TINVPAR MODIFICATIONS)           
W33304* W33304  04-08-2008 CHUCK EBERT HOLD INVENTORY LEDGER OPEN               
W33304*                                ACROSS PERIODS                           
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
27808      SELECT OPTIONAL RPT-REQUEST-CNTL-FILE                                
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT STORE-EXPECTED-ADJ-FILE                                       
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT STORE-EXPECTED-DTL-RPT                                        
               ASSIGN TO RPT01.                                                 
                                                                                
           SELECT STORE-EXPECTED-SUM-1-RPT                                      
               ASSIGN TO RPT02.                                                 
                                                                                
           SELECT STORE-EXPECTED-SUM-2-RPT                                      
               ASSIGN TO RPT03.                                                 
                                                                                
           SELECT STORE-EXPECTED-STATUS-RPT                                     
               ASSIGN TO RPT04.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  RPT-REQUEST-CNTL-FILE                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  RPT-REQUEST-CNTL-REC            PIC X(80).                           
                                                                                
       FD  STORE-EXPECTED-ADJ-FILE                                              
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-EXPECTED-ADJ-REC          PIC X(100).                          
                                                                                
       FD  STORE-EXPECTED-DTL-RPT                                               
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
27808      RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS REPORT-LINE-1.                                        
27808  01  REPORT-LINE-1                   PIC X(132).                          
                                                                                
       FD  STORE-EXPECTED-SUM-1-RPT                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS REPORT-LINE-2.                                        
       01  REPORT-LINE-2                   PIC X(132).                          
                                                                                
       FD  STORE-EXPECTED-SUM-2-RPT                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS REPORT-LINE-3.                                        
       01  REPORT-LINE-3                   PIC X(132).                          
                                                                                
       FD  STORE-EXPECTED-STATUS-RPT                                            
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS REPORT-LINE-4.                                        
       01  REPORT-LINE-4                   PIC X(132).                          
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
      ******************************************************************        
      *  HEADER LINE                                                            
      ******************************************************************        
      *01  DP132O-STANDRD-HEADER-132-COLS.                                      
      *01  DP132O-CNFDL-HDR-132-COLS.                                           
           COPY DPWS132O.                                                       
                                                                                
       01  RP-REPORT-TITLE-1A.                                                  
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(40)       VALUE SPACES.        
           05  FILLER                      PIC X(24)       VALUE                
               'INVENTORY STORE-EXPECTED'.                                      
           05  FILLER                      PIC X(18)       VALUE SPACES.        
               88  RP-REPORT-ADJ-A                         VALUE                
                   ' ADJ DETAIL REPORT'.                                        
               88  RP-REPORT-CUM-A                         VALUE                
                   ' CUM DETAIL REPORT'.                                        
           05  FILLER                      PIC X(10)       VALUE SPACES.        
               88  RP-REPORT-TITLE-BY-STORE-1A             VALUE                
                   ' BY STORE '.                                                
               88  RP-REPORT-TITLE-BY-DEPT-1A              VALUE                
                   ' BY DEPT  '.                                                
           05  FILLER                      PIC X(39)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-TITLE-2.                                                   
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-REPORT-TITLE-LIT         PIC X(08)       VALUE SPACES.        
               88  RP-REPORT-TITLE-BY-STORE-2              VALUE                
                   'STORE:  '.                                                  
               88  RP-REPORT-TITLE-BY-DEPT-2               VALUE                
                   ' DEPT:  '.                                                  
W26600     05  RP-REPORT-LOC-NBR           PIC X(04)       VALUE SPACES.        
           05  RP-REPORT-LOC-NBR-9 REDEFINES                                    
               RP-REPORT-LOC-NBR           PIC 9(04).                           
           05  RP-REPORT-DEPT REDEFINES                                         
               RP-REPORT-LOC-NBR           PIC X(04).                           
           05  RP-REPORT-DEPT-9 REDEFINES                                       
               RP-REPORT-LOC-NBR           PIC 9(04).                           
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-REPORT-TITLE-DESC        PIC X(40)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-HEADER-1A.                                                 
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(04)       VALUE SPACES.        
               88  RP-REPORT-HDR-BY-STORE-1A               VALUE 'DEPT'.        
               88  RP-REPORT-HDR-BY-DEPT-1A                VALUE 'LOCS'.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(04)       VALUE 'SCLS'.        
           05  FILLER                      PIC X(03)       VALUE SPACES.        
           05  FILLER                      PIC X(08)       VALUE                
               '  SKU   '.                                                      
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(09)       VALUE                
               '   PO    '.                                                     
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(09)       VALUE                
               ' RECEIVER'.                                                     
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(09)       VALUE                
               ' TRIP ID '.                                                     
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE                
               ' ADJ DATE '.                                                    
           05  FILLER                      PIC X(03)       VALUE SPACES.        
           05  FILLER                      PIC X(15)       VALUE                
               '      QUANTITY '.                                               
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(15)       VALUE                
               '   UNIT RETAIL '.                                               
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(21)       VALUE                
               '     EXTENDED RETAIL '.                                         
           05  FILLER                      PIC X(02)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-HEADER-2A.                                                 
           05  FILLER                      PIC X(01)      VALUE SPACES.         
           05  FILLER                      PIC X(04)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(04)      VALUE ALL '-'.        
           05  FILLER                      PIC X(03)      VALUE SPACES.         
           05  FILLER                      PIC X(08)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(09)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(09)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(09)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(10)      VALUE ALL '-'.        
           05  FILLER                      PIC X(03)      VALUE SPACES.         
           05  FILLER                      PIC X(15)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(15)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(21)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
                                                                                
       01  RP-REPORT-DETAIL-A.                                                  
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-DEPT-A                   PIC X(04)       VALUE SPACES.        
           05  RP-DEPT-A-9 REDEFINES RP-DEPT-A                                  
                                           PIC 9(04).                           
           05  RP-LOC-NBR-A REDEFINES RP-DEPT-A                                 
                                           PIC X(04).                           
           05  RP-LOC-NBR-A-9 REDEFINES RP-DEPT-A                               
                                           PIC 9(04).                           
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-CLASS-NBR-A              PIC X(04)       VALUE SPACES.        
           05  RP-CLASS-NBR-A-9 REDEFINES RP-CLASS-NBR-A                        
                                           PIC 9(04).                           
           05  FILLER                      PIC X(03)       VALUE SPACES.        
           05  RP-SKU-NBR-A                PIC X(08)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-PO-NBR-A                 PIC Z(09)       VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-RCVR-SEQ-NBR-A           PIC Z(09)       VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-OTBND-TRIP-ID-A          PIC Z(09)       VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
               88  RP-MULTIPLE-OTBND-TRIP                  VALUE '* '.          
           05  RP-ADJUST-DATE-A            PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(03)       VALUE SPACES.        
           05  RP-ADJ-QTY-A                PIC ZZ,ZZZ,ZZZ,ZZ9-                  
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-ADJ-RTL-AMT-A            PIC ZZZ,ZZZ,ZZ9.99-                  
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-EXTD-RTL-AMT-A           PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99-            
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-TOTALS-A.                                                  
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-TTL-TYPE-A               PIC X(10)       VALUE SPACES.        
               88  RP-DEPT-TOTALS-A                        VALUE                
                   ' DEPT     '.                                                
               88  RP-STORE-TOTALS-A                       VALUE                
                   'STORE     '.                                                
               88  RP-GRAND-TOTALS-A                       VALUE                
                   '     GRAND'.                                                
           05  FILLER REDEFINES RP-TTL-TYPE-A.                                  
               10  FILLER                  PIC X(06).                           
               10  RP-TTL-DEPT-A           PIC X(04).                           
               10  RP-TTL-DEPT-A-9 REDEFINES RP-TTL-DEPT-A                      
                                           PIC 9(04).                           
               10  RP-TTL-LOC-NBR-A REDEFINES RP-TTL-DEPT-A                     
                                           PIC X(04).                           
               10  RP-TTL-LOC-NBR-A-9 REDEFINES RP-TTL-DEPT-A                   
                                           PIC 9(04).                           
           05  FILLER                      PIC X(10)       VALUE                
               ' TOTAL:   '.                                                    
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(09)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(09)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(09)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(03)       VALUE SPACES.        
           05  RP-TTL-ADJ-QTY-A            PIC ZZ,ZZZ,ZZZ,ZZ9-                  
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(15)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-TTL-EXTD-RTL-AMT-A       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99-            
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-TRAILER-1A.                                                
           05  FILLER                      PIC X(49)       VALUE SPACES.        
           05  FILLER                      PIC X(34)       VALUE                
               '* MULTIPLE OUTBOUND TRIP ID EXIST.'.                            
           05  FILLER                      PIC X(49)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-TITLE-1B.                                                  
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(37)       VALUE SPACES.        
           05  FILLER                      PIC X(24)       VALUE                
               'INVENTORY STORE-EXPECTED'.                                      
           05  FILLER                      PIC X(04)       VALUE SPACES.        
               88  RP-REPORT-ADJ-B                         VALUE ' ADJ'.        
               88  RP-REPORT-CUM-B                         VALUE ' CUM'.        
           05  FILLER                      PIC X(30)       VALUE SPACES.        
               88  RP-REPORT-TITLE-BY-STORE-1B             VALUE                
                   ' DEPT SUMMARY REPORT BY STORE '.                            
               88  RP-REPORT-TITLE-BY-DEPT-1B              VALUE                
                   ' STORE SUMMARY REPORT BY DEPT '.                            
           05  FILLER                      PIC X(36)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-HEADER-1B.                                                 
           05  FILLER                      PIC X(25)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE SPACES.        
               88  RP-REPORT-HDR-BY-STORE-1B               VALUE                
                   'DEPARTMENT'.                                                
               88  RP-REPORT-HDR-BY-DEPT-1B                VALUE                
                   'LOCATION  '.                                                
           05  FILLER                      PIC X(20)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE                
               ' ADJ DATE '.                                                    
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(15)       VALUE                
               '      QUANTITY '.                                               
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(21)       VALUE                
               '     EXTENDED RETAIL '.                                         
           05  FILLER                      PIC X(25)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-HEADER-2B.                                                 
           05  FILLER                      PIC X(25)      VALUE SPACES.         
           05  FILLER                      PIC X(30)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(10)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(15)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(21)      VALUE ALL '-'.        
           05  FILLER                      PIC X(25)      VALUE SPACES.         
                                                                                
       01  RP-REPORT-DETAIL-B.                                                  
           05  FILLER                      PIC X(25)       VALUE SPACES.        
           05  RP-DEPT-B                   PIC X(04)       VALUE SPACES.        
           05  RP-DEPT-B-9 REDEFINES RP-DEPT-B                                  
                                           PIC 9(04).                           
           05  RP-LOC-NBR-B REDEFINES RP-DEPT-B                                 
                                           PIC X(04).                           
           05  RP-LOC-NBR-B-9 REDEFINES RP-DEPT-B                               
                                           PIC 9(04).                           
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-DESCRIPTION-B            PIC X(25)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-ADJUST-DATE-B            PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-ADJ-QTY-B                PIC ZZ,ZZZ,ZZZ,ZZ9-                  
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-EXTD-RTL-AMT-B           PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99-            
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(25)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-TOTALS-B.                                                  
           05  FILLER                      PIC X(26)       VALUE SPACES.        
           05  RP-TTL-TYPE-B               PIC X(10)       VALUE SPACES.        
               88  RP-DEPT-TOTALS-B                        VALUE                
                   ' DEPT     '.                                                
               88  RP-STORE-TOTALS-B                       VALUE                
                   'STORE     '.                                                
               88  RP-GRAND-TOTALS-B                       VALUE                
                   '     GRAND'.                                                
           05  FILLER REDEFINES RP-TTL-TYPE-B.                                  
               10  FILLER                  PIC X(06).                           
               10  RP-TTL-DEPT-B           PIC X(04).                           
               10  RP-TTL-DEPT-B-9 REDEFINES RP-TTL-DEPT-B                      
                                           PIC 9(04).                           
               10  RP-TTL-LOC-NBR-B REDEFINES RP-TTL-DEPT-B                     
                                           PIC X(04).                           
               10  RP-TTL-LOC-NBR-B-9 REDEFINES RP-TTL-DEPT-B                   
                                           PIC 9(04).                           
           05  FILLER                      PIC X(10)       VALUE                
               ' TOTAL:   '.                                                    
           05  FILLER                      PIC X(09)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-TTL-ADJ-QTY-B            PIC ZZ,ZZZ,ZZZ,ZZ9-                  
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-TTL-EXTD-RTL-AMT-B       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99-            
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(25)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-TITLE-1C.                                                  
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(37)       VALUE SPACES.        
           05  FILLER                      PIC X(24)       VALUE                
               'INVENTORY STORE-EXPECTED'.                                      
           05  FILLER                      PIC X(11)       VALUE SPACES.        
               88  RP-REPORT-ADJ-C                         VALUE                
                   ' ADJUSTMENT'.                                               
               88  RP-REPORT-CUM-C                         VALUE                
                   ' CUMULATIVE'.                                               
           05  FILLER                      PIC X(24)       VALUE SPACES.        
               88  RP-REPORT-TITLE-BY-STORE-1C             VALUE                
                   ' SUMMARY REPORT BY STORE'.                                  
               88  RP-REPORT-TITLE-BY-DEPT-1C              VALUE                
                   ' SUMMARY REPORT BY DEPT '.                                  
           05  FILLER                      PIC X(35)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-HEADER-1C.                                                 
           05  FILLER                      PIC X(25)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE SPACES.        
               88  RP-REPORT-HDR-BY-STORE-1C               VALUE                
                   'LOCATION  '.                                                
               88  RP-REPORT-HDR-BY-DEPT-1C                VALUE                
                   'DEPARTMENT'.                                                
           05  FILLER                      PIC X(20)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE                
               ' ADJ DATE '.                                                    
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(15)       VALUE                
               '      QUANTITY '.                                               
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(21)       VALUE                
               '     EXTENDED RETAIL '.                                         
           05  FILLER                      PIC X(25)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-HEADER-2C.                                                 
           05  FILLER                      PIC X(25)      VALUE SPACES.         
           05  FILLER                      PIC X(30)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(10)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(15)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(21)      VALUE ALL '-'.        
           05  FILLER                      PIC X(25)      VALUE SPACES.         
                                                                                
       01  RP-REPORT-DETAIL-C.                                                  
           05  FILLER                      PIC X(25)       VALUE SPACES.        
           05  RP-DEPT-C                   PIC X(04)       VALUE SPACES.        
           05  RP-DEPT-C-9 REDEFINES RP-DEPT-C                                  
                                           PIC 9(04).                           
           05  RP-LOC-NBR-C REDEFINES RP-DEPT-C                                 
                                           PIC X(04).                           
           05  RP-LOC-NBR-C-9 REDEFINES RP-DEPT-C                               
                                           PIC 9(04).                           
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-DESCRIPTION-C            PIC X(25)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-ADJUST-DATE-C            PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-ADJ-QTY-C                PIC ZZ,ZZZ,ZZZ,ZZ9-                  
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-EXTD-RTL-AMT-C           PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99-            
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(25)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-TOTALS-C.                                                  
           05  FILLER                      PIC X(26)       VALUE SPACES.        
           05  RP-TTL-TYPE-C               PIC X(20)       VALUE                
               '     GRAND TOTAL:   '.                                          
           05  FILLER                      PIC X(09)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-TTL-ADJ-QTY-C            PIC ZZ,ZZZ,ZZZ,ZZ9-                  
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-TTL-EXTD-RTL-AMT-C       PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99-            
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(25)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-TITLE-1S.                                                  
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(42)       VALUE SPACES.        
           05  FILLER                      PIC X(24)       VALUE                
               'INVENTORY STORE-EXPECTED'.                                      
           05  FILLER                      PIC X(24)       VALUE SPACES.        
               88  RP-REPORT-ADJ-S                         VALUE                
                   ' ADJ DAILY STORE STATUS '.                                  
               88  RP-REPORT-CUM-S                         VALUE                
                   ' CUMULATIVE STORE STATUS'.                                  
           05  FILLER                      PIC X(41)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-HEADER-1S.                                                 
           05  FILLER                      PIC X(25)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE                
               'LOCATION  '.                                                    
           05  FILLER                      PIC X(20)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE                
               '   DATE   '.                                                    
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(15)       VALUE                
               '  RECORD COUNT '.                                               
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(40)       VALUE                
               'COMMENTS/STATUS                         '.                      
           05  FILLER                      PIC X(06)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-HEADER-2S.                                                 
           05  FILLER                      PIC X(25)      VALUE SPACES.         
           05  FILLER                      PIC X(30)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(10)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(15)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(40)      VALUE ALL '-'.        
           05  FILLER                      PIC X(06)      VALUE SPACES.         
                                                                                
       01  RP-REPORT-DETAIL-S.                                                  
           05  FILLER                      PIC X(25)       VALUE SPACES.        
           05  RP-LOC-NBR-S                PIC X(04)       VALUE SPACES.        
           05  RP-LOC-NBR-S-9 REDEFINES RP-LOC-NBR-S                            
                                           PIC 9(04).                           
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-LOC-NM-S                 PIC X(25)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-ADJ-DATE-S               PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-RECORD-COUNT-S           PIC ZZ,ZZZ,ZZZ,ZZ9-                  
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-STATUS-S                 PIC X(40)       VALUE SPACES.        
               88  RP-MISSING-LOCATION-S                   VALUE                
                   '*** LOCATION MISSING FROM INPUT FILE.   '.                  
               88  RP-ADDITION-LOCATION-S                  VALUE                
                   'LOCATION IS UNEXPECTED; WILL BE IGNORED.'.                  
           05  FILLER                      PIC X(06)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-TOTALS-S.                                                  
           05  FILLER                      PIC X(26)       VALUE SPACES.        
           05  RP-TTL-TYPE-S               PIC X(20)       VALUE                
               '     GRAND TOTAL:   '.                                          
           05  FILLER                      PIC X(09)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-TTL-RECORD-COUNT-S       PIC ZZ,ZZZ,ZZZ,ZZ9-                  
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(40)       VALUE SPACES.        
           05  FILLER                      PIC X(06)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-END.                                                       
           05  FILLER                      PIC X(46)       VALUE SPACES.        
           05  FILLER                      PIC X(39)       VALUE                
               '*****  E N D   O F   R E P O R T  *****'.                       
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING CONSTANT BEGIN ***'.                                
                                                                                
       01  PC-PROGRAM-CONSTANTS-AREA.                                           
           05  PC-PRINT-LINE-MAX           PIC S9(04)      VALUE +57            
                               COMP.                                            
           05  PC-COMPANY                  PIC X(02)       VALUE '03'.          
           05  PC-MDSELV-DEPT-CDE          PIC X(03)       VALUE 'DPT'.         
           05  PC-INCOMPLETE-DTE           PIC X(10)       VALUE                
                                                           '9999-09-09'.        
           05  PC-Y                        PIC X           VALUE 'Y'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING SWITCHES BEGIN ***'.                                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
           05  PS-INVENTORY-STORE-STATUS   PIC X           VALUE SPACES.        
               88  VALID-MANIFESTED-INV-STORE              VALUE 'Y'.           
               88  INVALID-MANIFESTED-INV-STORE            VALUE 'N'.           
           05  PS-END-OF-ADJ-FILE          PIC X           VALUE 'N'.           
               88  END-OF-ADJ-FILE                         VALUE 'Y'.           
               88  NOT-END-OF-ADJ-FILE                     VALUE 'N'.           
           05  PS-END-OF-CNTL-FILE         PIC X           VALUE 'N'.           
               88  END-OF-CNTL-FILE                        VALUE 'Y'.           
               88  NOT-END-OF-CNTL-FILE                    VALUE 'N'.           
           05  PS-END-OF-INV-STR           PIC X           VALUE 'N'.           
               88  END-OF-INV-STR                          VALUE 'Y'.           
               88  NOT-END-OF-INV-STR                      VALUE 'N'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** REPORT REQUEST CONTROL REC ***'.                                
                                                                                
       01  RQ-RPT-CNTL-AREA.                                                    
           05  FILLER                      PIC X(01)       VALUE SPACE.         
               88  RQ-RPT-COMMENT                          VALUE '*'.           
           05  RQ-RPT-FR-DATE              PIC X(10)       VALUE SPACES.        
           05  FILLER REDEFINES RQ-RPT-FR-DATE.                                 
               10  RQ-RPT-FR-DATE-CCYY     PIC X(04).                           
               10  FILLER                  PIC X(01).                           
                   88  RQ-VALID-RQ-RPT-FR-DASH-1           VALUE '-'.           
               10  RQ-RPT-FR-DATE-MM       PIC X(02).                           
               10  FILLER                  PIC X(01).                           
                   88  RQ-VALID-RQ-RPT-FR-DASH-2           VALUE '-'.           
               10  RQ-RPT-FR-DATE-DD       PIC X(02).                           
           05  FILLER                      PIC X(01)       VALUE SPACE.         
           05  RQ-RPT-TO-DATE              PIC X(10)       VALUE SPACES.        
           05  FILLER REDEFINES RQ-RPT-TO-DATE.                                 
               10  RQ-RPT-TO-DATE-CCYY     PIC X(04).                           
               10  FILLER                  PIC X(01).                           
                   88  RQ-VALID-RQ-RPT-TO-DASH-1           VALUE '-'.           
               10  RQ-RPT-TO-DATE-MM       PIC X(02).                           
               10  FILLER                  PIC X(01).                           
                   88  RQ-VALID-RQ-RPT-TO-DASH-2           VALUE '-'.           
               10  RQ-RPT-TO-DATE-DD       PIC X(02).                           
           05  FILLER                      PIC X(01)       VALUE SPACE.         
           05  RQ-RPT-TYPE                 PIC X(10)       VALUE SPACES.        
               88  RQ-RPT-VALID-TYPE                       VALUES ARE           
                   'STORE/DEPT' SPACES                                          
                   'DEPT/STORE'.                                                
               88  RQ-RPT-BY-STORE-DEPT                    VALUE ARE            
                   'STORE/DEPT' SPACES.                                         
               88  RQ-RPT-BY-DEPT-STORE                    VALUE                
                   'DEPT/STORE'.                                                
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RQ-RPT-IND                  PIC X(06)       VALUE SPACES.        
               88  RQ-RPT-VALID-IND                        VALUES ARE           
                   'CUM   ' SPACES.                                             
               88  RQ-RPT-CUM-IND                          VALUE                
                   'CUM   '.                                                    
           05  FILLER                      PIC X(40)       VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING VARIABLE BEGIN ***'.                                
                                                                                
       01  PV-PROGRAM-VARIABLES-AREA.                                           
           05  PV-PAGE-COUNT-A             PIC S9(05)      VALUE ZEROES         
                               COMP.                                            
           05  PV-PRINT-LINE-COUNT-A       PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-ADVANCE-LINE-A           PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-PAGE-COUNT-B             PIC S9(05)      VALUE ZEROES         
                               COMP.                                            
           05  PV-PRINT-LINE-COUNT-B       PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-ADVANCE-LINE-B           PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-PAGE-COUNT-C             PIC S9(05)      VALUE ZEROES         
                               COMP.                                            
           05  PV-PRINT-LINE-COUNT-C       PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-ADVANCE-LINE-C           PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-PAGE-COUNT-S             PIC S9(05)      VALUE ZEROES         
                               COMP.                                            
           05  PV-PRINT-LINE-COUNT-S       PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-ADVANCE-LINE-S           PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-OPER-ATTEMPTED           PIC X(25)       VALUE SPACES.        
           05  PV-PROCESSING-DATE          PIC X(10)       VALUE SPACES.        
           05  PV-PROCESSING-DATE-RDF REDEFINES PV-PROCESSING-DATE.             
               10  PV-PROCESSING-CC        PIC X(02).                           
               10  PV-PROCESSING-YY        PIC X(02).                           
               10  PV-PROC-DSH1            PIC X(01).                           
               10  PV-PROCESSING-MM        PIC X(02).                           
               10  PV-PROC-DSH2            PIC X(01).                           
               10  PV-PROCESSING-DD        PIC X(02).                           
           05  PV-DATE-HOLD-CCYY-MM-DD     PIC X(10)       VALUE SPACES.        
           05  FILLER REDEFINES PV-DATE-HOLD-CCYY-MM-DD.                        
               10  PV-DATE-HOLD-CCYY       PIC X(04).                           
               10  FILLER REDEFINES PV-DATE-HOLD-CCYY.                          
                   15  PV-DATE-HOLD-CC     PIC X(02).                           
                   15  PV-DATE-HOLD-YY     PIC X(02).                           
               10  PV-DATE-HOLD-DASH-1     PIC X(01).                           
               10  PV-DATE-HOLD-MM         PIC X(02).                           
               10  PV-DATE-HOLD-DASH-2     PIC X(01).                           
               10  PV-DATE-HOLD-DD         PIC X(02).                           
           05  PV-DATE-RFMT-MM-DD-CCYY     PIC X(10)       VALUE SPACES.        
           05  FILLER REDEFINES PV-DATE-RFMT-MM-DD-CCYY.                        
               10  PV-DATE-RFMT-MM         PIC X(02).                           
               10  PV-DATE-RFMT-DASH-1     PIC X(01).                           
               10  PV-DATE-RFMT-DD         PIC X(02).                           
               10  PV-DATE-RFMT-DASH-2     PIC X(01).                           
               10  PV-DATE-RFMT-CCYY       PIC X(04).                           
               10  FILLER REDEFINES PV-DATE-RFMT-CCYY.                          
                   15  PV-DATE-RFMT-CC     PIC X(02).                           
                   15  PV-DATE-RFMT-YY     PIC X(02).                           
           05  PV-TODAYS-DATE.                                                  
               10  PV-TODAYS-DATE-YY       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-MM       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-DD       PIC 9(02)       VALUE ZEROES.        
           05  PV-TODAYS-TIME.                                                  
               10  PV-TODAYS-TIME-HR       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-MIN      PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-SEC      PIC 9(02)       VALUE ZEROES.        
               10  FILLER                  PIC 9(02)       VALUE ZEROES.        
           05  PV-CURR-ADJ-DATE            PIC X(10)       VALUE SPACES.        
W26600     05  PV-CURR-LOC-NBR             PIC X(5)        VALUE SPACES.        
           05  PV-CURR-LOC-NBR-9 REDEFINES PV-CURR-LOC-NBR                      
W26600                                     PIC 9(5).                            
           05  PV-CURR-DEPT                PIC X(4)        VALUE SPACES.        
           05  PV-CURR-DEPT-9 REDEFINES PV-CURR-DEPT                            
                                           PIC 9(04).                           
           05  FILLER REDEFINES PV-CURR-DEPT.                                   
               10  FILLER                  PIC X(01).                           
               10  PV-CURR-DEPT-3X         PIC X(03).                           
           05  PV-SUB-TTL-ADJ-QTY          PIC S9(11)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-SUB-TTL-EXTD-RTL-AMT     PIC S9(13)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-GRP-TTL-ADJ-QTY          PIC S9(11)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-GRP-TTL-EXTD-RTL-AMT     PIC S9(13)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-GRD-TTL-ADJ-QTY          PIC S9(11)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-GRD-TTL-EXTD-RTL-AMT     PIC S9(13)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-RECORD-COUNT             PIC S9(11)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-MULTI-OTBND-TRIP         PIC S9(11)      VALUE ZEROES         
                               COMP-3.                                          
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** IN-TRANSIT INV LEDGER ADJ EXTR ***'.                            
                                                                                
      *01  IN137-STR-EXPTED-RECORD.                                             
W26600     COPY INRD137.                                                        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** INTERNAL TABLE AREA ***'.                                       
                                                                                
       01  PT-PROGRAM-TABLE-AREA.                                               
           05  PT-STORE-TABLE.                                                  
               10  PT-STORE-TBL-INDX       PIC S9(05)      VALUE ZEROES         
                               COMP-3.                                          
               10  PT-STORE-TBL-MAX        PIC S9(05)      VALUE +99999         
                               COMP-3.                                          
               10  PT-STORE-TBL-COUNT      PIC S9(05)      VALUE ZEROES         
                               COMP-3.                                          
               10  PT-STORE-TBL OCCURS 99999 TIMES.                             
                   15  PT-STORE-TBL-LOC    PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
                   15  PT-STORE-TBL-DATE   PIC X(10)       VALUE SPACES.        
                   15  PT-STORE-TBL-LOC-TYP                                     
                                           PIC X(02)       VALUE SPACES.        
                   15  PT-STORE-TBL-LOC-NM PIC X(40)       VALUE SPACES.        
                   15  PT-STORE-TBL-SOURCE PIC X(02)       VALUE SPACES.        
                       88  PT-STORE-TBL-INVENTORY          VALUE 'IN'.          
                       88  PT-STORE-TBL-RECEIVING          VALUE 'RC'.          
                   15  PT-STORE-TBL-RECS   PIC S9(11)      VALUE ZEROES         
                               COMP-3.                                          
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** SQL WORK AREA BEGIN ***'.                                       
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
      *01  DP004-DB2-ERROR-FIELDS.                                              
           COPY DPWS004.                                                        
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  TABLE-EXCEEDED-ABEND-4003                   VALUE +4003.         
           88  REQUEST-CNTL-ABEND-4003                     VALUE +4003.         
           88  SQL-ABEND-4013                              VALUE +4013.         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TINVPAR - PHYSICAL INVENTORY PARAMETER TABLE               *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
P28545*---------------------------------------------------------------*         
P28545*    TINCNTL - PHYSICAL INVENTORY CONTROL MASTER TABLE                    
P28545*---------------------------------------------------------------*         
P28545     EXEC SQL                                                             
P28545         INCLUDE TINCNTL                                                  
P28545     END-EXEC.                                                            
P28545                                                                          
      *---------------------------------------------------------------*         
W26600*    XMT_LOC - STORE LOCATION TABLE                             *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
W26600         INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TMDSEAR - MERCH. HEIRARCHY                                 *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TINVPAR - INVENTORYING LOCATIONS CURSOR                    *         
      *---------------------------------------------------------------*         
           EXEC SQL   DECLARE INV-STR-CSR CURSOR FOR                            
                SELECT                                                          
                        A.LOC_NBR                                               
                       ,A.ACTL_INV_DTE                                          
                       ,A.PLND_INV_DTE                                          
                FROM                                                            
P28545                 TINVPAR A ,                                              
P28545                 TINCNTL B                                                
P28545          WHERE  B.ACTV_IND = 'Y'                                         
P28545            AND  A.INV_ID = B.INV_ID                                      
P28545            AND  PLND_INV_DTE                                             
                           BETWEEN :RQ-RPT-FR-DATE AND :RQ-RPT-TO-DATE          
W33304            AND  SCHD_PHY_CNT_CDE IN ('PI','NS','CL')                     
                  AND  A.MFSTNG_IND       = :PC-Y                               
W33304            AND  A.LOC_INV_STAT_CDE = 'IN'                                
                ORDER BY                                                        
                       LOC_NBR                                                  
           END-EXEC.                                                            
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE END ***'.                                       
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN RPOCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           EVALUATE TRUE                                                        
               WHEN RQ-RPT-BY-STORE-DEPT                                        
                   DISPLAY '  PROCESS BY STORE THEN DEPT'                       
                   SET RP-REPORT-TITLE-BY-STORE-1A                              
                       RP-REPORT-TITLE-BY-STORE-1B                              
                       RP-REPORT-TITLE-BY-STORE-1C                              
                       RP-REPORT-TITLE-BY-STORE-2                               
                       RP-REPORT-HDR-BY-STORE-1A                                
                       RP-REPORT-HDR-BY-STORE-1B                                
                       RP-REPORT-HDR-BY-STORE-1C     TO TRUE                    
                   PERFORM B200-PROCESS-ADJ-BY-STORE                            
                       UNTIL END-OF-ADJ-FILE                                    
               WHEN RQ-RPT-BY-DEPT-STORE                                        
                   DISPLAY '  PROCESS BY DEPT THEN STORE'                       
                   SET RP-REPORT-TITLE-BY-DEPT-1A                               
                       RP-REPORT-TITLE-BY-DEPT-1B                               
                       RP-REPORT-TITLE-BY-DEPT-1C                               
                       RP-REPORT-TITLE-BY-DEPT-2                                
                       RP-REPORT-HDR-BY-DEPT-1A                                 
                       RP-REPORT-HDR-BY-DEPT-1B                                 
                       RP-REPORT-HDR-BY-DEPT-1C      TO TRUE                    
                   PERFORM B250-PROCESS-ADJ-BY-DEPT                             
                       UNTIL END-OF-ADJ-FILE                                    
               WHEN OTHER                                                       
                   DISPLAY ' **** INVALID REQUEST TYPE ****'                    
           END-EVALUATE.                                                        
                                                                                
           PERFORM B900-FINALIZE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET              
      * THE REPORT NAME AND NUMBER.                                             
      ******************************************************************        
       B100-HOUSEKEEPING.                                                       
                                                                                
           ACCEPT PV-TODAYS-DATE FROM DATE.                                     
           ACCEPT PV-TODAYS-TIME FROM TIME.                                     
                                                                                
           MOVE PV-TODAYS-DATE-YY TO PV-PROCESSING-YY.                          
           MOVE PV-TODAYS-DATE-MM TO PV-PROCESSING-MM.                          
           MOVE PV-TODAYS-DATE-DD TO PV-PROCESSING-DD.                          
                                                                                
           MOVE '-' TO PV-PROC-DSH1.                                            
           MOVE '-' TO PV-PROC-DSH2.                                            
                                                                                
           IF PV-TODAYS-DATE-YY > '80'                                          
               MOVE '19' TO PV-PROCESSING-CC                                    
           ELSE                                                                 
               MOVE '20' TO PV-PROCESSING-CC                                    
           END-IF.                                                              
                                                                                
           MOVE PV-TODAYS-DATE-MM  TO DP132O-RUN-MONTH.                         
           MOVE PV-TODAYS-DATE-DD  TO DP132O-RUN-DAY.                           
           MOVE PV-TODAYS-DATE-YY  TO DP132O-RUN-YEAR.                          
           MOVE PV-TODAYS-TIME-HR  TO DP132O-RUN-HOUR.                          
           MOVE PV-TODAYS-TIME-MIN TO DP132O-RUN-MINUTE.                        
           MOVE 'INKRP191'         TO DP132O-PROGRAM-NAME.                      
                                                                                
           MOVE ZEROES TO PV-GRD-TTL-ADJ-QTY                                    
                          PV-GRD-TTL-EXTD-RTL-AMT.                              
                                                                                
           OPEN INPUT  RPT-REQUEST-CNTL-FILE                                    
                INPUT  STORE-EXPECTED-ADJ-FILE                                  
                OUTPUT STORE-EXPECTED-DTL-RPT                                   
                       STORE-EXPECTED-SUM-1-RPT                                 
                       STORE-EXPECTED-SUM-2-RPT                                 
                       STORE-EXPECTED-STATUS-RPT.                               
                                                                                
           DISPLAY ' OPTIONAL REQUEST:'.                                        
           DISPLAY '  ----+----1----+----2----+----3----+----4----+---'.        
           PERFORM                                                              
               WITH TEST AFTER                                                  
               UNTIL END-OF-CNTL-FILE                                           
                   OR NOT RQ-RPT-COMMENT                                        
               PERFORM Z200-READ-CNTL-FILE                                      
               DISPLAY '  ' RQ-RPT-CNTL-AREA                                    
           END-PERFORM.                                                         
                                                                                
           DISPLAY ' '.                                                         
           IF NOT-END-OF-CNTL-FILE                                              
      * VALIDATE FROM-DATE                                                      
               IF RQ-VALID-RQ-RPT-FR-DASH-1                                     
                   AND RQ-VALID-RQ-RPT-FR-DASH-2                                
                   AND RQ-RPT-FR-DATE-CCYY IS NUMERIC                           
                   AND RQ-RPT-FR-DATE-MM   IS NUMERIC                           
                   AND RQ-RPT-FR-DATE-DD   IS NUMERIC                           
                   CONTINUE                                                     
               ELSE                                                             
                   MOVE SPACES             TO RQ-RPT-FR-DATE                    
               END-IF                                                           
      * VALIDATE TO-DATE                                                        
               IF RQ-VALID-RQ-RPT-TO-DASH-1                                     
                   AND RQ-VALID-RQ-RPT-TO-DASH-2                                
                   AND RQ-RPT-TO-DATE-CCYY IS NUMERIC                           
                   AND RQ-RPT-TO-DATE-MM   IS NUMERIC                           
                   AND RQ-RPT-TO-DATE-DD   IS NUMERIC                           
                   CONTINUE                                                     
               ELSE                                                             
                   MOVE SPACES             TO RQ-RPT-TO-DATE                    
               END-IF                                                           
      * DEFAULT TO-DATE WITH FROM-DATE WHEN VALID FROM-DATE W/NO TO-DATE        
               IF RQ-RPT-TO-DATE = SPACES                                       
                   AND RQ-RPT-FR-DATE NOT = SPACES                              
                   DISPLAY '   DEFAULT TO-DATE TO '                             
                           RQ-RPT-FR-DATE                                       
                   MOVE RQ-RPT-FR-DATE     TO RQ-RPT-TO-DATE                    
               ELSE                                                             
      * DEFAULT FROM-DATE WITH TO-DATE WHEN VALID TO-DATE W/NO FROM-DATE        
                   IF RQ-RPT-FR-DATE = SPACES                                   
                       AND RQ-RPT-TO-DATE NOT = SPACES                          
                       DISPLAY '   DEFAULT FROM-DATE TO '                       
                               RQ-RPT-TO-DATE                                   
                       MOVE RQ-RPT-TO-DATE     TO RQ-RPT-FR-DATE                
                   END-IF                                                       
               END-IF                                                           
      * SWAP FROM-DATE WITH TO-DATE WHEN FROM-DATE IS AFTER TO-DATE             
               IF RQ-RPT-FR-DATE > RQ-RPT-TO-DATE                               
                   DISPLAY '   SWAP FROM/TO DATE: '                             
                           RQ-RPT-FR-DATE '/'                                   
                           RQ-RPT-TO-DATE                                       
                   MOVE RQ-RPT-FR-DATE     TO PV-DATE-HOLD-CCYY-MM-DD           
                   MOVE RQ-RPT-TO-DATE     TO RQ-RPT-FR-DATE                    
                   MOVE PV-DATE-HOLD-CCYY-MM-DD TO RQ-RPT-TO-DATE               
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM Z100-READ-ADJ-FILE.                                          
      * USE DATE FROM THE FIRST ADJUSTMENT FILE AS FROM/TO-DATE                 
           IF RQ-RPT-FR-DATE = SPACES                                           
               DISPLAY '   DEFAULT DATE TO FIRST ADJ DATE: '                    
                       IN137-ADJ-DATE                                           
               MOVE IN137-ADJ-DATE     TO RQ-RPT-FR-DATE                        
                                          RQ-RPT-TO-DATE                        
           END-IF.                                                              
                                                                                
           IF RQ-RPT-VALID-TYPE                                                 
               CONTINUE                                                         
           ELSE                                                                 
               DISPLAY '   DEFAULT REQUEST TYPE TO "STORE/DEPT"'                
               MOVE SPACES             TO RQ-RPT-TYPE                           
           END-IF.                                                              
                                                                                
           IF RQ-RPT-VALID-IND                                                  
               IF RQ-RPT-CUM-IND                                                
                   SET RP-REPORT-CUM-A                                          
                       RP-REPORT-CUM-B                                          
                       RP-REPORT-CUM-C                                          
                       RP-REPORT-CUM-S         TO TRUE                          
               ELSE                                                             
                   SET RP-REPORT-ADJ-A                                          
                       RP-REPORT-ADJ-B                                          
                       RP-REPORT-ADJ-C                                          
                       RP-REPORT-ADJ-S         TO TRUE                          
               END-IF                                                           
           ELSE                                                                 
               DISPLAY '   DEFAULT REQUEST INDICATOR TO "CUM   "'               
               MOVE SPACES             TO RQ-RPT-IND                            
           END-IF.                                                              
                                                                                
           MOVE ZEROES             TO PT-STORE-TBL-COUNT.                       
           PERFORM C100-LOAD-INV-STR.                                           
           DISPLAY '   ' PT-STORE-TBL-COUNT ' STORES LOADED'                    
                   ' FOR DATE: '                                                
                   RQ-RPT-FR-DATE '/'                                           
                   RQ-RPT-TO-DATE.                                              
                                                                                
      ******************************************************************        
      *    REPORT STORE-EXPECTED ADJUSTMENT BY STORE/DEPT                       
      ******************************************************************        
       B200-PROCESS-ADJ-BY-STORE.                                               
                                                                                
           IF IN137-STORE-NBR NOT = PV-CURR-LOC-NBR                             
               MOVE IN137-STORE-NBR TO PV-CURR-LOC-NBR                          
               PERFORM C200-VALIDATE-INV-STR                                    
           END-IF.                                                              
                                                                                
           IF VALID-MANIFESTED-INV-STORE                                        
               IF IN137-STORE-NBR-9 = RP-REPORT-LOC-NBR-9                       
                   CONTINUE                                                     
               ELSE                                                             
                   MOVE IN137-STORE-NBR-9  TO RP-REPORT-LOC-NBR-9               
                   MOVE XMT00001-LOC-NM    TO RP-REPORT-TITLE-DESC              
                   MOVE ZEROES             TO PV-GRP-TTL-ADJ-QTY                
                                              PV-GRP-TTL-EXTD-RTL-AMT           
                                              PV-PRINT-LINE-COUNT-A             
                                              PV-PRINT-LINE-COUNT-B             
                                              PV-MULTI-OTBND-TRIP               
               END-IF                                                           
                                                                                
               MOVE IN137-DEPT         TO PV-CURR-DEPT                          
                                                                                
               MOVE ZEROES TO PV-SUB-TTL-ADJ-QTY                                
                              PV-SUB-TTL-EXTD-RTL-AMT                           
                                                                                
               PERFORM B300-PRODUCE-REPORT                                      
                       UNTIL END-OF-ADJ-FILE                                    
                           OR IN137-STORE-NBR NOT = PV-CURR-LOC-NBR             
                           OR IN137-DEPT NOT = PV-CURR-DEPT                     
                                                                                
               PERFORM B400-PRODUCE-TOTALS                                      
           ELSE                                                                 
      ***************************************************************           
      *    THE READ LOGIC IS PRESENTED HERE FOR TEST PURPOSE ONLY.  *           
      *    THERE SHOULD BE NO DATA PASSED INTO THIS PROGREAM FOR    *           
      *    THE NON-MANIFESTED STORE, OR NON-INVENTORYING STORE.     *           
      ***************************************************************           
               DISPLAY ' *** SKIPPING NON-MANIFESTED STORE: '                   
                       PV-CURR-LOC-NBR ' ***'                                   
               PERFORM Z100-READ-ADJ-FILE                                       
                   UNTIL END-OF-ADJ-FILE                                        
                       OR IN137-STORE-NBR NOT = PV-CURR-LOC-NBR                 
           END-IF.                                                              
                                                                                
           IF END-OF-ADJ-FILE                                                   
               MOVE PV-RECORD-COUNT                                             
                   TO PT-STORE-TBL-RECS(PT-STORE-TBL-INDX)                      
           ELSE                                                                 
               COMPUTE PT-STORE-TBL-RECS(PT-STORE-TBL-INDX)                     
                   = PV-RECORD-COUNT - 1                                        
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    REPORT STORE-EXPECTED ADJUSTMENT BY DEPT/STORE                       
      ******************************************************************        
       B250-PROCESS-ADJ-BY-DEPT.                                                
                                                                                
           IF IN137-STORE-NBR NOT = PV-CURR-LOC-NBR                             
               MOVE IN137-STORE-NBR TO PV-CURR-LOC-NBR                          
               PERFORM C200-VALIDATE-INV-STR                                    
           END-IF.                                                              
                                                                                
           IF VALID-MANIFESTED-INV-STORE                                        
               IF IN137-DEPT-9 = RP-REPORT-DEPT-9                               
                   CONTINUE                                                     
               ELSE                                                             
                   MOVE IN137-DEPT-9       TO RP-REPORT-DEPT-9                  
                                              PV-CURR-DEPT-9                    
                   MOVE PV-CURR-DEPT-3X    TO MDSEAR-DEPT-NBR                   
                   PERFORM Z400-GET-DEPT-INFO                                   
                   MOVE MDSEAR-DESC        TO RP-REPORT-TITLE-DESC              
                   MOVE ZEROES             TO PV-GRP-TTL-ADJ-QTY                
                                              PV-GRP-TTL-EXTD-RTL-AMT           
                                              PV-PRINT-LINE-COUNT-A             
                                              PV-PRINT-LINE-COUNT-B             
                                              PV-MULTI-OTBND-TRIP               
               END-IF                                                           
                                                                                
               MOVE ZEROES TO PV-SUB-TTL-ADJ-QTY                                
                              PV-SUB-TTL-EXTD-RTL-AMT                           
                                                                                
               PERFORM B300-PRODUCE-REPORT                                      
                       UNTIL END-OF-ADJ-FILE                                    
                           OR IN137-STORE-NBR NOT = PV-CURR-LOC-NBR             
                           OR IN137-DEPT NOT = PV-CURR-DEPT                     
                                                                                
               PERFORM B400-PRODUCE-TOTALS                                      
           ELSE                                                                 
      ***************************************************************           
      *    THE READ LOGIC IS PRESENTED HERE FOR TEST PURPOSE ONLY.  *           
      *    THERE SHOULD BE NO DATA PASSED INTO THIS PROGREAM FOR    *           
      *    THE NON-MANIFESTED STORE, OR NON-INVENTORYING STORE.     *           
      ***************************************************************           
               PERFORM Z100-READ-ADJ-FILE                                       
                   UNTIL END-OF-ADJ-FILE                                        
                       OR IN137-STORE-NBR NOT = PV-CURR-LOC-NBR                 
           END-IF.                                                              
                                                                                
           IF END-OF-ADJ-FILE                                                   
               MOVE PV-RECORD-COUNT                                             
                   TO PT-STORE-TBL-RECS(PT-STORE-TBL-INDX)                      
           ELSE                                                                 
               COMPUTE PT-STORE-TBL-RECS(PT-STORE-TBL-INDX)                     
                   = PV-RECORD-COUNT - 1                                        
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    PRODUCE THE DETAIL REPORT                                            
      ******************************************************************        
       B300-PRODUCE-REPORT.                                                     
                                                                                
           PERFORM D100-FORMAT-DETAIL-RPT.                                      
                                                                                
           ADD IN137-ADJ-QTY            TO PV-SUB-TTL-ADJ-QTY                   
                                           PV-GRP-TTL-ADJ-QTY                   
                                           PV-GRD-TTL-ADJ-QTY.                  
                                                                                
           ADD IN137-EXTD-RTL-AMT       TO PV-SUB-TTL-EXTD-RTL-AMT              
                                           PV-GRP-TTL-EXTD-RTL-AMT              
                                           PV-GRD-TTL-EXTD-RTL-AMT.             
                                                                                
           PERFORM Z600-PRINT-DETAILS-A.                                        
                                                                                
           PERFORM Z100-READ-ADJ-FILE.                                          
                                                                                
      ******************************************************************        
      *    PRODUCE THE TOTALS FOR DEPARTMENTS, STORES, AND GRAND TOTAL          
      ******************************************************************        
       B400-PRODUCE-TOTALS.                                                     
                                                                                
DELETE*    DISPLAY ' --- DEPT: ' IN137-DEPT '/' PV-CURR-DEPT                    
DELETE*            ' STORE: ' IN137-STORE-NBR '/' PV-CURR-LOC-NBR.              
      * PRODUCE SUB-TOTAL FOR DETAIL RPT AND DETAIL FOR SUMMARY 1 RPT           
           IF RQ-RPT-BY-STORE-DEPT                                              
               SET RP-DEPT-TOTALS-A         TO TRUE                             
               MOVE PV-CURR-DEPT-9          TO RP-TTL-DEPT-A-9                  
           ELSE                                                                 
               SET RP-STORE-TOTALS-A        TO TRUE                             
               MOVE PV-CURR-LOC-NBR-9       TO RP-TTL-LOC-NBR-A-9               
           END-IF.                                                              
           MOVE PV-SUB-TTL-ADJ-QTY      TO RP-TTL-ADJ-QTY-A.                    
           MOVE PV-SUB-TTL-EXTD-RTL-AMT TO RP-TTL-EXTD-RTL-AMT-A.               
                                                                                
           MOVE RP-REPORT-TOTALS-A      TO RP-REPORT-DETAIL-A.                  
           MOVE 2                       TO PV-ADVANCE-LINE-A.                   
           PERFORM Z600-PRINT-DETAILS-A.                                        
                                                                                
           PERFORM D200-FORMAT-SUM-1-RPT.                                       
           PERFORM Z625-PRINT-DETAILS-B.                                        
                                                                                
      * PRODUCE GROUP-TOTAL FOR DETAIL RPT, SUB-TOT FOR SUMMARY 1 RPT           
      * AND DETAIL FOR SUMMARY 2 RPT                                            
           IF END-OF-ADJ-FILE                                                   
               OR (IN137-STORE-NBR NOT = PV-CURR-LOC-NBR AND                    
                   RQ-RPT-BY-STORE-DEPT)                                        
               OR (IN137-DEPT NOT = PV-CURR-DEPT AND                            
                   RQ-RPT-BY-DEPT-STORE)                                        
               IF RQ-RPT-BY-STORE-DEPT                                          
                   SET RP-STORE-TOTALS-A                                        
                       RP-STORE-TOTALS-B        TO TRUE                         
                   MOVE PV-CURR-LOC-NBR-9       TO RP-TTL-LOC-NBR-A-9           
                                                   RP-TTL-LOC-NBR-B-9           
               ELSE                                                             
                   SET RP-DEPT-TOTALS-A                                         
                       RP-DEPT-TOTALS-B         TO TRUE                         
                   MOVE PV-CURR-DEPT-9          TO RP-TTL-DEPT-A-9              
                                                   RP-TTL-DEPT-B-9              
               END-IF                                                           
               MOVE PV-GRP-TTL-ADJ-QTY      TO RP-TTL-ADJ-QTY-A                 
                                               RP-TTL-ADJ-QTY-B                 
               MOVE PV-GRP-TTL-EXTD-RTL-AMT TO RP-TTL-EXTD-RTL-AMT-A            
                                               RP-TTL-EXTD-RTL-AMT-B            
               MOVE RP-REPORT-TOTALS-A      TO RP-REPORT-DETAIL-A               
               MOVE 2 TO PV-ADVANCE-LINE-A                                      
               PERFORM Z600-PRINT-DETAILS-A                                     
                                                                                
               MOVE RP-REPORT-TOTALS-B      TO RP-REPORT-DETAIL-B               
               MOVE 2 TO PV-ADVANCE-LINE-B                                      
               PERFORM Z625-PRINT-DETAILS-B                                     
                                                                                
               PERFORM D300-FORMAT-SUM-2-RPT                                    
               PERFORM Z650-PRINT-DETAILS-C                                     
                                                                                
               IF PV-MULTI-OTBND-TRIP > ZEROES                                  
                   MOVE RP-REPORT-TRAILER-1A TO RP-REPORT-DETAIL-A              
                   MOVE 2                    TO PV-ADVANCE-LINE-A               
                   PERFORM Z600-PRINT-DETAILS-A                                 
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE 2 TO PV-ADVANCE-LINE-A.                                         
                                                                                
      ******************************************************************        
      *    FINALIZE THE REPORT AND CLOSE OUT FILES                              
      ******************************************************************        
       B900-FINALIZE.                                                           
                                                                                
           IF PV-PAGE-COUNT-A > 0                                               
               SET RP-GRAND-TOTALS-A                                            
                   RP-GRAND-TOTALS-B        TO TRUE                             
               MOVE PV-GRD-TTL-ADJ-QTY      TO RP-TTL-ADJ-QTY-A                 
                                               RP-TTL-ADJ-QTY-B                 
                                               RP-TTL-ADJ-QTY-C                 
               MOVE PV-GRD-TTL-EXTD-RTL-AMT TO RP-TTL-EXTD-RTL-AMT-A            
                                               RP-TTL-EXTD-RTL-AMT-B            
                                               RP-TTL-EXTD-RTL-AMT-C            
                                                                                
               MOVE RP-REPORT-TOTALS-A      TO RP-REPORT-DETAIL-A               
               MOVE 2 TO PV-ADVANCE-LINE-A                                      
               PERFORM Z600-PRINT-DETAILS-A                                     
                                                                                
               MOVE RP-REPORT-END TO RP-REPORT-DETAIL-A                         
               MOVE 3 TO PV-ADVANCE-LINE-A                                      
               PERFORM Z600-PRINT-DETAILS-A                                     
                                                                                
               MOVE RP-REPORT-TOTALS-B      TO RP-REPORT-DETAIL-B               
               MOVE 2 TO PV-ADVANCE-LINE-B                                      
               PERFORM Z625-PRINT-DETAILS-B                                     
                                                                                
               MOVE RP-REPORT-END TO RP-REPORT-DETAIL-B                         
               MOVE 3 TO PV-ADVANCE-LINE-B                                      
               PERFORM Z625-PRINT-DETAILS-B                                     
                                                                                
               MOVE RP-REPORT-TOTALS-C      TO RP-REPORT-DETAIL-C               
               MOVE 2 TO PV-ADVANCE-LINE-C                                      
               PERFORM Z650-PRINT-DETAILS-C                                     
                                                                                
               MOVE RP-REPORT-END TO RP-REPORT-DETAIL-C                         
               MOVE 3 TO PV-ADVANCE-LINE-C                                      
               PERFORM Z650-PRINT-DETAILS-C                                     
           END-IF.                                                              
                                                                                
           MOVE ZEROES        TO PV-RECORD-COUNT.                               
           PERFORM                                                              
               VARYING PT-STORE-TBL-INDX FROM 1 BY 1                            
               UNTIL PT-STORE-TBL-INDX > PT-STORE-TBL-COUNT                     
                                                                                
               MOVE PT-STORE-TBL-LOC(PT-STORE-TBL-INDX)                         
                   TO RP-LOC-NBR-S-9                                            
               MOVE PT-STORE-TBL-LOC-NM(PT-STORE-TBL-INDX)                      
                   TO RP-LOC-NM-S                                               
               MOVE PT-STORE-TBL-DATE(PT-STORE-TBL-INDX)                        
                   TO RP-ADJ-DATE-S                                             
               MOVE PT-STORE-TBL-RECS(PT-STORE-TBL-INDX)                        
                   TO RP-RECORD-COUNT-S                                         
               ADD PT-STORE-TBL-RECS(PT-STORE-TBL-INDX)                         
                   TO PV-RECORD-COUNT                                           
               IF PT-STORE-TBL-INVENTORY(PT-STORE-TBL-INDX)                     
                   IF PT-STORE-TBL-RECS(PT-STORE-TBL-INDX) > ZEROES             
                       CONTINUE                                                 
                   ELSE                                                         
                       SET RP-MISSING-LOCATION-S   TO TRUE                      
                   END-IF                                                       
               ELSE                                                             
                   SET RP-ADDITION-LOCATION-S  TO TRUE                          
               END-IF                                                           
                                                                                
               PERFORM Z675-PRINT-DETAILS-S                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-PAGE-COUNT-S > 0                                               
               MOVE PV-RECORD-COUNT         TO RP-TTL-RECORD-COUNT-S            
                                                                                
               MOVE RP-REPORT-TOTALS-S      TO RP-REPORT-DETAIL-S               
               MOVE 2 TO PV-ADVANCE-LINE-S                                      
               PERFORM Z675-PRINT-DETAILS-S                                     
                                                                                
               MOVE RP-REPORT-END TO RP-REPORT-DETAIL-S                         
               MOVE 3 TO PV-ADVANCE-LINE-S                                      
               PERFORM Z675-PRINT-DETAILS-S                                     
           END-IF.                                                              
                                                                                
           CLOSE   RPT-REQUEST-CNTL-FILE                                        
                   STORE-EXPECTED-ADJ-FILE                                      
                   STORE-EXPECTED-DTL-RPT                                       
                   STORE-EXPECTED-SUM-1-RPT                                     
                   STORE-EXPECTED-SUM-2-RPT                                     
                   STORE-EXPECTED-STATUS-RPT.                                   
                                                                                
      ******************************************************************        
      * LOAD STORES INTO WORKING STORAGE TABLE                                  
      ******************************************************************        
       C100-LOAD-INV-STR.                                                       
                                                                                
           PERFORM Z500-OPEN-INV-STR-CSR.                                       
                                                                                
           PERFORM Z550-FETCH-INV-STR-CSR.                                      
           PERFORM                                                              
               UNTIL END-OF-INV-STR                                             
               ADD +1                  TO PT-STORE-TBL-COUNT                    
               IF PT-STORE-TBL-COUNT > PT-STORE-TBL-MAX                         
                   SET TABLE-EXCEEDED-ABEND-4003 TO TRUE                        
                   DISPLAY ' *** STORE TABLE EXCEEDED'                          
                           ' IN C100-LOAD-INV-STR ***'                          
                   PERFORM Z999-ABEND                                           
               END-IF                                                           
                                                                                
               MOVE INVPAR-LOC-NBR                                              
                   TO PT-STORE-TBL-LOC(PT-STORE-TBL-COUNT)                      
                      XMT00001-LOC-NBR                                          
               MOVE INVPAR-PLND-INV-DTE                                         
                   TO PT-STORE-TBL-DATE(PT-STORE-TBL-COUNT)                     
                                                                                
               PERFORM Z300-GET-LOC-INFO                                        
               MOVE XMT00001-LOC-TYP-CDE                                        
                   TO PT-STORE-TBL-LOC-TYP(PT-STORE-TBL-COUNT)                  
               MOVE XMT00001-LOC-NM                                             
                   TO PT-STORE-TBL-LOC-NM(PT-STORE-TBL-COUNT)                   
                                                                                
               SET PT-STORE-TBL-INVENTORY(PT-STORE-TBL-COUNT) TO TRUE           
               MOVE ZEROES                                                      
                   TO PT-STORE-TBL-RECS(PT-STORE-TBL-COUNT)                     
                                                                                
               PERFORM Z550-FETCH-INV-STR-CSR                                   
           END-PERFORM.                                                         
                                                                                
           PERFORM Z575-CLOSE-INV-STR-CSR.                                      
                                                                                
      ******************************************************************        
      * SEARCH LOADED STORE WORKING STORAGE TABLE                               
      ******************************************************************        
       C200-VALIDATE-INV-STR.                                                   
                                                                                
DELETE*    DISPLAY ' --- SEARCHING FOR LOCATION: ' XMT00001-LOC-NBR.            
           MOVE PV-CURR-LOC-NBR-9 TO XMT00001-LOC-NBR.                          
           PERFORM C250-SEARCH-INV-STR.                                         
                                                                                
      * TRY TO ADD MORE STORES FROM INVENTORY PARAMETER, IF CUM                 
      * IF NOT PART OF THE ORIGINAL LOAD.                                       
           IF PT-STORE-TBL-INDX > PT-STORE-TBL-COUNT                            
               AND RQ-RPT-CUM-IND                                               
               IF IN137-ADJ-DATE < RQ-RPT-FR-DATE                               
                   OR IN137-ADJ-DATE > RQ-RPT-TO-DATE                           
                   MOVE IN137-ADJ-DATE TO RQ-RPT-FR-DATE                        
                                          RQ-RPT-TO-DATE                        
                   PERFORM C100-LOAD-INV-STR                                    
                   DISPLAY '   THERE ARE NOW ' PT-STORE-TBL-COUNT               
                           ' STORES WITH ADDITION OF DATE: '                    
                           RQ-RPT-FR-DATE '/'                                   
                           RQ-RPT-TO-DATE                                       
                                                                                
      * TRY TO SEARCH AGAIN                                                     
                   MOVE PV-CURR-LOC-NBR-9 TO XMT00001-LOC-NBR                   
                   PERFORM C250-SEARCH-INV-STR                                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
      * ADD TO WORKING STORAGE TABLE AND SET AS RECEIVING/UNEXPECTED            
           IF PT-STORE-TBL-INDX > PT-STORE-TBL-COUNT                            
               ADD +1               TO PT-STORE-TBL-COUNT                       
               IF PT-STORE-TBL-COUNT > PT-STORE-TBL-MAX                         
                   SET TABLE-EXCEEDED-ABEND-4003 TO TRUE                        
                   DISPLAY ' *** STORE TABLE EXCEEDED'                          
                           ' IN C200-VALIDATE-INV-STR ***'                      
                   PERFORM Z999-ABEND                                           
               END-IF                                                           
                                                                                
               MOVE PT-STORE-TBL-COUNT TO PT-STORE-TBL-INDX                     
               MOVE XMT00001-LOC-NBR                                            
                   TO PT-STORE-TBL-LOC(PT-STORE-TBL-INDX)                       
                      INVPAR-LOC-NBR                                            
               MOVE IN137-ADJ-DATE                                              
                   TO PT-STORE-TBL-DATE(PT-STORE-TBL-INDX)                      
                      INVPAR-PLND-INV-DTE                                       
                                                                                
               PERFORM Z300-GET-LOC-INFO                                        
               MOVE XMT00001-LOC-TYP-CDE                                        
                   TO PT-STORE-TBL-LOC-TYP(PT-STORE-TBL-INDX)                   
               MOVE XMT00001-LOC-NM                                             
                   TO PT-STORE-TBL-LOC-NM(PT-STORE-TBL-INDX)                    
                                                                                
               SET PT-STORE-TBL-RECEIVING(PT-STORE-TBL-INDX)  TO TRUE           
                                                                                
               MOVE ZEROES  TO PT-STORE-TBL-RECS(PT-STORE-TBL-INDX)             
                               PV-RECORD-COUNT                                  
           ELSE                                                                 
DELETE*        DISPLAY ' --- FOUND LOCATION: ' XMT00001-LOC-NBR                 
               MOVE PT-STORE-TBL-LOC(PT-STORE-TBL-INDX)                         
                   TO INVPAR-LOC-NBR                                            
               MOVE PT-STORE-TBL-DATE(PT-STORE-TBL-INDX)                        
                   TO INVPAR-PLND-INV-DTE                                       
               MOVE PT-STORE-TBL-LOC-TYP(PT-STORE-TBL-INDX)                     
                   TO XMT00001-LOC-TYP-CDE                                      
               MOVE PT-STORE-TBL-LOC-NM(PT-STORE-TBL-INDX)                      
                   TO XMT00001-LOC-NM                                           
               MOVE PT-STORE-TBL-RECS(PT-STORE-TBL-INDX)                        
                   TO PV-RECORD-COUNT                                           
           END-IF.                                                              
                                                                                
           IF PT-STORE-TBL-INVENTORY(PT-STORE-TBL-INDX)                         
               SET VALID-MANIFESTED-INV-STORE   TO TRUE                         
           ELSE                                                                 
               SET INVALID-MANIFESTED-INV-STORE TO TRUE                         
           END-IF.                                                              
                                                                                
      * ACCOUNT FOR RECORD ALREADY READ                                         
           COMPUTE PV-RECORD-COUNT = PV-RECORD-COUNT + 1.                       
                                                                                
      ******************************************************************        
      * SEARCH LOADED STORE WORKING STORAGE TABLE                               
      ******************************************************************        
       C250-SEARCH-INV-STR.                                                     
                                                                                
DELETE*    DISPLAY ' --- SEARCHING FOR LOCATION: ' XMT00001-LOC-NBR.            
           PERFORM                                                              
               VARYING PT-STORE-TBL-INDX FROM 1 BY 1                            
               UNTIL PT-STORE-TBL-INDX > PT-STORE-TBL-COUNT                     
                   OR (PT-STORE-TBL-LOC(PT-STORE-TBL-INDX)                      
                       = XMT00001-LOC-NBR                                       
                       AND PT-STORE-TBL-DATE(PT-STORE-TBL-INDX)                 
                           = IN137-ADJ-DATE)                                    
               CONTINUE                                                         
           END-PERFORM.                                                         
                                                                                
      ******************************************************************        
      * FORMAT DETAIL REPORT                                                    
      ******************************************************************        
       D100-FORMAT-DETAIL-RPT.                                                  
                                                                                
           IF RQ-RPT-BY-STORE-DEPT                                              
               MOVE IN137-DEPT-9            TO RP-DEPT-A-9                      
           ELSE                                                                 
               MOVE IN137-STORE-NBR-9       TO RP-LOC-NBR-A-9                   
           END-IF.                                                              
           MOVE IN137-CLASS-9           TO RP-CLASS-NBR-A-9.                    
           MOVE IN137-SKU               TO RP-SKU-NBR-A.                        
                                                                                
           MOVE IN137-PO-NBR            TO RP-PO-NBR-A.                         
           MOVE IN137-RCVR-SEQ-NBR      TO RP-RCVR-SEQ-NBR-A.                   
                                                                                
           MOVE IN137-OTBND-TRIP-ID     TO RP-OTBND-TRIP-ID-A.                  
           IF IN137-MULTI-OTBND-TRIP                                            
               SET RP-MULTIPLE-OTBND-TRIP   TO TRUE                             
               ADD +1                       TO PV-MULTI-OTBND-TRIP              
           END-IF                                                               
                                                                                
           MOVE IN137-ADJ-DATE          TO PV-DATE-HOLD-CCYY-MM-DD              
                                           PV-CURR-ADJ-DATE.                    
           PERFORM E100-REFORMAT-DATE.                                          
           MOVE PV-DATE-RFMT-MM-DD-CCYY TO RP-ADJUST-DATE-A.                    
                                                                                
           MOVE IN137-RTL-AMT           TO RP-ADJ-RTL-AMT-A.                    
           MOVE IN137-ADJ-QTY           TO RP-ADJ-QTY-A.                        
           MOVE IN137-EXTD-RTL-AMT      TO RP-EXTD-RTL-AMT-A.                   
                                                                                
      ******************************************************************        
      * FORMAT SUMMARY 1 REPORT                                                 
      ******************************************************************        
       D200-FORMAT-SUM-1-RPT.                                                   
                                                                                
           IF RQ-RPT-BY-STORE-DEPT                                              
               MOVE PV-CURR-DEPT-9     TO RP-DEPT-B-9                           
                                                                                
               MOVE PV-CURR-DEPT-3X    TO MDSEAR-DEPT-NBR                       
               PERFORM Z400-GET-DEPT-INFO                                       
               MOVE MDSEAR-DESC        TO RP-DESCRIPTION-B                      
           ELSE                                                                 
               MOVE PV-CURR-LOC-NBR-9  TO RP-LOC-NBR-B-9                        
               MOVE XMT00001-LOC-NM    TO RP-DESCRIPTION-B                      
           END-IF.                                                              
                                                                                
           MOVE PV-CURR-ADJ-DATE   TO PV-DATE-HOLD-CCYY-MM-DD.                  
           PERFORM E100-REFORMAT-DATE.                                          
           MOVE PV-DATE-RFMT-MM-DD-CCYY TO RP-ADJUST-DATE-B.                    
                                                                                
           MOVE PV-SUB-TTL-ADJ-QTY      TO RP-ADJ-QTY-B.                        
           MOVE PV-SUB-TTL-EXTD-RTL-AMT TO RP-EXTD-RTL-AMT-B.                   
                                                                                
      ******************************************************************        
      * FORMAT SUMMARY 2 REPORT                                                 
      ******************************************************************        
       D300-FORMAT-SUM-2-RPT.                                                   
                                                                                
           IF RQ-RPT-BY-STORE-DEPT                                              
               MOVE PV-CURR-LOC-NBR-9  TO RP-LOC-NBR-C-9                        
               MOVE XMT00001-LOC-NM    TO RP-DESCRIPTION-C                      
           ELSE                                                                 
               MOVE PV-CURR-DEPT-9     TO RP-DEPT-C-9                           
                                                                                
               MOVE PV-CURR-DEPT-3X    TO MDSEAR-DEPT-NBR                       
               PERFORM Z400-GET-DEPT-INFO                                       
               MOVE MDSEAR-DESC        TO RP-DESCRIPTION-C                      
           END-IF.                                                              
                                                                                
           MOVE PV-CURR-ADJ-DATE   TO PV-DATE-HOLD-CCYY-MM-DD.                  
           PERFORM E100-REFORMAT-DATE.                                          
           MOVE PV-DATE-RFMT-MM-DD-CCYY TO RP-ADJUST-DATE-C.                    
                                                                                
           MOVE PV-GRP-TTL-ADJ-QTY      TO RP-ADJ-QTY-C.                        
           MOVE PV-GRP-TTL-EXTD-RTL-AMT TO RP-EXTD-RTL-AMT-C.                   
                                                                                
      ******************************************************************        
      *    REFORMAT DATE FOR OUTPUT FROM CCYY-MM-DD TO MM-DD-CCYY               
      ******************************************************************        
       E100-REFORMAT-DATE.                                                      
                                                                                
           MOVE PV-DATE-HOLD-CCYY TO PV-DATE-RFMT-CCYY.                         
           MOVE PV-DATE-HOLD-MM   TO PV-DATE-RFMT-MM.                           
           MOVE PV-DATE-HOLD-DD   TO PV-DATE-RFMT-DD.                           
           MOVE PV-DATE-HOLD-DASH-1 TO PV-DATE-RFMT-DASH-1.                     
           MOVE PV-DATE-HOLD-DASH-2 TO PV-DATE-RFMT-DASH-2.                     
                                                                                
      ******************************************************************        
      *    READ THE NEXT RECORD FROM STORE-EXPECTED-ADJ-FILE                    
      ******************************************************************        
       Z100-READ-ADJ-FILE.                                                      
                                                                                
           READ STORE-EXPECTED-ADJ-FILE                                         
               INTO IN137-STR-EXPTED-RECORD                                     
               AT END SET END-OF-ADJ-FILE TO TRUE                               
               NOT AT END ADD +1          TO PV-RECORD-COUNT.                   
                                                                                
      ******************************************************************        
      *    READ THE NEXT CONTROL RECORD FROM OPTIONAL INPUT                     
      ******************************************************************        
       Z200-READ-CNTL-FILE.                                                     
                                                                                
           READ RPT-REQUEST-CNTL-FILE                                           
               INTO RQ-RPT-CNTL-AREA                                            
               AT END SET END-OF-CNTL-FILE TO TRUE.                             
                                                                                
      ******************************************************************        
      *    GET LOCATION INFORMATION                                             
      ******************************************************************        
       Z300-GET-LOC-INFO.                                                       
                                                                                
           EXEC SQL                                                             
W26600          SELECT                                                          
W26600                   B.LOC_TYP_CDE                                          
W26600                 , B.LOC_NM                                               
W26600             INTO                                                         
W26600                   :XMT00001-LOC-TYP-CDE                                  
W26600                  ,:XMT00001-LOC-NM                                       
                FROM                                                            
W26600                 XMT_LOC B                                                
                WHERE                                                           
                       B.LOC_NBR          = :XMT00001-LOC-NBR                   
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE XMT00001-LOC-NBR TO PV-CURR-LOC-NBR-9                   
                   DISPLAY ' *** INVALID STORE: ' PV-CURR-LOC-NBR               
                   MOVE 'GET TINVPAR/STORE INFO' TO  PV-OPER-ATTEMPTED          
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    GET DEPT INFORMATION                                                 
      ******************************************************************        
       Z400-GET-DEPT-INFO.                                                      
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                         B.MDSEAR_DESC                                          
                   INTO                                                         
                         :MDSEAR-DESC                                           
                FROM                                                            
                       TMDSEAR B                                                
                WHERE                                                           
                       B.OPERCO_NBR       = :PC-COMPANY                         
                  AND  B.DEPT_NBR         = :MDSEAR-DEPT-NBR                    
                  AND  B.MDSELV_CDE       = :PC-MDSELV-DEPT-CDE                 
                  AND  :RQ-RPT-FR-DATE                                          
                          BETWEEN B.STRT_DTE AND B.END_DTE                      
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   DISPLAY ' *** INVALID DEPT: ' PV-CURR-DEPT                   
                   MOVE 'GET TMDSEAR/DEPT INFO' TO  PV-OPER-ATTEMPTED           
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    OPEN/SETUP INVENTORYING STORE CURSOR                                 
      ******************************************************************        
       Z500-OPEN-INV-STR-CSR.                                                   
                                                                                
           EXEC SQL                                                             
               OPEN INV-STR-CSR                                                 
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               DISPLAY ' DATE FROM ' RQ-RPT-FR-DATE                             
                       ' TO ' RQ-RPT-TO-DATE                                    
               MOVE 'OPEN INV-STR-CSR' TO PV-OPER-ATTEMPTED                     
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
           SET NOT-END-OF-INV-STR  TO TRUE.                                     
                                                                                
      ******************************************************************        
      *    FETCH INVENTORYING STORE CURSOR                                      
      ******************************************************************        
       Z550-FETCH-INV-STR-CSR.                                                  
                                                                                
           EXEC SQL FETCH INV-STR-CSR                                           
              INTO                                                              
                   :INVPAR-LOC-NBR                                              
                  ,:INVPAR-ACTL-INV-DTE                                         
                  ,:INVPAR-PLND-INV-DTE                                         
                  ,:XMT00001-LOC-TYP-CDE                                        
                  ,:XMT00001-LOC-NM                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN +100                                                        
                   SET END-OF-INV-STR  TO TRUE                                  
               WHEN OTHER                                                       
                   MOVE 'FETCH INV-STR-CSR' TO  PV-OPER-ATTEMPTED               
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    CLOSE/TERMINATE INVENTORYING STORE CURSOR                            
      ******************************************************************        
       Z575-CLOSE-INV-STR-CSR.                                                  
                                                                                
           EXEC SQL                                                             
                CLOSE INV-STR-CSR                                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'CLOSE INV-STR-CSR' TO PV-OPER-ATTEMPTED                    
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    PRINT THE DETAIL REPORT                                              
      ******************************************************************        
       Z600-PRINT-DETAILS-A.                                                    
                                                                                
           IF (PV-PRINT-LINE-COUNT-A + PV-ADVANCE-LINE-A                        
                   > PC-PRINT-LINE-MAX                                          
               OR PV-PRINT-LINE-COUNT-A = 0)                                    
               AND ((IN137-DEPT = PV-CURR-DEPT AND                              
                       RQ-RPT-BY-STORE-DEPT)                                    
                   OR (IN137-STORE-NBR = PV-CURR-LOC-NBR AND                    
                       RQ-RPT-BY-DEPT-STORE))                                   
               PERFORM Z700-PRINT-HEADINGS-A                                    
           END-IF.                                                              
                                                                                
           WRITE REPORT-LINE-1 FROM RP-REPORT-DETAIL-A                          
               AFTER ADVANCING PV-ADVANCE-LINE-A LINES.                         
                                                                                
           ADD  PV-ADVANCE-LINE-A TO PV-PRINT-LINE-COUNT-A.                     
           MOVE 1                 TO PV-ADVANCE-LINE-A.                         
           MOVE SPACES TO RP-REPORT-DETAIL-A.                                   
                                                                                
      ******************************************************************        
      *    PRINT THE SUMMARY 1 REPORT                                           
      ******************************************************************        
       Z625-PRINT-DETAILS-B.                                                    
                                                                                
           IF (PV-PRINT-LINE-COUNT-B + PV-ADVANCE-LINE-B                        
                   > PC-PRINT-LINE-MAX                                          
               OR PV-PRINT-LINE-COUNT-B = 0)                                    
               PERFORM Z725-PRINT-HEADINGS-B                                    
           END-IF.                                                              
                                                                                
           WRITE REPORT-LINE-2 FROM RP-REPORT-DETAIL-B                          
               AFTER ADVANCING PV-ADVANCE-LINE-B LINES.                         
                                                                                
           ADD  PV-ADVANCE-LINE-B TO PV-PRINT-LINE-COUNT-B.                     
           MOVE 1                 TO PV-ADVANCE-LINE-B.                         
           MOVE SPACES TO RP-REPORT-DETAIL-B.                                   
                                                                                
      ******************************************************************        
      *    PRINT THE SUMMARY 2 REPORT                                           
      ******************************************************************        
       Z650-PRINT-DETAILS-C.                                                    
                                                                                
           IF (PV-PRINT-LINE-COUNT-C + PV-ADVANCE-LINE-C                        
                   > PC-PRINT-LINE-MAX                                          
               OR PV-PRINT-LINE-COUNT-C = 0)                                    
               PERFORM Z750-PRINT-HEADINGS-C                                    
           END-IF.                                                              
                                                                                
           WRITE REPORT-LINE-3 FROM RP-REPORT-DETAIL-C                          
               AFTER ADVANCING PV-ADVANCE-LINE-C LINES.                         
                                                                                
           ADD  PV-ADVANCE-LINE-C TO PV-PRINT-LINE-COUNT-C.                     
           MOVE 1                 TO PV-ADVANCE-LINE-C.                         
           MOVE SPACES TO RP-REPORT-DETAIL-C.                                   
                                                                                
      ******************************************************************        
      *    PRINT THE STORE STATUS REPORT                                        
      ******************************************************************        
       Z675-PRINT-DETAILS-S.                                                    
                                                                                
           IF (PV-PRINT-LINE-COUNT-S + PV-ADVANCE-LINE-S                        
                   > PC-PRINT-LINE-MAX                                          
               OR PV-PRINT-LINE-COUNT-S = 0)                                    
               PERFORM Z775-PRINT-HEADINGS-S                                    
           END-IF.                                                              
                                                                                
           WRITE REPORT-LINE-4 FROM RP-REPORT-DETAIL-S                          
               AFTER ADVANCING PV-ADVANCE-LINE-S LINES.                         
                                                                                
           ADD  PV-ADVANCE-LINE-S TO PV-PRINT-LINE-COUNT-S.                     
           MOVE 1                 TO PV-ADVANCE-LINE-S.                         
           MOVE SPACES TO RP-REPORT-DETAIL-S.                                   
                                                                                
      ******************************************************************        
      *    PRINT THE DETAIL REPORT HEADINGS                                     
      ******************************************************************        
       Z700-PRINT-HEADINGS-A.                                                   
                                                                                
           ADD 1 TO PV-PAGE-COUNT-A.                                            
           MOVE PV-PAGE-COUNT-A TO DP132O-PAGE-NUMBER.                          
           IF RQ-RPT-CUM-IND                                                    
               MOVE 5               TO DP132O-REPORT-NUMBER                     
           ELSE                                                                 
               MOVE 1               TO DP132O-REPORT-NUMBER                     
           END-IF.                                                              
           WRITE REPORT-LINE-1 FROM DP132O-STANDRD-HEADER-132-COLS              
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE REPORT-LINE-1 FROM RP-REPORT-TITLE-1A.                         
           WRITE REPORT-LINE-1 FROM RP-REPORT-TITLE-2.                          
                                                                                
           WRITE REPORT-LINE-1 FROM RP-REPORT-HEADER-1A                         
               AFTER ADVANCING 2 LINES.                                         
           WRITE REPORT-LINE-1 FROM RP-REPORT-HEADER-2A.                        
                                                                                
           MOVE 7 TO PV-PRINT-LINE-COUNT-A.                                     
           MOVE 2 TO PV-ADVANCE-LINE-A.                                         
                                                                                
      ******************************************************************        
      *    PRINT THE SUMMARY 1 REPORT HEADINGS                                  
      ******************************************************************        
       Z725-PRINT-HEADINGS-B.                                                   
                                                                                
           ADD 1 TO PV-PAGE-COUNT-B.                                            
           MOVE PV-PAGE-COUNT-B TO DP132O-PAGE-NUMBER.                          
           IF RQ-RPT-CUM-IND                                                    
               MOVE 6               TO DP132O-REPORT-NUMBER                     
           ELSE                                                                 
               MOVE 2               TO DP132O-REPORT-NUMBER                     
           END-IF.                                                              
           WRITE REPORT-LINE-2 FROM DP132O-STANDRD-HEADER-132-COLS              
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE REPORT-LINE-2 FROM RP-REPORT-TITLE-1B.                         
           WRITE REPORT-LINE-2 FROM RP-REPORT-TITLE-2.                          
                                                                                
           WRITE REPORT-LINE-2 FROM RP-REPORT-HEADER-1B                         
               AFTER ADVANCING 2 LINES.                                         
           WRITE REPORT-LINE-2 FROM RP-REPORT-HEADER-2B.                        
                                                                                
           MOVE 7 TO PV-PRINT-LINE-COUNT-B.                                     
           MOVE 2 TO PV-ADVANCE-LINE-B.                                         
                                                                                
      ******************************************************************        
      *    PRINT THE SUMMARY 2 REPORT HEADINGS                                  
      ******************************************************************        
       Z750-PRINT-HEADINGS-C.                                                   
                                                                                
           ADD 1 TO PV-PAGE-COUNT-C.                                            
           MOVE PV-PAGE-COUNT-C TO DP132O-PAGE-NUMBER.                          
           IF RQ-RPT-CUM-IND                                                    
               MOVE 7               TO DP132O-REPORT-NUMBER                     
           ELSE                                                                 
               MOVE 3               TO DP132O-REPORT-NUMBER                     
           END-IF.                                                              
           WRITE REPORT-LINE-3 FROM DP132O-STANDRD-HEADER-132-COLS              
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE REPORT-LINE-3 FROM RP-REPORT-TITLE-1C.                         
                                                                                
           WRITE REPORT-LINE-3 FROM RP-REPORT-HEADER-1C                         
               AFTER ADVANCING 2 LINES.                                         
           WRITE REPORT-LINE-3 FROM RP-REPORT-HEADER-2C.                        
                                                                                
           MOVE 6 TO PV-PRINT-LINE-COUNT-C.                                     
           MOVE 2 TO PV-ADVANCE-LINE-C.                                         
                                                                                
      ******************************************************************        
      *    PRINT THE STORE STATUS RPT HEADINGS                                  
      ******************************************************************        
       Z775-PRINT-HEADINGS-S.                                                   
                                                                                
           ADD 1 TO PV-PAGE-COUNT-S.                                            
           MOVE PV-PAGE-COUNT-S TO DP132O-PAGE-NUMBER.                          
           IF RQ-RPT-CUM-IND                                                    
               MOVE 8               TO DP132O-REPORT-NUMBER                     
           ELSE                                                                 
               MOVE 4               TO DP132O-REPORT-NUMBER                     
           END-IF.                                                              
           WRITE REPORT-LINE-4 FROM DP132O-STANDRD-HEADER-132-COLS              
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE REPORT-LINE-4 FROM RP-REPORT-TITLE-1S.                         
                                                                                
           WRITE REPORT-LINE-4 FROM RP-REPORT-HEADER-1S                         
               AFTER ADVANCING 2 LINES.                                         
           WRITE REPORT-LINE-4 FROM RP-REPORT-HEADER-2S.                        
                                                                                
           MOVE 6 TO PV-PRINT-LINE-COUNT-S.                                     
           MOVE 2 TO PV-ADVANCE-LINE-S.                                         
                                                                                
      ******************************************************************        
      *    SQL ERROR - ABEND                                                    
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           SET SQL-ABEND-4013         TO TRUE.                                  
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKRP191'.                             
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED                    
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           PERFORM Z999-ABEND.                                                  
                                                                                
      ******************************************************************        
      *    FORCE ABEND                                                          
      ******************************************************************        
       Z999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
