00001 ******************************************************************03/08/90
00002  IDENTIFICATION DIVISION.                                         DPKRP012
00003 ******************************************************************   LV020
00004                                                                   DPKRP012
00005  PROGRAM-ID.             DPKRP012.                                   CL**2
00006  AUTHOR.                 JOYCE KNOKE (TCC).                          CL**2
00007  INSTALLATION.           KOHLS.                                   DPKRP012
00008  DATE-WRITTEN            MARCH,  1990.                               CL**2
00009  DATE-COMPILED.                                                   DPKRP012
00010                                                                   DPKRP012
00011 ******************************************************************DPKRP012
00012 *      THIS PROGRAM READS THE ESDS THAT CONTAINS THE BEFORE AND   DPKRP012
00013 *  AFTER IMAGES OF ALL OF THE DB2 CHECKPOINT RESTART FILE ROWS       CL*20
00014 *  THAT WERE INVOLVED IN MAINTENANCE.  A LASER REPORT IS PRODUCED    CL*20
00015 *  INDICATING WHAT CHANGES WERE LOGGED.                              CL*20
00016 ******************************************************************DPKRP012
00017                                                                   DPKRP012
00018 ******************************************************************DPKRP012
00019  ENVIRONMENT DIVISION.                                            DPKRP012
00020 ******************************************************************DPKRP012
00021                                                                   DPKRP012
00022  CONFIGURATION SECTION.                                           DPKRP012
00023                                                                   DPKRP012
00024  SOURCE-COMPUTER.        IBM-3090.                                DPKRP012
00025  OBJECT-COMPUTER.        IBM-3090.                                DPKRP012
00026                                                                   DPKRP012
00027  INPUT-OUTPUT SECTION.                                            DPKRP012
00028                                                                   DPKRP012
00029  FILE-CONTROL.                                                    DPKRP012
00030                                                                   DPKRP012
00031      SELECT DB2-CHKPT-MAINT-LOG-FILE                                 CL**2
00032          ORGANIZATION IS SEQUENTIAL                               DPKRP012
00033          ACCESS MODE IS SEQUENTIAL                                DPKRP012
00034          ASSIGN AS-INP01.                                         DPKRP012
00035                                                                   DPKRP012
00036      SELECT DB2-CHKPT-MAINT-LOG-REPORT                               CL**2
00037          ASSIGN TO RPT01.                                         DPKRP012
00038                                                                   DPKRP012
00039 ******************************************************************DPKRP012
00040  DATA DIVISION.                                                   DPKRP012
00041 ******************************************************************DPKRP012
00042                                                                   DPKRP012
00043  FILE SECTION.                                                    DPKRP012
00044                                                                   DPKRP012
00045  FD  DB2-CHKPT-MAINT-LOG-FILE                                        CL**2
00046      LABEL RECORDS ARE STANDARD.                                  DPKRP012
00047                                                                   DPKRP012
00048  01  DB2-CHKPT-MAINT-LOG-RECORD          PIC X(60).                  CL**2
00049                                                                   DPKRP012
00050  FD  DB2-CHKPT-MAINT-LOG-REPORT                                      CL**2
00051      RECORDING MODE IS F                                          DPKRP012
00052      LABEL RECORDS ARE STANDARD                                   DPKRP012
00053      BLOCK CONTAINS 0 RECORDS.                                    DPKRP012
00054                                                                   DPKRP012
00055  01  DB2-CHKPT-MAINT-LOG-LINE            PIC X(132).                 CL**2
00056                                                                   DPKRP012
00057 ******************************************************************DPKRP012
00058  WORKING-STORAGE SECTION.                                         DPKRP012
00059 ******************************************************************DPKRP012
00060 *                                                                 DPKRP012
00061 *  COPY BOOK FOR THE DB2 CHECKPOINT MAINTENANCE LOG FILE             CL**2
00062 *  LAYOUT - INPUT.                                                DPKRP012
00063 *                                                                 DPKRP012
00064      COPY DPRD500I.                                                  CL**2
00065                                                                   DPKRP012
00066 *                                                                 DPKRP012
00067 *  STANDARD REPORT HEADER FOR 200 COLUMN REPORT                   DPKRP012
00068 *                                                                 DPKRP012
00069      COPY DPWS132O.                                                  CL**2
00070                                                                   DPKRP012
00071  01  CD-CPU-DATE.                                                 DPKRP012
00072      05  CD-YEAR                         PIC  9(02)  VALUE ZEROS. DPKRP012
00073      05  CD-MONTH                        PIC  9(02)  VALUE ZEROS. DPKRP012
00074      05  CD-DAY                          PIC  9(02)  VALUE ZEROS. DPKRP012
00075                                                                   DPKRP012
00076  01  CT-CPU-TIME.                                                 DPKRP012
00077      05  CT-HOUR                         PIC  9(02)  VALUE ZEROS. DPKRP012
00078      05  CT-MINUTE                       PIC  9(02)  VALUE ZEROS. DPKRP012
00079      05  FILLER                          PIC  9(04)  VALUE ZEROS. DPKRP012
00080                                                                   DPKRP012
00081  01  DL-DETAIL-LINE.                                              DPKRP012
00082      05  FILLER                          PIC  X(10)  VALUE SPACE.    CL**2
00083      05  DL-JOB-NAME                     PIC  X(08)  VALUE SPACE.    CL**2
00084      05  FILLER                          PIC  X(05)  VALUE SPACE.    CL**2
00085      05  DL-PLAN-NAME                    PIC  X(08)  VALUE SPACE.    CL**2
00086      05  FILLER                          PIC  X(03)  VALUE SPACE.    CL**2
00087      05  DL-TRANSACTION-DATE.                                        CL**2
00088          10  DL-TRANSACTION-MONTH        PIC  X(02)  VALUE SPACE. DPKRP012
00089          10  DL-SLASH-ONE                PIC  X(01)  VALUE '/'.   DPKRP012
00090          10  DL-TRANSACTION-DAY          PIC  X(02)  VALUE SPACE. DPKRP012
00091          10  DL-SLASH-TWO                PIC  X(01)  VALUE '/'.   DPKRP012
00092          10  DL-TRANSACTION-YEAR         PIC  X(02)  VALUE SPACE. DPKRP012
00093      05  FILLER                          PIC  X(03)  VALUE SPACE.    CL**2
00094      05  DL-TRANSACTION-TIME.                                     DPKRP012
00095          10  DL-TRANSACTION-HOUR         PIC  9(02)  VALUE ZEROS. DPKRP012
00096          10  DL-COLON1                   PIC  X(01)  VALUE SPACE. DPKRP012
00097          10  DL-TRANSACTION-MINUTE       PIC  9(02)  VALUE ZEROS. DPKRP012
00098          10  DL-COLON2                   PIC  X(01)  VALUE SPACE. DPKRP012
00099          10  DL-TRANSACTION-SECOND       PIC  9(02)  VALUE ZEROS. DPKRP012
00100      05  FILLER                          PIC  X(03)  VALUE SPACE.    CL**2
00101      05  DL-USER-ID                      PIC  X(08)  VALUE SPACE.    CL**2
00102      05  FILLER                          PIC  X(06)  VALUE SPACE.    CL**2
00103      05  DL-BEFORE-IMAGE.                                            CL**2
00104          10  DL-BEFORE-CHKPT-STATUS      PIC  X(01)  VALUE SPACE.    CL**2
00105          10  FILLER                      PIC  X(06)  VALUE SPACE.    CL**2
00106          10  DL-BEFORE-CHKPT-CONTROL     PIC  X(01)  VALUE SPACE.    CL**2
00107          10  FILLER                      PIC  X(05)  VALUE SPACE.    CL**2
00108          10  DL-BEFORE-CHKPT-TYPE        PIC  X(01)  VALUE SPACE.    CL**2
00109          10  FILLER                      PIC  X(03)  VALUE SPACE.    CL**2
00110          10  DL-BEFORE-FREQUENCY         PIC  ZZZZZ  VALUE ZEROS.    CL*12
00111      05  FILLER                          PIC  X(06)  VALUE SPACE.    CL**2
00112      05  DL-AFTER-IMAGE.                                             CL**2
00113          10  DL-AFTER-CHKPT-STATUS       PIC  X(01)  VALUE SPACE.    CL**2
00114          10  FILLER                      PIC  X(06)  VALUE SPACE.    CL**2
00115          10  DL-AFTER-CHKPT-CONTROL      PIC  X(01)  VALUE SPACE.    CL**2
00116          10  FILLER                      PIC  X(05)  VALUE SPACE.    CL**2
00117          10  DL-AFTER-CHKPT-TYPE         PIC  X(01)  VALUE SPACE.    CL**2
00118          10  FILLER                      PIC  X(03)  VALUE SPACE.    CL**2
00119          10  DL-AFTER-FREQUENCY          PIC  ZZZZZ  VALUE ZEROS.    CL*12
00120      05  FILLER                          PIC  X(12)  VALUE SPACE.    CL**2
00121                                                                      CL**2
00122  01  END-OF-REPORT-LINE.                                          DPKRP012
00123      05  FILLER                          PIC  X(54)  VALUE SPACE.    CL**2
00124      05  FILLER                          PIC  X(25)  VALUE        DPKRP012
00125          '***** END OF REPORT *****'.                             DPKRP012
00126      05  FILLER                          PIC  X(53)  VALUE SPACE.    CL**2
00127                                                                   DPKRP012
00128  01  HL1-HEADING-LINE-1.                                          DPKRP012
00129      05  FILLER                          PIC  X(48)  VALUE SPACE.    CL**2
00130      05  FILLER                          PIC  X(38)  VALUE           CL**2
00131          'DB2 CHECKPOINT RESTART MAINTENANCE LOG'.                   CL**2
00132      05  FILLER                          PIC  X(46)  VALUE SPACE.    CL**2
00133                                                                   DPKRP012
00134  01  HL2-HEADING-LINE-2.                                          DPKRP012
00135      05  FILLER                          PIC  X(68)  VALUE SPACE.    CL**2
00136      05  FILLER                          PIC  X(28)  VALUE           CL**2
00137          '------BEFORE IMAGE------'.                                 CL**2
00138      05  FILLER                          PIC  X(36)  VALUE           CL**2
00139          '-------AFTER IMAGE------'.                                 CL**2
00140                                                                   DPKRP012
00141  01  HL3-HEADING-LINE-3.                                          DPKRP012
00142      05  FILLER                          PIC  X(10)  VALUE SPACE.    CL**2
00143      05  FILLER                          PIC  X(37)  VALUE           CL**2
00144          'JOB NAME    PLAN NAME     DATE'.                           CL**2
00145      05  FILLER                          PIC  X(21)  VALUE           CL**2
00146          'TIME     USER ID'.                                         CL**2
00147      05  FILLER                          PIC  X(28)  VALUE           CL**2
00148          'STATUS  CNTL  TYPE  FREQ'.                                 CL**2
00149      05  FILLER                          PIC  X(36)  VALUE           CL**2
00150          'STATUS  CNTL  TYPE  FREQ'.                                 CL**2
00151                                                                   DPKRP012
00152  01  PC-PROGRAM-CONSTANTS.                                        DPKRP012
00153      05  PC-PAGE-SIZE                    PIC S9(04)  VALUE +60       CL*13
00154                                                      COMP SYNC.   DPKRP012
00155      05  PC-COLON                        PIC  X(01)  VALUE ':'.   DPKRP012
00156      05  PC-SLASH                        PIC  X(01)  VALUE '/'.   DPKRP012
00157                                                                   DPKRP012
00158  01  PS-PROGRAM-SWITCHES.                                         DPKRP012
00159      05  PS-EOF-DB2-CHKPT-MAINTLOG-IND   PIC  X(01)  VALUE 'N'.      CL**4
00160          88  PS-END-OF-DB2-CHKPT-MAINT-LOG           VALUE 'Y'.      CL**4
00161                                                                      CL*10
00162  01  PV-PROGRAM-VARIABLES.                                        DPKRP012
00163      05  PV-PAGE-COUNT                   PIC S9(04)  VALUE +0     DPKRP012
00164                                                      COMP.        DPKRP012
00165      05  PV-LINE-COUNT                   PIC S9(04)  VALUE +0     DPKRP012
00166                                                      COMP.        DPKRP012
00167      05  PV-ADVANCE-COUNT                PIC S9(04)  VALUE +0     DPKRP012
00168                                                      COMP.        DPKRP012
00169                                                                   DPKRP012
00170 ******************************************************************DPKRP012
00171  PROCEDURE DIVISION.                                              DPKRP012
00172 ******************************************************************DPKRP012
00173                                                                   DPKRP012
00174 ******************************************************************DPKRP012
00175 * 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE     DPKRP012
00176 * PROGRAM.                                                        DPKRP012
00177 ******************************************************************DPKRP012
00178  0000-PROGRAM-MAINLINE.                                           DPKRP012
00179      PERFORM 1000-INITIALIZE.                                     DPKRP012
00180      PERFORM 2000-PROC-DB2-CHKPT-LOG-FILE                            CL**5
00181          UNTIL PS-END-OF-DB2-CHKPT-MAINT-LOG.                        CL**3
00182      PERFORM 9000-EOJ-ROUTINE.                                    DPKRP012
00183      STOP RUN.                                                    DPKRP012
00184 *0000-EXIT.                                                       DPKRP012
00185                                                                   DPKRP012
00186 ******************************************************************DPKRP012
00187 * 1000-INITIALIZE - ACCEPTS THE CPU DATE AND TIME. OPENS ALL      DPKRP012
00188 * FILES ASSOCIATED WITH THE JOB.  FILLS APPROPRIATE HEADING       DPKRP012
00189 * INFORMATION AND PERFORMS THE PRIMING READ OF THE DB2 CHECKPOINT    CL*20
00190 * RESTART MAINTENANCE LOG FILE.                                      CL*20
00191 ******************************************************************DPKRP012
00192  1000-INITIALIZE.                                                 DPKRP012
00193      ACCEPT CD-CPU-DATE     FROM DATE.                            DPKRP012
00194      ACCEPT CT-CPU-TIME     FROM TIME.                            DPKRP012
00195                                                                   DPKRP012
00196      OPEN INPUT  DB2-CHKPT-MAINT-LOG-FILE                            CL**3
00197           OUTPUT DB2-CHKPT-MAINT-LOG-REPORT.                         CL**3
00198                                                                   DPKRP012
00199      MOVE CD-YEAR     TO  DP132O-RUN-YEAR.                           CL**5
00200      MOVE CD-MONTH    TO  DP132O-RUN-MONTH.                          CL**5
00201      MOVE CD-DAY      TO  DP132O-RUN-DAY.                            CL**5
00202      MOVE CT-HOUR     TO  DP132O-RUN-HOUR.                           CL**5
00203      MOVE CT-MINUTE   TO  DP132O-RUN-MINUTE.                         CL**5
00204      MOVE 'DPKRP012'  TO  DP132O-PROGRAM-NAME.                       CL**5
00205                                                                   DPKRP012
00206      PERFORM 7000-READ-DB2-CHKPT-LOG-FILE.                           CL*18
00207                                                                      CL*18
00208      IF DP500I-DB2-CHKPT-MAINT-RECORD = LOW-VALUES                   CL*18
00209          PERFORM 7000-READ-DB2-CHKPT-LOG-FILE.                       CL*18
00210                                                                      CL**4
00211      IF PS-END-OF-DB2-CHKPT-MAINT-LOG                                CL*14
00212          DISPLAY '****  NO REPORT GENERATED  ****' UPON CONSOLE      CL*14
00213          DISPLAY '** NORMAL END OF JOB DPKRP012 **' UPON CONSOLE.    CL*14
00214                                                                      CL*14
00215      IF NOT PS-END-OF-DB2-CHKPT-MAINT-LOG                            CL*14
00216          PERFORM 8200-WRITE-HEADING-LINES                         DPKRP012
00217      END-IF.                                                      DPKRP012
00218 *1000-EXIT.                                                       DPKRP012
00219                                                                   DPKRP012
00220 ******************************************************************DPKRP012
00221 * 2000-PROC-DB2-CHKPT-LOG-FILE - FILLS THE DETAIL LINE WITH THE      CL*20
00222 * VSAM INFORMATION AND PERFORM THE NEXT READ.                        CL*20
00223 ******************************************************************DPKRP012
00224                                                                      CL**3
00225  2000-PROC-DB2-CHKPT-LOG-FILE.                                       CL**5
00226      MOVE SPACES TO DL-DETAIL-LINE.                                  CL**4
00227      MOVE DP500I-JOB-NAME           TO DL-JOB-NAME.                  CL**3
00228      MOVE DP500I-PLAN-NAME          TO DL-PLAN-NAME.                 CL**3
00229      MOVE DP500I-MAINT-YY           TO DL-TRANSACTION-YEAR.          CL**3
00230      MOVE PC-SLASH                  TO DL-SLASH-ONE.              DPKRP012
00231      MOVE DP500I-MAINT-MM           TO DL-TRANSACTION-MONTH.         CL**3
00232      MOVE PC-SLASH                  TO DL-SLASH-TWO.              DPKRP012
00233      MOVE DP500I-MAINT-DD           TO DL-TRANSACTION-DAY.           CL**3
00234      MOVE DP500I-MAINT-HRS          TO DL-TRANSACTION-HOUR.          CL**3
00235      MOVE PC-COLON                  TO DL-COLON1.                 DPKRP012
00236      MOVE DP500I-MAINT-MIN          TO DL-TRANSACTION-MINUTE.        CL**3
00237      MOVE PC-COLON                  TO DL-COLON2.                 DPKRP012
00238      MOVE DP500I-MAINT-SEC          TO DL-TRANSACTION-SECOND.        CL**3
00239      MOVE DP500I-USER-ID            TO DL-USER-ID.                   CL**3
00240      MOVE DP500I-BEFORE-STATUS      TO DL-BEFORE-CHKPT-STATUS.       CL**3
00241      MOVE DP500I-BEFORE-CONTROL     TO DL-BEFORE-CHKPT-CONTROL.      CL**3
00242      MOVE DP500I-BEFORE-TYPE        TO DL-BEFORE-CHKPT-TYPE.         CL**3
00243      MOVE DP500I-BEFORE-FREQ        TO DL-BEFORE-FREQUENCY.          CL**3
00244      MOVE DP500I-AFTER-STATUS       TO DL-AFTER-CHKPT-STATUS.        CL**3
00245      MOVE DP500I-AFTER-CONTROL      TO DL-AFTER-CHKPT-CONTROL.       CL**3
00246      MOVE DP500I-AFTER-TYPE         TO DL-AFTER-CHKPT-TYPE.          CL**3
00247      MOVE DP500I-AFTER-FREQ         TO DL-AFTER-FREQUENCY.           CL**3
00248                                                                      CL**3
00249      PERFORM 8000-WRITE-DETAIL-LINE.                                 CL**4
00250      PERFORM 7000-READ-DB2-CHKPT-LOG-FILE.                           CL**5
00251                                                                   DPKRP012
00252      IF PS-END-OF-DB2-CHKPT-MAINT-LOG                                CL**5
00253          PERFORM 8100-WRITE-END-OF-REPORT-LINE                    DPKRP012
00254      END-IF.                                                      DPKRP012
00255 *2000-EXIT.                                                       DPKRP012
00256                                                                   DPKRP012
00257 ******************************************************************DPKRP012
00258 * 7000-READ-DB2-CHKPT-LOG-FILE - READS THE DB2 CHECKPOINT RESTART    CL*20
00259 * MAINTENANCE LOG FILE.                                           DPKRP012
00260 ******************************************************************DPKRP012
00261  7000-READ-DB2-CHKPT-LOG-FILE.                                       CL**4
00262      READ DB2-CHKPT-MAINT-LOG-FILE                                   CL**4
00263          INTO DP500I-DB2-CHKPT-MAINT-RECORD                          CL**4
00264              AT END                                               DPKRP012
00265                  SET PS-END-OF-DB2-CHKPT-MAINT-LOG TO TRUE           CL**4
00266      END-READ.                                                    DPKRP012
00267 *7000-EXIT.                                                       DPKRP012
00268                                                                   DPKRP012
00269 ******************************************************************DPKRP012
00270 * 8000-WRITE-DETAIL-LINE - WRITES A DETAIL LINE AND COMPUTES THE  DPKRP012
00271 * LINE COUNT.  DETERMINES IF A PAGE BREAK IS NECESSARY.           DPKRP012
00272 ******************************************************************DPKRP012
00273  8000-WRITE-DETAIL-LINE.                                          DPKRP012
00274      IF (PV-LINE-COUNT + PV-ADVANCE-COUNT) >                      DPKRP012
00275              PC-PAGE-SIZE                                         DPKRP012
00276          INITIALIZE PV-LINE-COUNT                                 DPKRP012
00277          PERFORM 8200-WRITE-HEADING-LINES                         DPKRP012
00278      END-IF.                                                      DPKRP012
00279                                                                   DPKRP012
00280      WRITE DB2-CHKPT-MAINT-LOG-LINE                                  CL**4
00281          FROM DL-DETAIL-LINE                                      DPKRP012
00282              AFTER ADVANCING PV-ADVANCE-COUNT LINES.              DPKRP012
00283                                                                   DPKRP012
00284      COMPUTE PV-LINE-COUNT =                                      DPKRP012
00285              PV-LINE-COUNT + PV-ADVANCE-COUNT.                    DPKRP012
00286                                                                   DPKRP012
00287 *8000-EXIT.                                                       DPKRP012
00288                                                                   DPKRP012
00289 ******************************************************************DPKRP012
00290 * 8100-WRITE-END-OF-REPORT-LINE - WRITE THE KOHLS STANDARD END OF DPKRP012
00291 * REPORT LINE, VERIFYING IN WILL STILL FIT ON THE PAGE.           DPKRP012
00292 ******************************************************************DPKRP012
00293  8100-WRITE-END-OF-REPORT-LINE.                                   DPKRP012
00294      IF (PV-LINE-COUNT + PV-ADVANCE-COUNT) >                      DPKRP012
00295              PC-PAGE-SIZE                                         DPKRP012
00296          INITIALIZE PV-LINE-COUNT                                 DPKRP012
00297          PERFORM 8200-WRITE-HEADING-LINES                         DPKRP012
00298      END-IF.                                                      DPKRP012
00299                                                                   DPKRP012
00300      WRITE DB2-CHKPT-MAINT-LOG-LINE                                  CL**4
00301          FROM END-OF-REPORT-LINE                                  DPKRP012
00302              AFTER ADVANCING PV-ADVANCE-COUNT LINES.              DPKRP012
00303 *8100-EXIT.                                                       DPKRP012
00304                                                                   DPKRP012
00305 ******************************************************************DPKRP012
00306 * 8200-WRITE-HEADING-LINES - WRITES ALL HEADING LINES ASSOCIATED  DPKRP012
00307 * WITH THE REPORT.                                                DPKRP012
00308 ******************************************************************DPKRP012
00309  8200-WRITE-HEADING-LINES.                                        DPKRP012
00310      MOVE 1             TO DP132O-REPORT-NUMBER.                     CL**6
00311      ADD 1              TO PV-PAGE-COUNT.                         DPKRP012
00312      MOVE PV-PAGE-COUNT TO DP132O-PAGE-NUMBER.                       CL**6
00313      WRITE DB2-CHKPT-MAINT-LOG-LINE                                  CL**4
00314          FROM DP132O-STANDRD-HEADER-132-COLS                         CL**6
00315              AFTER ADVANCING PAGE.                                DPKRP012
00316                                                                   DPKRP012
00317      WRITE DB2-CHKPT-MAINT-LOG-LINE                                  CL**4
00318          FROM HL1-HEADING-LINE-1                                  DPKRP012
00319              AFTER ADVANCING 1 LINES.                             DPKRP012
00320                                                                   DPKRP012
00321      WRITE DB2-CHKPT-MAINT-LOG-LINE                                  CL**4
00322          FROM HL2-HEADING-LINE-2                                  DPKRP012
00323              AFTER ADVANCING 2 LINES.                             DPKRP012
00324                                                                   DPKRP012
00325      WRITE DB2-CHKPT-MAINT-LOG-LINE                                  CL**4
00326          FROM HL3-HEADING-LINE-3                                  DPKRP012
00327              AFTER ADVANCING 1 LINES.                             DPKRP012
00328                                                                   DPKRP012
00329      MOVE 2 TO PV-ADVANCE-COUNT.                                  DPKRP012
00330      MOVE 5 TO PV-LINE-COUNT.                                     DPKRP012
00331 *8200-EXIT.                                                       DPKRP012
00332                                                                   DPKRP012
00333 ******************************************************************DPKRP012
00334 * 9000-EOJ-ROUTINE - CLOSES ALL FILES ASSOCIATED WITH THE JOB.    DPKRP012
00335 ******************************************************************DPKRP012
00336  9000-EOJ-ROUTINE.                                                DPKRP012
00337      CLOSE DB2-CHKPT-MAINT-LOG-FILE                                  CL**4
00338            DB2-CHKPT-MAINT-LOG-REPORT.                               CL**4
00339 *9000-EXIT.                                                       DPKRP012
