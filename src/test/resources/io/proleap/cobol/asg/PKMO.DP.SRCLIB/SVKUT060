       IDENTIFICATION DIVISION.                                         00010097
       PROGRAM-ID.         SVKUT060.                                    00020099
       AUTHOR.             DANIEL OKYERE.                               00030099
       INSTALLATION.       KOHLS DEPARTMENT STORES.                     00040097
       DATE-WRITTEN.       SEPTEMBER 2004.                              00050099
       DATE-COMPILED.                                                   00060097
                                                                        00070097
      ******************************************************************00080097
      * THIS PROGRAM EXTRACTS THE DAILY STORED VALUE SAFEWAY ACTIVATION*00090099
      * ACTIVITY FROM TABLE SVT_KOHLS_CRD_TXN.                         *00091099
      *                                                                *00110097
      * OUTPUT FILES:  KMC-DAILY-ACTIVITY        DDNAME = OUT01        *00150097
      *                                                                *00160097
      ******************************************************************00170099
                                                                        00180097
      ******************************************************************00190097
       ENVIRONMENT DIVISION.                                            00200097
      ******************************************************************00210097
                                                                        00220097
       CONFIGURATION SECTION.                                           00230097
                                                                        00240097
       SOURCE-COMPUTER.    IBM-3090.                                    00250097
       OBJECT-COMPUTER.    IBM-3090.                                    00260097
                                                                        00270097
       INPUT-OUTPUT SECTION.                                            00280097
                                                                        00290097
       FILE-CONTROL.                                                    00300097
                                                                        00310097
           SELECT KMC-DAILY-ACTIVITY   ASSIGN TO OUT01.                 00320097
                                                                        00340097
      ******************************************************************00350097
       DATA DIVISION.                                                   00360097
      ******************************************************************00370097
                                                                        00380097
       FILE SECTION.                                                    00390097
                                                                        00400097
       FD  KMC-DAILY-ACTIVITY                                           00410097
           RECORDING MODE IS F                                          00420097
           BLOCK CONTAINS 0 RECORDS                                     00430097
           LABEL RECORDS ARE OMITTED.                                   00440097
                                                                        00450097
       01  KMC-DAILY-RECORD            PIC  X(110).                     00460097
                                                                        00470097
       WORKING-STORAGE SECTION.                                         00550097
                                                                        00560097
       01  ABEND-CODE                  PIC S9(04)     VALUE +0  COMP.   00570097
                                                                        00580097
      ******************************************************************00720097
      *    PC-PROGRAM CONSTANTS                                        *00730097
      ******************************************************************00740097
       01  PC-PROGRAM-CONSTANTS.                                        00750097
           05  PC-PROGRAM-NAME         PIC X(08)      VALUE 'SVKUT060'. 00760099
                                                                        00790097
      ******************************************************************00800097
      *    PV-PROGRAM VARIABLES                                        *00810097
      ******************************************************************00820097
       01  PV-PROGRAM-VARIABLES.                                        00830097
           05  PV-PARAGRAPH-NAME            PIC  X(30)     VALUE SPACES.00840097
           05  PV-DB2-OPERATION-ATTEMPTED   PIC  X(30)     VALUE SPACES.00850097
           05  PV-CARD-BALANCE              PIC S9(07)V9(02)            00870097
                                                           VALUE ZEROES 00880097
                                                           COMP-3.      00890097
                                                                        00900099
      ******************************************************************01350097
      *    PS-PROGRAM SWITCHES                                         *01360097
      ******************************************************************01370097
       01  PS-PROGRAM-SWITCHES.                                         01380097
           05  PS-END-OF-SVT00002-CSR-SW    PIC  X(01)     VALUE 'N'.   01390097
               88  PS-END-OF-SVT00002-CURSOR               VALUE 'Y'.   01400097
                                                                        01430097
      ******************************************************************01440097
      *    PA-PROGRAM ACCUMULATORS                                     *01450097
      ******************************************************************01460097
       01  PA-PROGRAM-ACCUMULATORS.                                     01470097
           05  PA-ACTIVITY-ROWS-FETCHED     PIC S9(11)     COMP-3       01480097
                                                           VALUE ZEROES.01490097
           05  PA-ACTIVITY-ROWS-WRITTEN     PIC S9(11)     COMP-3       01500097
                                                           VALUE ZEROES.01510097
                                                                        01520097
      ******************************************************************01530097
      *    KMC DAILY EXTRACT RECORD LAYOUT                             *01540097
      ******************************************************************01550097
           COPY SVRD022.                                                01560097
                                                                        01570097
      ******************************************************************01580097
      *    REFUND AUTHORIZATION CONSTANTS                              *01590097
      ******************************************************************01600097
           COPY SVWS004.                                                01610097
                                                                        01620097
      ******************************************************************01630097
      *    DATE ROUTINE COPY MEMBERS                                   *01640097
      ******************************************************************01650097
           COPY DPWS500.                                                01660097
                                                                        01670097
      ******************************************************************01680097
      *    VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004     *01690097
      ******************************************************************01700097
           COPY DPWS004.                                                01710097
                                                                        01720097
      ******************************************************************01730097
      *    SV-KOHL'S STORED VALUE CARD ACTIVITY TABLE                  *01740097
      ******************************************************************01750097
           EXEC SQL                                                     01760097
               INCLUDE SVT00002                                         01770097
           END-EXEC.                                                    01780097
                                                                        01790097
      ******************************************************************01800097
      *    SV-KOHL'S STORED VALUE CARD TABLE                           *01810097
      ******************************************************************01820097
           EXEC SQL                                                     01830097
               INCLUDE SVT00001                                         01840097
           END-EXEC.                                                    01850097
                                                                        01860097
                                                                        01930099
      ******************************************************************01940097
      *    STANDARD LAYOUT FOR THE SQL COMMUNCATIONS AREA              *01950097
      ******************************************************************01960097
           EXEC SQL                                                     01970097
               INCLUDE SQLCA                                            01980097
           END-EXEC.                                                    01990097
                                                                        02000097
           COPY SQLCA2.                                                 02010097
                                                                        02020097
      ******************************************************************02030097
      *    CURSOR DECLARATION FOR KMC ACTIVITY CURSOR                  *02040097
      ******************************************************************02050097
           EXEC SQL                                                     02060097
               DECLARE  KMC_ACTVY_CURSOR CURSOR FOR                     02070097
                SELECT  A.CRD_NBR                                       02080097
                       ,TRN_AUTH_TMST                                   02090097
                       ,TRN_RVRSL_CDE                                   02110097
                       ,TRN_VOID_IND                                    02120097
                       ,ACTVY_TYP_CDE                                   02130097
                       ,POS_DTE                                         02140097
                       ,A.STR_NBR                                       02150097
                       ,POS_TRMNL_NBR                                   02160097
                       ,POS_TRN_NBR                                     02170097
                       ,APP_AUTH_AMT                                    02180097
                       ,POS_OPRT_ID                                     02190097
                       ,APVL_USR_ID                                     02200097
                       ,ACTVY_STAT_CDE                                  02210097
                       ,ORIG_AUTH_AMT                                   02220097
                       ,CRD_TYP_CDE                                     02230099
                       ,A.ST_CDE                                        02240099
                  FROM  SVT_KOHLS_CRD_TXN A                             02250099
                       ,SVT_KOHLS_CRD_ACCT T                            02260099
                 WHERE  A.CRD_NBR = T.CRD_NBR                           02280099
                    AND A.STR_NBR = 918                                 02290099
                    AND APVL_USR_ID = 'SAFEWAY'                         02291099
                    AND ACTVY_TYP_CDE = 2                               02300099
                    AND ACTVY_STAT_CDE = 1                              02301099
                 WITH UR                                                02340097
           END-EXEC.                                                    02350097
                                                                        02360097
       PROCEDURE DIVISION.                                              02370097
                                                                        02380097
      ******************************************************************02390097
       A100-MAINLINE.                                                   02400097
      ******************************************************************02410097
                                                                        02420097
           PERFORM B100-INITIALIZE.                                     02430097
                                                                        02440097
           PERFORM B200-PROCESS-CARD-ACTIVITY                           02450097
               UNTIL PS-END-OF-SVT00002-CURSOR.                         02460097
                                                                        02470097
           PERFORM B300-EOJ-ROUTINE.                                    02570097
                                                                        02580097
           GOBACK.                                                      02590097
      ******************************************************************02600097
      *     B100-INITIALIZE                                            *02610097
      * ==> PROCESSES:                                                 *02620097
      *     -READS THE DAILY SALES FILE TO GET THE DATE                *02630097
      *     -CALLS THE DATE ROUTINE TO CALCULATE THE CENTURY FOR THE   *02640097
      *      CURRENT DATE AND ALSO TO CALCULATE THE NEXT DAY           *02650097
      *     -OPENS THE CURSOR FOR THE KMC ACTIVITY TABLE AND FETCHES   *02660097
      *      THE FIRST ROW                                             *02670097
      * ==> PERFORMED FROM: A100-MAINLINE                              *02680097
      ******************************************************************02690097
       B100-INITIALIZE.                                                 02700097
                                                                        02710097
           OPEN OUTPUT KMC-DAILY-ACTIVITY.                              02730097
           PERFORM C200-OPEN-SVT00002-CURSOR.                           02930097
           PERFORM D100-FETCH-SVT00002-CURSOR.                          02940099
                                                                        02950097
      ******************************************************************02960097
      *     B200-PROCESS-CARD-ACTIVITY                                 *02970097
      * ==> PROCESSES:                                                 *02980097
      *     -FETCHES THE NEXT ROW FROM THE CURSOR                      *02990097
      * ==> PERFORMED FROM: A100-MAINLINE                              *03000097
      ******************************************************************03010097
       B200-PROCESS-CARD-ACTIVITY.                                      03020097
                                                                        03030097
           PERFORM E200-WRITE-SVT00002-ACTIVITY.                        03040099
           PERFORM D100-FETCH-SVT00002-CURSOR.                          03041099
                                                                        03050097
      ******************************************************************03160097
      *     B300-EOJ-ROUTINE                                           *03170097
      * ==> PROCESSES:                                                 *03180097
      *     -CLOSES THE KMC ACTIVITY CURSOR                            *03190097
      * ==> PERFORMED FROM: A100-MAINLINE                              *03200097
      ******************************************************************03210097
       B300-EOJ-ROUTINE.                                                03220097
                                                                        03230097
           PERFORM C300-CLOSE-SVT00002-CURSOR                           03231099
                                                                        03232099
           CLOSE KMC-DAILY-ACTIVITY.                                    03240099
                                                                        03260097
           DISPLAY ' '.                                                 03270097
           DISPLAY PC-PROGRAM-NAME                                      03280097
                   '  ROWS FETCHED FROM SVT_KOHLS_CRD_TXN  '            03290097
                                              PA-ACTIVITY-ROWS-FETCHED. 03300097
           DISPLAY PC-PROGRAM-NAME '  ROWS WRITTEN TO OUTPUT FILE:  '   03310097
                                              PA-ACTIVITY-ROWS-WRITTEN. 03320097
           DISPLAY ' '.                                                 03330097
                                                                        03340097
      ******************************************************************04180097
      *     C200-OPEN-SVT00002-CURSOR.                                 *04190097
      * ==> PROCESSES:                                                 *04200097
      *     -OPENS THE KMC ACTIVITY CURSOR                             *04210097
      * ==> PERFORMED FROM: B100-INITIALIZE                            *04220097
      ******************************************************************04230097
       C200-OPEN-SVT00002-CURSOR.                                       04240097
                                                                        04250097
           EXEC SQL                                                     04260097
               OPEN KMC_ACTVY_CURSOR                                    04270097
           END-EXEC.                                                    04280097
                                                                        04290097
           EVALUATE TRUE                                                04300097
               WHEN SQLCODE = +0                                        04310097
                   CONTINUE                                             04320097
               WHEN OTHER                                               04330097
                   MOVE 'C200-OPEN-SVT00002-CURSOR       '              04340097
                                     TO PV-PARAGRAPH-NAME               04350097
                   MOVE 'OPEN KMC ACTIVITY CURSOR      '                04360097
                                     TO PV-DB2-OPERATION-ATTEMPTED      04370097
                   PERFORM Z900-SQL-ABEND                               04380097
           END-EVALUATE.                                                04390097
                                                                        04400097
      ******************************************************************04640097
      *     C300-CLOSE-SVT00002-CURSOR.                                *04650097
      * ==> PROCESSES:                                                 *04660097
      *     -CLOSES THE KMC ACTIVITY CURSOR                            *04670097
      * ==> PERFORMED FROM: B300-EOJ-ROUTINE                           *04680097
      ******************************************************************04690097
       C300-CLOSE-SVT00002-CURSOR.                                      04700097
                                                                        04710097
           EXEC SQL                                                     04720097
               CLOSE KMC_ACTVY_CURSOR                                   04730097
           END-EXEC.                                                    04740097
                                                                        04750097
           EVALUATE TRUE                                                04760097
               WHEN SQLCODE = +0                                        04770097
                   CONTINUE                                             04780097
               WHEN OTHER                                               04790097
                   MOVE 'C300-CLOSE-SVT00002-CURSOR      '              04800097
                                     TO PV-PARAGRAPH-NAME               04810097
                   MOVE 'CLOSE KMC ACTIVITY CURSOR     '                04820097
                                     TO PV-DB2-OPERATION-ATTEMPTED      04830097
                   PERFORM Z900-SQL-ABEND                               04840097
           END-EVALUATE.                                                04850097
                                                                        04860097
      ******************************************************************05100097
      *     D100-FETCH-SVT00002-CURSOR.                                *05110097
      * ==> PROCESSES:                                                 *05120097
      *     -FETCHES A ROW FROM THE KMC ACTIVITY CURSOR                *05130097
      * ==> PERFORMED FROM: B100-INITIALIZE                            *05140097
      *                     B200-PROCESS-CARD-ACTIVITY                 *05150097
      ******************************************************************05160097
       D100-FETCH-SVT00002-CURSOR.                                      05170097
                                                                        05180097
           EXEC SQL                                                     05190097
               FETCH  KMC_ACTVY_CURSOR                                  05200097
                INTO  :SVT00002-CRD-NBR                                 05210097
                     ,:SVT00002-TRN-AUTH-TMST                           05220097
                     ,:SVT00002-TRN-RVRSL-CDE                           05240097
                     ,:SVT00002-TRN-VOID-IND                            05250097
                     ,:SVT00002-ACTVY-TYP-CDE                           05260097
                     ,:SVT00002-POS-DTE                                 05270097
                     ,:SVT00002-STR-NBR                                 05280097
                     ,:SVT00002-POS-TRMNL-NBR                           05290097
                     ,:SVT00002-POS-TRN-NBR                             05300097
                     ,:SVT00002-APP-AUTH-AMT                            05310097
                     ,:SVT00002-POS-OPRT-ID                             05320097
                     ,:SVT00002-APVL-USR-ID                             05330097
                     ,:SVT00002-ACTVY-STAT-CDE                          05340097
                     ,:SVT00002-ORIG-AUTH-AMT                           05350097
                     ,:SVT00001-CRD-TYP-CDE                             05360099
                     ,:SVT00002-ST-CDE                                  05370097
           END-EXEC.                                                    05380097
                                                                        05390097
           EVALUATE TRUE                                                05400097
               WHEN SQLCODE = +0                                        05410097
                   ADD +1 TO PA-ACTIVITY-ROWS-FETCHED                   05420097
               WHEN SQLCODE = +100                                      05450097
                   SET PS-END-OF-SVT00002-CURSOR                        05460097
                                     TO TRUE                            05470097
               WHEN OTHER                                               05480097
                   MOVE 'D100-FETCH-SVT00002-CURSOR      '              05490097
                                     TO PV-PARAGRAPH-NAME               05500097
                   MOVE 'FETCH KMC ACTIVITY CURSOR     '                05510097
                                     TO PV-DB2-OPERATION-ATTEMPTED      05520097
                   PERFORM Z900-SQL-ABEND                               05530097
           END-EVALUATE.                                                05540097
                                                                        05550097
      ******************************************************************06310097
      *     E200-WRITE-SVT00002-ACTIVITY                               *06320097
      * ==> PROCESSES:                                                 *06330097
      *     -WRITES THE OUTPUT RECORD                                  *06340097
      *     -THE EXTRACT FILE WILL NOT PASS THE VOID INDICATOR FOR     *06350097
      *      THOSE TRANSACTIONS WHICH ARE ORIGINAL TRANSACTIONS WHICH  *06360097
      *      WERE VOIDED.  THIS IS BECAUSE THE POS FILE WILL NOT PASS  *06370097
      *      THE VOID INDICATOR ON THE ORIGINAL TRANSACTION, JUST ON   *06380097
      *      THE VOID.                                                 *06390097
      * ==> PERFORMED FROM:  D100-FETCH-SVT00002-CURSOR                *06400097
      ******************************************************************06410097
       E200-WRITE-SVT00002-ACTIVITY.                                    06420097
                                                                        06430097
           INITIALIZE SV022-KMCACT-EXTRACT-RECORD.                      06440097
                                                                        06450097
           MOVE SVT00001-CRD-TYP-CDE  TO SV022-CRD-TYP-CDE.             06460099
           MOVE SVT00002-CRD-NBR      TO SV022-CRD-NBR.                 06470097
           IF (SVT00002-TRN-RVRSL-CDE = SV004-REV-CDE-NORMAL            06490097
           AND SVT00002-TRN-VOID-IND = SV004-VOID-TRANS)                06500097
               MOVE 'N'               TO SV022-TRN-VOID-IND             06510097
           ELSE                                                         06520097
               MOVE SVT00002-TRN-VOID-IND                               06530097
                                      TO SV022-TRN-VOID-IND             06540097
           END-IF.                                                      06550097
           MOVE SVT00002-TRN-RVRSL-CDE  TO SV022-TRN-RVRSL-CDE.         06560099
           MOVE SVT00002-ACTVY-TYP-CDE  TO SV022-ACTVY-TYP-CDE.         06570099
           MOVE SVT00002-TRN-AUTH-TMST  TO SV022-TRN-AUTH-TMST.         06580099
           MOVE SVT00002-POS-DTE        TO SV022-POS-DTE.               06590099
           MOVE SVT00002-STR-NBR        TO SV022-STR-NBR.               06600099
           MOVE SVT00002-POS-TRMNL-NBR  TO SV022-TRMNL-NBR.             06610099
           MOVE SVT00002-POS-TRN-NBR    TO SV022-TRN-NBR.               06620099
           MOVE SVT00002-APP-AUTH-AMT   TO SV022-APP-AUTH-AMT.          06630099
           MOVE SVT00002-ACTVY-STAT-CDE TO SV022-ACTVY-STAT-CDE.        06640097
           MOVE SVT00002-ORIG-AUTH-AMT  TO SV022-ORIG-AUTH-AMT.         06650099
           MOVE SVT00002-POS-OPRT-ID    TO SV022-POS-OPER-ID.           06660099
           MOVE SVT00002-APVL-USR-ID    TO SV022-APPR-USER-ID.          06670099
           MOVE SVT00002-ST-CDE         TO SV022-ST-CDE                 06680099
                                                                        06750097
           WRITE KMC-DAILY-RECORD                                       06760097
               FROM SV022-KMCACT-EXTRACT-RECORD.                        06770097
                                                                        06780097
           ADD +1 TO PA-ACTIVITY-ROWS-WRITTEN.                          06790097
                                                                        06800097
      ******************************************************************06810097
      *     Z900-SQL-ABEND                                             *06820097
      * ==> PROCESSES:                                                 *06830097
      *     -PERFORMS ABEND PROCESSING FOR SQL ERRORS.                 *06840097
      * ==> PERFORMED FROM:                                            *06850097
      ******************************************************************06860097
       Z900-SQL-ABEND.                                                  06870097
                                                                        06880097
           DISPLAY 'Z900-SQL-ABEND'.                                    06890097
           DISPLAY '**  ABEND     **'.                                  06900097
           DISPLAY '**  PROGRAM   ** = ' PC-PROGRAM-NAME.               06910097
           DISPLAY '**  PARAGRAPH ** = ' PV-PARAGRAPH-NAME.             06920097
           DISPLAY '**  OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.    06930097
                                                                        06940097
      ******************************************************************06950097
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *06960097
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *06970097
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *06980097
      * FORMAT.                                                        *06990097
      ******************************************************************07000097
           COPY DPPD004.                                                07010097
                                                                        07020097
           MOVE +4013 TO ABEND-CODE.                                    07030097
           CALL 'ILBOABN0' USING ABEND-CODE.                            07040097
