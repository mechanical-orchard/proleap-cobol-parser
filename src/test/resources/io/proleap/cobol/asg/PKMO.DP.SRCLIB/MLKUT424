       IDENTIFICATION DIVISION.                                         00030001
       PROGRAM-ID.    MLKUT424.                                         00040001
       AUTHOR.        DAVE METZGER.                                     00050001
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00060001
       DATE-WRITTEN.  12-13-99.                                         00070001
       DATE-COMPILED.                                                   00080001
      ******************************************************************00090001
      *                                                                 00100001
      * MLKUT424 - DESCRIPTION.............                             00110001
      *                                                                 00120001
      * THIS PROGRAM WILL CREATE THE DATA INTEGRITY EXTRACT FILE FOR    00130001
      * TMNDPTS.                                                        00140001
      *                                                                 00150001
      *CHANGE HISTORY:                                                  00151009
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00152009
      * ------- ---------- ----------- -------------------------------- 00153009
W26603* W26603  03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION     00154009
      ******************************************************************00160001
      *                                                                 00170001
       ENVIRONMENT DIVISION.                                            00180001
      *                                                                 00190001
       CONFIGURATION SECTION.                                           00200001
      *                                                                 00210001
       SOURCE-COMPUTER.        IBM-3090.                                00220001
       OBJECT-COMPUTER.        IBM-3090.                                00230001
      *                                                                 00240001
       INPUT-OUTPUT SECTION.                                            00250001
       FILE-CONTROL.                                                    00260001
                                                                        00270001
           SELECT DATA-INTEGRITY-CTL-FILE                               00280001
               ASSIGN TO INP01.                                         00290001
                                                                        00300001
           SELECT DATA-INTEGRITY-EXTR-FILE                              00310001
               ASSIGN TO OUT01.                                         00320001
                                                                        00330001
      ******************************************************************00340001
       DATA DIVISION.                                                   00350001
      ******************************************************************00360001
                                                                        00370001
       FILE SECTION.                                                    00380001
                                                                        00390001
       FD  DATA-INTEGRITY-CTL-FILE                                      00400001
           LABEL RECORDS ARE STANDARD                                   00410001
           RECORDING MODE IS F                                          00420001
           BLOCK CONTAINS 0 RECORDS.                                    00430001
       01  DATA-INTEGRITY-CTL-REC      PIC X(80).                       00440001
                                                                        00450001
       FD  DATA-INTEGRITY-EXTR-FILE                                     00460001
           RECORDING MODE IS F                                          00470001
           LABEL RECORDS ARE STANDARD                                   00480001
           BLOCK CONTAINS 0 RECORDS.                                    00490001
                                                                        00500001
W26603 01  DATA-INTEGRITY-EXTR-REC     PIC X(120).                      00510010
                                                                        00520001
      *                                                                 00530001
      ******************************************************************00540001
       WORKING-STORAGE SECTION.                                         00550001
      ******************************************************************00560001
                                                                        00570001
       01  PS-PROGRAM-SWITCHES.                                         00580001
           05  PS-TMNDPTS-DONE         PIC X       VALUE 'N'.           00590001
               88  PS-TMNDPTS-IS-DONE              VALUE 'Y'.           00600001
               88  PS-TMNDPTS-IS-NOT-DONE          VALUE 'N'.           00610001
                                                                        00620001
       01  PA-PROGRAM-ACCUMULATORS                 COMP-3.              00630001
           05  PA-TMNDPTS-ROWS-FETCHED PIC S9(13)  VALUE ZERO.          00640001
           05  PA-OUTPUT-RECS-WRITTEN  PIC S9(13)  VALUE ZERO.          00650001
                                                                        00660001
       01  PC-PROGRAM-CONSTANTS.                                        00670004
           05  PC-MAJ-CL-NBR-ZEROS     PIC  X(02)  VALUE '00'.          00680004
           05  PC-SUB-CL-NBR-ZEROS     PIC  X(02)  VALUE '00'.          00690004
                                                                        00770001
       01  CD-COUNTER-DISPLAY.                                          00780001
           05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.           00790001
                                                                        00800001
       01 ABEND-AREAS.                                                  00810001
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO           00820001
                                                   COMP SYNC.           00830001
               88  AC-BAD-DATA                     VALUE +4003.         00840001
               88  AC-DB2-ERROR                    VALUE +4013.         00850001
           05  AA-ABEND-LIT            PIC X(40)   VALUE                00860001
                 '*****       ABEND'.                                   00870001
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                00880001
                   '*****   PROGRAM: MLKUT424'.                         00890001
           05  AA-PARAGRAPH-LIT.                                        00900001
               10  FILLER              PIC X(17)   VALUE                00910001
                   '***** PARAGRAPH: '.                                 00920001
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.        00930001
           05  AA-MESSAGE-LINE.                                         00940001
               10  FILLER              PIC X(06)   VALUE '*****'.       00950001
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.        00960001
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                00970001
                   '*****    DB2 ERROR'.                                00980001
           05  AA-DB2-OPERATION-LIT.                                    00990001
               10  FILLER              PIC X(17)   VALUE                01000001
                   '***** OPERATION: '.                                 01010001
               10  AA-DB2-OPERATION.                                    01020001
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.        01030001
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.        01040001
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.        01050001
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.        01060001
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.        01070001
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.        01080001
                                                                        01090001
       EJECT                                                            01100001
                                                                        01110001
      ******************************************************************01111001
      *** DATA INTEGRITY CONTROL CARD                                   01120001
      ******************************************************************01121001
           COPY MLRD110.                                                01130001
                                                                        01140001
                                                                        01150001
      ******************************************************************01151001
      *** DATA INTEGRITY TMNDPTS EXTRACT FILE                           01160001
      ******************************************************************01161001
W26603     COPY MLRD111.                                                01170009
                                                                        01180001
       EJECT                                                            01190001
                                                                        01200001
      ******************************************************************01210001
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01220001
      ******************************************************************01230001
           COPY DPWS004.                                                01240001
                                                                        01250001
      ******************************************************************01260001
      *  DB2 ERRORS                                                     01270001
      ******************************************************************01280001
           COPY SQLCA2.                                                 01290001
                                                                        01300001
      ******************************************************************01310001
      *  DB2 COMMUNICATION AREA                                         01320001
      ******************************************************************01330001
           EXEC SQL                                                     01340001
                INCLUDE SQLCA                                           01350001
           END-EXEC.                                                    01360001
                                                                        01370001
      ******************************************************************01380001
      *  DB2 TMNDPTS TABLE LAYOUT                                       01390001
      ******************************************************************01400001
           EXEC SQL                                                     01410001
                INCLUDE TMNDPTS                                         01420008
           END-EXEC.                                                    01430001
                                                                        01440001
      ******************************************************************01450001
      *  DB2 TMNDPTS CURSOR                                             01460001
      ******************************************************************01470001
           EXEC SQL                                                     01480001
               DECLARE TMNDPTS_CURSOR CURSOR FOR                        01490001
                SELECT FSCL_MN_END_DTE,                                 01500001
                       FSCL_MN_NBR,                                     01510001
                       DEPT_NBR,                                        01520001
                       STR_NBR,                                         01540001
                       SLS_RTL_AMT,                                     01550001
                       MKDN_RTL_AMT,                                    01560001
                       MKUP_RTL_AMT,                                    01570001
                       XFER_IN_RTL_AMT,                                 01580001
                       XFER_OUT_RTL_AMT,                                01590001
                       RCPT_RTL_AMT,                                    01600001
                       RCPT_NET_COST_AMT,                               01610001
                       PADJ_RTL_AMT,                                    01620001
                       PADJ_NET_COST_AMT,                               01630001
                       INV_ADJ_RTL_AMT,                                 01640001
                       SHNK_RTL_AMT,                                    01650005
                       END_INV_RTL_AMT                                  01660005
                  FROM TMNDPTS                                          01670001
                 WHERE (FSCL_MN_END_DTE >= :ML110-STRT-DTE)             01680001
                   AND (FSCL_MN_END_DTE <= :ML110-END-DTE)              01690001
              ORDER BY FSCL_MN_END_DTE,                                 01700001
                       DEPT_NBR,                                        01710001
                       STR_NBR                                          01730001
W26603        WITH UR                                                   01731009
           END-EXEC.                                                    01740001
                                                                        01750001
       EJECT                                                            01760001
                                                                        01770001
      ******************************************************************01780001
       PROCEDURE DIVISION.                                              01790001
      ******************************************************************01800001
                                                                        01810001
      ******************************************************************01820001
       0000-MAINLINE-MLKUT424.                                          01830001
      ******************************************************************01840001
      *                                                                 01850001
           PERFORM 1000-INITIALIZATION.                                 01860001
                                                                        01870001
           PERFORM 2000-MAIN-PROCESS.                                   01880001
                                                                        01890001
           PERFORM 8000-EOJ.                                            01900001
                                                                        01910001
           STOP RUN.                                                    01920001
                                                                        01930001
       EJECT                                                            01940001
                                                                        01950001
      ******************************************************************01960001
       1000-INITIALIZATION.                                             01970001
      ******************************************************************01980001
      *                                                                 01990001
           OPEN INPUT  DATA-INTEGRITY-CTL-FILE                          02000001
                OUTPUT DATA-INTEGRITY-EXTR-FILE.                        02010001
                                                                        02020001
           PERFORM 4000-READ-CONTROL-CARD.                              02030001
                                                                        02040001
           DISPLAY ' '.                                                 02050001
           DISPLAY '*** MLKUT424 INPUT CONTROL CARD VALUES ***'         02060001
           DISPLAY ML110-DATA-INTEGRITY-CC-REC.                         02070001
                                                                        02080001
       EJECT                                                            02090001
                                                                        02100001
      ******************************************************************02110001
       2000-MAIN-PROCESS.                                               02120001
      ******************************************************************02130001
      *                                                                 02140001
           PERFORM 7100-OPEN-CURSOR-TMNDPTS.                            02150001
                                                                        02160001
           PERFORM 7200-FETCH-TMNDPTS.                                  02170001
                                                                        02180001
           PERFORM                                                      02190001
             UNTIL PS-TMNDPTS-IS-DONE                                   02200001
               PERFORM 5000-WRITE-EXTRACT                               02210001
               PERFORM 7200-FETCH-TMNDPTS                               02220001
           END-PERFORM.                                                 02230001
                                                                        02240001
           PERFORM 7300-CLOSE-CURSOR-TMNDPTS.                           02250001
                                                                        02260001
       EJECT                                                            02270001
                                                                        02280001
      ******************************************************************02290001
       4000-READ-CONTROL-CARD.                                          02300001
      ******************************************************************02310001
      *                                                                 02320001
           READ DATA-INTEGRITY-CTL-FILE                                 02330001
             INTO ML110-DATA-INTEGRITY-CC-REC                           02340001
               AT END                                                   02350001
                   MOVE '4000-READ-CONTROL-CARD'                        02360001
                                       TO AA-PARAGRAPH-NAME             02370001
                   MOVE 'CONTROL CARD IS MISSING'                       02380001
                                       TO AA-MESSAGE                    02390001
                   MOVE +4001          TO ABEND-CODE                    02400001
                   PERFORM 9900-COMMON-ABEND-ROUTINE                    02410001
           END-READ.                                                    02420001
                                                                        02430001
       EJECT                                                            02440001
                                                                        02450001
      ******************************************************************02460001
       5000-WRITE-EXTRACT.                                              02470001
      ******************************************************************02480001
      *                                                                 02490001
           MOVE MNDPTS-FSCL-MN-END-DTE  TO ML111-FSCL-MN-END-DTE.       02500001
           MOVE MNDPTS-FSCL-MN-NBR      TO ML111-FSCL-MN-NBR.           02510001
           MOVE MNDPTS-DEPT-NBR         TO ML111-DEPT-NBR.              02520001
           MOVE PC-MAJ-CL-NBR-ZEROS     TO ML111-MAJ-CL-NBR.            02530004
           MOVE PC-SUB-CL-NBR-ZEROS     TO ML111-SUB-CL-NBR.            02540004
           MOVE MNDPTS-STR-NBR          TO ML111-STR-NBR.               02580001
           MOVE MNDPTS-SLS-RTL-AMT      TO ML111-SLS-RTL-AMT.           02590001
           MOVE MNDPTS-MKDN-RTL-AMT     TO ML111-MKDN-RTL-AMT.          02600001
           MOVE MNDPTS-MKUP-RTL-AMT     TO ML111-MKUP-RTL-AMT.          02610001
           MOVE MNDPTS-XFER-IN-RTL-AMT  TO ML111-XFER-IN-RTL-AMT.       02620001
           MOVE MNDPTS-XFER-OUT-RTL-AMT TO ML111-XFER-OUT-RTL-AMT.      02630001
           MOVE MNDPTS-RCPT-RTL-AMT     TO ML111-RCPT-RTL-AMT.          02640001
           MOVE MNDPTS-RCPT-NET-COST-AMT                                02650001
                                        TO ML111-RCPT-NET-COST-AMT.     02660001
           MOVE MNDPTS-PADJ-RTL-AMT     TO ML111-PADJ-RTL-AMT.          02670001
           MOVE MNDPTS-PADJ-NET-COST-AMT                                02680001
                                        TO ML111-PADJ-NET-COST-AMT.     02690001
           MOVE MNDPTS-INV-ADJ-RTL-AMT  TO ML111-INV-ADJ-RTL-AMT.       02700001
           MOVE MNDPTS-SHNK-RTL-AMT     TO ML111-SHNK-RTL-AMT.          02710001
           MOVE MNDPTS-END-INV-RTL-AMT  TO ML111-END-INV-RTL-AMT.       02720001
                                                                        02730001
           WRITE DATA-INTEGRITY-EXTR-REC                                02740001
               FROM ML111-DATA-INTEGRITY-EXTR-REC.                      02750001
           ADD 1                        TO PA-OUTPUT-RECS-WRITTEN.      02760001
                                                                        02770001
       EJECT                                                            02780001
                                                                        02790001
      ******************************************************************02800001
       7100-OPEN-CURSOR-TMNDPTS.                                        02810001
      ******************************************************************02820001
      *                                                                 02830001
           EXEC SQL                                                     02840001
               OPEN TMNDPTS_CURSOR                                      02850001
           END-EXEC.                                                    02860001
                                                                        02870001
           EVALUATE TRUE                                                02880001
               WHEN SQLCODE = 0                                         02890001
                   CONTINUE                                             02900001
               WHEN OTHER                                               02910001
                   MOVE '7100-OPEN-CURSOR-TMNDPTS'                      02920001
                                       TO AA-PARAGRAPH-NAME             02930001
                   MOVE 'TMNDPTS TABLE ERROR'                           02940001
                                       TO AA-MESSAGE                    02950001
                   MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION              02960001
                   PERFORM 9901-SQL-ERROR                               02970001
           END-EVALUATE.                                                02980001
                                                                        02990001
       EJECT                                                            03000001
                                                                        03010001
      ******************************************************************03020001
       7200-FETCH-TMNDPTS.                                              03030001
      ******************************************************************03040001
      *                                                                 03050001
           EXEC SQL                                                     03060001
               FETCH TMNDPTS_CURSOR                                     03070001
                INTO :MNDPTS-FSCL-MN-END-DTE,                           03080001
                     :MNDPTS-FSCL-MN-NBR,                               03090001
                     :MNDPTS-DEPT-NBR,                                  03100001
                     :MNDPTS-STR-NBR,                                   03120001
                     :MNDPTS-SLS-RTL-AMT,                               03130001
                     :MNDPTS-MKDN-RTL-AMT,                              03140001
                     :MNDPTS-MKUP-RTL-AMT,                              03150001
                     :MNDPTS-XFER-IN-RTL-AMT,                           03160001
                     :MNDPTS-XFER-OUT-RTL-AMT,                          03170001
                     :MNDPTS-RCPT-RTL-AMT,                              03180001
                     :MNDPTS-RCPT-NET-COST-AMT,                         03190001
                     :MNDPTS-PADJ-RTL-AMT,                              03200001
                     :MNDPTS-PADJ-NET-COST-AMT,                         03210001
                     :MNDPTS-INV-ADJ-RTL-AMT,                           03220001
                     :MNDPTS-SHNK-RTL-AMT,                              03230006
                     :MNDPTS-END-INV-RTL-AMT                            03240006
           END-EXEC.                                                    03250001
                                                                        03260001
           EVALUATE TRUE                                                03270001
               WHEN SQLCODE = 0                                         03280001
                   ADD +1              TO PA-TMNDPTS-ROWS-FETCHED       03290001
               WHEN SQLCODE = +100                                      03300001
                   MOVE 'Y'            TO PS-TMNDPTS-DONE               03310001
               WHEN OTHER                                               03320001
                   MOVE '7200-FETCH-TMNDPTS'                            03330001
                                       TO AA-PARAGRAPH-NAME             03340001
                   MOVE 'TMNDPTS TABLE ERROR'                           03350001
                                       TO AA-MESSAGE                    03360001
                   MOVE 'FETCH'        TO AA-DB2-OPERATION              03370001
                   PERFORM 9901-SQL-ERROR                               03380001
           END-EVALUATE.                                                03390001
                                                                        03400001
       EJECT                                                            03410001
                                                                        03420001
      ******************************************************************03430001
       7300-CLOSE-CURSOR-TMNDPTS.                                       03440001
      ******************************************************************03450001
      *                                                                 03460001
           EXEC SQL                                                     03470001
               CLOSE TMNDPTS_CURSOR                                     03480001
           END-EXEC.                                                    03490001
                                                                        03500001
           EVALUATE TRUE                                                03510001
               WHEN SQLCODE = 0                                         03520001
                   CONTINUE                                             03530001
               WHEN OTHER                                               03540001
                   MOVE '7300-CLOSE-CURSOR-TMNDPTS'                     03550001
                                       TO AA-PARAGRAPH-NAME             03560001
                   MOVE 'TMNDPTS TABLE ERROR'                           03570001
                                       TO AA-MESSAGE                    03580001
                   MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION              03590001
                   PERFORM 9901-SQL-ERROR                               03600001
           END-EVALUATE.                                                03610001
                                                                        03620001
       EJECT                                                            03630001
                                                                        03640001
      ******************************************************************03650001
       8000-EOJ.                                                        03660001
      ******************************************************************03670001
      *                                                                 03680001
           DISPLAY ' '.                                                 03690001
           DISPLAY '  PROGRAM MLKUT424 PROCESSING SUMMARY'.             03700001
           DISPLAY '  -----------------------------------'.             03710001
           DISPLAY ' '.                                                 03720001
      *                                                                 03730001
           MOVE PA-TMNDPTS-ROWS-FETCHED TO CD-COUNT-HOLD1.              03740001
           DISPLAY 'TMNDPTS ROWS FETCHED =      ' CD-COUNT-HOLD1.       03750001
      *                                                                 03760001
           MOVE PA-OUTPUT-RECS-WRITTEN TO CD-COUNT-HOLD1.               03770001
           DISPLAY 'EXTRACT RECORDS WRITTEN =   ' CD-COUNT-HOLD1.       03780001
      *                                                                 03790001
           DISPLAY ' '.                                                 03800001
                                                                        03810001
           CLOSE DATA-INTEGRITY-CTL-FILE.                               03820001
           CLOSE DATA-INTEGRITY-EXTR-FILE.                              03830001
                                                                        03840001
       EJECT                                                            03850001
                                                                        03860001
      ******************************************************************03870001
       9900-COMMON-ABEND-ROUTINE.                                       03880001
      ******************************************************************03890001
      *                                                                 03900001
           DISPLAY AA-ABEND-LIT.                                        03910001
           DISPLAY AA-PROGRAM-LIT.                                      03920001
           DISPLAY AA-PARAGRAPH-LIT.                                    03930001
           DISPLAY AA-MESSAGE-LINE.                                     03940001
           DISPLAY ABEND-CODE.                                          03950001
                                                                        03960001
           CALL 'ILBOABN0' USING ABEND-CODE.                            03970001
                                                                        03980001
       EJECT                                                            03990001
                                                                        04000001
      ******************************************************************04010001
       9901-SQL-ERROR.                                                  04020001
      ******************************************************************04030001
      *                                                                 04040001
           CLOSE DATA-INTEGRITY-CTL-FILE                                04050001
                 DATA-INTEGRITY-EXTR-FILE.                              04060001
                                                                        04070001
           DISPLAY AA-ABEND-LIT.                                        04080001
           DISPLAY AA-DB2-ERROR-LIT.                                    04090001
           DISPLAY AA-PROGRAM-LIT.                                      04100001
           DISPLAY AA-PARAGRAPH-LIT.                                    04110001
           DISPLAY AA-MESSAGE-LINE.                                     04120001
           DISPLAY AA-DB2-OPERATION-LIT.                                04130001
           SET AC-DB2-ERROR TO TRUE.                                    04140001
           MOVE +4013                  TO ABEND-CODE.                   04150001
                                                                        04160001
           COPY DPPD004.                                                04170001
           CALL 'ILBOABN0' USING ABEND-CODE.                            04180001
                                                                        04190001
