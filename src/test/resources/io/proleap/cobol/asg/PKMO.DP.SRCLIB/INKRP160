      ***************************************************************** 00010000
       IDENTIFICATION DIVISION.                                         00020000
      ***************************************************************** 00030000
       PROGRAM-ID.      INKRP160.                                       00040004
       AUTHOR.          JOHN SELWITSCHKA.                               00050001
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00060000
       DATE-WRITTEN.    05-18-2006.                                     00070001
                                                                        00080000
      ***************************************************************** 00090000
      *           SKU / STYLE DELETE ERROR REPORT                     * 00100001
      ***************************************************************** 00110000
      *  THIS PROGRAM "GETS" MESSAGES OFF THE MQ SERIES               * 00120001
      *  IN.ITEM.ERROR.Q QUEUE AND WRITES THEM TO A GDG AND PRODUCES  * 00130001
      *  THE SKU / STYLE DELETE ERROR REPORT.                         * 00131001
      *                                                               * 00150000
      *  EACH MESSAGE WILL RESULT IN A RECORD WRITTEN TO THE REPORT   * 00160001
      *  AND THE GDG.                                                 * 00161001
      *                                                               * 00170000
      ***************************************************************** 00180000
      *                                                               * 00210100
      * 05/18/2006 J. SELWITSCHKA WR28545 - NEW PROGRAM               * 00210201
      ***************************************************************** 00220000
       ENVIRONMENT DIVISION.                                            00230000
      ***************************************************************** 00240000
                                                                        00250000
       CONFIGURATION SECTION.                                           00260000
                                                                        00270000
       SOURCE-COMPUTER.       IBM-3090.                                 00280000
       OBJECT-COMPUTER.       IBM-3090.                                 00290000
                                                                        00300000
       INPUT-OUTPUT SECTION.                                            00310000
                                                                        00320000
       FILE-CONTROL.                                                    00330000
           SELECT CONTROL-CARD           ASSIGN TO INP01.               00340001
           SELECT CONTROL-CARD-2         ASSIGN TO INP02.               00360001
           SELECT ERROR-MSG-FILE         ASSIGN TO OUT01.               00361001
           SELECT ERROR-MSG-RPT          ASSIGN TO RPT01.               00362001
                                                                        00370000
                                                                        00380000
                                                                        00390000
      ***************************************************************** 00400000
       DATA DIVISION.                                                   00410000
      ***************************************************************** 00420000
       FILE SECTION.                                                    00430000
                                                                        00440000
       FD  CONTROL-CARD                                                 00441001
           RECORDING MODE F                                             00442001
           BLOCK CONTAINS 0 RECORDS                                     00443001
           LABEL RECORDS ARE OMITTED.                                   00444001
       01  CONTROL-CARD-LINE                PIC  X(80).                 00445001
                                                                        00446001
       FD  CONTROL-CARD-2                                               00447001
           RECORDING MODE F                                             00448001
           BLOCK CONTAINS 0 RECORDS                                     00449001
           LABEL RECORDS ARE OMITTED.                                   00449101
       01  CONTROL-CARD-LINE-2              PIC  X(80).                 00449201
                                                                        00449301
       FD  ERROR-MSG-FILE                                               00450001
           RECORDING MODE F                                             00460000
           BLOCK CONTAINS 0 RECORDS                                     00470000
           LABEL RECORDS ARE OMITTED.                                   00480000
       01  ERROR-MSG-RECORD                 PIC  X(450).                00490015
                                                                        00500000
       FD  ERROR-MSG-RPT                                                00570001
           RECORDING MODE F                                             00580000
           LABEL RECORDS ARE STANDARD                                   00590000
           RECORD CONTAINS 150 CHARACTERS                               00600010
           BLOCK CONTAINS  0 RECORDS                                    00610000
           DATA RECORD IS ERROR-MSG-RPT-LINE.                           00620003
       01  ERROR-MSG-RPT-LINE               PIC X(150).                 00630010
                                                                        00640000
      ***************************************************************** 00650000
       WORKING-STORAGE SECTION.                                         00660000
      ***************************************************************** 00670000
      *    API CONTROL BLOCKS                                           00680000
                                                                        00690000
       01  MQM-OBJECT-DESCRIPTOR.                                       00700000
           COPY CMQODV.                                                 00710000
       01  MQM-MESSAGE-DESCRIPTOR.                                      00720000
           COPY CMQMDV.                                                 00730000
       01  MQM-GET-MESSAGE-OPTIONS.                                     00740000
           COPY CMQGMOV.                                                00750000
       01  MQM-PUT-MESSAGE-OPTIONS.                                     00760000
           COPY CMQPMOV.                                                00770000
                                                                        00780000
      *    MQV CONTAINS CONSTANTS (FOR FILLING IN THE CONTROL BLOCKS)   00790000
      *    AND RETURN CODES (FOR TESTING THE RESULT OF A CALL)          00800000
                                                                        00810000
       01  MQM-CONSTANTS.                                               00820000
           COPY CMQV.                                                   00830000
      *                                                                 00840000
      ***************************************************************** 00850000
      *    CONTROL CARD                                                 00860000
      ***************************************************************** 00870000
       01  CC-CONTROL-CARD.                                             00880000
           05  CC-QMGR-NAME              PIC  X(48).                    00890000
           05  FILLER                    PIC  X(32).                    00900000
      *                                                                 00910000
      ***************************************************************** 00920000
      *    CONTROL CARD-2                                               00930000
      ***************************************************************** 00940000
       01  CC-CONTROL-CARD-2.                                           00950000
           05  CC-ERR-Q-NAME             PIC  X(48).                    00960001
           05  FILLER                    PIC  X(32).                    00970000
      *                                                                 00980000
      ***************************************************************** 00990000
      *    PROGRAM ACCUMULATORS                                         01000000
      ***************************************************************** 01010000
       01  PA-PROGRAM-ACCUMULATORS.                                     01020000
           05  PA-Q-MESSAGES             PIC 9(07) VALUE 0.             01040000
           05  PA-WRITTEN                PIC 9(07) VALUE 0.             01050020
                                                                        01060000
      ***************************************************************** 01070000
      *    PROGRAM CONSTANTS                                            01080000
      ***************************************************************** 01090000
       01  PC-PROGRAM-CONSTANTS.                                        01100000
           05  PC-KOHLS-OP-CO            PIC X(02) VALUE '03'.          01110000
           05  PC-FSK020-JOB             PIC X(06) VALUE 'FSK020'.      01120000
           05  PC-PGM-NAME               PIC X(09) VALUE 'INKRP160 '.   01130004
           05  PC-FIFTY-STARS            PIC X(50)  VALUE ALL '*'.      01140000
           05  PC-30-SPACES              PIC X(30) VALUE SPACES.        01150000
           05  PC-END-PGM-MSG            PIC X(100)                     01160000
                                         VALUE 'END RUN OF PROGRAM '.   01170000
           05  PC-LINE-LIMIT             PIC S9(02)    VALUE +55.       01171000
                                                                        01180000
      ***************************************************************** 01190000
      *    PROGRAM SWITCHES                                             01200000
      ***************************************************************** 01210000
       01  PS-PROGRAM-SWITCHES.                                         01220000
           05  PS-CONTROL-CARD-SW        PIC X(01) VALUE 'N'.           01230000
               88  PS-CONTROL-CARD-END             VALUE 'Y'.           01240000
               88  PS-CONTROL-CARD-START           VALUE 'N'.           01250000
           05  PS-CONTROL-CARD-2-SW      PIC X(01) VALUE 'N'.           01260000
               88  PS-CONTROL-CARD-2-END           VALUE 'Y'.           01270000
               88  PS-CONTROL-CARD-2-START         VALUE 'N'.           01280000
           05  PS-MQGET-SUCCESSFUL-SW    PIC X(01) VALUE 'N'.           01290000
               88  PS-MQGET-SUCCESSFUL             VALUE 'Y'.           01300000
           05  PS-NO-MQ-MESSAGES-IND     PIC X(01) VALUE 'N'.           01310000
               88  PS-NO-MQ-MESSAGES               VALUE 'Y'.           01320000
           05  PS-END-OF-Q-IND           PIC X(01) VALUE 'N'.           01330000
               88  PS-END-OF-Q                     VALUE 'Y'.           01340000
                                                                        01350000
      ***************************************************************** 01360000
      *    PROGRAM VARIABLES                                            01370000
      ***************************************************************** 01380000
       01  PV-PROGRAM-VARIABLES.                                        01390000
           05  PV-ERROR-MESSAGE           PIC X(48)    VALUE SPACES.    01400000
           05  PV-ERROR-LINE-CNT          PIC S9(02)   VALUE +95.       01420000
           05  PV-ERROR-PAGE-NMBR         PIC S9(05)   VALUE ZERO.      01422000
           05  PV-FR-DEPT-NBR-FMT.                                      01422100
               10  FILLER            PIC X(01)     VALUE SPACES.        01422200
               10  PV-FR-DEPT-NBR    PIC X(03)     VALUE SPACES.        01422300
           05  PV-FR-MAJ-CL-NBR-FMT.                                    01422400
               10  FILLER            PIC X(02)     VALUE SPACES.        01422700
               10  PV-FR-MAJ-CL-NBR  PIC X(02)     VALUE SPACES.        01422800
           05  PV-FR-SUB-CL-NBR-FMT.                                    01422900
               10  FILLER            PIC X(02)     VALUE SPACES.        01424000
               10  PV-FR-SUB-CL-NBR  PIC X(02)     VALUE SPACES.        01425000
           05  PV-STR-NBR-FMT             PIC ZZZZZ    VALUE ZERO.      01426000
                                                                        01430000
           05  SD-SYSTEM-DATE.                                          01440000
               10  SD-SYSTEM-YEAR         PIC 9(02)    VALUE ZERO.      01450000
               10  SD-SYSTEM-MONTH        PIC 9(02)    VALUE ZERO.      01460000
               10  SD-SYSTEM-DAY          PIC 9(02)    VALUE ZERO.      01470000
                                                                        01480000
           05  ST-SYSTEM-TIME.                                          01490000
               10  ST-SYSTEM-TIME-FORMAT.                               01500000
                   15  ST-SYSTEM-HOUR     PIC 9(02)    VALUE ZERO.      01510000
                   15  ST-SYSTEM-MINUTE   PIC 9(02)    VALUE ZERO.      01520000
                   15  ST-SYSTEM-SECOND   PIC 9(02)    VALUE ZERO.      01530000
               10  ST-SYSTEM-TIME-NUM  REDEFINES                        01540000
                                        ST-SYSTEM-TIME-FORMAT           01550000
                                          PIC 9(06).                    01560000
               10  FILLER                 PIC X(4)      VALUE SPACES.   01570000
                                                                        01580000
       01  PV-MQ-VARIABLES.                                             01590000
           05  PV-QM-NAME              PIC X(48).                       01600000
           05  PV-PARAGRAPH            PIC X(50).                       01610000
           05  PV-MSGBUFFER            PIC X(500) VALUE SPACES.         01620014
           05  PV-MSGLENGTH            PIC S9(9)  BINARY VALUE +500.    01630014
           05  PV-DATA-LENGTH          PIC S9(9)  BINARY.               01640000
           05  PV-SELECT-COUNT         PIC S9(9)  BINARY VALUE 1.       01650000
           05  PV-SELECTORS.                                            01660000
               10  PV-Q-DEPTH          PIC S9(9)  BINARY VALUE 0.       01670000
           05  PV-INTATTR-COUNT        PIC S9(9)  BINARY VALUE 1.       01680000
           05  PV-INTATTRS.                                             01690000
               10  PV-Q-DEPTH-ATTR     PIC S9(9)  BINARY VALUE 0.       01700000
           05  PV-CHARATTR-LENGTH      PIC S9(9)  BINARY VALUE 0.       01710000
           05  PV-CHARATTRS            PIC X(48).                       01720000
           05  PV-HCONN                PIC S9(9)  BINARY VALUE 0.       01730000
           05  PV-HANDLE               PIC S9(9)  BINARY VALUE 0.       01740001
           05  PV-OPEN-OPTIONS         PIC S9(9)  BINARY.               01750000
           05  PV-COMPLETION-CODE      PIC S9(9)  BINARY.               01760000
           05  PV-REASON               PIC S9(9)  BINARY.               01770000
           05  PV-CON-REASON           PIC S9(9)  BINARY.               01780000
           05  PV-Q-DEPTH-NBR          PIC S9(9)  COMP-3 VALUE 0.       01790000
                                                                        01800000
       01  PH-PROGRAM-HOLD-VARIABLES.                                   01810000
           05  PH-COMPLETION-CODE          PIC  S9(09) BINARY.          01820000
           05  PH-REASON                   PIC  S9(09) BINARY.          01830000
                                                                        01840000
      ***************************************************************** 01850000
      *    MQ WORKING STORAGE VARIABLES                                 01860000
      ***************************************************************** 01870000
       01  WS-MQCONN                 PIC X(8)  VALUE 'CSQBCONN'.        01880000
       01  WS-MQOPEN                 PIC X(8)  VALUE 'CSQBOPEN'.        01890000
       01  WS-MQINQ                  PIC X(8)  VALUE 'CSQBINQ'.         01900000
       01  WS-MQGET                  PIC X(8)  VALUE 'CSQBGET'.         01910000
       01  WS-MQPUT1                 PIC X(8)  VALUE 'CSQBPUT1'.        01920000
       01  WS-MQBACK                 PIC X(8)  VALUE 'CSQBBACK'.        01930000
       01  WS-MQCMIT                 PIC X(8)  VALUE 'CSQBCOMM'.        01940000
       01  WS-MQCLOSE                PIC X(8)  VALUE 'CSQBCLOS'.        01950000
       01  WS-MQDISC                 PIC X(8)  VALUE 'CSQBDISC'.        01960000
                                                                        01970000
      ***************************************************************** 01980000
      *  ERROR MQ COPYBOOK - INPUT                                      01990000
      ***************************************************************** 02000000
           COPY IMRD008.                                                02010003
           05  ERROR-REASON          PIC X(50)     VALUE SPACES.        02011014
                                                                        02020000
      ***************************************************************** 02030000
      *  STANDARD HEADING FOR 150 COLUMNS                               02040010
      ***************************************************************** 02050000
           COPY DPWS150O.                                               02060010
                                                                        02070000
       01  HL1-HEADING-LINE.                                            02080000
           05  FILLER                PIC X(63)     VALUE SPACES.        02090013
           05  FILLER                PIC X(25)     VALUE                02100001
               'SKU / STYLE DELETE ERRORS'.                             02110001
           05  FILLER                PIC X(62)     VALUE SPACES.        02120013
       01  HL2-HEADING-LINE.                                            02121001
           05  FILLER                PIC X(02)     VALUE SPACES.        02122001
           05  FILLER                PIC X(17)     VALUE                02123001
               'PROCESSING DATE: '.                                     02124001
           05  HL2-PROCESS-DATE      PIC X(10)     VALUE SPACES.        02125001
           05  FILLER                PIC X(121)    VALUE SPACES.        02126013
       01  HL3-HEADING-LINE.                                            02130001
           05  FILLER                PIC X(03)     VALUE SPACES.        02140000
           05  FILLER                PIC X(08)     VALUE                02150001
               'DEPT-SCL'.                                              02160001
           05  FILLER                PIC X(05)     VALUE SPACES.        02170001
           05  FILLER                PIC X(03)     VALUE                02180000
               'SKU'.                                                   02190001
           05  FILLER                PIC X(09)     VALUE SPACES.        02200010
           05  FILLER                PIC X(03)     VALUE                02210001
               'UPC'.                                                   02220001
           05  FILLER                PIC X(05)     VALUE SPACES.        02230011
           05  FILLER                PIC X(04)     VALUE                02240001
               'TYPE'.                                                  02250001
           05  FILLER                PIC X(01)     VALUE SPACES.        02260011
           05  FILLER                PIC X(10)     VALUE                02270001
               'INTR UPCID'.                                            02280001
           05  FILLER                PIC X(02)     VALUE SPACES.        02290011
           05  FILLER                PIC X(06)     VALUE                02300001
               'UPC-ID'.                                                02310001
           05  FILLER                PIC X(05)     VALUE SPACES.        02320012
           05  FILLER                PIC X(05)     VALUE                02330001
               'STYLE'.                                                 02340001
           05  FILLER                PIC X(07)     VALUE SPACES.        02350012
           05  FILLER                PIC X(05)     VALUE                02360018
               'VS ID'.                                                 02370018
           05  FILLER                PIC X(09)     VALUE SPACES.        02380018
           05  FILLER                PIC X(08)     VALUE                02390001
               'SUBCL ID'.                                              02400001
           05  FILLER                PIC X(04)     VALUE SPACES.        02410017
           05  FILLER                PIC X(05)     VALUE                02420001
               'ENTID'.                                                 02430001
           05  FILLER                PIC X(04)     VALUE SPACES.        02440017
           05  FILLER                PIC X(21)     VALUE                02450021
               'INKCS060 ERROR REASON'.                                 02461021
           05  FILLER                PIC X(18)     VALUE SPACES.        02470021
                                                                        02510000
       01  DL1-DETAIL-LINE.                                             02520000
           05  FILLER                PIC X(03)     VALUE SPACES.        02530001
           05  DL1-DEPT              PIC ZZZ9      VALUE ZERO.          02540001
           05  FILLER                PIC X(01)     VALUE SPACES.        02550001
           05  DL1-SUBCL             PIC ZZZ9      VALUE ZERO.          02560001
           05  FILLER                PIC X(02)     VALUE SPACES.        02570001
           05  DL1-SKU               PIC 9(08)     VALUE ZERO.          02580001
           05  FILLER                PIC X(01)     VALUE SPACES.        02590001
           05  DL1-UPC               PIC 9(13)     VALUE ZERO.          02600001
           05  FILLER                PIC X(01)     VALUE SPACES.        02610001
           05  DL1-EVENT-TYPE        PIC ZZ9       VALUE ZERO.          02640001
           05  FILLER                PIC X(01)     VALUE SPACES.        02670001
           05  DL1-INTR-UPC-ID       PIC 9(10)     VALUE ZERO.          02680001
           05  FILLER                PIC X(02)     VALUE SPACES.        02690001
           05  DL1-UPC-ID            PIC 9(10)     VALUE ZERO.          02710001
           05  FILLER                PIC X(01)     VALUE SPACES.        02730001
           05  DL1-RMS-STYL-ID       PIC 9(08)     VALUE ZERO.          02740001
           05  FILLER                PIC X(01)     VALUE SPACES.        02750001
           05  DL1-VS-NBR            PIC X(20)     VALUE SPACES.        02760001
           05  FILLER                PIC X(01)     VALUE SPACES.        02770001
           05  DL1-SUBCL-MDSE-ID     PIC 9(06)     VALUE ZERO.          02780001
           05  FILLER                PIC X(01)     VALUE SPACES.        02790001
           05  DL1-ENT-ID            PIC 9(09)     VALUE ZERO.          02800001
           05  FILLER                PIC X(01)     VALUE SPACES.        02801001
           05  DL1-ERROR-REASON      PIC X(39)     VALUE SPACES.        02802013
                                                                        02810000
      *                                                                 02820000
      ***************************************************************** 02830000
      *    ABEND CODES THE ARE APPLICATION DEFINED                      02840000
      ***************************************************************** 02850000
       01  ABEND-CODE                  PIC S9(4)    VALUE ZEROS         02860000
                                                    COMP SYNC.          02870000
           88  AC-USER-ERROR                        VALUE +4003.        02880000
           88  AC-MQ-ERROR                          VALUE +4020.        02890000
                                                                        02910000
      ***************************************************************** 02920000
       PROCEDURE DIVISION.                                              02930000
      ***************************************************************** 02940000
       0000-MAINLINE.                                                   02950000
                                                                        02960000
           PERFORM 1000-INITIALIZE.                                     02970000
           PERFORM 1050-PREPARE-HEADING-DATA.                           02980000
                                                                        02990000
           IF PS-CONTROL-CARD-END                                       03000000
             OR PS-CONTROL-CARD-2-END                                   03010000
               CLOSE ERROR-MSG-RPT                                      03020002
               SET AC-USER-ERROR TO TRUE                                03030000
               CALL 'ILBOABN0' USING ABEND-CODE                         03040000
           ELSE                                                         03050000
               IF PS-NO-MQ-MESSAGES                                     03060000
                  DISPLAY PC-FIFTY-STARS                                03060100
                  DISPLAY 'NO ERRORS WERE FOUND'                        03061003
                  DISPLAY PC-FIFTY-STARS                                03062000
               ELSE                                                     03100000
                  PERFORM 2000-PROCESS-MQ-MESSAGES                      03110000
                    UNTIL(PS-END-OF-Q)                                  03120000
                      OR (NOT PS-MQGET-SUCCESSFUL)                      03130000
               END-IF                                                   03140010
           END-IF.                                                      03141010
                                                                        03150000
           PERFORM 3000-EOJ-PROCESSING.                                 03160000
                                                                        03170000
           STOP RUN.                                                    03180000
                                                                        03190000
      ***************************************************************** 03200000
      *  INITIALIZATION ROUTINE.                                        03210000
      ***************************************************************** 03220000
       1000-INITIALIZE.                                                 03230000
                                                                        03240000
           DISPLAY PC-FIFTY-STARS.                                      03250000
           DISPLAY '***** START OF PROGRAM INKRP160 *****'.             03260004
                                                                        03270000
           SET PS-CONTROL-CARD-START TO TRUE.                           03280000
           SET PS-CONTROL-CARD-2-START TO TRUE.                         03290000
           INITIALIZE PS-END-OF-Q-IND                                   03300000
                      DL1-DETAIL-LINE.                                  03310000
                                                                        03320000
           OPEN INPUT CONTROL-CARD                                      03330000
                      CONTROL-CARD-2.                                   03340000
           OPEN OUTPUT ERROR-MSG-FILE                                   03350015
                       ERROR-MSG-RPT.                                   03351015
                                                                        03360000
      *  READ THE FIRST CONTROL CARD FOR THE QUEUE MGR NAME.            03370000
                                                                        03380000
           PERFORM 8000-READ-CONTROL-CARD.                              03390000
           IF PS-CONTROL-CARD-END                                       03400000
               DISPLAY PC-FIFTY-STARS                                   03410000
               DISPLAY PC-PGM-NAME                                      03420000
                    'CONTROL CARD MISSING - FOR QMGR NAME'              03430000
           ELSE                                                         03440000
               IF CC-QMGR-NAME > SPACES                                 03450003
                   DISPLAY PC-FIFTY-STARS                               03460003
                   DISPLAY PC-PGM-NAME                                  03470003
                           '- CONTROL CARD FOR QMGR: '                  03480003
                           CC-CONTROL-CARD                              03490003
               ELSE                                                     03500003
                   SET PS-CONTROL-CARD-END TO TRUE                      03510003
                   DISPLAY PC-PGM-NAME                                  03520003
                        'INVALID QMGR NAME '                            03530003
               END-IF                                                   03540003
           END-IF.                                                      03550000
                                                                        03560000
           CLOSE CONTROL-CARD.                                          03570000
                                                                        03580000
      *  READ THE SECOND CONTROL CARD FOR THE QUEUE NAME                03590000
                                                                        03600000
            PERFORM 8010-READ-CONTROL-CARD-2.                           03610000
            IF PS-CONTROL-CARD-2-END                                    03620000
                DISPLAY PC-PGM-NAME                                     03630000
                   'CONTROL CARD MISSING - ERROR QUEUE NAME '           03640002
            ELSE                                                        03650000
                IF CC-ERR-Q-NAME > SPACES                               03660001
                    DISPLAY PC-FIFTY-STARS                              03670000
                    DISPLAY PC-PGM-NAME                                 03680000
                            '- CONTROL CARD FOR Q-NAME: '               03690000
                            CC-CONTROL-CARD-2                           03700000
                    DISPLAY PC-FIFTY-STARS                              03710000
                ELSE                                                    03720000
                    SET PS-CONTROL-CARD-2-END TO TRUE                   03730000
                    DISPLAY PC-PGM-NAME                                 03740000
                       'INVALID ERROR QUEUE NAME'                       03750002
                END-IF                                                  03760000
           END-IF.                                                      03770000
                                                                        03780000
           CLOSE CONTROL-CARD-2.                                        03790000
                                                                        03800000
           IF PS-CONTROL-CARD-END                                       03810000
              OR PS-CONTROL-CARD-2-END                                  03820000
               CONTINUE                                                 03830015
           ELSE                                                         03840015
               PERFORM 1100-MQ-CONNECT                                  03850015
               PERFORM 1200-MQ-INQUIRY-OPEN                             03860015
               PERFORM 1300-MQ-INQUIRY-MQINQ                            03870015
               PERFORM 1400-MQ-INQUIRY-CLOSE                            03880015
               PERFORM 1500-MQ-ERR-INPUT-OPEN                           03890015
               PERFORM 8100-MQGET-MSG                                   03900015
           END-IF.                                                      03910015
                                                                        03920000
      ***************************************************************** 03930000
      *  THIS MODULE PAREPARES THE HEADING DATA THAT WILL BE PRINTED    03940000
      *  ON THE REPORT.                                                 03950000
      ***************************************************************** 03960000
       1050-PREPARE-HEADING-DATA.                                       03970000
                                                                        03980000
           ACCEPT SD-SYSTEM-DATE FROM DATE.                             03990000
           MOVE SD-SYSTEM-MONTH        TO DP150O-RUN-MONTH.             04000010
           MOVE SD-SYSTEM-DAY          TO DP150O-RUN-DAY.               04010010
           MOVE SD-SYSTEM-YEAR         TO DP150O-RUN-YEAR.              04020010
                                                                        04030000
           ACCEPT ST-SYSTEM-TIME FROM TIME.                             04040000
           MOVE ST-SYSTEM-HOUR         TO DP150O-RUN-HOUR.              04050010
           MOVE ST-SYSTEM-MINUTE       TO DP150O-RUN-MINUTE.            04060010
                                                                        04070000
      ***************************************************************** 04080000
      *  MQ - CONNECT TO THE QUEUE MANAGER.                             04090000
      ***************************************************************** 04100000
       1100-MQ-CONNECT.                                                 04110000
                                                                        04120000
      *    CONNECT TO THE QUEUE MANAGER                                 04130000
                                                                        04140000
           MOVE CC-QMGR-NAME TO PV-QM-NAME.                             04150000
           CALL WS-MQCONN USING PV-QM-NAME                              04160000
                                PV-HCONN                                04170000
                                PV-COMPLETION-CODE                      04180000
                                PV-REASON.                              04190000
                                                                        04200000
      *    IF CONNECTION FAILED THEN DISPLAY ERROR MESSAGE              04210000
      *    AND EXIT                                                     04220000
                                                                        04230000
           MOVE PV-REASON TO PV-CON-REASON.                             04240000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   04250000
              MOVE 'MQCONN ERROR'   TO PV-ERROR-MESSAGE                 04260000
              MOVE '1100-MQ-CONNECT' TO  PV-PARAGRAPH                   04270000
              PERFORM 9000-DISPLAY-MQ-ERROR                             04280000
           ELSE                                                         04290000
              DISPLAY 'MQCONN SUCCESSFUL TO ', PV-QM-NAME               04300000
           END-IF.                                                      04310000
                                                                        04320000
      ***************************************************************** 04330000
      *  MQ - OPEN THE ERROR QUEUE FOR INQUIRY.                         04340002
      ***************************************************************** 04350000
       1200-MQ-INQUIRY-OPEN.                                            04360000
                                                                        04370000
      *    OPEN ERROR QUEUE FOR INQUIRY OF QUEUE DEPTH                  04380002
                                                                        04390000
           COMPUTE PV-OPEN-OPTIONS = MQOO-INQUIRE +                     04400000
                                  MQOO-FAIL-IF-QUIESCING.               04410000
           MOVE MQOT-Q            TO MQOD-OBJECTTYPE.                   04420000
           MOVE PV-QM-NAME        TO MQOD-OBJECTQMGRNAME.               04430000
           MOVE CC-ERR-Q-NAME     TO MQOD-OBJECTNAME.                   04440001
                                                                        04450000
           CALL WS-MQOPEN USING PV-HCONN                                04460000
                                MQM-OBJECT-DESCRIPTOR                   04470000
                                PV-OPEN-OPTIONS                         04480000
                                PV-HANDLE                               04490001
                                PV-COMPLETION-CODE                      04500000
                                PV-REASON.                              04510000
                                                                        04520000
      *    IF OPEN FAILED THEN DISPLAY ERROR MESSAGE                    04530000
      *    AND EXIT.                                                    04540000
                                                                        04550000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   04560000
              MOVE 'MQOPEN ERROR'   TO PV-ERROR-MESSAGE                 04570000
              MOVE '1200-MQ-INQUIRY-OPEN' TO  PV-PARAGRAPH              04580000
              MOVE PV-COMPLETION-CODE TO PH-COMPLETION-CODE             04590000
              MOVE PV-REASON          TO PH-REASON                      04600000
              PERFORM 1700-DISCONNECT-QMGR                              04610000
              MOVE PH-COMPLETION-CODE TO PV-COMPLETION-CODE             04620000
              MOVE PH-REASON          TO PV-REASON                      04630000
              PERFORM 9000-DISPLAY-MQ-ERROR                             04640000
           ELSE                                                         04650000
              DISPLAY 'MQOPEN SUCCESSFUL - INQUIRE OF Q DEPTH'          04660000
           END-IF.                                                      04670000
                                                                        04680000
      ***************************************************************** 04690000
      *  MQ - INQUIRE ON THE QUEUE DEPTH.                               04700000
      ***************************************************************** 04710000
       1300-MQ-INQUIRY-MQINQ.                                           04720000
                                                                        04730000
      *    INQUIRE ON ERROR QUEUE DEPTH                                 04740002
                                                                        04750000
           MOVE MQIA-CURRENT-Q-DEPTH TO PV-Q-DEPTH.                     04760000
                                                                        04770000
           CALL WS-MQINQ USING                                          04780000
                        PV-HCONN,                                       04790000
                        PV-HANDLE,                                      04800001
                        PV-SELECT-COUNT,                                04810000
                        PV-SELECTORS,                                   04820000
                        PV-INTATTR-COUNT,                               04830000
                        PV-INTATTRS,                                    04840000
                        PV-CHARATTR-LENGTH,                             04850000
                        PV-CHARATTRS,                                   04860000
                        PV-COMPLETION-CODE,                             04870000
                        PV-REASON.                                      04880000
                                                                        04890000
      *    IF INQUIRE FAILED THEN DISPLAY ERROR MESSAGE                 04900000
      *    AND EXIT.                                                    04910000
                                                                        04920000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   04930000
              MOVE 'MQINQ ERROR'   TO PV-ERROR-MESSAGE                  04940000
              MOVE '1300-MQ-INQUIRY-MQINQ' TO PV-PARAGRAPH              04950000
              PERFORM 9000-DISPLAY-MQ-ERROR                             04960000
           ELSE                                                         04970000
              DISPLAY 'MQINQ SUCCESSFUL, QUEUE DEPTH: ', PV-Q-DEPTH-ATTR04980000
              MOVE PV-Q-DEPTH-ATTR TO PV-Q-DEPTH-NBR                    04990000
              IF PV-Q-DEPTH-NBR > ZERO                                  05000000
                 CONTINUE                                               05010000
              ELSE                                                      05020000
                 SET PS-NO-MQ-MESSAGES TO TRUE                          05030000
              END-IF                                                    05040000
           END-IF.                                                      05050000
                                                                        05060000
      ***************************************************************** 05070000
      *  MQ - CLOSE THE ERROR QUEUE THAT WAS USED FOR INQUIRY           05080002
      ***************************************************************** 05090000
       1400-MQ-INQUIRY-CLOSE.                                           05100000
                                                                        05110000
      *    CLOSE ERROR QUEUE FOR INQUIRY ON QUEUE DEPTH                 05120002
                                                                        05130000
           CALL WS-MQCLOSE USING PV-HCONN                               05140000
                                 PV-HANDLE                              05150001
                                 MQCO-NONE                              05160000
                                 PV-COMPLETION-CODE                     05170000
                                 PV-REASON.                             05180000
                                                                        05190000
      *    IF CLOSE FAILED THEN DISPLAY ERROR MESSAGE                   05200000
      *    AND EXIT.                                                    05210000
                                                                        05220000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   05230000
              MOVE 'MQCLOSE ERROR'  TO PV-ERROR-MESSAGE                 05240000
              MOVE '1400-MQ-INQUIRY-CLOSE' TO PV-PARAGRAPH              05250000
              PERFORM 9000-DISPLAY-MQ-ERROR                             05260000
           ELSE                                                         05270000
              DISPLAY 'MQCLOSE SUCCESSFUL - INQUIRE OF Q DEPTH'         05280000
           END-IF.                                                      05290000
                                                                        05300000
      ***************************************************************** 05310000
      *  MQ - OPEN THE ERROR QUEUE FOR INPUT.                           05320002
      ***************************************************************** 05330000
       1500-MQ-ERR-INPUT-OPEN.                                          05340001
                                                                        05350000
      *    OPEN THE ERROR QUEUE FOR INPUT                               05360002
                                                                        05370000
           COMPUTE PV-OPEN-OPTIONS = MQOO-INPUT-SHARED +                05380000
                                     MQOO-FAIL-IF-QUIESCING.            05390000
           MOVE MQOT-Q                   TO MQOD-OBJECTTYPE.            05400000
           MOVE PV-QM-NAME               TO MQOD-OBJECTQMGRNAME.        05410000
           MOVE CC-ERR-Q-NAME            TO MQOD-OBJECTNAME.            05420001
      *                                                                 05430000
           CALL WS-MQOPEN USING PV-HCONN                                05440000
                                MQM-OBJECT-DESCRIPTOR                   05450000
                                PV-OPEN-OPTIONS                         05460000
                                PV-HANDLE                               05470001
                                PV-COMPLETION-CODE                      05480000
                                PV-REASON.                              05490000
      *                                                                 05500000
      *    IF OPEN FAILED THEN DISPLAY ERROR MESSAGE                    05510000
      *    AND EXIT.                                                    05520000
      *                                                                 05530000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   05540000
              MOVE 'MQOPEN ERROR'   TO PV-ERROR-MESSAGE                 05550000
              MOVE '1500-MQ-ERR-INPUT-OPEN' TO PV-PARAGRAPH             05560001
              MOVE PV-COMPLETION-CODE TO PH-COMPLETION-CODE             05570000
              MOVE PV-REASON          TO PH-REASON                      05580000
              PERFORM 1700-DISCONNECT-QMGR                              05590000
              MOVE PH-COMPLETION-CODE TO PV-COMPLETION-CODE             05600000
              MOVE PH-REASON          TO PV-REASON                      05610000
              PERFORM 9000-DISPLAY-MQ-ERROR                             05620000
           ELSE                                                         05630000
              DISPLAY 'MQOPEN SUCCESSFUL - ERROR INPUT'                 05640002
           END-IF.                                                      05650000
                                                                        05660000
      ***************************************************************** 05670000
      *  MQ - CLOSE THE ERROR QUEUE FOR INPUT.                          05680002
      ***************************************************************** 05690000
       1600-MQ-ERR-INPUT-CLOSE.                                         05700001
                                                                        05710000
           CALL WS-MQCLOSE USING PV-HCONN                               05720000
                                 PV-HANDLE                              05730001
                                 MQCO-NONE                              05740000
                                 PV-COMPLETION-CODE                     05750000
                                 PV-REASON.                             05760000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   05770000
              MOVE 'MQCLOSE ERROR' TO PV-ERROR-MESSAGE                  05780000
              MOVE '1600-MQ-ERR-INPUT-CLOSE' TO PV-PARAGRAPH            05790001
              MOVE PV-COMPLETION-CODE TO PH-COMPLETION-CODE             05800000
              MOVE PV-REASON          TO PH-REASON                      05810000
              PERFORM 1700-DISCONNECT-QMGR                              05820000
              MOVE PH-COMPLETION-CODE TO PV-COMPLETION-CODE             05830000
              MOVE PH-REASON          TO PV-REASON                      05840000
              PERFORM 9000-DISPLAY-MQ-ERROR                             05850000
           ELSE                                                         05860000
              DISPLAY 'MQCLOSE SUCCESSFUL - ERROR INPUT'                05870002
           END-IF.                                                      05880000
                                                                        05890000
                                                                        05900000
      ***************************************************************** 05910000
      *  DISCONNECT QUEUE MANAGER                                       05920000
      ***************************************************************** 05930000
       1700-DISCONNECT-QMGR.                                            05940000
                                                                        05950000
           IF PV-CON-REASON IS NOT EQUAL TO MQRC-ALREADY-CONNECTED      05960000
              CALL WS-MQDISC USING PV-HCONN                             05970000
                                   PV-COMPLETION-CODE                   05980000
                                   PV-REASON                            05990000
              IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                06000000
                 MOVE 'MQDISC ERROR'   TO PV-ERROR-MESSAGE              06010000
                 MOVE '1700-DISCONNECT-QMGR' TO PV-PARAGRAPH            06020000
                 PERFORM 9000-DISPLAY-MQ-ERROR                          06030000
              ELSE                                                      06040000
                 DISPLAY 'MQDISC SUCCESSFUL'                            06050000
              END-IF                                                    06060000
           END-IF.                                                      06070000
                                                                        06080000
      ***************************************************************** 06090000
      *  PROCESS MQ MESSAGES.                                           06100000
      ***************************************************************** 06110000
       2000-PROCESS-MQ-MESSAGES.                                        06120000
                                                                        06130000
           MOVE IM008-ITEM-KEY-DEPT        TO DL1-DEPT.                 06161503
           MOVE IM008-ITEM-KEY-SUBCL       TO DL1-SUBCL.                06161603
           MOVE IM008-ITEM-KEY-SKU         TO DL1-SKU.                  06161703
           MOVE IM008-ITEM-KEY-UPC-NBR     TO DL1-UPC.                  06161803
           MOVE IM008-BUSINESS-EVENT-ID    TO DL1-EVENT-TYPE.           06161903
           MOVE IM008-ITEM-KEY-INTR-UPC-ID TO DL1-INTR-UPC-ID.          06162003
           MOVE IM008-ITEM-KEY-UPC-ID      TO DL1-UPC-ID.               06162103
           MOVE IM008-ITEM-KEY-RMS-STYL-ID TO DL1-RMS-STYL-ID.          06162203
           MOVE IM008-ITEM-KEY-VS-ID       TO DL1-VS-NBR.               06162303
           MOVE IM008-ITEM-KEY-SUBCL-MDSE-ID                            06252903
                                           TO DL1-SUBCL-MDSE-ID.        06253003
           MOVE IM008-ITEM-KEY-ENT-ID      TO DL1-ENT-ID.               06253103
           MOVE ERROR-REASON               TO DL1-ERROR-REASON.         06253314
                                                                        06290000
           PERFORM 2200-PRINT-ERROR-LINE.                               06310015
                                                                        06350800
           PERFORM 4000-WRITE-ERR-MSG-RECORD.                           06350915
                                                                        06351015
           PERFORM 8500-COMMIT-MQ.                                      06352000
                                                                        06380000
           PERFORM 8100-MQGET-MSG.                                      06390000
                                                                        06400000
      ***************************************************************** 06410000
      *  THIS PARAGRAPH WILL PRINT THE ERROR ERROR LINE.                06420002
      ***************************************************************** 06440000
       2200-PRINT-ERROR-LINE.                                           06450001
                                                                        06451000
           IF PV-ERROR-LINE-CNT > PC-LINE-LIMIT                         06452000
              PERFORM 2400-PRINT-ERROR-HEADINGS                         06453015
              PERFORM 5000-WRITE-ERR-RECORD                             06453115
              ADD +2 TO PV-ERROR-LINE-CNT                               06453215
           ELSE                                                         06454000
              PERFORM 5000-WRITE-ERR-RECORD                             06460015
              ADD +2 TO PV-ERROR-LINE-CNT                               06460115
           END-IF.                                                      06460200
                                                                        06461000
      ***************************************************************** 06461200
      *  THIS PARAGRAPH WILL PRINT THE ERROR REPORT HEADINGS.           06461302
      ***************************************************************** 06461400
       2400-PRINT-ERROR-HEADINGS.                                       06461501
                                                                        06461600
           MOVE +7  TO PV-ERROR-LINE-CNT.                               06461703
           ADD +1   TO PV-ERROR-PAGE-NMBR.                              06461800
           MOVE 1   TO DP150O-REPORT-NUMBER.                            06461910
           MOVE PV-ERROR-PAGE-NMBR TO DP150O-PAGE-NUMBER.               06462010
           MOVE 'INKRP160' TO DP150O-PROGRAM-NAME.                      06462110
                                                                        06462200
           PERFORM 5100-WRITE-ERR-HEADINGS.                             06462301
                                                                        06462800
      ***************************************************************** 06462900
      *  CLOSE FILES AND DISPLAY PROGRAM TOTALS.                        06463000
      *  DISCONNECT FROM QUEUE MANAGER.                                 06464000
      ***************************************************************** 06465000
       3000-EOJ-PROCESSING.                                             06466000
                                                                        06467000
           DISPLAY PC-FIFTY-STARS.                                      06471000
           DISPLAY 'END OF JOB COUNTERS'.                               06480000
           DISPLAY PC-FIFTY-STARS.                                      06490100
           DISPLAY 'NUMBER OF QUEUE MESSAGES: ', PA-Q-MESSAGES.         06490200
           DISPLAY 'NUMBER OF QUEUE MESSAGES WRITTEN TO GDG: '          06491020
                                                 PA-WRITTEN.            06492020
           DISPLAY PC-FIFTY-STARS.                                      06521000
                                                                        06530000
           PERFORM 1600-MQ-ERR-INPUT-CLOSE.                             06540001
           PERFORM 1700-DISCONNECT-QMGR.                                06550000
                                                                        06560000
           CLOSE ERROR-MSG-FILE                                         06570015
                 ERROR-MSG-RPT.                                         06571015
                                                                        06580000
      ***************************************************************** 06590000
      *  WRITE THE ERROR MESSAGE RECORD.                                06600002
      ***************************************************************** 06610000
       4000-WRITE-ERR-MSG-RECORD.                                       06620015
                                                                        06630000
           MOVE IMRD008-RECORD   TO ERROR-MSG-RECORD.                   06631015
           WRITE ERROR-MSG-RECORD.                                      06640016
                                                                        06641100
           INITIALIZE ERROR-MSG-RECORD.                                 06641215
                                                                        06641300
           ADD +1 TO PA-WRITTEN.                                        06642020
                                                                        06660000
      ***************************************************************** 06660115
      *  WRITE THE ERROR MESSAGE RECORD.                                06660215
      ***************************************************************** 06660315
       5000-WRITE-ERR-RECORD.                                           06660415
                                                                        06660515
           WRITE ERROR-MSG-RPT-LINE FROM DL1-DETAIL-LINE                06660615
              AFTER ADVANCING 2 LINES.                                  06660715
                                                                        06660815
           INITIALIZE DL1-DETAIL-LINE.                                  06660915
                                                                        06661015
      ***************************************************************** 06661315
      *  WRITE THE ERROR RECORD HEADINGS.                               06662002
      ***************************************************************** 06663000
       5100-WRITE-ERR-HEADINGS.                                         06664001
                                                                        06665000
           WRITE ERROR-MSG-RPT-LINE FROM                                06666002
              DP150O-STANDRD-HEADER-150-COLS                            06666110
                AFTER ADVANCING PAGE.                                   06667000
                                                                        06667100
           MOVE SPACES TO DP150O-LASER-NEW-PAGE-INDICATR.               06667210
                                                                        06667300
           WRITE ERROR-MSG-RPT-LINE FROM                                06667402
              HL1-HEADING-LINE                                          06667500
                AFTER ADVANCING 1 LINE.                                 06667600
                                                                        06668000
           MOVE IM008-CRE-TMST (1:10) TO HL2-PROCESS-DATE.              06670003
           WRITE ERROR-MSG-RPT-LINE FROM HL2-HEADING-LINE               06670102
              AFTER ADVANCING 2 LINES.                                  06670203
                                                                        06670300
           WRITE ERROR-MSG-RPT-LINE FROM HL3-HEADING-LINE               06670403
              AFTER ADVANCING 3 LINES.                                  06670503
                                                                        06670603
      ***************************************************************** 06672003
      * READ CONTROL CARD TO OBTAIN THE INPUT QUEUE MGR NAME.           06680000
      ***************************************************************** 06690000
       8000-READ-CONTROL-CARD.                                          06700000
                                                                        06710000
            READ CONTROL-CARD INTO CC-CONTROL-CARD                      06720000
                AT END                                                  06730000
                    SET PS-CONTROL-CARD-END TO TRUE.                    06740000
                                                                        06750000
      ***************************************************************** 06760000
      * READ SECOND CONTROL CARD TO OBTAIN THE INPUT QUEUE NAME.        06770000
      ***************************************************************** 06780000
       8010-READ-CONTROL-CARD-2.                                        06790000
                                                                        06800000
            READ CONTROL-CARD-2 INTO CC-CONTROL-CARD-2                  06810000
                AT END                                                  06820000
                    SET PS-CONTROL-CARD-2-END TO TRUE.                  06830000
                                                                        06840000
      ***************************************************************** 06850000
      * GET ADJUSTMENT MESSAGE FROM MQ SERIES.                          06860000
      ***************************************************************** 06870000
       8100-MQGET-MSG.                                                  06880000
                                                                        06890000
           INITIALIZE PS-MQGET-SUCCESSFUL-SW                            06900000
                      PS-END-OF-Q-IND                                   06910000
                      PV-MSGBUFFER.                                     06920006
                                                                        06940000
           MOVE MQMI-NONE              TO MQMD-MSGID.                   06950000
           MOVE MQCI-NONE              TO MQMD-CORRELID.                06960000
           MOVE CC-ERR-Q-NAME          TO MQOD-OBJECTNAME.              06970001
           COMPUTE MQGMO-OPTIONS  = MQGMO-SYNCPOINT +                   06980000
                                    MQGMO-FAIL-IF-QUIESCING.            06990000
                                                                        07000000
           CALL WS-MQGET USING PV-HCONN                                 07010000
                               PV-HANDLE                                07020001
                               MQM-MESSAGE-DESCRIPTOR                   07030000
                               MQM-GET-MESSAGE-OPTIONS                  07040000
                               PV-MSGLENGTH                             07050000
                               PV-MSGBUFFER                             07060000
                               PV-DATA-LENGTH                           07070000
                               PV-COMPLETION-CODE                       07080000
                               PV-REASON.                               07090000
                                                                        07100000
      *        IF GET FAILED THEN DISPLAY ERROR MESSAGE                 07110000
      *        AND BREAK OUT OF LOOP                                    07120000
                                                                        07130000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK)                        07140000
              IF (PV-REASON NOT = MQRC-NO-MSG-AVAILABLE)                07150000
                 MOVE PV-COMPLETION-CODE TO PH-COMPLETION-CODE          07160015
                 MOVE PV-REASON          TO PH-REASON                   07170015
                 PERFORM 8600-BACK-OUT-MQ                               07180015
                 PERFORM 1600-MQ-ERR-INPUT-CLOSE                        07190015
                 PERFORM 1700-DISCONNECT-QMGR                           07200015
                 MOVE 'MQGET ERROR'      TO PV-ERROR-MESSAGE            07210015
                 MOVE PH-COMPLETION-CODE TO PV-COMPLETION-CODE          07220015
                 MOVE PH-REASON          TO PV-REASON                   07230015
                 DISPLAY 'PV-MSGLENGTH = ' PV-MSGLENGTH                 07240015
                 DISPLAY 'PV-MSGBUFFER = ' PV-MSGBUFFER                 07250015
                 DISPLAY 'PV-DATA-LENGTH = ' PV-DATA-LENGTH             07260015
                 PERFORM 9000-DISPLAY-MQ-ERROR                          07270015
              ELSE                                                      07280000
                 DISPLAY 'MQGET - END OF MQ MESSAGES '                  07290015
                 SET PS-END-OF-Q TO TRUE                                07291015
              END-IF                                                    07300000
           ELSE                                                         07310000
              SET PS-MQGET-SUCCESSFUL TO TRUE                           07320000
              MOVE PV-MSGBUFFER TO IMRD008-RECORD                       07360003
              ADD +1 TO PA-Q-MESSAGES                                   07371000
           END-IF.                                                      07390000
                                                                        07400000
      ******************************************************************07410000
      *  COMMIT MQ PROCESSING SINCE THE POINT OF THE LAST COMMIT       *07420000
      ******************************************************************07430000
       8500-COMMIT-MQ.                                                  07440000
                                                                        07450000
           CALL WS-MQCMIT USING PV-HCONN                                07460000
                                PV-COMPLETION-CODE                      07470000
                                PV-REASON.                              07480000
                                                                        07490000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK)                        07500000
              MOVE '8500-COMMIT-MQ'  TO PV-PARAGRAPH                    07510000
              MOVE 'MQCMIT ERROR'    TO PV-ERROR-MESSAGE                07520000
              MOVE PV-COMPLETION-CODE TO PH-COMPLETION-CODE             07530000
              MOVE PV-REASON          TO PH-REASON                      07540000
              PERFORM 8600-BACK-OUT-MQ                                  07550000
              PERFORM 1600-MQ-ERR-INPUT-CLOSE                           07560001
              PERFORM 1700-DISCONNECT-QMGR                              07570000
              MOVE PH-COMPLETION-CODE TO PV-COMPLETION-CODE             07580000
              MOVE PH-REASON          TO PV-REASON                      07590000
              PERFORM 9000-DISPLAY-MQ-ERROR                             07600000
           END-IF.                                                      07610000
                                                                        07620000
      ******************************************************************07630000
      * BACK OUT MQ PROCESSING SINCE THE POINT OF THE LAST COMMIT      *07640000
      ******************************************************************07650000
       8600-BACK-OUT-MQ.                                                07660000
                                                                        07670000
           CALL WS-MQBACK USING PV-HCONN                                07680000
                                PV-COMPLETION-CODE                      07690000
                                PV-REASON.                              07700000
                                                                        07710000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   07720000
              MOVE '8600-BACK-OUT-MQ'  TO PV-PARAGRAPH                  07730000
              MOVE 'MQBACK ERROR'    TO PV-ERROR-MESSAGE                07740000
              MOVE PV-COMPLETION-CODE TO PH-COMPLETION-CODE             07750000
              MOVE PV-REASON          TO PH-REASON                      07760000
              PERFORM 1600-MQ-ERR-INPUT-CLOSE                           07770001
              PERFORM 1700-DISCONNECT-QMGR                              07780000
              MOVE PH-COMPLETION-CODE TO PV-COMPLETION-CODE             07790000
              MOVE PH-REASON          TO PV-REASON                      07800000
              PERFORM 9000-DISPLAY-MQ-ERROR                             07810000
           END-IF.                                                      07820000
                                                                        07830000
      ***************************************************************** 07840000
      * DISPLAY THE VALUES OF A FAILED MQ API COMMAND                   07850000
      ***************************************************************** 07860000
       9000-DISPLAY-MQ-ERROR.                                           07870000
                                                                        07880000
           DISPLAY '************************************************'.  07890000
           DISPLAY '**** ABEND *************************************'.  07900000
           DISPLAY '************************************************'.  07910000
           DISPLAY '** MQSERIES ERROR **'                               07920000
           DISPLAY '** PROGRAM:    ', PC-PGM-NAME                       07930000
           DISPLAY '** PARAGRAPH:  ', PV-PARAGRAPH                      07940000
           DISPLAY '** MESSAGE:    ', PV-ERROR-MESSAGE.                 07950000
           DISPLAY '** COMP CODE:  ', PV-COMPLETION-CODE.               07960000
           DISPLAY '** RSN CODE:   ', PV-REASON.                        07970000
           DISPLAY '************************************************'.  07980000
           DISPLAY '**** PROGRAM COUNTERS AT TIME OF ABEND *********'.  07990000
           DISPLAY '************************************************'.  08000000
           DISPLAY '** NUMBER OF Q MESSAGES PROCESSED: ', PA-Q-MESSAGES.08010000
           DISPLAY '** NUMBER OF Q MESSAGES TO GDG: ',                  08020020
                                                      PA-WRITTEN.       08021020
           DISPLAY '************************************************'.  08030000
           SET AC-MQ-ERROR TO TRUE.                                     08040000
           CALL 'ILBOABN0' USING ABEND-CODE.                            08050000
                                                                        08060000
