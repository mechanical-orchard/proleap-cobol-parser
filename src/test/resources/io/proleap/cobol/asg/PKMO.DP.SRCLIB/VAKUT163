       ID  DIVISION.                                                            
W34517 PROGRAM-ID.         VAKUT163.                                            
       AUTHOR.             JEFF HARTIG/COMPUWARE.                               
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       06/09/98.                                            
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
W34517*  CLONE OF INKUT163 USING NEW VERSION OF MNLYSUM TABLE                   
      ******************************************************************        
      *               LEDGER CUTOFF LOAD TO TINSCLS                             
      *                                                                         
      *  THIS PROGRAM READS THE MLT-MNLY-VND TABLE FOR THE OPEN                 
      *  FISCAL MTH AND LOADS DATA TO TINSCLS, FOR STORES THAT ARE              
      *  DOING INVENTORY AND HAVE A NON-ZERO SALES RETAIL AMOUNT,               
      *  SHRINKAGE RETAIL AMOUNT OR END INVENTORY RETAIL AMOUNT.                
      *                                                                         
      *  THE LEDGER-CUTOFF-TIMESTAMP ON THE TINVPAR TABLE IS ALSO               
      *  UPDATED.                                                               
      *                                                                         
      ******************** HISTORY OF CHANGES **************************        
      *                                                                *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
W34517* W34517  10-14-2008  INFOSYS    CLONE OF INKUT163 FOR VA INVNTRY         
W34517*                                STREAM. NEW VERSION OF MNLYSUM           
W34517*                                TABLE TO BE USED.                        
W33304* W33304  09-25-2008  D. THEEL   HILO CHANGES INCLUDING:                  
W33304*         ADDED LOGIC TO TINVPAR CURSOR TO EXCLUDE DELETED                
W33304*         STORES AND STORES THAT WERE ALREADY CUT OVER TO LEDGER.         
W33304*         ADDED LOGIC WHEN A STORE IS FOUND ON INTERNAL TABLE,            
W33304*         CHECK IF IT EXISTS ON THE TINSCLS                               
W33304*         TABLE FOR THE CURRENT MONTH AND DELETE ANY ROWS FOUND.          
W33304*         ADDED NEW COLUMN ML_PSTG_MN_END_DTE TO INSERT OF                
W33304*         TINSCLS.  ADDED DISPLAY TO END OF JOB                           
W33304*         FOR NUMBER OF TINSCLS ROWS DELETED.                             
W33304*         ANY ROWS DELETED WILL BE FIRST WRITTEN TO AN OUTPUT FILE        
W33304*         IN TABLE DCLGEN FORMAT. IF NO STORES ARE BEING CUT OVER,        
W33304*         WE WILL SEND OUT A 4095 RETURN CODE SO REST OF STEPS IN         
W33304*         JOB DO NOT RUN.                                                 
W34557* WR34557 09-13-2011 KARTHIKEYAN M                                        
W34557*         THIS PROCESS IS CHANGED TO USE ALTERNATE DATE LOGIC             
W34557*         DURING THE CUT OFF (CUTOVER) PROCESS. A NEW DATE CONTROL        
W34557*         FILE IS USED TO CONTROL THE PROCESS. DURING CLOSING WEEK        
W34557*         WEEK, THE ML LEDGER PARTITIONS STILL NEED TO POINT TO           
W34557*         THE CLOSING MONTH WHILE THE INSERTS TO THE INVENTORY            
W34557*         LEDGER NEED TO HAVE THE NEW MONTH DATES.                        
W34557*         IN ADDITION, STORES BEING CUT OVER DURING CLOSING               
W34557*         WEEK NEED CUTOVER SALES AND SHRINK SET TO ZEROS                 
W34557*         BECAUSE THERE ARE NO SALES AND SHRINK YET POSTED FOR            
W34557*         THE NEXT MONTH. EIR CONTINUES TO BE TAKEN FROM THE              
W34557*         MONTH TO BE CLOSED.                                             
W34557*                                                                         
W34557*                                                                         
W34557* PLEASE SEE INRD001 FOR EXAMPLES OF THE ALTERNATE DATE CARD              
W34557* AND ITS VALUES                                                          
W34557*                                                                         
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
W33304     SELECT TINSCLS-DELETE                                                
W33304         ASSIGN TO OUT01.                                                 
                                                                                
W34557     SELECT ALTERNATE-DATE-CNTL-FILE                                      
W34557         ASSIGN TO INP01.                                                 
W34557                                                                          
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
W33304 FD  TINSCLS-DELETE                                                       
W33304     LABEL RECORDS ARE STANDARD                                           
W33304     RECORDING MODE IS F                                                  
W33304     BLOCK CONTAINS 0 RECORDS.                                            
W33304 01  TINSCLS-DELETE-RECORD   PIC X(131).                                  
                                                                                
W34557 FD  ALTERNATE-DATE-CNTL-FILE                                             
W34557     LABEL RECORDS ARE STANDARD                                           
W34557     RECORDING MODE F                                                     
W34557     BLOCK CONTAINS 0 RECORDS.                                            
W34557                                                                          
W34557 01  ALTERNATE-DATE-CNTL-REC       PIC X(80).                             
W34557                                                                          
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ****************************************************************          
      *  ERROR MESSAGE AREA FOR DB2                                             
      ****************************************************************          
                                                                                
           COPY DPWS004.                                                        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-OPEN-FSCL-MN-SW    PIC  X     VALUE 'N'.               
               88  PS-END-OF-OPEN-FSCL-MN              VALUE 'Y'.               
           05  PS-END-OF-TINVPAR-SW         PIC  X     VALUE 'N'.               
               88  PS-END-OF-TINVPAR                   VALUE 'Y'.               
W20445     05  PS-END-OF-TMLT-MNLY-SUM-SW   PIC  X     VALUE 'N'.               
W20445         88  PS-END-OF-TMLT-MNLY-SUM             VALUE 'Y'.               
W33304     05  PS-END-OF-TINSCLS-SW         PIC  X     VALUE 'N'.               
W33304         88  PS-END-OF-TINSCLS                   VALUE 'Y'.               
           05  ST-STORE-MATCH-SW            PIC  X     VALUE 'N'.               
               88  ST-STORE-MATCH                      VALUE 'Y'.               
               88  ST-NO-STORE-MATCH                   VALUE 'N'.               
           05  PS-FIRST-TIME-SW                PIC X     VALUE 'N'.             
               88  PS-FIRST-TIME-THRU                    VALUE 'Y'.             
           05  PS-FIRST-COMMIT-SW              PIC X     VALUE 'N'.             
               88  PS-FIRST-COMMIT                       VALUE 'Y'.             
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES          PIC S9(03) VALUE +3.                     
           05  PC-INVENTORY-DATE       PIC  X(10) VALUE '9999-09-09'.           
W34517     05  PC-JOB-NAME             PIC  X(06)    VALUE 'VAK885'.            
W34517     05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'VAKUT163'.          
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
W20445     05  PV-EDIT-SUB-CLASS       PIC 9(03)  VALUE ZEROS.                  
W20445     05  PV-EDIT-SUB-CLASS-RED REDEFINES                                  
W20445         PV-EDIT-SUB-CLASS.                                               
W20445         10  PV-EDIT-CLASS-P1    PIC X(01).                               
W20445         10  PV-EDIT-CLASS-P2    PIC X(02).                               
W20445     05  PV-EDIT-DEPT-NBR        PIC 9(03)  VALUE ZEROS.                  
W20445     05  PV-EDIT-DEPT-NBR-RED REDEFINES                                   
W20445         PV-EDIT-DEPT-NBR        PIC X(03).                               
           05  PV-RETRY-COUNT          PIC S9(04) COMP SYNC                     
                                                  VALUE ZERO.                   
W34517     05  PV-PROGRAM-NAME         PIC  X(08) VALUE 'VAKUT163'.             
           05  PV-PARAGRAPH-NAME       PIC  X(30) VALUE SPACE.                  
           05  PV-CURRENT-TIMESTAMP    PIC  X(26) VALUE SPACES.                 
           05  PV-COMMIT-TIMESTAMP     PIC  X(26) VALUE SPACES.                 
           05  PV-DB2-OPER-ATTEMPTED   PIC  X(30) VALUE SPACE.                  
           05  PV-SQL-SUB              PIC S9(04) VALUE ZERO  COMP.             
W20445     05  PV-SLS-RTL-AMT          PIC S9(11)V9(2) COMP-3.                  
W20445     05  PV-SHNK-RTL-AMT         PIC S9(11)V9(2) COMP-3.                  
W20445     05  PV-END-INV-RTL-AMT      PIC S9(11)V9(2) COMP-3.                  
W26600     05  PV-STORE-NBR            PIC X(05)  VALUE ZERO.                   
           05  PV-STORE-NBR-N REDEFINES PV-STORE-NBR                            
                                       PIC 9(05).                               
W26600     05  PV-STORE-SUB            PIC S9(04) VALUE ZERO  COMP.             
W33304     05  PV-DB2-TINSCLS-DEL      PIC  9(09) VALUE ZERO.                   
           05  PV-DB2-TINSCLS-INS      PIC  9(09) VALUE ZERO.                   
W26600     05  PV-MAX-STORE-COUNT      PIC S9(04) COMP VALUE +9999.             
           05  PA-COMMIT-COUNT         PIC  9(09) VALUE ZEROES.                 
W33304     05  PV-OUTPUT-RECS          PIC  9(09) VALUE ZEROES.                 
W33304     05  PV-TINSCLS-FETCH        PIC  9(09) VALUE ZEROES.                 
W33304     05  PV-TINVPAR-FETCH        PIC  9(09) VALUE ZEROES.                 
W33304     05  PV-MANUAL-TO-COMMIT-CNT PIC  9(09) VALUE ZEROES.                 
W33304     05  PV-MANUAL-COMMIT-VALUE  PIC  9(09) VALUE 1500.                   
W33304     05  PV-MANUAL-TOTAL-COMMITS PIC  9(09) VALUE ZEROES.                 
           05  PV-TINSCLS-UPDATE-VARIABLES.                                     
               10  PV-HOLD-FISCAL-NBR          PIC  X(02) VALUE SPACES.         
               10  PV-HOLD-FISCAL-MN-END-DTE   PIC  X(10) VALUE SPACES.         
W20445         10  PV-HOLD-DEPT-NBR            PIC  S9(4)V COMP-3.              
W20445         10  PV-HOLD-SUB-CL-NBR          PIC  S9(4)V COMP-3.              
               10  PV-HOLD-STR-NBR             PIC  S9(04) COMP.                
W20445***      10  PV-HOLD-ENT-ID              PIC  S9(09)  COMP.               
W20445         10  PV-ML-LWST-ID               PIC  S9(09)  COMP.               
W30692     05  PV-EDIT-CUTOFF-TIMESTAMP PIC X(26) VALUE SPACES.                 
W33304     05  PA-COUNT                    PIC S9(7)    VALUE ZERO              
W33304                                                  COMP-3.                 
W33304 01  ERROR-CODE                       PIC S9(04)  VALUE +0                
                                                         COMP SYNC.             
                                                                                
       01  ABEND-CODE                  PIC S9(04) VALUE 0     COMP SYNC.        
                                                                                
      ******************************************************************        
      *  STORE TABLE IS USED TO HOLD THE STORE NUMBERS OF THE STORES TO         
W33304*  BE INVENTORIED AND THEIR ASSOCIATED INVENTORY IDS.                     
      ******************************************************************        
       01  WS-STORE-TABLE.                                                      
W33304     05  WS-STORE-TAB OCCURS 9999 TIMES INDEXED BY STR-IDX.               
W26600         10  WS-INV-STORE            PIC S9(04) COMP.                     
W33304         10  WS-INV-ID               PIC S9(09) COMP.                     
                                                                                
      ******************************************************************        
      *  M/L OPEN FISCAL MONTH CONTROL TABLE                                    
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
                                                                                
W20445******************************************************************        
W20445*  M/L DETAIL LEDGER PARTITION BALANCE TBL (MLT_PARTN_BAL)                
W20445******************************************************************        
W20445     EXEC SQL                                                             
W20445         INCLUDE MLT00002                                                 
W20445     END-EXEC.                                                            
W20445                                                                          
W20445******************************************************************        
W20445*  M/L DETAIL LEDGER PARTITION CONTROL TBL (MLT_PARTN_CNTL)               
W20445******************************************************************        
W20445     EXEC SQL                                                             
W20445         INCLUDE MLT00003                                                 
W20445     END-EXEC.                                                            
W20445                                                                          
      ******************************************************************        
W34517*  M/L MONTHLY DETAIL LEDGER SUMMARY TABLE (MLT_MNLY_VND)                 
      ******************************************************************        
           EXEC SQL                                                             
W34517*        INCLUDE MLT00004                                                 
W34517         INCLUDE MLT00018                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  INVENTORY MONTHLY SUBCLASS TABLE                                       
      ******************************************************************        
           EXEC SQL                                                             
W20445         INCLUDE TINSCLS                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  INVENTORY PARAMETERS TABLE                                             
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
W28545******************************************************************        
W28545*  INVENTORY CYCLE CONTROL TABLE                                          
W28545******************************************************************        
W28545     EXEC SQL                                                             
W28545         INCLUDE TINCNTL                                                  
W28545     END-EXEC.                                                            
W28545                                                                          
      ****************************************************                      
      *CHECKPOINT RESTART CONTROL AND INFORMATION TABLES *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TCKRSTCN                                                 
           END-EXEC.                                                            
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TCKRSTIN                                                 
           END-EXEC.                                                            
W34557                                                                          
W34557*****************************************************************         
W34557*  DB2 DATE ATTRIBUTES                                                    
W34557*****************************************************************         
W34557                                                                          
W34557     EXEC SQL                                                             
W34557         INCLUDE TDTATTR                                                  
W34557     END-EXEC.                                                            
W34557                                                                          
W34557*****************************************************************         
W34557*  COPYBOOK OF INPUT ALTERNATE DATE CONTROL CARD RECORD LAYOUT  *         
W34557*****************************************************************         
W34557*                                                                         
W34557     COPY INRD001.                                                        
W34557                                                                          
W34557*****************************************************************         
W34557*  COPYBOOK OF FIELDS USED IN THE EDIT / VALIDATE PROCESS.      *         
W34557*****************************************************************         
W34557                                                                          
W34557     COPY INWS050.                                                        
      *                                                                         
      *-----------------------------------------------------------------        
      * DB2 INVENTORY PARAMETERS CURSOR DECLARATION                             
      *                                                                         
      * SELECT EACH STORE WHICH WILL BE INVENTORIED.                            
      * ACTL_INV_DTE = '9999-09-09'                                             
      *-----------------------------------------------------------------        
                                                                                
           EXEC SQL                                                             
W33304         DECLARE TINVPAR-CSR CURSOR WITH HOLD FOR                         
W26600             SELECT   LOC_NBR,                                            
W28545                      A.INV_ID,                                           
                            ACTL_LDG_CTOF_TMST,                                 
W34557                      OPN_FSCL_MN_DTE                                     
W28545             FROM     TINVPAR A                                           
W28545                     ,TINCNTL B                                           
                   WHERE    ACTL_FIN_BK_DTE  = :PC-INVENTORY-DATE               
W33304             AND      A.LOC_INV_STAT_CDE = 'IN'                           
W33304             AND      SCHD_PHY_CNT_CDE <> 'EI'                            
W33304             AND      A.ACTL_LDG_CTOF_TMST =                              
W33304                              '9999-12-31-23.59.59.999999'                
W28545             AND      B.ACTV_IND       = 'Y'                              
W28545             AND      A.INV_ID         = B.INV_ID                         
           END-EXEC.                                                            
                                                                                
      *-----------------------------------------------------------------        
W20445* DB2 M/L MONTHLY DETAIL LEDGER SUMMARY CURSOR DECLARATION                
      *                                                                         
      * SELECT EACH LEDGER ROW WHICH HAS A NON-ZERO AMOUNT FOR THE              
      * OPEN FISCAL MONTH                                                       
      *                                                                         
      * FOR CHECK-POINT RESTART PURPOSES, THE CURSOR WILL START WITH            
      * STORES GREATER THAN THE PV-HOLD-STR FIELD.  FOR NORMAL                  
      * PROCESSING THE PV-HOLD-STR WILL BE ZERO, FOR RESTART PROCESSING         
      * THE PV-HOLD-STR WILL CONTAIN THE LAST STORE PROCESSED.                  
      *-----------------------------------------------------------------        
                                                                                
           EXEC SQL                                                             
W20445       DECLARE MLT-MNLY-SUM-CSR CURSOR WITH HOLD FOR                      
W20445         SELECT FSCL_MN_END_DTE,                                          
W20445                DEPT_NBR,                                                 
W20445                SUB_CL_NBR,                                               
W20445                LOC_NBR,                                                  
S28582                ML_LO_MDSE_LVL_ID,                                        
W20445                SLS_REG_RTL_AMT,                                          
W20445                SLS_PROMO_RTL_AMT,                                        
W20445                SLS_PROCLR_RTL_AMT,                                       
W20445                SLS_CLR_RTL_AMT,                                          
W20445                SLS_OTH_RTL_AMT,                                          
W20445                SHNK_RTL_AMT,                                             
W20445                END_INV_RTL_AMT                                           
W34517         FROM MLT_MNLY_VND                                                
W20445         WHERE PARTN_NBR IN                                               
W20445               (SELECT DISTINCT PARTN_NBR                                 
W20445                FROM MLT_PARTN_CNTL                                       
W20445                WHERE TBL_TM_LVL_CDE = 'M'                                
W20445                  AND FSCL_DTE = :MLCNTL-OPN-FSCL-MN-DTE)                 
W20445           AND LOC_NBR > :PV-HOLD-STR-NBR                                 
W20445           AND ((SLS_REG_RTL_AMT                                          
W20445                + SLS_PROMO_RTL_AMT                                       
W20445                + SLS_PROCLR_RTL_AMT                                      
W20445                + SLS_CLR_RTL_AMT                                         
W20445                + SLS_OTH_RTL_AMT) <> 0                                   
W20445            OR SHNK_RTL_AMT    <> 0                                       
W20445            OR END_INV_RTL_AMT <> 0)                                      
               ORDER BY FSCL_MN_END_DTE,                                        
                        DEPT_NBR,                                               
                        SUB_CL_NBR,                                             
                        LOC_NBR,                                                
                        ML_LO_MDSE_LVL_ID                                       
           END-EXEC.                                                            
                                                                                
      *-----------------------------------------------------------------        
W33304* DB2 SUB CLASS INVENTORY CURSOR DECLARATION                              
      *-----------------------------------------------------------------        
W33304     EXEC SQL                                                             
W33304       DECLARE TINSCLS-CSR CURSOR WITH HOLD FOR                           
W33304         SELECT DEPT_NBR                                                  
W33304               ,SUB_CL_NBR                                                
W33304               ,IN_LO_MDSE_LVL_ID                                         
W33304               ,SLS_RTL_AMT                                               
W33304               ,MKDN_RTL_AMT                                              
W33304               ,MKUP_RTL_AMT                                              
W33304               ,XFER_IN_RTL_AMT                                           
W33304               ,XFER_OUT_RTL_AMT                                          
W33304               ,RCPT_RTL_AMT                                              
W33304               ,PADJ_RTL_AMT                                              
W33304               ,SHNK_RTL_AMT                                              
W33304               ,END_INV_RTL_AMT                                           
W33304               ,CTOFF_SLS_AMT                                             
W33304               ,CTOFF_SHNK_AMT                                            
W33304               ,CTOFF_END_INV_AMT                                         
OCFSFS               ,STR_PARL_SHIP_RTL_AMT                                     
W33304               ,STR_EXPTED_RTL_AMT                                        
W33304               ,ML_PSTG_MN_END_DTE                                        
W33304         FROM TINSCLS                                                     
W33304         WHERE FSCL_MN_NBR = :INSCLS-FSCL-MN-NBR                          
W33304           AND FSCL_MN_END_DTE = :INSCLS-FSCL-MN-END-DTE                  
W33304           AND STR_NBR = :INSCLS-STR-NBR                                  
           END-EXEC.                                                            
      ******************************************************************        
      * CHECKPOINT RESTART VARIABLES                                   *        
      ******************************************************************        
       01  CR-CHECKPOINT-RESTART-AREA.                                          
W20445     05  CR-AREA-LEN                  PIC S9(04) VALUE +33  COMP.         
           05  CR-CHECKPOINT-RESTART-VAR.                                       
W02445         10  CR-PREV-DTL-KEY          PIC  X(24) VALUE SPACES.            
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                            
                   15  CR-FISCAL-NBR        PIC  X(02).                         
                   15  CR-FISCAL-MN-END-DTE PIC  X(10).                         
W20445             15  CR-DEPT-NBR          PIC  S9(4)V COMP-3.                 
W20445             15  CR-SUB-CL-NBR        PIC  S9(4)V COMP-3.                 
                   15  CR-STR-NBR           PIC  S9(04) COMP.                   
S28582*            15  CR-ENT-ID            PIC  S9(09)  COMP.                  
S28582             15  CR-ML-LWST-ID        PIC  S9(09)  COMP.                  
               10  CR-COMMIT-COUNT          PIC  9(09) VALUE ZEROES.            
           EJECT                                                                
      *-----------------------------------------------------------------        
      * DB2 SQL COMMUNICATIONS AREA                                             
      *-----------------------------------------------------------------        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
           EJECT                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAINLINE.                                                           
                                                                                
W34557*W33304     OPEN OUTPUT TINSCLS-DELETE.                                   
W34557     OPEN  INPUT ALTERNATE-DATE-CNTL-FILE                                 
W34557          OUTPUT TINSCLS-DELETE.                                          
W34557     PERFORM 0050-INIT.                                                   
           PERFORM 0100-GET-OPN-FSCL-MN.                                        
                                                                                
W34557*    DISPLAY 'OPEN FISCAL MONTH: '                                        
W34557     DISPLAY 'MLCNTL OPEN FISCAL MONTH: '                                 
               MLCNTL-OPN-FSCL-MN-DTE                                           
W34557     DISPLAY 'IN001  OPEN FISCAL MONTH: '                                 
W34557         IN001-OPN-FSCL-MN-DTE                                            
W34557     DISPLAY 'IN001  OPEN FISCAL WEEK DATE: '                             
W34557         IN001-OPN-FSCL-WK-DTE                                            
W34557*    DISPLAY '    OPEN FISCAL MONTH NBR:  '                               
W34557     DISPLAY 'MLCNTL OPEN FISCAL MONTH NBR:  '                            
               MLCNTL-OPN-FSCL-MN-NBR.                                          
W34557     DISPLAY 'IN001 OPEN FISCAL MONTH NBR:  '                             
W34557         IN001-OPN-FSCL-MN-NBR.                                           
W30692                                                                          
W30692     STRING MLCNTL-OPN-FSCL-WK-DTE DELIMITED BY SIZE                      
W30692            '-23.59.59.999999' DELIMITED BY SIZE                          
W30692                         INTO PV-EDIT-CUTOFF-TIMESTAMP.                   
W30692                                                                          
W30692     DISPLAY '    CUTOFF TIMESTAMP     :  '                               
W30692         PV-EDIT-CUTOFF-TIMESTAMP.                                        
                                                                                
           PERFORM 8000-OPEN-TINVPAR-CSR.                                       
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
W33304             PERFORM 0110-LOAD-WS-STORE-TABLE                             
W33304                VARYING PV-STORE-SUB FROM 1 BY 1                          
                      UNTIL PS-END-OF-TINVPAR                                   
                   PERFORM 8200-CLOSE-TINVPAR-CURSOR                            
               WHEN SQLCODE = +100                                              
                   CONTINUE                                                     
           END-EVALUATE.                                                        
W33304     PERFORM 8150-UPDATE-TINVPAR                                          
W33304        VARYING PV-STORE-SUB FROM 1 BY 1                                  
W33304        UNTIL WS-INV-STORE(PV-STORE-SUB) = ZERO                           
W33304        OR PV-STORE-SUB > PV-MAX-STORE-COUNT.                             
      *                                                                         
      * CHECKPOINT/RESTART SET-UP AND PREPERATION                               
           PERFORM 7650-INIT-CHECKPOINT-SELECT.                                 
      *                                                                         
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                                   
                PERFORM 7700-SELECT-RESTART-INFO                                
                MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                           
                                              CR-CHECKPOINT-RESTART-VAR         
                DISPLAY 'RESTART - LAST STORE PROCESSED : ', CR-STR-NBR         
                MOVE CR-STR-NBR                     TO PV-HOLD-STR-NBR          
           ELSE                                                                 
               SET PS-FIRST-COMMIT TO TRUE                                      
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                             
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                          
                                 TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS '        
                   ' (AFTER THE PROCESSING FOR A STORE HAS COMPLETED).'         
           END-IF.                                                              
      *                                                                         
           SET PS-FIRST-TIME-THRU TO TRUE.                                      
      *                                                                         
      *--------------------------------------------------------------           
W20445* WHEN MLT-MNLY-SUM-CSR IS OPENED, THE FOLLOWING TO RETRIEVED:            
      *                                                                         
W20445* NORMAL MODE:  MLT-MNLY-SUM-CSR WILL GET DATA FOR STORES > 0             
W20445* RESTART MODE: MLT-MNLY-SUM-CSR WILL GET DATA FOR STORES >               
      *                          LAST STORE SUCCESSFULLY PROCESSED              
      *--------------------------------------------------------------           
      *                                                                         
W20445     PERFORM 8300-OPEN-MLT-MNLY-SUM-CSR.                                  
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
W20445             PERFORM 0120-PROCESS-TMLT-MNLY-SUM                           
W20445                UNTIL PS-END-OF-TMLT-MNLY-SUM                             
W20445             PERFORM 8500-CLOSE-MNLY-SUM-CURSOR                           
               WHEN SQLCODE = +100                                              
                   CONTINUE                                                     
           END-EVALUATE.                                                        
                                                                                
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                               
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                               
                                                                                
           DISPLAY PV-DB2-TINSCLS-INS, '  TINSCLS ROWS INSERTED'.               
W33304     DISPLAY PV-DB2-TINSCLS-DEL, '  TINSCLS ROWS DELETED'.                
W33304     DISPLAY PV-MANUAL-TOTAL-COMMITS,                                     
W33304                              'TOTAL MANUAL COMMITS'.                     
W33304     DISPLAY PV-OUTPUT-RECS,                                              
W33304                              'DELETE RECS WRITTEN'.                      
W33304     DISPLAY PV-TINSCLS-FETCH,                                            
W33304               'INVENTORY SUBCLASS ROWS FETCHED'.                         
                                                                                
W34557*W33304     CLOSE TINSCLS-DELETE.                                         
W34557     CLOSE ALTERNATE-DATE-CNTL-FILE                                       
W34557           TINSCLS-DELETE.                                                
           MOVE ERROR-CODE TO RETURN-CODE.                                      
           STOP RUN.                                                            
                                                                                
W34557*****************************************************************         
W34557* 0050-INIT.                                                              
W34557* PERFORMS DATE VALIDATION FOR ALTERNATE DATE CONTROL FILE                
W34557*                                                                         
W34557*****************************************************************         
W34557 0050-INIT.                                                               
W34557                                                                          
W34557     INITIALIZE IN001-RECORD.                                             
W34557                                                                          
W34557     MOVE 'VAKUT163'                  TO PV-IN050-PGM-NUMBER              
W34557     PERFORM 1100-EDIT-VALIDATE-CNTL.                                     
                                                                                
       0100-GET-OPN-FSCL-MN.                                                    
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                        
                    OR (SQLCODE         = +000)                                 
                    OR (SQLCODE         = +100)                                 
                                                                                
               EXEC SQL                                                         
                    SELECT                                                      
W30692                  OPN_FSCL_WK_DTE                                         
W30692                 ,OPN_FSCL_WK_NBR                                         
                       ,OPN_FSCL_MN_DTE                                         
                       ,OPN_FSCL_MN_NBR                                         
                    INTO                                                        
W30692                 :MLCNTL-OPN-FSCL-WK-DTE                                  
W30692                ,:MLCNTL-OPN-FSCL-WK-NBR                                  
                      ,:MLCNTL-OPN-FSCL-MN-DTE                                  
                      ,:MLCNTL-OPN-FSCL-MN-NBR                                  
                    FROM                                                        
                        TMLCNTL                                                 
W20445              WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE        = +000                                   
                                                                                
                   WHEN SQLCODE        = -911                                   
W33304             WHEN SQLCODE        = -913                                   
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE    '0100-GET-OPN-FSCL-MN'                           
                                                TO PV-PARAGRAPH-NAME            
                       MOVE    'SELECT ROW FROM TMLCNTL'                        
                                                TO PV-DB2-OPER-ATTEMPTED        
                       PERFORM Z400-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               DISPLAY '** MAX RETRIES EXCEEDED ON TMLCNTL **'                  
               MOVE    '0100-GET-OPN-FSCL-MN'                                   
                                                TO PV-PARAGRAPH-NAME            
               MOVE    'SELECT ROW FROM TMLCNTL'                                
                                                TO PV-DB2-OPER-ATTEMPTED        
               PERFORM Z400-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
W33304 0110-LOAD-WS-STORE-TABLE.                                                
      ******************************************************************        
W33304*  LOAD THE WORKING STORAGE STORE TABLE WITH THE STORES DOING             
W33304*  INVENTORY                                                              
      ******************************************************************        
                                                                                
           PERFORM 8100-FETCH-TINVPAR-CSR.                                      
                                                                                
           IF  PS-END-OF-TINVPAR                                                
               CONTINUE                                                         
           ELSE                                                                 
W26600         MOVE INVPAR-LOC-NBR   TO WS-INV-STORE(PV-STORE-SUB)              
W33304         MOVE INVPAR-INV-ID    TO WS-INV-ID(PV-STORE-SUB)                 
W26600         MOVE INVPAR-LOC-NBR   TO PV-STORE-NBR                            
W26600         DISPLAY 'INVENTORING STR: ' PV-STORE-NBR                         
W33304         PERFORM 1000-CK-TINSCLS                                          
W33304         IF SQLCODE = +0                                                  
W33304****OPEN CURSOR, FETCH, WRITE THIS ROW TO OUTPUT FILE, THEN               
W33304****DELETE IN A LOOP FOR EACH ROW FOR THIS STORE FOR THE CURRENT          
W33304****FISCAL MONTH AND FSCL MONTH END DATE                                  
W33304             PERFORM 1100-OPEN-TINSCLS-CSR                                
W33304             MOVE 'N' TO PS-END-OF-TINSCLS-SW                             
W33304             PERFORM 1200-FETCH-TINSCLS                                   
W33304                UNTIL PS-END-OF-TINSCLS                                   
W33304             PERFORM 1500-CLOSE-TINSCLS-CSR                               
W33304             PERFORM 1600-CK-MANUAL-COMMIT                                
W33304         END-IF                                                           
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------------      
      *  INSERT A ROW IN THE TINSCLS TABLE FOR EVERY ROW IN TMNSCSL THAT        
      *  MATCHES THE OPEN FISCAL MONTH AND THE STORE IS DOING INVENTORY E.      
      *-------------------------------------------------------------------      
W20445 0120-PROCESS-TMLT-MNLY-SUM.                                              
                                                                                
W20445     PERFORM 8400-FETCH-MLT-MNLY-SUM-CSR.                                 
      *                                                                         
W20445     IF  PS-END-OF-TMLT-MNLY-SUM AND                                      
               PS-FIRST-TIME-THRU                                               
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                               
                   DISPLAY 'NO RECORDS TO PROCESS AFTER RESTART'                
               ELSE                                                             
                   DISPLAY 'NO RECORDS TO SELECT FOR LOAD'                      
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
W20445     IF  PS-END-OF-TMLT-MNLY-SUM                                          
               CONTINUE                                                         
           ELSE                                                                 
W34517         IF MLT00018-LOC-NBR = PV-HOLD-STR-NBR                            
                  CONTINUE                                                      
               ELSE                                                             
                  IF NOT PS-FIRST-TIME-THRU                                     
                     PERFORM 7800-COMMIT-PROCESSING                             
                  END-IF                                                        
W20445            MOVE MLCNTL-OPN-FSCL-MN-NBR TO PV-HOLD-FISCAL-NBR             
W34517            MOVE MLT00018-FSCL-MN-END-DTE                                 
                                            TO PV-HOLD-FISCAL-MN-END-DTE-DTE    
W34517            MOVE MLT00018-DEPT-NBR    TO PV-HOLD-DEPT-NBR                 
W34517            MOVE MLT00018-SUB-CL-NBR  TO PV-HOLD-SUB-CL-NBR               
W34517            MOVE MLT00018-ML-LO-MDSE-LVL-ID                               
S28582                                      TO PV-ML-LWST-ID                    
W34517            MOVE MLT00018-LOC-NBR     TO PV-HOLD-STR-NBR                  
               END-IF                                                           
                                                                                
W34517         IF MLT00018-LOC-NBR = PV-HOLD-STR-NBR                            
                   IF PS-FIRST-TIME-THRU                                        
                       MOVE 'N' TO PS-FIRST-TIME-SW                             
                   END-IF                                                       
               END-IF                                                           
                                                                                
      *-------------------------------------------------------------------      
      *        ROWS ARE MOVED FOR ONLY STORES DOING INVENTORY                   
      *-------------------------------------------------------------------      
                                                                                
W33304         SET STR-IDX TO 1                                                 
W33304         SEARCH WS-STORE-TAB                                              
W33304            VARYING STR-IDX                                               
W33304            AT END                                                        
W33304                CONTINUE                                                  
W33304            WHEN WS-INV-STORE(STR-IDX) = MLT00018-LOC-NBR                 
W33304                PERFORM 0140-LOAD-TINSCLS                                 
W33304            WHEN WS-INV-STORE(STR-IDX) = ZERO                             
W33304                CONTINUE                                                  
W33304         END-SEARCH                                                       
W33304     END-IF.                                                              
                                                                                
       0140-LOAD-TINSCLS.                                                       
                                                                                
W34557*W20445    MOVE MLCNTL-OPN-FSCL-MN-NBR TO INSCLS-FSCL-MN-NBR.             
W34557*W34517    MOVE MLT00018-FSCL-MN-END-DTE TO INSCLS-FSCL-MN-END-DTE        
W34557*W33304                                INSCLS-ML-PSTG-MN-END-DTE.         
W34557     MOVE MLT00018-FSCL-MN-END-DTE TO INSCLS-ML-PSTG-MN-END-DTE.          
W34557     MOVE IN001-OPN-FSCL-MN-NBR  TO INSCLS-FSCL-MN-NBR.                   
W34557     MOVE IN001-OPN-FSCL-MN-DTE  TO INSCLS-FSCL-MN-END-DTE.               
W20445                                                                          
W34517     MOVE MLT00018-DEPT-NBR     TO PV-EDIT-DEPT-NBR.                      
W20445     MOVE PV-EDIT-DEPT-NBR-RED  TO INSCLS-DEPT-NBR.                       
W20445                                                                          
W34517     MOVE MLT00018-SUB-CL-NBR   TO PV-EDIT-SUB-CLASS.                     
W20445     MOVE PV-EDIT-CLASS-P2      TO INSCLS-SUB-CL-NBR.                     
W20445                                                                          
W34517     MOVE MLT00018-ML-LO-MDSE-LVL-ID                                      
S28582                                TO INSCLS-IN-LO-MDSE-LVL-ID.              
W34517     MOVE MLT00018-LOC-NBR      TO INSCLS-STR-NBR.                        
W34557                                                                          
W34557     IF INCNTL-OPN-FSCL-MN-DTE > MLCNTL-OPN-FSCL-MN-DTE                   
W34557         MOVE ZEROS             TO INSCLS-CTOFF-SHNK-AMT                  
W34557         MOVE ZEROS             TO INSCLS-CTOFF-SLS-AMT                   
W34557     ELSE                                                                 
W34557         COMPUTE PV-SLS-RTL-AMT =  MLT00018-SLS-REG-RTL-AMT               
W34557                                +  MLT00018-SLS-PROMO-RTL-AMT             
W34557                                +  MLT00018-SLS-PROCLR-RTL-AMT            
W34557                                +  MLT00018-SLS-CLR-RTL-AMT               
W34557                                +  MLT00018-SLS-OTH-RTL-AMT               
W34557         MOVE PV-SLS-RTL-AMT    TO INSCLS-CTOFF-SLS-AMT                   
W34557         MOVE MLT00018-SHNK-RTL-AMT                                       
W34557                                TO INSCLS-CTOFF-SHNK-AMT                  
W34557     END-IF.                                                              
W34557                                                                          
W34517*    COMPUTE PV-SLS-RTL-AMT =  MLT00018-SLS-REG-RTL-AMT                   
W34517*                           +  MLT00018-SLS-PROMO-RTL-AMT                 
W34517*                           +  MLT00018-SLS-PROCLR-RTL-AMT                
W34517*                           +  MLT00018-SLS-CLR-RTL-AMT                   
W34517*                           +  MLT00018-SLS-OTH-RTL-AMT.                  
W20445*    MOVE PV-SLS-RTL-AMT        TO INSCLS-CTOFF-SLS-AMT.                  
W34517*    MOVE MLT00018-SHNK-RTL-AMT TO INSCLS-CTOFF-SHNK-AMT.                 
W34557                                                                          
W34517     MOVE MLT00018-END-INV-RTL-AMT TO INSCLS-CTOFF-END-INV-AMT.   .       
           MOVE ZEROS                 TO INSCLS-SLS-RTL-AMT                     
                                         INSCLS-MKDN-RTL-AMT                    
                                         INSCLS-MKUP-RTL-AMT                    
                                         INSCLS-XFER-IN-RTL-AMT                 
                                         INSCLS-XFER-OUT-RTL-AMT                
                                         INSCLS-RCPT-RTL-AMT                    
                                         INSCLS-PADJ-RTL-AMT                    
                                         INSCLS-SHNK-RTL-AMT                    
OCFSFS                                   INSCLS-STR-PARL-SHIP-RTL-AMT           
W27808                                   INSCLS-STR-EXPTED-RTL-AMT              
                                         INSCLS-END-INV-RTL-AMT.                
                                                                                
           EXEC SQL                                                             
               INSERT INTO TINSCLS (FSCL_MN_NBR,                                
                                    FSCL_MN_END_DTE,                            
                                    DEPT_NBR,                                   
                                    SUB_CL_NBR,                                 
                                    STR_NBR,                                    
W20445*                             ENT_ID,                                     
W20445                              IN_LO_MDSE_LVL_ID,                          
                                    SLS_RTL_AMT,                                
                                    MKDN_RTL_AMT,                               
                                    MKUP_RTL_AMT,                               
                                    XFER_IN_RTL_AMT,                            
                                    XFER_OUT_RTL_AMT,                           
                                    RCPT_RTL_AMT,                               
                                    PADJ_RTL_AMT,                               
                                    SHNK_RTL_AMT,                               
                                    END_INV_RTL_AMT,                            
                                    CTOFF_SLS_AMT,                              
                                    CTOFF_SHNK_AMT,                             
OCFSFS                              STR_PARL_SHIP_RTL_AMT,                      
W27808                              STR_EXPTED_RTL_AMT,                         
                                    CTOFF_END_INV_AMT,                          
W33304                              ML_PSTG_MN_END_DTE)                         
                    VALUES         (:INSCLS-FSCL-MN-NBR,                        
                                    :INSCLS-FSCL-MN-END-DTE,                    
                                    :INSCLS-DEPT-NBR,                           
                                    :INSCLS-SUB-CL-NBR,                         
                                    :INSCLS-STR-NBR,                            
S28582*                             :INSCLS-ENT-ID,                             
W28582                              :INSCLS-IN-LO-MDSE-LVL-ID,                  
                                    :INSCLS-SLS-RTL-AMT,                        
                                    :INSCLS-MKDN-RTL-AMT,                       
                                    :INSCLS-MKUP-RTL-AMT,                       
                                    :INSCLS-XFER-IN-RTL-AMT,                    
                                    :INSCLS-XFER-OUT-RTL-AMT,                   
                                    :INSCLS-RCPT-RTL-AMT,                       
                                    :INSCLS-PADJ-RTL-AMT,                       
                                    :INSCLS-SHNK-RTL-AMT,                       
                                    :INSCLS-END-INV-RTL-AMT,                    
                                    :INSCLS-CTOFF-SLS-AMT,                      
                                    :INSCLS-CTOFF-SHNK-AMT,                     
OCFSFS                              :INSCLS-STR-PARL-SHIP-RTL-AMT,              
W27808                              :INSCLS-STR-EXPTED-RTL-AMT,                 
                                    :INSCLS-CTOFF-END-INV-AMT,                  
W33304                              :INSCLS-ML-PSTG-MN-END-DTE)                 
           END-EXEC.                                                            
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                       ADD 1 TO PV-DB2-TINSCLS-INS                              
                   WHEN SQLCODE = -803                                          
                       MOVE '0140-LOAD-TINSCLS'                                 
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'INSERT OF DUPLICATE ROW ATTEMPTED'                 
                                     TO  PV-DB2-OPER-ATTEMPTED                  
W34517                 DISPLAY 'MLT00018-FSCL-MN-END-DTE ',                     
W34517                          MLT00018-FSCL-MN-END-DTE                        
TEMP                   DISPLAY 'PV-EDIT-DEPT-NBR-RED ',                         
                                PV-EDIT-DEPT-NBR-RED                            
TEMP                   DISPLAY 'PV-EDIT-SUB-CLASS ',                            
                                PV-EDIT-SUB-CLASS                               
W34517                 DISPLAY 'MLT00018-ML-LO-MDSE-LVL-ID ',                   
W34517                          MLT00018-ML-LO-MDSE-LVL-ID                      
W34517                 DISPLAY 'MLT00018-LOC-NBR ',                             
W34517                          MLT00018-LOC-NBR                                
                       PERFORM Z400-SQL-ERROR                                   
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '0140-LOAD-TINSCLS'                                 
                                      TO  PV-PARAGRAPH-NAME                     
                        MOVE 'INSERT ON INVENTORY COPY TABLE'                   
                                      TO  PV-DB2-OPER-ATTEMPTED                 
                        PERFORM Z400-SQL-ERROR                                  
               END-EVALUATE.                                                    
      *                                                                         
W33304******************************************************************        
W33304* 1000-CK-TINSCLS.                                               *        
W33304* PROCESSES: EXISTENCE CHECK AGAINST INVENTORY SUBCLASS TABLE    *        
W33304* (TINSCLS)  FOR SPECIFIC STORE AND FISCAL MONTH                 *        
W33304******************************************************************        
W33304 1000-CK-TINSCLS.                                                         
W34557*W33304     MOVE MLCNTL-OPN-FSCL-MN-NBR TO INSCLS-FSCL-MN-NBR.            
W34557*W33304     MOVE MLCNTL-OPN-FSCL-MN-DTE TO INSCLS-FSCL-MN-END-DTE.        
W34557     MOVE IN001-OPN-FSCL-MN-NBR  TO INSCLS-FSCL-MN-NBR.                   
W34557     MOVE IN001-OPN-FSCL-MN-DTE  TO INSCLS-FSCL-MN-END-DTE.               
W33304     MOVE INVPAR-LOC-NBR      TO INSCLS-STR-NBR.                          
W33304     PERFORM                                                              
W33304         WITH TEST AFTER                                                  
W33304         VARYING PV-RETRY-COUNT FROM 1 BY 1                               
W33304           UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                        
W33304              OR (SQLCODE         = +000)                                 
W33304              OR (SQLCODE         = +100)                                 
                                                                                
W33304         EXEC SQL                                                         
W33304              SELECT 1                                                    
W33304              INTO :PA-COUNT                                              
W33304              FROM TINSCLS                                                
W33304              WHERE FSCL_MN_NBR = :INSCLS-FSCL-MN-NBR                     
W33304                AND FSCL_MN_END_DTE = :INSCLS-FSCL-MN-END-DTE             
W33304                AND STR_NBR = :INSCLS-STR-NBR                             
W33304              FETCH FIRST 1 ROWS ONLY                                     
W33304              WITH UR                                                     
W33304         END-EXEC                                                         
                                                                                
W33304         EVALUATE TRUE                                                    
W33304             WHEN SQLCODE        = +000                                   
W33304             WHEN SQLCODE        = +100                                   
W33304             WHEN SQLCODE        = -911                                   
W33304             WHEN SQLCODE        = -913                                   
W33304                 CONTINUE                                                 
W33304             WHEN OTHER                                                   
W33304                 MOVE    '1000-CK-TINSCLS'                                
W33304                                          TO PV-PARAGRAPH-NAME            
W33304                 MOVE    'SELECT FROM TINSCLS'                            
W33304                                          TO PV-DB2-OPER-ATTEMPTED        
W33304                 PERFORM Z400-SQL-ERROR                                   
W33304         END-EVALUATE                                                     
W33304     END-PERFORM.                                                         
                                                                                
W33304     IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
W33304         DISPLAY '** MAX RETRIES EXCEEDED - TINSCLS**'                    
W33304         MOVE    '1000-CK-TINSCLS'                                        
W33304                                          TO PV-PARAGRAPH-NAME            
W33304         MOVE    'SELECT FROM TINSCLS'                                    
W33304                                          TO PV-DB2-OPER-ATTEMPTED        
W33304         PERFORM Z400-SQL-ERROR                                           
W33304     END-IF.                                                              
                                                                                
W33304******************************************************************        
W33304* 1100-OPEN-TINSCLS-CSR.                                         *        
W33304* PROCESSES: AGAINST INVENTORY SUBCLASS TABLE                    *        
W33304*            FOR SPECIFIC STORE AND FISCAL MONTH                 *        
W33304******************************************************************        
W33304 1100-OPEN-TINSCLS-CSR.                                                   
W34557*W33304     MOVE MLCNTL-OPN-FSCL-MN-NBR TO INSCLS-FSCL-MN-NBR.            
W34557*W33304     MOVE MLCNTL-OPN-FSCL-MN-DTE TO INSCLS-FSCL-MN-END-DTE.        
W34557     MOVE IN001-OPN-FSCL-MN-NBR  TO INSCLS-FSCL-MN-NBR.                   
W34557     MOVE IN001-OPN-FSCL-MN-DTE  TO INSCLS-FSCL-MN-END-DTE.               
                                                                                
W33304     PERFORM WITH TEST AFTER                                              
W33304         VARYING PV-SQL-SUB FROM 1 BY 1                                   
W33304         UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
W33304            OR   SQLCODE = 0                                              
W33304            OR   SQLCODE = +100)                                          
                                                                                
W33304         EXEC SQL                                                         
W33304             OPEN TINSCLS-CSR                                             
W33304         END-EXEC                                                         
                                                                                
W33304         EVALUATE TRUE                                                    
W33304             WHEN SQLCODE = -911                                          
W33304             WHEN SQLCODE = -913                                          
W33304             WHEN SQLCODE = +100                                          
W33304             WHEN SQLCODE = 0                                             
W33304                 CONTINUE                                                 
W33304             WHEN SQLWARN NOT = SPACES                                    
W33304             WHEN SQLCODE NOT = ZERO                                      
W33304                 MOVE '1100-OPEN-TINSCLS-CSR'                             
W33304                               TO  PV-PARAGRAPH-NAME                      
W33304                 MOVE 'OPEN ON INVENTORY SUBCLASS CSR'                    
W33304                               TO  PV-DB2-OPER-ATTEMPTED                  
W33304                 PERFORM Z400-SQL-ERROR                                   
W33304         END-EVALUATE                                                     
W33304     END-PERFORM.                                                         
                                                                                
W33304     IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
W33304         MOVE '1100-OPEN-TINSCLS-CSR' TO PV-PARAGRAPH-NAME                
W33304         MOVE 'OPEN RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED            
W33304         PERFORM Z400-SQL-ERROR                                           
W33304     END-IF.                                                              
                                                                                
W33304******************************************************************        
W33304* 1200-FETCH-TINSCLS.                                            *        
W33304* PROCESSES: FETCH EACH ROW FROM INVENTORY SUBCLASS TABLE        *        
W33304*            FOR A SPECIFIC STORE AND THE CURRENT FISCAL MONTH   *        
W33304*            WRITE OUTPUT RECORD IN DCLGN FORMAT                 *        
W33304*            DELETE RECORD FROM TINSCLS                          *        
W33304*            CHECK IF TIME FOR COMMIT                            *        
W33304******************************************************************        
W33304 1200-FETCH-TINSCLS.                                                      
W33304     PERFORM WITH TEST AFTER                                              
W33304         VARYING PV-SQL-SUB FROM 1 BY 1                                   
W33304         UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
W33304            OR   SQLCODE = 0                                              
W33304            OR   SQLCODE = +100)                                          
W33304         EXEC SQL                                                         
W33304             FETCH TINSCLS-CSR                                            
W33304             INTO  :INSCLS-DEPT-NBR                                       
W33304                  ,:INSCLS-SUB-CL-NBR                                     
W33304                  ,:INSCLS-IN-LO-MDSE-LVL-ID                              
W33304                  ,:INSCLS-SLS-RTL-AMT                                    
W33304                  ,:INSCLS-MKDN-RTL-AMT                                   
W33304                  ,:INSCLS-MKUP-RTL-AMT                                   
W33304                  ,:INSCLS-XFER-IN-RTL-AMT                                
W33304                  ,:INSCLS-XFER-OUT-RTL-AMT                               
W33304                  ,:INSCLS-RCPT-RTL-AMT                                   
W33304                  ,:INSCLS-PADJ-RTL-AMT                                   
W33304                  ,:INSCLS-SHNK-RTL-AMT                                   
W33304                  ,:INSCLS-END-INV-RTL-AMT                                
W33304                  ,:INSCLS-CTOFF-SLS-AMT                                  
W33304                  ,:INSCLS-CTOFF-SHNK-AMT                                 
W33304                  ,:INSCLS-CTOFF-END-INV-AMT                              
OCFSFS                  ,:INSCLS-STR-PARL-SHIP-RTL-AMT                          
W33304                  ,:INSCLS-STR-EXPTED-RTL-AMT                             
W33304                  ,:INSCLS-ML-PSTG-MN-END-DTE                             
W33304         END-EXEC                                                         
                                                                                
W33304         EVALUATE TRUE                                                    
W33304             WHEN SQLCODE = ZERO                                          
W33304                 ADD 1 TO PV-TINSCLS-FETCH                                
W33304                 PERFORM 1300-WRITE-TINSCLS                               
W33304                 PERFORM 1400-DELETE-TINSCLS                              
W33304                 PERFORM 1600-CK-MANUAL-COMMIT                            
W33304             WHEN SQLCODE = +100                                          
W33304                 SET PS-END-OF-TINSCLS TO TRUE                            
W33304             WHEN SQLWARN0 NOT = SPACE                                    
W33304             WHEN SQLCODE  NOT = ZERO                                     
W33304                 MOVE '1200-FETCH-TINSCLS'                                
W33304                               TO  PV-PARAGRAPH-NAME                      
W33304                 MOVE 'FETCH INVENTORY SUBCLS CURSOR'                     
W33304                               TO  PV-DB2-OPER-ATTEMPTED                  
W33304                 PERFORM Z400-SQL-ERROR                                   
W33304         END-EVALUATE                                                     
W33304     END-PERFORM.                                                         
W33304     IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
W33304         MOVE '1200-FETCH-TINSCLS' TO PV-PARAGRAPH-NAME                   
W33304         MOVE 'FETCH INVEN SCL' TO PV-DB2-OPER-ATTEMPTED                  
W33304         PERFORM Z400-SQL-ERROR                                           
W33304     END-IF.                                                              
                                                                                
W33304******************************************************************        
W33304* 1300-WRITE-TINSCLS.                                            *        
W33304* PROCESSES: WRITE EACH ROW TO DATA SET IN TINSCLS               *        
W33304*            DCLGEN FORMAT BEFORE PHYSICAL DELETE                *        
W33304******************************************************************        
W33304 1300-WRITE-TINSCLS.                                                      
W33304     WRITE TINSCLS-DELETE-RECORD                                          
W33304         FROM DCLTINSCLS.                                                 
W33304     ADD 1 TO PV-OUTPUT-RECS.                                             
                                                                                
W33304******************************************************************        
W33304* 1400-DELETE-TINSCLS.                                           *        
W33304* PROCESSES: DELETE EACH ROW FROM INVENTORY SUBCLASS TABLE       *        
W33304*            FOR A SPECIFIC STORE AND AND CURRENT FISCAL MONTH   *        
W33304*            USING ALL KEYS OF UNIQUE INDEX IN WHERE CLAUSE      *        
W33304******************************************************************        
W33304 1400-DELETE-TINSCLS.                                                     
W33304     PERFORM                                                              
W33304         WITH TEST AFTER                                                  
W33304         VARYING PV-RETRY-COUNT FROM 1 BY 1                               
W33304           UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                        
W33304              OR (SQLCODE         = +000)                                 
W33304              OR (SQLCODE         = +100)                                 
                                                                                
W33304         EXEC SQL                                                         
W33304          DELETE FROM TINSCLS                                             
W33304          WHERE FSCL_MN_NBR = :INSCLS-FSCL-MN-NBR                         
W33304            AND FSCL_MN_END_DTE = :INSCLS-FSCL-MN-END-DTE                 
W33304            AND DEPT_NBR = :INSCLS-DEPT-NBR                               
W33304            AND SUB_CL_NBR = :INSCLS-SUB-CL-NBR                           
W33304            AND STR_NBR = :INSCLS-STR-NBR                                 
W33304            AND IN_LO_MDSE_LVL_ID = :INSCLS-IN-LO-MDSE-LVL-ID             
W33304         END-EXEC                                                         
                                                                                
W33304         EVALUATE TRUE                                                    
W33304             WHEN SQLCODE        = +000                                   
W33304                 ADD SQLERRD(3) TO PV-DB2-TINSCLS-DEL                     
W33304                 ADD 1 TO PV-MANUAL-TO-COMMIT-CNT                         
W33304             WHEN SQLCODE        = +100                                   
W33304             WHEN SQLCODE        = -911                                   
W33304             WHEN SQLCODE        = -913                                   
W33304                 CONTINUE                                                 
W33304             WHEN OTHER                                                   
W33304                 MOVE    '1400-DELETE-TINSCLS'                            
W33304                                          TO PV-PARAGRAPH-NAME            
W33304                 MOVE    'DELETE FROM TINSCLS'                            
W33304                                          TO PV-DB2-OPER-ATTEMPTED        
W33304                 PERFORM Z400-SQL-ERROR                                   
W33304         END-EVALUATE                                                     
W33304     END-PERFORM.                                                         
                                                                                
W33304     IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
W33304         DISPLAY '** MAX RETRIES EXCEEDED - TINSCLS**'                    
W33304         MOVE    '1400-DELETE-TINSCLS'                                    
W33304                                          TO PV-PARAGRAPH-NAME            
W33304         MOVE    'DELETE FROM TINSCLS'                                    
W33304                                          TO PV-DB2-OPER-ATTEMPTED        
W33304         PERFORM Z400-SQL-ERROR                                           
W33304     END-IF.                                                              
                                                                                
W33304******************************************************************        
W33304* 1500-CLOSE-TINSCLS-CSR.                                        *        
W33304******************************************************************        
W33304 1500-CLOSE-TINSCLS-CSR.                                                  
W33304     PERFORM WITH TEST AFTER                                              
W33304         VARYING PV-SQL-SUB FROM 1 BY 1                                   
W33304         UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
W33304            OR   SQLCODE = 0                                              
W33304            OR   SQLCODE = +100)                                          
                                                                                
W33304         EXEC SQL                                                         
W33304             CLOSE TINSCLS-CSR                                            
W33304         END-EXEC                                                         
                                                                                
W33304         EVALUATE TRUE                                                    
W33304             WHEN SQLCODE = -911                                          
W33304             WHEN SQLCODE = -913                                          
W33304             WHEN SQLCODE = +100                                          
W33304             WHEN SQLCODE = 0                                             
W33304                 CONTINUE                                                 
W33304             WHEN SQLWARN NOT = SPACES                                    
W33304             WHEN SQLCODE NOT = ZERO                                      
W33304                 MOVE '1500-CLOSE-TINSCLS-CSR'                            
W33304                                         TO  PV-PARAGRAPH-NAME            
W33304                 MOVE 'CLOSE INVENTORY SUBCLS CURSOR'                     
W33304                                         TO  PV-DB2-OPER-ATTEMPTED        
W33304                 PERFORM Z400-SQL-ERROR                                   
W33304         END-EVALUATE                                                     
W33304     END-PERFORM.                                                         
                                                                                
W33304     IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
W33304         MOVE '1500-CLOSE-TINSCLS-CSR' TO PV-PARAGRAPH-NAME               
W33304         MOVE 'CLOSE RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED           
W33304         PERFORM Z400-SQL-ERROR                                           
W33304     END-IF.                                                              
                                                                                
W33304******************************************************************        
W33304* 1600-CK-MANUAL-COMMIT.                                         *        
W33304******************************************************************        
W33304 1600-CK-MANUAL-COMMIT.                                                   
W33304     IF PV-MANUAL-TO-COMMIT-CNT > PV-MANUAL-COMMIT-VALUE                  
W33304         EXEC SQL                                                         
W33304             COMMIT                                                       
W33304         END-EXEC                                                         
W33304         ADD 1 TO PV-MANUAL-TOTAL-COMMITS                                 
W33304         MOVE ZERO TO PV-MANUAL-TO-COMMIT-CNT                             
W33304     END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7600-GET-COMMIT-TIMESTAMP.                                     *        
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *        
      *            CONTROL TABLE                                       *        
      ******************************************************************        
       7600-GET-COMMIT-TIMESTAMP.                                               
      *                                                                         
           EXEC SQL                                                             
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)               
               INTO :PV-COMMIT-TIMESTAMP                                        
               FROM TCKRSTCNTL                                                  
              WHERE JOB_NAME  = :PC-JOB-NAME                                    
                AND PLAN_NAME = :PC-PROGRAM-NAME                                
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE NOT EQUAL ZERO                                      
                   MOVE '7600-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME        
                   MOVE 'SELECT COMMIT TIMESTAMP' TO                            
                                PV-DB2-OPER-ATTEMPTED                           
                   PERFORM Z400-SQL-ERROR                                       
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      * 7650-INIT-CHECKPOINT-SELECT                                    *        
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *        
      *            IF WE ARE IN A RESTART CONDITION.                   *        
      ******************************************************************        
       7650-INIT-CHECKPOINT-SELECT.                                             
      *                                                                         
           EXEC SQL                                                             
             SELECT  CKPT_STAT_CODE                                             
                    ,CKPT_FRQNCY_QTY                                            
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                                 
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                                
               FROM  TCKRSTCNTL                                                 
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
      *                                                                         
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '7650-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME          
               MOVE 'SELECT RESTART INFO' TO PV-DB2-OPER-ATTEMPTED              
               PERFORM Z400-SQL-ERROR                                           
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 7700-SELECT-RESTART-INFO                                       *        
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *        
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *        
      ******************************************************************        
       7700-SELECT-RESTART-INFO.                                                
      *                                                                         
           EXEC SQL                                                             
             SELECT  WORK_STRG_DESC                                             
               INTO  :TCKRSTINF-WORK-STRG-DESC                                  
               FROM  TCKRSTINF                                                  
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
      *                                                                         
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '*7700-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME           
               MOVE 'SELECT RESTART PARAMETERS' TO PV-DB2-OPER-ATTEMPTED        
               PERFORM Z400-SQL-ERROR                                           
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      * 7750-SET-CURRENT-TIMESTAMP.                                    *        
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *        
      ******************************************************************        
       7750-SET-CURRENT-TIMESTAMP.                                              
      *                                                                         
           EXEC SQL                                                             
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP                   
           END-EXEC.                                                            
      *                                                                         
      *    DISPLAY 'CURRENT TIMESTAMP ', PV-CURRENT-TIMESTAMP                   
      *                                                                         
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'SET CURRENT TIMESTAMP' TO PV-DB2-OPER-ATTEMPTED            
               MOVE '*7750-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME         
               PERFORM Z400-SQL-ERROR                                           
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 7800-COMMIT-PROCESSING.                                        *        
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *        
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*        
      ******************************************************************        
       7800-COMMIT-PROCESSING.                                                  
           MOVE '*7800-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.             
      *                                                                         
           PERFORM 7750-SET-CURRENT-TIMESTAMP.                                  
      *                                                                         
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                        
      *        PREPARE COMMIT RECORD FOR UPDATE                                 
               ADD 1                           TO PA-COMMIT-COUNT               
               MOVE MLCNTL-OPN-FSCL-MN-NBR     TO CR-FISCAL-NBR                 
               MOVE MLCNTL-OPN-FSCL-MN-DTE     TO CR-FISCAL-MN-END-DTE          
               MOVE PV-HOLD-DEPT-NBR           TO CR-DEPT-NBR                   
               MOVE PV-HOLD-SUB-CL-NBR         TO CR-SUB-CL-NBR                 
               MOVE PV-HOLD-STR-NBR            TO CR-STR-NBR                    
W28582*        MOVE PV-HOLD-ENT-ID             TO CR-ENT-ID                     
W28582         MOVE PV-ML-LWST-ID              TO CR-ML-LWST-ID                 
               MOVE PA-COMMIT-COUNT            TO CR-COMMIT-COUNT               
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                          
               MOVE CR-CHECKPOINT-RESTART-AREA TO                               
                                             TCKRSTINF-WORK-STRG-DESC           
               IF PS-FIRST-COMMIT                                               
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                        
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                        
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                               
               END-IF                                                           
               PERFORM 7850-COMMIT-CHANGES                                      
               PERFORM 7600-GET-COMMIT-TIMESTAMP                                
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      * 7850-COMMIT-CHANGES.                                                    
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.              
      *            TAKE A COMMIT.                                               
      *  ==> PERFORMED FROM: 7800-COMMIT-PROCESSING.                            
      ******************************************************************        
       7850-COMMIT-CHANGES.                                                     
      *                                                                         
           EXEC SQL                                                             
             UPDATE TCKRSTINF                                                   
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC                  
              WHERE JOB_NAME       = :PC-JOB-NAME                               
                AND PLAN_NAME      = :PC-PROGRAM-NAME                           
           END-EXEC.                                                            
      *                                                                         
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'UPDATE CHECKPOINT ROW'    TO PV-DB2-OPER-ATTEMPTED         
               MOVE  '* 7850-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME             
               PERFORM Z400-SQL-ERROR                                           
           END-IF.                                                              
      *                                                                         
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
      *                                                                         
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'COMMIT CHANGES'           TO PV-DB2-OPER-ATTEMPTED         
               MOVE  '* 7850-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME             
               PERFORM Z400-SQL-ERROR                                           
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 7900-UPDATE-CHECKPOINT-STATUS                                           
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE           
      *                                                                         
      ******************************************************************        
       7900-UPDATE-CHECKPOINT-STATUS.                                           
      *                                                                         
           EXEC SQL                                                             
             UPDATE  TCKRSTCNTL                                                 
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE                
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
      *                                                                         
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME        
               MOVE 'UPDATE CHECKPOINT STATUS' TO PV-DB2-OPER-ATTEMPTED         
               PERFORM Z400-SQL-ERROR                                           
           END-IF.                                                              
      *                                                                         
           EJECT                                                                
      *                                                                         
       8000-OPEN-TINVPAR-CSR.                                                   
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   OPEN TINVPAR-CSR                                             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -911                                          
W33304             WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '8000-OPEN-TINVPAR-CSR'                             
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'OPEN ON INVENTORY PARAMETERS CURSOR'               
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z400-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '8000-OPEN-TINVPAR-CSR' TO PV-PARAGRAPH-NAME                
               MOVE 'OPEN RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED            
               PERFORM Z400-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
       8100-FETCH-TINVPAR-CSR.                                                  
                                                                                
           EXEC SQL                                                             
                FETCH TINVPAR-CSR                                               
W26600          INTO  :INVPAR-LOC-NBR,                                          
W28545                :INVPAR-INV-ID,                                           
W34557                :INVPAR-ACTL-LDG-CTOF-TMST,                               
W34557                :INCNTL-OPN-FSCL-MN-DTE                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
W33304             ADD 1 TO PV-TINVPAR-FETCH                                    
               WHEN SQLCODE = +100                                              
                   SET PS-END-OF-TINVPAR TO TRUE                                
W33304             IF PV-TINVPAR-FETCH = ZERO                                   
W33304                MOVE 4095 TO ERROR-CODE                                   
W33304             END-IF                                                       
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE  NOT = ZERO                                         
                   MOVE '8100-FETCH-TINVPAR-CSR'                                
                                 TO  PV-PARAGRAPH-NAME                          
                   MOVE 'FETCH INVENTORY PARAMETERS CURSOR'                     
                                 TO  PV-DB2-OPER-ATTEMPTED                      
                   PERFORM Z400-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8150-UPDATE-TINVPAR.                                                     
                                                                                
W30692     MOVE PV-EDIT-CUTOFF-TIMESTAMP TO INVPAR-ACTL-LDG-CTOF-TMST.          
W33304*    MOVE WS-INV-STORE(PV-STORE-SUB) TO INVPAR-LOC-NBR.                   
W33304*    MOVE WS-INV-ID(PV-STORE-SUB) TO INVPAR-INV-ID.                       
W30692                                                                          
W34517*    EXEC SQL                                                             
W34517*        UPDATE TINVPAR                                                   
W34517*        SET ACTL_LDG_CTOF_TMST  = :INVPAR-ACTL-LDG-CTOF-TMST             
W28545*        WHERE CURRENT OF TINVPAR-CSR                                     
W34517*        WHERE INV_ID            = :INVPAR-INV-ID                         
W34517*          AND LOC_NBR           = :INVPAR-LOC-NBR                        
W34517*    END-EXEC.                                                            
                                                                                
       8200-CLOSE-TINVPAR-CURSOR.                                               
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   CLOSE TINVPAR-CSR                                            
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -911                                          
W33304             WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '8200-CLOSE-TINVPAR-CSR'                            
                                               TO  PV-PARAGRAPH-NAME            
                       MOVE 'CLOSE ON INVENTORY PARAMETERS CURSOR'              
                                               TO  PV-DB2-OPER-ATTEMPTED        
                       PERFORM Z400-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '8200-CLOSE-TINVPAR-CSR' TO PV-PARAGRAPH-NAME               
               MOVE 'CLOSE RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED           
               PERFORM Z400-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
W20445 8300-OPEN-MLT-MNLY-SUM-CSR.                                              
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
W20445             OPEN MLT-MNLY-SUM-CSR                                        
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -911                                          
W33304             WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
W20445                 MOVE '8000-OPEN-MLT-MNLY-SUM-CSR'                        
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'OPEN ON M/L MTH SUB CLASS CURSOR'                  
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z400-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
W20445         MOVE '8300-OPEN-MLT-MNLY-SUM-CSR' TO PV-PARAGRAPH-NAME           
               MOVE 'OPEN RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED            
               PERFORM Z400-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
W20445 8400-FETCH-MLT-MNLY-SUM-CSR.                                             
                                                                                
           EXEC SQL                                                             
W20445          FETCH MLT-MNLY-SUM-CSR                                          
W34517          INTO  :MLT00018-FSCL-MN-END-DTE,                                
W34517                :MLT00018-DEPT-NBR,                                       
W34517                :MLT00018-SUB-CL-NBR,                                     
W34517                :MLT00018-LOC-NBR,                                        
W34517                :MLT00018-ML-LO-MDSE-LVL-ID,                              
W34517                :MLT00018-SLS-REG-RTL-AMT,                                
W34517                :MLT00018-SLS-PROMO-RTL-AMT,                              
W34517                :MLT00018-SLS-PROCLR-RTL-AMT,                             
W34517                :MLT00018-SLS-CLR-RTL-AMT,                                
W34517                :MLT00018-SLS-OTH-RTL-AMT,                                
W34517                :MLT00018-SHNK-RTL-AMT,                                   
W34517                :MLT00018-END-INV-RTL-AMT                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLCODE = +100                                              
W20445             SET PS-END-OF-TMLT-MNLY-SUM TO TRUE                          
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE  NOT = ZERO                                         
W20445             MOVE '8400-FETCH-MLT-MNLY-SUM-CSR'                           
                                 TO  PV-PARAGRAPH-NAME                          
                   MOVE 'FETCH M/L MTH SUBCLASS CURSOR'                         
                                 TO  PV-DB2-OPER-ATTEMPTED                      
                   PERFORM Z400-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
W20445 8500-CLOSE-MNLY-SUM-CURSOR.                                              
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0                                              
                  OR   SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
W20445             CLOSE MLT-MNLY-SUM-CSR                                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -911                                          
W33304             WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
W20445                 MOVE '8500-CLOSE-MLT-MNLY-SUM-CSR'                       
                                               TO  PV-PARAGRAPH-NAME            
                       MOVE 'CLOSE ON M/L MTH SUBCLASS CURSOR'                  
                                               TO  PV-DB2-OPER-ATTEMPTED        
                       PERFORM Z400-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
W20445         MOVE '8500-CLOSE-MLT-MNLY-SUM-CSR' TO PV-PARAGRAPH-NAME          
               MOVE 'CLOSE RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED           
               PERFORM Z400-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
      *--------------------------------------------------------------*          
      *    THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.        *          
      *--------------------------------------------------------------*          
                                                                                
       Z400-SQL-ERROR.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
W34517     DISPLAY '**  PROGRAM   **  =  VAKUT163'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
W34557*****************************************************************         
W34557*  COPYBOOK OF DATE EDIT LOGIC FOR INPUT ALTERNATE DATES        *         
W34557*****************************************************************         
W34557     COPY INPD050.                                                        
W34557*****************************************************************         
