       ID  DIVISION.                                                            
       PROGRAM-ID.         VAKUP014.                                            
       AUTHOR.             MURALITHARAN V.                                      
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       03/23/2011.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * REMARKS                                                        *        
      * -------                                                        *        
      *                                                                *        
      * TITLE        -  FINAL CUTOVER FROM ML TO IN FOR BV/IN LEDGER   *        
      *                                                                *        
      *                                                                *        
      ***************************************************************  *        
      *                                                                *        
      * DESCRIPTION  -  THE PURPOSE OF THIS PROGRAM IS TO DO THE FINAL *        
      * CUT OVER FOR THE MERCHANDISE LEDGER TO THE TINSCLS             *        
      * TABLE FOR THOSE STORES THAT PLANNED TO TAKE INVENTORY DURING   *        
      * THE CLOSING WEEK AND ANY OTHER STORES IN THE SAME CYCLE.       *        
      * THE ML DATA FOR STORES THAT PLANNED TO TAKE INVENTORY                   
      * DURING THE CLOSING WEEK WAS IDENTIFIED IN VAKUP013.                     
      * THIS PROGRAM WILL READ THE FILE AND INSERT INTO THE                     
      * TINSCLS TABLE. CHECKPOINTS ARE TAKEN EVERY 9000 INPUT RECORDS           
      * FOR RESTART PURPOSES.                                                   
      *                                                                         
      * PLEASE SEE INRD001 FOR EXAMPLES OF THE ALTERNATE DATE CARD              
      * AND ITS VALUES                                                          
      *                                                                         
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * WR NUMBER     PROGRAMMER       DATE                            *        
      * ---------------------------------------------------------------*        
      * WR34557       MURALITHARAN     09/13/2011                      *        
      * INITIAL VERSION                                                         
      * INC2071974    COGNIZANT        03/02/2012                      *        
      *               CHNAGED THE COMMIT FREQUENCY FROM 9000 TO 5000            
      * INC2071974    COGNIZANT        03/02/2012                      *        
      *               CHNAGED THE COMMIT FREQUENCY FROM 5000 TO 1000            
      * INC2071974    COGNIZANT        03/02/2012                      *        
      *               CHANGED 2300-GET-COMMIT-TIMESTAMP PARA FOR COMMIT         
      *               LOGIC.                                                    
      *               REVERTED BACK THE COMMIT FREQ TO 9000                     
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT ML-CUTOVER-FILE                                               
               ASSIGN                    TO INP01.                              
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   ML-CUTOVER-FILE                                                     
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01   ML-CUTOVER-FILE-REC          PIC X(131).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ****************************************************************          
      *  ERROR MESSAGE AREA FOR DB2                                             
      ****************************************************************          
                                                                                
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  PA-PROGRAM ACCUMULATORS                                       *        
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-ROWS-INSERTED          PIC  9(09)  VALUE ZEROES.              
                                                                                
      ****************************************************************          
      *  PROGRAM CONSTANT DECLARATION                                           
      ****************************************************************          
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES            PIC S9(03) VALUE +3.                   
           05  PC-JOB-NAME               PIC  X(06) VALUE 'VAK520'.             
           05  PC-PROGRAM-NAME           PIC  X(08) VALUE 'VAKUP014'.           
INC032*    03  PC-INSERT-COMMIT-LIMIT    PIC  9(04) VALUE 9000.                 
INC032*    03  PC-INSERT-COMMIT-LIMIT    PIC  9(04) VALUE 5000.                 
INC032*    03  PC-INSERT-COMMIT-LIMIT    PIC  9(04) VALUE 1000.                 
INC032     03  PC-INSERT-COMMIT-LIMIT    PIC  9(04) VALUE 9000.                 
                                                                                
      ****************************************************************          
      *  PROGRAM SWITCH DECLARATION                                             
      ****************************************************************          
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-EOF-ML-CUTOVR-FILE-SW  PIC  X(01) VALUE 'N'.                  
               88  PS-EOF-ML-CUTOVR-FILE            VALUE 'Y'.                  
           05  PS-FIRST-COMMIT-SW        PIC  X(01) VALUE 'N'.                  
               88  PS-FIRST-COMMIT                  VALUE 'Y'.                  
           05  PS-EMPTY-FILE-SW          PIC  X(01) VALUE 'N'.                  
               88  PS-EMPTY-FILE                    VALUE 'Y'.                  
                                                                                
      ****************************************************************          
      *  PROGRAM VARIABLE DECLARATION                                           
      ****************************************************************          
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME         PIC  X(30) VALUE SPACE.                
           05  PV-CURRENT-TIMESTAMP      PIC  X(26) VALUE SPACES.               
           05  PV-COMMIT-TIMESTAMP       PIC  X(26) VALUE SPACES.               
           05  PV-DB2-OPER-ATTEMPTED     PIC  X(30) VALUE SPACE.                
           05  PV-DB2-TINSCLS-INS        PIC  9(09) VALUE ZERO.                 
           05  PV-REC-READ-CNT           PIC  9(09) VALUE ZEROES.               
                                                                                
      ****************************************************************          
      *  ABEND CODE DECLARATION                                                 
      ****************************************************************          
       01  ABEND-CODE                    PIC S9(04) VALUE 0 COMP SYNC.          
           88  ABEND-4013-DB2-ERROR                 VALUE +4013.                
                                                                                
      ******************************************************************        
      *  INVENTORY MONTHLY SUBCLASS TABLE TINSCLS                               
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TINSCLS                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************                      
      *CHECKPOINT RESTART CONTROL AND INFORMATION TABLES *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TCKRSTCN                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TCKRSTIN                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * CHECKPOINT RESTART VARIABLES                                   *        
      ******************************************************************        
       01  CR-CHECKPOINT-RESTART-AREA.                                          
           05  CR-AREA-LEN               PIC S9(04) VALUE +9  COMP.             
           05  CR-CHECKPOINT-RESTART-VAR.                                       
               10  CR-RECORD-COUNT       PIC  9(09) VALUE ZEROES.               
           EJECT                                                                
      *-----------------------------------------------------------------        
      * DB2 SQL COMMUNICATIONS AREA                                             
      *-----------------------------------------------------------------        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
           EJECT                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
      *        MAINLINE ROUTINE                                       *         
      *****************************************************************         
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INIT.                                                   
                                                                                
           PERFORM 2000-MAIN-PROCESSING                                         
             UNTIL PS-EOF-ML-CUTOVR-FILE                                        
                                                                                
           PERFORM 8000-CLOSE-FILES.                                            
                                                                                
                                                                                
           DISPLAY '******************************************'.                
           DISPLAY '************VAKUP014 ENDING***************'.                
           DISPLAY '******************************************'.                
           STOP RUN.                                                            
                                                                                
       1000-INIT.                                                               
      ******************************************************************        
      * DESCRIPTION     : INITIALISE FIELDS AND OPEN FILES USED        *        
      ******************************************************************        
                                                                                
           DISPLAY '******************************************'.                
           DISPLAY '************INKUP014 STARTING*************'.                
           DISPLAY '******************************************'.                
                                                                                
           OPEN INPUT ML-CUTOVER-FILE.                                          
           INITIALIZE DCLTINSCLS.                                               
                                                                                
      *    CHECKPOINT/RESTART SET-UP AND PREPERATION                            
           PERFORM 1100-INIT-CHECKPOINT-SELECT.                                 
                                                                                
           IF  TCKRSTCNTL-CKPT-STAT-CODE = '9'                                  
                                                                                
               PERFORM 1150-SELECT-RESTART-INFO                                 
                                                                                
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT                               
                                         TO CR-CHECKPOINT-RESTART-VAR           
                                                                                
               PERFORM 3000-READ-ML-CUTOVER-FILE                                
                     UNTIL PV-REC-READ-CNT = CR-RECORD-COUNT                    
                        OR PS-EOF-ML-CUTOVR-FILE                                
                                                                                
           ELSE                                                                 
                                                                                
               SET PS-FIRST-COMMIT       TO TRUE                                
                                                                                
           END-IF.                                                              
                                                                                
           INITIALIZE CR-CHECKPOINT-RESTART-VAR                                 
                                                                                
INC032*    DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                              
INC032*             TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS '                     
INC032*    '(AFTER THE PROCESSING EVERY 9000 RECORDS FROM I/P FILE).'           
INC032*    '(AFTER THE PROCESSING EVERY 5000 RECORDS FROM I/P FILE).'           
INC032*    '(AFTER THE PROCESSING EVERY 1000 RECORDS FROM I/P FILE).'           
INC032     DISPLAY 'COMMITS WILL BE TAKEN',                                     
INC032     '(AFTER THE PROCESSING EVERY 9000 RECORDS FROM I/P FILE).'           
                                                                                
           PERFORM 3000-READ-ML-CUTOVER-FILE                                    
                                                                        02069100
           IF  PS-EOF-ML-CUTOVR-FILE                                            
                                                                        02069100
               SET PS-EMPTY-FILE             TO TRUE                            
                                                                        02069100
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                               
                   DISPLAY 'NO RECORDS TO PROCESS AFTER RESTART'                
               ELSE                                                             
                   DISPLAY 'NO RECORDS IN FILE ML-CUTOVER-FILE'                 
               END-IF                                                           
                                                                        02069100
           ELSE                                                                 
                                                                        02069100
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02061000
                   DISPLAY '** RESTART RUN **'                          06720800
                   DISPLAY 'INPUT RECORD RESTARTED ON RECORD: '         02067000
                            PV-REC-READ-CNT                             02068000
                   DISPLAY 'LOCATION NUMBER: ' INSCLS-STR-NBR           06720800
               END-IF                                                   02061000
                                                                        02069100
           END-IF                                                       02069000
           .                                                                    
      ******************************************************************        
      * 1100-INIT-CHECKPOINT-SELECT                                    *        
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *        
      *            IF WE ARE IN A RESTART CONDITION.                   *        
      ******************************************************************        
       1100-INIT-CHECKPOINT-SELECT.                                             
                                                                                
           EXEC SQL                                                             
             SELECT  CKPT_STAT_CODE                                             
                    ,CKPT_FRQNCY_QTY                                            
               INTO :TCKRSTCNTL-CKPT-STAT-CODE                                  
                   ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                                 
               FROM  TCKRSTCNTL                                                 
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '1100-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME          
               MOVE 'SELECT RESTART INFO' TO PV-DB2-OPER-ATTEMPTED              
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 1150-SELECT-RESTART-INFO                                       *        
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *        
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *        
      ******************************************************************        
       1150-SELECT-RESTART-INFO.                                                
                                                                                
           EXEC SQL                                                             
             SELECT  WORK_STRG_DESC                                             
               INTO  :TCKRSTINF-WORK-STRG-DESC                                  
               FROM  TCKRSTINF                                                  
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '*1150-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME           
               MOVE 'SELECT RESTART PARAMETERS' TO PV-DB2-OPER-ATTEMPTED        
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
       2000-MAIN-PROCESSING.                                                    
      ******************************************************************        
      * DESCRIPTION     : MAIN PROCESSING                              *        
      ******************************************************************        
                                                                                
      *-------------------------------------------------------------------      
      *  INSERT A ROW IN THE TINSCLS TABLE FOR EVERY ROW IN                     
      *  TMNSCSL THAT MATCHES THE OPEN FISCAL MONTH AND THE STORE IS            
      *  DOING INVENTORY                                                        
      *-------------------------------------------------------------------      
                                                                                
           EXEC SQL                                                             
               INSERT INTO TINSCLS (FSCL_MN_NBR,                                
                                    FSCL_MN_END_DTE,                            
                                    DEPT_NBR,                                   
                                    SUB_CL_NBR,                                 
                                    STR_NBR,                                    
                                    IN_LO_MDSE_LVL_ID,                          
                                    SLS_RTL_AMT,                                
                                    MKDN_RTL_AMT,                               
                                    MKUP_RTL_AMT,                               
                                    XFER_IN_RTL_AMT,                            
                                    XFER_OUT_RTL_AMT,                           
                                    RCPT_RTL_AMT,                               
                                    PADJ_RTL_AMT,                               
                                    SHNK_RTL_AMT,                               
                                    END_INV_RTL_AMT,                            
                                    CTOFF_SLS_AMT,                              
                                    CTOFF_SHNK_AMT,                             
OCFSFS                              STR_PARL_SHIP_RTL_AMT,                      
                                    STR_EXPTED_RTL_AMT,                         
                                    CTOFF_END_INV_AMT,                          
                                    ML_PSTG_MN_END_DTE)                         
                    VALUES         (:INSCLS-FSCL-MN-NBR,                        
                                    :INSCLS-FSCL-MN-END-DTE,                    
                                    :INSCLS-DEPT-NBR,                           
                                    :INSCLS-SUB-CL-NBR,                         
                                    :INSCLS-STR-NBR,                            
                                    :INSCLS-IN-LO-MDSE-LVL-ID,                  
                                    :INSCLS-SLS-RTL-AMT,                        
                                    :INSCLS-MKDN-RTL-AMT,                       
                                    :INSCLS-MKUP-RTL-AMT,                       
                                    :INSCLS-XFER-IN-RTL-AMT,                    
                                    :INSCLS-XFER-OUT-RTL-AMT,                   
                                    :INSCLS-RCPT-RTL-AMT,                       
                                    :INSCLS-PADJ-RTL-AMT,                       
                                    :INSCLS-SHNK-RTL-AMT,                       
                                    :INSCLS-END-INV-RTL-AMT,                    
                                    :INSCLS-CTOFF-SLS-AMT,                      
                                    :INSCLS-CTOFF-SHNK-AMT,                     
OCFSFS                              :INSCLS-STR-PARL-SHIP-RTL-AMT,              
                                    :INSCLS-STR-EXPTED-RTL-AMT,                 
                                    :INSCLS-CTOFF-END-INV-AMT,                  
                                    :INSCLS-ML-PSTG-MN-END-DTE)                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   ADD 1                 TO PV-DB2-TINSCLS-INS                  
                                            PA-ROWS-INSERTED                    
               WHEN SQLCODE = -803                                              
                   MOVE '2000-MAIN-PROCESSING'                                  
                                        TO  PV-PARAGRAPH-NAME                   
                   MOVE 'INSERT OF DUPLICATE ROW ATTEMPTED'                     
                                        TO  PV-DB2-OPER-ATTEMPTED               
                   DISPLAY 'INSCLS-FSCL-MN-END-DTE ',                           
                                            INSCLS-FSCL-MN-END-DTE              
                   DISPLAY 'INSCLS-DEPT-NBR ',                                  
                                            INSCLS-DEPT-NBR                     
                   DISPLAY 'INSCLS-SUB-CL-NBR' ,                                
                                            INSCLS-SUB-CL-NBR                   
                   DISPLAY 'INSCLS-IN-LO-MDSE-LVL-ID ',                         
                                            INSCLS-IN-LO-MDSE-LVL-ID            
                   DISPLAY 'INSCLS-STR-NBR ',                                   
                                            INSCLS-STR-NBR                      
                   PERFORM 9900-SQL-ABEND                                       
                                                                                
               WHEN SQLWARN NOT = SPACES                                        
               WHEN SQLCODE NOT = ZERO                                          
                                                                                
                   MOVE '2000-MAIN-PROCESSING'                                  
                                         TO PV-PARAGRAPH-NAME                   
                    MOVE 'INSERT ON INVENTORY COPY TABLE'                       
                                         TO PV-DB2-OPER-ATTEMPTED               
                    PERFORM 9900-SQL-ABEND                                      
                                                                                
           END-EVALUATE.                                                        
                                                                                
           IF  PA-ROWS-INSERTED = PC-INSERT-COMMIT-LIMIT                        
               PERFORM 2100-COMMIT-PROCESSING                                   
               MOVE ZERO                 TO PA-ROWS-INSERTED                    
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-ML-CUTOVER-FILE                                    
           .                                                                    
                                                                                
      ******************************************************************        
      * 2100-COMMIT-PROCESSING.                                        *        
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *        
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*        
      ******************************************************************        
       2100-COMMIT-PROCESSING.                                                  
                                                                                
           MOVE '*2100-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.             
                                                                                
           PERFORM 2150-SET-CURRENT-TIMESTAMP.                                  
                                                                                
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                        
                                                                                
      *        PREPARE COMMIT RECORD FOR UPDATE                                 
               MOVE PV-REC-READ-CNT            TO CR-RECORD-COUNT               
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                          
               MOVE CR-CHECKPOINT-RESTART-AREA TO                               
                                             TCKRSTINF-WORK-STRG-DESC           
                                                                                
               IF  PS-FIRST-COMMIT                                              
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                        
                   PERFORM 2200-UPDATE-CHECKPOINT-STATUS                        
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                               
               END-IF                                                           
                                                                                
               PERFORM 2250-COMMIT-CHANGES                                      
               PERFORM 2300-GET-COMMIT-TIMESTAMP                                
                                                                                
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2200-UPDATE-CHECKPOINT-STATUS                                           
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE           
      *                                                                         
      ******************************************************************        
       2200-UPDATE-CHECKPOINT-STATUS.                                           
                                                                                
           EXEC SQL                                                             
             UPDATE  TCKRSTCNTL                                                 
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE                
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '2200-UPDATE-CHECKPOINT-STATUS'                             
                                               TO PV-PARAGRAPH-NAME             
               MOVE 'UPDATE CHECKPOINT STATUS' TO PV-DB2-OPER-ATTEMPTED         
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           EJECT                                                                
                                                                                
      ******************************************************************        
      * 2250-COMMIT-CHANGES.                                                    
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.              
      *            TAKE A COMMIT.                                               
      *  ==> PERFORMED FROM: 2100-COMMIT-PROCESSING.                            
      ******************************************************************        
       2250-COMMIT-CHANGES.                                                     
                                                                                
           EXEC SQL                                                             
             UPDATE TCKRSTINF                                                   
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC                  
              WHERE JOB_NAME       = :PC-JOB-NAME                               
                AND PLAN_NAME      = :PC-PROGRAM-NAME                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'UPDATE CHECKPOINT ROW'    TO PV-DB2-OPER-ATTEMPTED         
               MOVE  '* 2250-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME             
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           PERFORM 4000-ISSUE-COMMIT.                                           
                                                                                
      ******************************************************************        
      * 2300-GET-COMMIT-TIMESTAMP.                                     *        
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *        
      *            CONTROL TABLE                                       *        
      ******************************************************************        
       2300-GET-COMMIT-TIMESTAMP.                                               
                                                                                
           EXEC SQL                                                             
INC032*      SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)               
INC032       SELECT (CURRENT TIMESTAMP)                                         
               INTO :PV-COMMIT-TIMESTAMP                                        
               FROM TCKRSTCNTL                                                  
              WHERE JOB_NAME  = :PC-JOB-NAME                                    
                AND PLAN_NAME = :PC-PROGRAM-NAME                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE NOT EQUAL ZERO                                      
                   MOVE '2300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME        
                   MOVE 'SELECT COMMIT TIMESTAMP'   TO                          
                                PV-DB2-OPER-ATTEMPTED                           
                   PERFORM 9900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 2150-SET-CURRENT-TIMESTAMP.                                    *        
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *        
      ******************************************************************        
       2150-SET-CURRENT-TIMESTAMP.                                              
                                                                                
           EXEC SQL                                                             
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP                   
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'SET CURRENT TIMESTAMP' TO PV-DB2-OPER-ATTEMPTED            
               MOVE '*2150-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME         
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 3000-READ-ML-CUTOVER-FILE.                                     *        
      * PROCESSES: READ ML CUT OVER FILE                                        
      ******************************************************************        
       3000-READ-ML-CUTOVER-FILE.                                               
                                                                                
           READ ML-CUTOVER-FILE         INTO DCLTINSCLS                         
             AT END                                                             
               SET PS-EOF-ML-CUTOVR-FILE  TO TRUE                               
           END-READ.                                                            
                                                                                
           IF  PS-EOF-ML-CUTOVR-FILE-SW = 'N'                                   
               ADD +1                     TO PV-REC-READ-CNT                    
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 4000-ISSUE-COMMIT.                                                      
      * PROCESSES: COMMITING THE INSERTS AND UPDATES                            
      *  ==> PERFORMED FROM: 2250-COMMIT-PROCESSING.                            
      *                      8000-CLOSE-FILES.                                  
      ******************************************************************        
       4000-ISSUE-COMMIT.                                                       
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'COMMIT CHANGES'           TO PV-DB2-OPER-ATTEMPTED         
               MOVE  '* 4000-ISSUE-COMMIT  *'  TO PV-PARAGRAPH-NAME             
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * 8000-CLOSE-FILES : CLOSE INPUT/OUTPUT FILES                   *         
      *****************************************************************         
       8000-CLOSE-FILES.                                                        
                                                                                
      *****************************************************************         
      * COMMITING THE INSERTS AFTER THE LAST COMMIT                   *         
      *****************************************************************         
           IF  PS-EMPTY-FILE-SW = 'N'                                           
      *        PREPARE COMMIT RECORD FOR UPDATE                                 
               MOVE PV-REC-READ-CNT            TO CR-RECORD-COUNT               
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                          
               MOVE CR-CHECKPOINT-RESTART-AREA TO                               
                                             TCKRSTINF-WORK-STRG-DESC           
               PERFORM 2250-COMMIT-CHANGES                                      
           END-IF                                                               
                                                                                
           CLOSE ML-CUTOVER-FILE                                                
                                                                                
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                               
           PERFORM 2200-UPDATE-CHECKPOINT-STATUS.                               
           PERFORM 4000-ISSUE-COMMIT.                                           
                                                                                
           DISPLAY PV-DB2-TINSCLS-INS,                                          
                                    'TINSCLS ROWS INSERTED'.                    
                                                                                
      ******************************************************************        
      * 9900-SQL-ABEND : SQL ABEND ROUTINE                             *        
      ******************************************************************        
       9900-SQL-ABEND.                                                          
                                                                                
           SET ABEND-4013-DB2-ERROR         TO TRUE.                            
           DISPLAY '**  ABEND          **'.                                     
           DISPLAY '**  PROGRAM        **  = ' PC-PROGRAM-NAME.                 
           DISPLAY '**  PARAGRAPH      **  = ' PV-PARAGRAPH-NAME.               
           DISPLAY '**  OPERATION      **  = ' PV-DB2-OPER-ATTEMPTED.           
                                                                                
      * THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                    
      * DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                   
      *                                                                         
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
      *****************************************************************         
