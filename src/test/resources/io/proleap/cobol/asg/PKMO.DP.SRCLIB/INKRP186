       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         INKRP186.                                            
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       01/24/2001.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *PURPOSE:                                                                 
      * THIS PROGRAM REPORTS ON THE PURCHASES, TRANSFERS, MARKDOWNS, AND        
      * MARKUPS WHICH WILL BE INPUT INTO THE MERCHANDISE LEDGER UPDATE.         
      * THE REPORT WILL LIST TOTALS BY TYPE AND BATCH NUMBER FOR ALL            
      * TRANSACTIONS WHICH SURVIVED THE EDIT AND REFORMAT.                      
      *                                                                         
      *  MODIFIED  03/08/2002  BY: CAROL WOJCIECHOWSKI  PROJECT: 23511          
      *                                                                         
      *  MODIFIED PROGRAM TO EXPAND FIELDS IN WORKING STORAGE                   
      *                                                                         
      ******************************************************************        
207210*  MODIFIED  09/04/2018  BY: JIM HUNTER  CHG0207210                       
207210*                                                                         
207210*  RENAMED COPYBOOK KDM2809 TO MLWS050.                                   
207210*                                                                         
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BATCH-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT CONTROL-CARD-FILE                                             
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT REPORT-FILE                                                   
               ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
       FD  BATCH-FILE                                                           
           RECORDING MODE F                                                     
           LABEL RECORDS STANDARD                                               
           BLOCK CONTAINS  0 RECORDS.                                           
       01  BATCH-RECORD           PIC X(125).                                   
                                                                                
       FD  CONTROL-CARD-FILE                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CONTROL-CARD-RECORD    PIC X(80).                                    
                                                                                
       FD  REPORT-FILE                                                          
           RECORDING MODE F                                                     
           LABEL RECORDS OMITTED                                                
           BLOCK CONTAINS  0 RECORDS.                                           
       01  REPORT-RECORD.                                                       
           05  REPORT-LINE        PIC X(132).                                   
                                                                                
      *****************************************************************         
       WORKING-STORAGE SECTION.                                                 
      *****************************************************************         
                                                                                
       01  PA-PROGRAM-ACCUMULATORS                 COMP-3.                      
23511      05  PA-RETAIL             PIC S9(11)V99  VALUE ZERO.                 
23511      05  PA-COST               PIC S9(11)V99  VALUE ZERO.                 
23511      05  PA-TOTAL-RETAIL       PIC S9(11)V99  VALUE ZERO.                 
23511      05  PA-TOTAL-COST         PIC S9(11)V99  VALUE ZERO.                 
23511      05  PA-GRAND-RETAIL       PIC S9(11)V99  VALUE ZERO.                 
23511      05  PA-GRAND-COST         PIC S9(11)V99  VALUE ZERO.                 
           05  PA-RECORD-COUNT       PIC S9(9)     VALUE ZERO.                  
                                                                                
       01  PC-PRINT-CONTROL                        COMP-3.                      
            05  PC-LINE-COUNT        PIC S9(9)     VALUE +56.                   
            05  PC-PAGE-COUNT        PIC S9(5)     VALUE ZERO.                  
            05  PC-LINE-MAX          PIC S9(9)     VALUE +55.                   
            05  PC-ADVANCE           PIC S9(3)     VALUE ZERO.                  
            05  PC-SUB1              PIC S9(2)     VALUE ZERO.                  
            05  PC-SUB2              PIC S9(2)     VALUE ZERO.                  
                                                                                
       01  PC-TRAN-TYPES.                                                       
            05  BATCH-RECEIPTS       PIC 9(3)       VALUE ZEROES.               
            05  BATCH-PURCHASE-ADJ   PIC 9(3)       VALUE ZEROES.               
            05  PC-AP-CODES          PIC X(2).                                  
                88 RECEIPTS-AND-ADJ                 VALUE '01'.                 
                                                                                
       01  CC-CONTROL-CARD.                                                     
           05  CC-CALENDAR-TYPE      PIC X(02)     VALUE SPACES.                
           05  FILLER                PIC X(01)     VALUE SPACES.                
           05  SYSIN-DATE            PIC X(10)     VALUE ZERO.                  
                                                                                
       01  SA-SAVE-AREAS.                                                       
           05  SA-BATCH              PIC X(4)      VALUE SPACES.                
           05  SA-BATCH-2 REDEFINES SA-BATCH.                                   
               10  FILLER            PIC X.                                     
               10  SA-SHORT-BATCH    PIC X(3).                                  
           05  SA-BATCH-CONVERT      PIC X(4)      VALUE SPACES.                
           05  SA-BATCH-NUMERIC REDEFINES SA-BATCH-CONVERT.                     
               10  FILLER            PIC X.                                     
               10  SA-BATCH-NUM      PIC 9(3).                                  
           05  SA-CODE               PIC 9(2)      VALUE ZERO.                  
           05  SA-PURCH-BATCH.                                                  
               10  SA-P-BATCH        PIC X(3)      VALUE ZERO.                  
               10  FILLER            PIC X         VALUE SPACES.                
           05  SA-DATE.                                                         
               10  SA-YEAR           PIC X(2)      VALUE SPACES.                
               10  SA-MONTH          PIC X(2)      VALUE SPACES.                
               10  SA-DAY            PIC X(2)      VALUE SPACES.                
           05  SA-TIME.                                                         
               10  SA-HOUR           PIC X(2)      VALUE SPACES.                
               10  SA-MINUTE         PIC X(2)      VALUE SPACES.                
               10  FILLER            PIC X(2).                                  
           05  SA-DATE-CCYYMMDD-N    PIC 9(8)      VALUE ZERO.                  
           05  SA-DATE-CCYYMMDD REDEFINES SA-DATE-CCYYMMDD-N.                   
               10  SA-CC             PIC 9(2).                                  
               10  SA-YY             PIC 9(2).                                  
               10  SA-MM             PIC 9(2).                                  
               10  SA-DD             PIC 9(2).                                  
           05  SA-DATE-ISO-FMT.                                                 
               10  SA-MM-ISO         PIC X(2).                                  
               10  SA-DD-ISO         PIC X(2).                                  
               10  SA-YY-ISO         PIC X(2).                                  
               10  SA-CC-ISO         PIC X(2).                                  
           05  SA-DATE-GREG-FMT-N    PIC 9(8).                                  
           05  SA-DATE-GREG-FMT REDEFINES SA-DATE-GREG-FMT-N.                   
               10  SA-MM-GREG        PIC 9(2).                                  
               10  SA-DD-GREG        PIC 9(2).                                  
               10  SA-CC-GREG        PIC 9(2).                                  
               10  SA-YY-GREG        PIC 9(2).                                  
           05  HOLD-JULIAN            PIC 9(5).                                 
           05  HOLD-JULIAN-RED REDEFINES HOLD-JULIAN.                           
               10  FILLER             PIC 99.                                   
               10  JULIAN-DAY-HOLD    PIC 999.                                  
           05  HOLD-JULIAN-PLUS.                                                
               10  JULIAN-DAY-PLUS    PIC 999.                                  
           05  SA-BATCH-NO-JRNL.                                                
               10  SA-BATCH-NO-JRNL-X PIC X(3)     VALUE SPACE.                 
               10  FILLER             PIC X(1)     VALUE SPACE.                 
           05  SA-BATCH-NO-UNJRNL.                                              
               10  SA-BATCH-NO-UNJRNL-X PIC X(3)   VALUE SPACE.                 
               10  FILLER             PIC X(1)     VALUE SPACE.                 
                                                                                
       01  PT-PURCHASE-TYPE          PIC X(2)      VALUE SPACE.                 
           88  PT-REVERSAL                         VALUE '02' '03'              
                                                         '04' '05'              
                                                         '08' '11'              
                                                         '14'.                  
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-TRANSFER-BATCH.                                               
               10  FILLER            PIC 9         VALUE ZERO.                  
               10  PV-TRANSFER-BATCH-POS2-4                                     
                                     PIC X(3)      VALUE SPACES.                
                                                                                
       01  HR-HOLD-RECORD.                                                      
           05  HR-CODE               PIC X(2)      VALUE SPACES.                
               88  HR-PURCHASE                     VALUE '10'.                  
               88  HR-TRANSFER                     VALUE '30'                   
                                                         '31'.                  
               88  HR-MARKDOWN                     VALUE '40'.                  
               88  HR-MARKON                       VALUE '50'.                  
           05  HR-BATCH              PIC 9(4)      VALUE ZERO.                  
           05  FILLER                PIC X(94)     VALUE SPACES.                
                                                                                
       01  SW-SWITCHES.                                                         
           05  PS-CONTROL-CARD-EOF-SW PIC  X       VALUE 'N'.                   
               88  PS-END-OF-CONTROL-CARD-FILE     VALUE 'Y'.                   
           05  SW-EOF-BATCH          PIC X         VALUE 'N'.                   
               88  SW-EOF-BATCH-FILE               VALUE 'Y'.                   
           05  SW-FIRST-PASS-THRU    PIC X         VALUE 'Y'.                   
               88  SW-FIRST-PASS                   VALUE 'Y'.                   
                                                                                
       01  ABEND-CODE                PIC S9(04)    VALUE +0 COMP SYNC.          
                                                                                
       01  H2-HEADING2.                                                         
           05  FILLER                PIC X(38)     VALUE SPACES.                
           05  FILLER                PIC X(57)     VALUE                        
           'INVENTORY WEEKLY BEFORE BATCH TOTALS FOR ESTIMATED STORES'.         
           05  FILLER                PIC X(37)     VALUE SPACES.                
                                                                                
       01  H3-HEADING3.                                                         
           05  FILLER                PIC X(37)     VALUE SPACES.                
           05  FILLER                PIC X(60)     VALUE                        
           'CODE  BATCH NO  RETAIL AMOUNT     COST AMOUNT'.                     
           05  FILLER                PIC X(35)     VALUE SPACES.                
                                                                                
       01  D1-DETAIL-LINE.                                                      
           05  FILLER                PIC X(38)     VALUE SPACE.                 
           05  D1-CODE               PIC XX        VALUE SPACES.                
           05  FILLER                PIC X(5)      VALUE SPACES.                
           05  D1-BATCH              PIC 999       VALUE ZERO.                  
           05  D1-RETAIL             PIC ZZ,ZZZ,ZZZ,ZZZ.99-.                    
           05  D1-COST               PIC ZZ,ZZZ,ZZZ,ZZZ.99-.                    
                                                                                
       01  T1-TOTAL-LINE.                                                       
           05  FILLER                PIC X(35)     VALUE SPACE.                 
           05  T1-HOLD               PIC X(13)     VALUE SPACES.                
           05  T1-RETAIL             PIC ZZ,ZZZ,ZZZ,ZZZ.99-                     
                                                   VALUE ZERO.                  
           05  T1-COST               PIC ZZ,ZZZ,ZZZ,ZZZ.99-                     
                                                   VALUE ZERO.                  
                                                                                
       01  RL-RECORD-LINE.                                                      
           05  FILLER                PIC X         VALUE SPACE.                 
           05  RL-RECORDS            PIC ZZ,ZZZ,ZZZ                             
                                                   VALUE ZERO.                  
           05  FILLER                PIC X(20)     VALUE                        
           ' RECORDS TOTALED'.                                                  
                                                                                
       01  D3-JOURNAL-LINE.                                                     
           05  FILLER                PIC X(42)      VALUE SPACES.               
           05  D3-VERIFIED-TYPE      PIC X(31).                                 
           05  FILLER                PIC X(02)      VALUE SPACES.               
           05  D3-VERIFIED-AMTS      PIC Z,ZZZ,ZZZ,ZZ9.99-.                     
           05  FILLER                PIC X(24)      VALUE SPACES.               
                                                                                
       01  D4-TOTAL-LINE.                                                       
           05  FILLER                PIC X(39)      VALUE SPACES.               
           05  D4-BATCH-TITLE        PIC X(05)      VALUE 'BATCH'.              
           05  FILLER                PIC X(01)      VALUE SPACES.               
           05  D4-BATCH-TYPE         PIC X(04)      VALUE SPACES.               
           05  FILLER                PIC X(01)      VALUE SPACES.               
           05  D4-RETAIL-VALUE       PIC X(19)      VALUE                       
                                              ' RETAIL VALUE      '.            
           05  D4-VERIFIED-AMTS      PIC Z,ZZZ,ZZZ,ZZ9.99-.                     
           05  FILLER                PIC X(30)      VALUE SPACES.               
      *                                                                         
       01  T2-TOTAL-LINE.                                                       
           05  FILLER                PIC X(22)      VALUE SPACES.               
           05  FILLER                PIC X(37)      VALUE                       
                             'TOTAL RECEIPT & ADJ VERIFIED WEEK OF '.           
           05  T2-CURR-DATE          PIC X(08)      VALUE SPACES.               
           05  FILLER                PIC X(08)      VALUE SPACES.               
           05  T2-VERIFIED-AMTS      PIC Z,ZZZ,ZZZ,ZZ9.99-.                     
           05  FILLER                PIC X(20)      VALUE SPACES.               
                                                                                
      /****************************************************************         
      * COPYBOOK - ON ORDER DESCRIPTION FILE ARGUMENTS.                         
      *****************************************************************         
207210     COPY MLWS050.                                                        
                                                                                
      /****************************************************************         
      * COPYBOOK - KOHLS STANDARD 132 COLUMN HEADER DESCRIPTION AREA.           
      *****************************************************************         
           COPY DPWS132O.                                                       
                                                                                
      /****************************************************************         
      * COPYBOOK - KOHL'S CALENDAR ROUTINE, DPKUT500, ARGUMENTS.                
      *****************************************************************         
           COPY DPWS500.                                                        
                                                                                
      /****************************************************************         
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
                                                                                
       A000-MAINLINE.                                                           
                                                                                
            PERFORM A100-INITIALIZATION-ROUTINE.                                
                                                                                
            PERFORM B200-PROCESS-BATCHES                                        
               UNTIL SW-EOF-BATCH-FILE.                                         
                                                                                
            PERFORM C100-WRITE-BATCH-LINE.                                      
                                                                                
            PERFORM C200-WRITE-CODE-TOTAL.                                      
                                                                                
            PERFORM B300-WRITE-FINAL-TOTAL.                                     
                                                                                
            CLOSE BATCH-FILE                                                    
                  CONTROL-CARD-FILE                                             
                  REPORT-FILE.                                                  
                                                                                
       Z999-EXIT-PROGRAM.                                                       
            STOP RUN.                                                           
                                                                                
      ******************************************************************        
      * OPEN BATCH/REPORT FILES. OBTAIN SATURDAY SYSIN DATE FOR PROCESS.        
      * NEW MODULE   -  WAS PREVIOUSLY EXECUTED DIRECLTY IN A000-MAIN***        
      ******************************************************************        
       A100-INITIALIZATION-ROUTINE.                                             
           OPEN INPUT  BATCH-FILE                                               
                INPUT  CONTROL-CARD-FILE                                        
                OUTPUT REPORT-FILE                                              
                                                                                
           PERFORM B100-FORMAT-DATE-TIME.                                       
                                                                                
      ***************************************************************           
      *  DETERMINE THE PURCHASE ADJ/RECEIPTS BATCH NUMBER FROM                  
      *  THE CURRENT DATE OR FROM THE OVERRIDE CONTROL CARD NUMBER              
      ***************************************************************           
                                                                                
            ACCEPT HOLD-JULIAN FROM DAY.                                        
                                                                                
            COMPUTE JULIAN-DAY-PLUS = (JULIAN-DAY-HOLD + 500).                  
            MOVE JULIAN-DAY-HOLD TO SA-BATCH-NO-UNJRNL-X.                       
            MOVE JULIAN-DAY-PLUS TO SA-BATCH-NO-JRNL-X.                         
                                                                                
            MOVE 'INKRP186' TO DP132O-PROGRAM-NAME.                             
            MOVE '1' TO DP132O-REPORT-NUMBER.                                   
                                                                                
            PERFORM Z100-READ-BATCH-FILE.                                       
                                                                                
      ***************************************************************           
      * B100-FORMAT-DATE-TIME  THE CURRENT DATE AND TIME ARE MOVED  *           
      * TO THE REPORT HEADING LINES.                                *           
      ***************************************************************           
        B100-FORMAT-DATE-TIME.                                                  
            ACCEPT SA-DATE FROM DATE.                                           
            ACCEPT SA-TIME FROM TIME.                                           
            MOVE SA-MONTH TO DP132O-RUN-MONTH.                                  
            MOVE SA-DAY TO DP132O-RUN-DAY.                                      
            MOVE SA-YEAR TO DP132O-RUN-YEAR.                                    
            MOVE SA-HOUR TO DP132O-RUN-HOUR.                                    
            MOVE SA-MINUTE TO DP132O-RUN-MINUTE.                                
                                                                                
      ******************************************************************        
      *  VALIDATE THE JOBTRAC DATE AND OBTAIN BEGINNING OF WEEK(SUNDAY)         
      ******************************************************************        
                                                                                
           PERFORM Z200-READ-CONTROL-CARD-FILE.                                 
           DISPLAY 'CONTROL CARD: ' CC-CONTROL-CARD.                            
                                                                                
           IF PS-END-OF-CONTROL-CARD-FILE                                       
               DISPLAY '** ABEND IN PROGRAM INKRP186'                           
               DISPLAY '** PARAGRAPH B100-FORMAT-DATE-TIME'                     
               DISPLAY '** CONTROL CARD FILE IS EMPTY.'                         
               MOVE +4003 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE                         03990000
           END-IF.                                                              
                                                                                
           MOVE SA-CC              TO SA-CC-ISO.                                
           MOVE SA-YY              TO SA-YY-ISO.                                
           MOVE SA-MM              TO SA-MM-ISO.                                
           MOVE SA-DD              TO SA-DD-ISO.                                
                                                                        03770000
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              03780000
           ACCEPT DPG51-SYSTEM-DATE FROM DATE.                          03790000
           SET  DPG52-LK-DTE-ISO-FMT                                    03810000
                DPG51-FISCAL-454-CALENDAR TO TRUE.                      03810000
           MOVE SYSIN-DATE           TO DPG52-LK-DATE-INPUT.            03820000
                                                                        03830000
           CALL DP500-KOHLS-CALENDAR-ROUTINE                            03840000
               USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.               03850000
                                                                        03860000
           EVALUATE TRUE                                                03870000
               WHEN DPG54-SEVERE-ERROR                                  03880000
                   DISPLAY '** ABEND **'                                03890000
                   DISPLAY '** PGM = MLKUP004'                          03900000
                   DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE. '  03910000
                   DISPLAY '** ERROR MESSAGE = '                        03920000
                           DPG54-ERROR-MESSAGE                          03930000
                   DISPLAY '** DPG51 = ' DPG51                          03940000
                   DISPLAY '** DPG52 = ' DPG52                          03950000
                   DISPLAY '** DPG53 = ' DPG53                          03960000
                   DISPLAY '** DPG54 = ' DPG54                          03970000
                   MOVE +4003 TO ABEND-CODE                             03980000
                   CALL 'ILBOABN0' USING ABEND-CODE                     03990000
               WHEN OTHER                                               04000000
                   CONTINUE                                             04010000
           END-EVALUATE.                                                04020000
                                                                        04030000
           IF  DPG55-NUMERIC-WEEK-DAY-SAT                               04040000
               CONTINUE                                                 04050000
           ELSE                                                         04060000
               DISPLAY '** ABEND **'                                    04070000
               DISPLAY '** PGM = MLKUP004'                              04080000
               DISPLAY '** SYSIN DATE NOT A SATURDAY DATE.'             04090000
               MOVE +4003 TO ABEND-CODE                                 04100000
               CALL 'ILBOABN0' USING ABEND-CODE                         04110000
           END-IF.                                                      04120000
                                                                        04130000
      ****************************************************************          
      * B200-PROCESS-BATCHES    CONTROLS THE PROCESSING OF TRANSACTION          
      * TYPES.  CHECKS FOR CHANGES IN BATCH OR TRANSACTION TYPES, AND           
      * CALCULATES THE COST AND RETAIL AMOUNTS FOR EACH BATCH.                  
      ***************************************************************           
        B200-PROCESS-BATCHES.                                                   
                                                                                
            IF SW-FIRST-PASS                                                    
                MOVE HR-CODE TO SA-CODE                                         
                MOVE HR-BATCH TO SA-BATCH                                       
                MOVE 'N' TO SW-FIRST-PASS-THRU                                  
            END-IF.                                                             
                                                                                
            IF HR-CODE NOT = SA-CODE                                            
                PERFORM C100-WRITE-BATCH-LINE                                   
                PERFORM C200-WRITE-CODE-TOTAL                                   
            ELSE                                                                
                IF HR-BATCH NOT = SA-BATCH                                      
                    PERFORM C100-WRITE-BATCH-LINE                               
                END-IF                                                          
            END-IF.                                                             
                                                                                
            IF HR-PURCHASE                                                      
                MOVE D115-TRANS-CD TO PT-PURCHASE-TYPE                          
                IF PT-REVERSAL                                                  
                    PERFORM C300-PROCESS-REVERSAL-AMT                           
                ELSE                                                            
                    COMPUTE PA-RETAIL = PA-RETAIL + D115-RETAIL-AMT             
                    COMPUTE PA-COST = PA-COST + D115-NET-COST                   
                END-IF                                                          
                PERFORM B210-PROCESS-RECEIPTS                                   
            END-IF.                                                             
            IF HR-TRANSFER                                                      
                COMPUTE PA-RETAIL = PA-RETAIL +                                 
                    (D115-RETAIL-PER * D115-UNITS-TRANSFER)                     
                MOVE ZERO TO PA-COST                                            
            END-IF.                                                             
            IF HR-MARKDOWN                                                      
                COMPUTE PA-RETAIL = PA-RETAIL +                                 
                    ((D115-MKDWN-OPRICE - D115-MKDWN-NPRICE) *                  
                    D115-MKDWN-QTY)                                             
                MOVE ZERO TO PA-COST                                            
            END-IF.                                                             
            IF HR-MARKON                                                        
                COMPUTE PA-RETAIL = PA-RETAIL +                                 
                    ((D115-MARKON-NPRICE - D115-MARKON-OPRICE) *                
                    D115-MARKON-QTY)                                            
                MOVE ZERO TO PA-COST                                            
            END-IF.                                                             
            ADD 1 TO PA-RECORD-COUNT.                                           
            MOVE HR-CODE TO SA-CODE.                                            
            MOVE HR-BATCH TO SA-BATCH                                           
            PERFORM Z100-READ-BATCH-FILE.                                       
                                                                                
      ******************************************************************        
      * B210-PROCESS  CONVERT SYSIN DATE TO CALENDAR FORMATS                    
      * RECEIPTS AP CODE = '01'  ANY OTHER AP CODES LISTED AS 'OTHER'           
      ******************************************************************        
       B210-PROCESS-RECEIPTS.                                                   
            MOVE D115-AP-TRANS-CDE TO PC-AP-CODES.                              
            MOVE DPG55-1ST-WKDY-GREG-CC-N TO SA-DATE-GREG-FMT.                  
            MOVE SA-MM-GREG TO SA-MM.                                           
            MOVE SA-DD-GREG TO SA-DD.                                           
            MOVE SA-YY-GREG TO SA-YY.                                           
            MOVE SA-CC-GREG TO SA-CC.                                           
                                                                                
      *********************************************************                 
      * B300-WRITE-FINAL-TOTAL  FORMATS FINAL TOTAL LINE      *                 
      *********************************************************                 
        B300-WRITE-FINAL-TOTAL.                                                 
            MOVE 'FINAL TOTALS' TO T1-HOLD.                                     
            MOVE PA-GRAND-RETAIL TO T1-RETAIL.                                  
            MOVE PA-GRAND-COST TO T1-COST.                                      
            MOVE T1-TOTAL-LINE TO REPORT-RECORD.                                
            WRITE REPORT-RECORD                                                 
                AFTER ADVANCING 2.                                              
            MOVE PA-RECORD-COUNT TO RL-RECORDS.                                 
            MOVE RL-RECORD-LINE TO REPORT-RECORD.                               
            WRITE REPORT-RECORD                                                 
                AFTER ADVANCING 2.                                              
                                                                                
      ************************************************************              
      * C100-WRITE-BATCH-LINE   FORMATS TOTAL FOR A BATCH NUMBER *              
      ************************************************************              
        C100-WRITE-BATCH-LINE.                                                  
            IF PC-LINE-COUNT IS GREATER THAN PC-LINE-MAX                        
                 PERFORM D100-WRITE-HEADINGS.                                   
            MOVE SA-CODE TO D1-CODE.                                            
            MOVE PA-RETAIL TO D1-RETAIL.                                        
            ADD PA-RETAIL TO PA-TOTAL-RETAIL.                                   
            MOVE ZERO TO PA-RETAIL.                                             
            IF SA-CODE = '10'                                                   
                MOVE PA-COST TO D1-COST                                         
                ADD  PA-COST TO PA-TOTAL-COST                                   
                MOVE ZERO TO PA-COST                                            
                MOVE SA-BATCH TO SA-PURCH-BATCH                                 
                MOVE SA-P-BATCH TO D1-BATCH                                     
            ELSE                                                                
                MOVE SA-BATCH  TO D1-BATCH                                      
                MOVE ZERO TO D1-COST                                            
            END-IF.                                                             
            MOVE HR-BATCH TO SA-BATCH.                                          
            MOVE D1-DETAIL-LINE TO REPORT-LINE.                                 
            WRITE REPORT-RECORD                                                 
                AFTER ADVANCING PC-ADVANCE.                                     
            MOVE 1 TO PC-ADVANCE.                                               
            ADD PC-ADVANCE TO PC-LINE-COUNT.                                    
                                                                                
      ********************************************************                  
      * C200-WRITE-CODE-TOTAL  FORMATS TOTAL FOR A TRAN TYPE *                  
      ********************************************************                  
       C200-WRITE-CODE-TOTAL.                                                   
            IF PC-LINE-COUNT IS GREATER THAN PC-LINE-MAX                        
                 PERFORM D100-WRITE-HEADINGS.                                   
            MOVE 'CODE TOTAL' TO T1-HOLD.                                       
            MOVE PA-TOTAL-RETAIL TO T1-RETAIL.                                  
            MOVE PA-TOTAL-COST TO T1-COST.                                      
            MOVE T1-TOTAL-LINE TO REPORT-LINE.                                  
            ADD PA-TOTAL-RETAIL TO PA-GRAND-RETAIL.                             
            ADD PA-TOTAL-COST TO PA-GRAND-COST.                                 
            MOVE ZERO TO PA-TOTAL-RETAIL                                        
                         PA-TOTAL-COST.                                         
            MOVE HR-CODE TO SA-CODE.                                            
            MOVE 2 TO PC-ADVANCE.                                               
            WRITE REPORT-RECORD                                                 
                AFTER ADVANCING PC-ADVANCE.                                     
            ADD PC-ADVANCE TO PC-LINE-COUNT.                                    
                                                                                
      *******************************************************                   
      * C300-PROCESS-REVERSAL-AMT   PURCHASE RECORDS WITH   *                   
      * TRANSACTION CODES OF 02, 03, 04, 05, 08, 11 AND 14  *                   
      * MUST HAVE THERE SIGNS REVERSED PRIOR TO BEING ADDED *                   
      *******************************************************                   
       C300-PROCESS-REVERSAL-AMT.                                               
            COMPUTE D115-NET-COST = D115-NET-COST * -1.                         
            COMPUTE D115-RETAIL-AMT = D115-RETAIL-AMT * -1.                     
            COMPUTE PA-RETAIL = PA-RETAIL + D115-RETAIL-AMT.                    
            COMPUTE PA-COST = PA-COST + D115-NET-COST.                          
                                                                                
      *********************************************************                 
      * D100-WRITE-HEADINGS   FORMATS HEADINGS FOR A NEW PAGE *                 
      *********************************************************                 
       D100-WRITE-HEADINGS.                                                     
           ADD 1 TO PC-PAGE-COUNT.                                              
           MOVE PC-PAGE-COUNT TO DP132O-PAGE-NUMBER.                            
           MOVE DP132O-STANDRD-HEADER-132-COLS TO REPORT-RECORD.                
           WRITE REPORT-RECORD                                                  
               AFTER ADVANCING PAGE.                                            
           MOVE H2-HEADING2 TO REPORT-RECORD.                                   
           WRITE REPORT-RECORD                                                  
               AFTER ADVANCING 1.                                               
           MOVE H3-HEADING3 TO REPORT-RECORD.                                   
           WRITE REPORT-RECORD                                                  
               AFTER ADVANCING 2                                                
           MOVE ZERO TO PC-LINE-COUNT.                                          
           MOVE 6 TO PC-LINE-COUNT.                                             
           MOVE 2 TO PC-ADVANCE.                                                
                                                                                
      ******************************************************************        
      * READ THE INPUT FILE. FOR TRANSFERS (CODES 30 AND 31), THE BATCH         
      * NUMBER IS NOT IN THE SAME POSITION AS ON THE OTHER TRANSACTIONS,        
      * SO MOVE IT INTO THE SAME POSITION BEFORE FURTHER PROCESSING.            
      ******************************************************************        
       Z100-READ-BATCH-FILE.                                                    
                                                                                
           READ BATCH-FILE INTO D115-ON-ORDER-REC                               
               AT END MOVE 'Y' TO SW-EOF-BATCH.                                 
                                                                                
           MOVE D115-ON-ORDER-REC TO HR-HOLD-RECORD                             
                                     D115-PURCH-JOURNAL-REC                     
                                     D115-TRANSFER-TO-REC                       
                                     D115-TRANSFER-FROM-REC                     
                                     D115-MKDWN-REC                             
                                     D115-MARKON-REC                            
                                     D115-ADJ-REC.                              
                                                                                
           IF NOT SW-EOF-BATCH-FILE                                             
               IF HR-TRANSFER                                                   
                   MOVE D115-TRANS-BATCH  TO PV-TRANSFER-BATCH-POS2-4           
                   MOVE PV-TRANSFER-BATCH TO HR-BATCH                           
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * READ FILE BACKPOSTING CONTROL CARD.....                                 
      ******************************************************************        
       Z200-READ-CONTROL-CARD-FILE.                                             
                                                                                
            READ CONTROL-CARD-FILE NEXT RECORD                                  
              INTO CC-CONTROL-CARD                                              
                AT END SET PS-END-OF-CONTROL-CARD-FILE TO TRUE.                 
                                                                                
