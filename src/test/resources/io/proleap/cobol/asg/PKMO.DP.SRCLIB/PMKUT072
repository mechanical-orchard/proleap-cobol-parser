      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             PMKUT072.                                        
       AUTHOR.                 AMANDA NEWHART                                   
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           FEBRUARY 2003.                                   
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *    PMKUT072 - PROMOTION BUSINESS EVENT INTEGRITY CHECK.        *        
      *                                                                *        
      *    THIS PROGRAM CREATES THE PROMOTION FILE USED IN THE DATA    *        
      *    INTEGRITY PROCESS.  FIRST OPEN THE TWO INPUT FILES & ONE    *        
      *    OUTPUT FILE.  ONCE OPEN READ THE TWO INPUT FILES.  IF       *        
      *    NOTHING IS IN EITHER OF THESE FILES ABEND THE PROGRAM.      *        
      *    NEXT TRY TO LOG ON TO ORACLE.  IF SUCCESSFUL THEN FIND THE  *        
      *    CURRENT SYS DATE USING A SELECT STATMENT.  ONCE THE SYS DATE*        
      *    HAS BEEN FOUND THEN SELECT THE REGULAR PROMO RECORDS ON     *        
      *    THE PMV_REG_PROM TABLE THAT FALL WITHIN THE DATE RANGE AND  *        
      *    HAVE NOT BEEN UPDATED SINCE THE TIMESTAMP FILE TIME.  MOVE  *        
      *    THE SELECTED VARIABLES TO A COPYBOOK AND WRITE THAT         *        
      *    LINE TO THE OUTPUT FILE.  NEXT FIND THE CLEARANCE PROMO     *        
      *    RECORDS ON THE PMT_CLRNC_PROMO TABLE THAT FALL WITHIN THE   *        
      *    DATE RANGE AND HAVE NOT BEEN UPDATED SINCE THE TIMESTAMP    *        
      *    FILE TIME.  MOVE THE SELECTED VARIABLES TO A COPYBOOK       *        
      *    AND WRITE THAT COPYBOOK LINE TO THE OUTPUT FILE.  AFTER THE *        
      *    LAST RECORD HAS BEEN WRITTEN TO THE FILE WRITE A TOTAL LINE *        
      *    TO THE FILE.  FINALLY CLOSE THE TWO INPUT FILES AND THE ONE *        
      *    OUTPUT FILE.                                                *        
      *                                                                *        
      ******************************************************************        
      **   CHANGE LOG:                                                 *        
      ******************************************************************        
      *    MM/DD/YYYY - NAME HERE - WR NO. HERE                        *        
      *                                                                *        
      *    07/01/2003 - AMANDA NEWHART - WR 21770                      *        
      *      - ADDED A TIMESTAMP TO ALL THE WHERE CLAUSES THAT WERE    *        
      *        LOOKING FOR A DATE.                                     *        
      *                                                                *        
      *    08/11/2003 - AMANDA NEWHART - WR 21770                      *        
      *      - FORMATED THE LAST UPDATE TIMESTAMP FROM CLRNC TO LOOK   *        
      *        LIKE THE ONE COMING FROM REGULAR.                       *        
      *                                                                *        
      *    07/30/2018 - COGNIZANT - CHG0204126                         *        
      *      - ORACLE LOGON STANDARDS PROGRAM CHANGE                   *        
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT DATE-RANGE-FILE                                               
               ASSIGN                       TO INP01.                           
                                                                                
           SELECT TMST-FILE                                                     
               ASSIGN                       TO INP02.                           
                                                                                
070941     SELECT ORACLE-LOGON-FILE                                             
070941         ASSIGN                       TO ORALOGON.                        
070941                                                                          
           SELECT PROMO-FILE                                                    
               ASSIGN                       TO OUT01.                           
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  DATE-RANGE-FILE                                                      
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  DATE-RANGE-LINE                  PIC  X(80).                         
                                                                                
       FD  TMST-FILE                                                            
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  TMST-LINE                        PIC  X(80).                         
                                                                                
070941 FD  ORACLE-LOGON-FILE                                                    
070941     RECORDING MODE IS F                                                  
070941     LABEL RECORDS ARE STANDARD                                           
070941     BLOCK CONTAINS 0 RECORDS                                             
070941     RECORD CONTAINS 80 CHARACTERS                                        
070941     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
070941 01  OL-ORACLE-LOGON-RECORD           PIC X(80).                          
                                                                                
       FD  PROMO-FILE                                                           
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  PROMO-LINE                       PIC X(250).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0.         
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.           
           88  ABEND-4002-READ-ERROR                     VALUE +4002.           
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.           
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.           
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.           
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.           
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.           
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.           
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
           88  ABEND-4014-ORACLE-LOGON-ERROR             VALUE +4014.           
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.           
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.           
                                                                                
       01  CC-DATE-RANGE-FILE.                                                  
           05  CC-START-DATE                PIC X(10).                          
           05  FILLER                       PIC X(01).                          
           05  CC-END-DATE                  PIC X(10).                          
                                                                                
       01  CC-TMST-FILE.                                                        
           05  CC-LAST-UPD-TMST             PIC X(19).                          
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-DATE-RANGE-FILE-SW        PIC  X(01)  VALUE 'N'.              
               88  PS-DATE-RANGE-FILE-END               VALUE 'Y'.              
               88  PS-DATE-RANGE-FILE-START             VALUE 'N'.              
           05  PS-TMST-FILE-SW              PIC  X(01)  VALUE 'N'.              
               88  PS-TMST-FILE-END                     VALUE 'Y'.              
               88  PS-TMST-FILE-START                   VALUE 'N'.              
           05  PS-END-OF-PROMO-REG-CSR-SWITCH PIC X(1)  VALUE 'N'.              
               88  PS-END-OF-PROMO-REG-CSR              VALUE 'Y'.              
               88  PS-NOT-END-OF-PROMO-REG-CSR          VALUE 'N'.              
           05  PS-END-OF-PROMO-CLR-CSR-SWITCH PIC X(1)  VALUE 'N'.              
               88  PS-END-OF-PROMO-CLR-CSR              VALUE 'Y'.              
               88  PS-NOT-END-OF-PROMO-CLR-CSR          VALUE 'N'.              
           05  PS-TYPE                          PIC X(01).                      
               88  PS-CLRNC-TYP                            VALUE 'C'.           
               88  PS-REG-TYP                              VALUE 'R'.           
070941     05  PS-END-OF-LOGON-FILE-SW          PIC X(1)   VALUE 'N'.           
070941         88  PS-END-OF-LOGON-FILE                    VALUE 'Y'.           
070941     05  PS-ORA-SRC-CTL-DONE-SW           PIC X(1)   VALUE 'N'.           
070941         88  PS-ORA-SRC-EMPTY                        VALUE 'Y'.           
                                                                                
070941 01  CC-ORACLE-CONNECTION.                                                
070941     05  CC-ORACLE-USERID             PIC X(40).                          
070941         88 CC-AUTO-LOGON                         VALUE '/'.              
070941     05  CC-ORACLE-PASSWORD           PIC X(40).                          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)  VALUE                   
                                                           'PMKUT072'.          
           05  PC-BUSINESS-EVNT-ID          PIC  9(03)  VALUE 020.              
           05  PC-ACTION-CDE                PIC  X(01)  VALUE 'U'.              
           05  PC-THREE                     PIC  9(02)  VALUE 03.               
           05  PC-REG-PROMO                 PIC  X(01)  VALUE 'R'.              
           05  PC-CLRNC-PROMO               PIC  X(01)  VALUE 'C'.              
           05  PC-DASH                      PIC  X(01)  VALUE '-'.              
           05  PC-SPACE                     PIC  X(01)  VALUE ' '.              
           05  PC-BEG-TMST                  PIC  X(08)  VALUE                   
                                                          '00:00:00'.           
           05  PC-END-TMST                  PIC  X(08)  VALUE                   
                                                          '23:59:59'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
070941     05  PV-ORACLE-USERID-LEN         PIC S9(4)   VALUE ZEROS             
070941                                                  COMP SYNC.              
070941     05  PV-ORACLE-PASSWORD-LEN       PIC S9(4)   VALUE ZEROS             
070941                                                  COMP SYNC.              
           05  PV-SQL                       PIC S9(04)  VALUE ZEROS.            
           05  PV-TABLE-NAME                PIC  X(12)  VALUE SPACES.           
           05  PV-OPERATION-MSG             PIC  X(60)  VALUE SPACES.           
           05  PV-OPERATION-MSG-1           PIC  X(40)  VALUE SPACES.           
           05  PV-PARAGRAPH-NAME            PIC  X(30)  VALUE SPACES.           
           05  PV-SYS-DATE                  PIC  X(26)  VALUE SPACES.           
           05  PV-SYS-DATE-XS REDEFINES PV-SYS-DATE.                            
               10  PV-SYS-DATE-DATE         PIC  X(10).                         
               10  PV-PERIOD                PIC  X(01).                         
               10  PV-SYS-DATE-TIME         PIC  X(15).                         
           05  PV-NEW-STRT-DATE             PIC  X(26)     VALUE SPACES.        
           05  PV-NEW-END-DATE              PIC  X(26)     VALUE SPACES.        
                                                                                
       01 PV-SELECT-VARIABLES.                                                  
           05  PV-PROMO-ID                  PIC  9(10)  VALUE ZEROS.            
           05  PV-AD-EVNT-ID                PIC  9(10)  VALUE ZEROS.            
           05  PV-PROMO-NBR-TXT             PIC  X(15)  VALUE SPACES.           
           05  PV-CPLT-NM                   PIC  X(40)  VALUE SPACES.           
           05  PV-ABBR-NM                   PIC  X(05)  VALUE SPACES.           
           05  PV-PROMO-REG-TYP-CDE         PIC  X(02)  VALUE SPACES.           
           05  PV-PROMO-REG-ADV-TYP-CDE     PIC  X(02)  VALUE SPACES.           
           05  PV-STRT-TMST                 PIC  X(19)  VALUE SPACES.           
           05  PV-END-TMST                  PIC  X(19)  VALUE SPACES.           
           05  PV-ACT-START-TIME            PIC  X(19)  VALUE SPACES.           
           05  PV-ACT-END-TIME              PIC  X(19)  VALUE SPACES.           
           05  PV-ESL-TOPR-TYP              PIC  X(03)  VALUE SPACES.           
           05  PV-STAT-CDE                  PIC  X(01)  VALUE SPACES.           
           05  PV-LAST-UPD-TMST             PIC  X(19)  VALUE SPACES.           
           05  PV-ESL-TOPR-TYP-NULL         PIC S9(04)  COMP                    
                                                        VALUE ZEROS.            
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECS-WRITTEN              PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-PROMO-REG-CNT             PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-PROMO-CLR-CNT             PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE PROMO BUSINESS EVENT                                
      ******************************************************************        
           COPY PMRD702.                                                        
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE TOTAL RECORD                                        
      ******************************************************************        
           COPY PMRD700.                                                        
                                                                                
      *****************************************************************         
      *                      O  R  A  C  L  E                                   
      *================================================================         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *****************************************************************         
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *---------------------------------------------------------------*         
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *---------------------------------------------------------------*         
       01      PC-ENT01-TYPE-CDE          PIC X(02)      VALUE '01'.            
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
                                                                                
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
                                                                                
       01      ORACLE-RETURN              PIC X(200)     VALUE SPACES.          
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      ********************                                                      
       EJECT                                                                    
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      * ==> PROCESSES:                                                *         
      *  - RESET SWITCHES                                             *         
      *  - OPEN INPUT AND OUTPUT FILES                                *         
      *  - READ THE DATE RANGE FILE                                   *         
      *  - READ THE TIMESTAMP FILE                                    *         
      *  - SET UP ORACLE                                              *         
      *  - FIND THE CURRENT SYS DATE                                  *         
      *  - RETRIEVE PROMO INFO FOR REGULAR PRICED ITEMS               *         
      *  - RETRIEVE PROMO INFO FOR CLEARANCE PRICED ITEMS             *         
      *  - WRITE THE TOTAL LINES                                      *         
      *  - CLOSE THE INPUT AND OUTPUT FILES                           *         
      * ==> PERFORMED FROM: START                                     *         
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
           SET PS-DATE-RANGE-FILE-START TO TRUE.                                
           SET PS-TMST-FILE-START TO TRUE.                                      
           PERFORM B100-OPEN-FILES.                                             
           PERFORM B200-READ-DATE-RANGE-FILE.                                   
           PERFORM B300-READ-TMST-FILE.                                         
                                                                                
070941*    PERFORM PM700-A000-ORACLE-SETUP.                                     
070941     PERFORM B110-ORACLE-SETUP.                                           
                                                                                
           PERFORM C100-FIND-SYSDATE.                                           
                                                                                
           PERFORM C200-DECLARE-PROMO-REG-CSR.                                  
           PERFORM C300-OPEN-PROMO-REG-CSR.                                     
           PERFORM C400-FETCH-PROMO-REG-CSR.                                    
           PERFORM C500-PROCESS-PROMO-REG-CSR                                   
               UNTIL PS-END-OF-PROMO-REG-CSR                                    
           PERFORM C600-CLOSE-PROMO-REG-CSR.                                    
                                                                                
           PERFORM C250-DECLARE-PROMO-CLRNC-CSR.                                
           PERFORM C350-OPEN-PROMO-CLRNC-CSR.                                   
           PERFORM C450-FETCH-PROMO-CLRNC-CSR.                                  
           PERFORM C550-PROCESS-PROMO-CLRNC-CSR                                 
               UNTIL PS-END-OF-PROMO-CLR-CSR                                    
           PERFORM C650-CLOSE-PROMO-CLRNC-CSR.                                  
                                                                                
           PERFORM W200-WRITE-TOTAL-LINE.                                       
                                                                                
           PERFORM B900-CLOSE-FILES.                                            
                                                                                
           DISPLAY 'PROMO REG FETCHES FROM ORACLE = ' PA-PROMO-REG-CNT.         
           DISPLAY 'PROMO CLR FETCHES FROM ORACLE = ' PA-PROMO-CLR-CNT.         
           DISPLAY 'RECORDS WRITTEN TO FILE = ' PA-RECS-WRITTEN.                
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * B100-OPEN-FILES.                                              *         
      * ==> PROCESSES:                                                *         
      *  - OPEN TWO INPUT FILES AND ONE OUTPUT FILE                   *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B100-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  DATE-RANGE-FILE                                          
                INPUT  TMST-FILE                                                
                OUTPUT PROMO-FILE.                                              
                                                                                
070941 B110-ORACLE-SETUP.                                                       
070941                                                                          
070941     OPEN INPUT ORACLE-LOGON-FILE.                                        
070941                                                                          
070941     PERFORM B120-READ-ORACLE-LOGON.                                      
070941                                                                          
070941     CLOSE ORACLE-LOGON-FILE.                                             
070941                                                                          
070941     IF ((PS-END-OF-LOGON-FILE)                                           
070941             OR (CC-ORACLE-CONNECTION = SPACES))                          
070941         DISPLAY '** ABEND **'                                            
070941         DISPLAY PC-CURRENT-PROGRAM-NAME                                  
070941                 ' - ORACLE LOGON CONTROL CARD '                          
070941                   'NOT PROVIDED'                                         
070941         SET PS-ORA-SRC-EMPTY TO TRUE                                     
070941         CALL 'ILBOABN0' USING ABEND-CODE                                 
070941     END-IF.                                                              
070941                                                                          
070941     IF CC-AUTO-LOGON                                                     
070941         DISPLAY PC-CURRENT-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'        
070941         EXEC SQL                                                         
070941              CONNECT :USERNAME                                           
070941         END-EXEC                                                         
070941     ELSE                                                                 
070941         INITIALIZE PV-ORACLE-USERID-LEN                                  
070941                    PV-ORACLE-PASSWORD-LEN                                
070941         INSPECT CC-ORACLE-USERID                                         
070941             TALLYING PV-ORACLE-USERID-LEN                                
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      INSPECT CC-ORACLE-PASSWORD                                          
070941          TALLYING PV-ORACLE-PASSWORD-LEN                                 
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
070941                USERNAME-ARR                                              
070941      MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
070941      MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
070941                PASSWD-ARR                                                
070941      MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
070941      DISPLAY PC-CURRENT-PROGRAM-NAME                                     
070941              ' - LOGON TO ORACLE AS USER '                               
070941                USERNAME-ARR                                              
070941      EXEC SQL                                                            
070941        CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
070941      END-EXEC                                                            
070941      INITIALIZE CC-ORACLE-PASSWORD                                       
070941                 PASSWD                                                   
070941      END-IF.                                                             
070941                                                                          
070941     IF (SQLCODE NOT = 0)                                                 
070941         DISPLAY PV-PARAGRAPH-NAME                                        
070941         DISPLAY PC-CURRENT-PROGRAM-NAME                                  
070941                 ' - ERROR LOGGING ON TO ORACLE'                          
070941         PERFORM Z100-ABEND                                               
070941     END-IF.                                                              
070941                                                                          
070941******************************************************************        
070941* NEEDS TO BE IN FOR NOW TIL KURT FIXES CURSOR SHARING BUG                
070941* OTHERWISE GET AN ORACLE ERROR (IE: 3113).                               
070941******************************************************************        
070941     EXEC SQL                                                             
070941      ALTER SESSION                                                       
070941            SET CURSOR_SHARING = 'EXACT'                                  
070941     END-EXEC.                                                            
070941                                                                          
070941*****************************************************************         
070941*  B120-READ-ORACLE-LOGON.                                      *         
070941* ==> PROCESSES:                                                *         
070941*   - READ ORCALE CONTROL CARD                                  *         
070941* ==> PERFORMED FROM:  B110-ORACLE-SETUP                        *         
070941*****************************************************************         
070941 B120-READ-ORACLE-LOGON.                                                  
070941                                                                          
070941     READ ORACLE-LOGON-FILE                                               
070941         INTO CC-ORACLE-CONNECTION                                        
070941         AT END                                                           
070941             SET PS-END-OF-LOGON-FILE TO TRUE                             
070941     END-READ.                                                            
070941                                                                          
      *****************************************************************         
      * B200-READ-DATE-RANGE-FILE.                                    *         
      * ==> PROCESSES:                                                *         
      *  - READ IN THE DATE RANGE FILE                                *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B200-READ-DATE-RANGE-FILE.                                               
                                                                                
           READ DATE-RANGE-FILE INTO CC-DATE-RANGE-FILE                         
              AT END                                                            
                 SET PS-DATE-RANGE-FILE-END TO TRUE                             
           END-READ.                                                            
                                                                                
           IF PS-DATE-RANGE-FILE-END                                            
               MOVE 'NO DATE RANGE FILE TO PROCESS'                             
                                                 TO PV-OPERATION-MSG-1          
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM Z910-ABEND                                               
           ELSE                                                                 
               DISPLAY 'CC-START-DATE = ' CC-START-DATE                         
               DISPLAY 'CC-END-DATE   = ' CC-END-DATE                           
                                                                                
               STRING CC-START-DATE DELIMITED BY SPACE                          
                      PC-SPACE DELIMITED BY SIZE                                
                      PC-BEG-TMST   DELIMITED BY SIZE                           
                      INTO PV-NEW-STRT-DATE                                     
                                                                                
               STRING CC-END-DATE DELIMITED BY SPACE                            
                      PC-SPACE DELIMITED BY SIZE                                
                      PC-END-TMST   DELIMITED BY SIZE                           
                      INTO PV-NEW-END-DATE                                      
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B300-READ-IN-TMST-FILE.                                       *         
      * ==> PROCESSES:                                                *         
      *  - READ IN THE TMST FILE                                      *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B300-READ-TMST-FILE.                                                     
                                                                                
           READ TMST-FILE INTO CC-TMST-FILE                                     
              AT END                                                            
                 SET PS-TMST-FILE-END TO TRUE                                   
           END-READ.                                                            
                                                                                
           IF PS-TMST-FILE-END                                                  
               MOVE 'NO TMST FILE TO PROCESS'                                   
                                                 TO PV-OPERATION-MSG-1          
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM Z910-ABEND                                               
           ELSE                                                                 
               DISPLAY 'CC-LAST-UPD-TMST = ' CC-LAST-UPD-TMST                   
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B900-CLOSE-FILES                                              *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE TWO INPUT FILES AND ONE OUTPUT FILE              *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B900-CLOSE-FILES.                                                        
                                                                                
           CLOSE  DATE-RANGE-FILE                                               
                  TMST-FILE                                                     
                  PROMO-FILE.                                                   
                                                                                
      *****************************************************************         
      * C100-FIND-SYSDATE:                                            *         
      * ==> PROCESSES:                                                *         
      *  - SINCE NO TABLE IS ACTUALLY USED WE USE THE DUAL TABLE      *         
      *  - FIND THE CURRENT DATE AND TIME                                       
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C100-FIND-SYSDATE.                                                       
           EXEC SQL                                                             
               SELECT                                                           
                   SYSDATE                                                      
               INTO :PV-SYS-DATE                                                
               FROM                                                             
                   DUAL                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
               MOVE PC-DASH TO PV-PERIOD                                        
               STRING PV-SYS-DATE-XS DELIMITED BY SPACE                         
                      '.000000'      DELIMITED BY SIZE                          
                      INTO PV-SYS-DATE-XS                                       
               DISPLAY 'SYS DATE = ' PV-SYS-DATE-XS                             
               CONTINUE                                                         
             WHEN OTHER                                                         
               MOVE SQLCODE               TO PV-SQL                             
               MOVE 'DUAL'                TO PV-TABLE-NAME                      
               MOVE 'C100-FIND-SYSDATE'   TO PV-PARAGRAPH-NAME                  
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C200-DECLARE-PROMO-REG-CSR                                    *         
      * ==> PROCESSES:                                                *         
      *  - USED A VIEW IN THE FROM CLAUSE INSTEAD OF IND TABLES       *         
      *  - DECLARE CSR THAT RETRIEVES REG PROMO INFO FROM             *         
      *    PMV_REG_PROMO                                              *         
      *  - MUST USE ' '  INSTEAD OF '' WHEN DEFAULTING THE VALUE      *         
      *    BECAUSE OTHERWISE A -1405 ERROR WILL OCCUR IN THE FETCH    *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C200-DECLARE-PROMO-REG-CSR.                                              
                                                                                
           EXEC SQL                                                             
                DECLARE PROMOREGCSR                                             
                CURSOR FOR                                                      
                    SELECT                                                      
                         A.PROMO_ID,                                            
                         A.AD_EVNT_ID,                                          
                         A.PROMO_NBR_TXT,                                       
                         A.CPLT_NM,                                             
                         A.ABBR_NM,                                             
                         A.PROMO_REG_TYP_CDE REG_TYP_CDE,                       
                         DECODE(NVL(A.PROMO_REG_ADV_TYP_CDE,''),                
                          '',' ',A.PROMO_REG_ADV_TYP_CDE) SPEC_TYP_CDE,         
                         TO_CHAR(A.DFLT_STRT_TMST,                              
                          'YYYY-MM-DD-HH24.MI.SS'),                             
                         TO_CHAR(A.DFLT_END_TMST,                               
                          'YYYY-MM-DD-HH24.MI.SS'),                             
                         TO_CHAR(A.DFLT_STRT_TMST, 'HH24.MI'),                  
                         TO_CHAR(A.DFLT_END_TMST, 'HH24.MI'),                   
                         A.ESL_TOPR_TYP_CDE,                                    
                         A.STAT_CDE,                                            
                         TO_CHAR(A.LAST_UPD_TMST,                               
                                             'YYYY-MM-DD-HH24.MI.SS')           
                    FROM                                                        
                         PMV_REG_PROMO A                                        
                    WHERE                                                       
                          A.DFLT_END_TMST >= TO_DATE(                           
                            :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')         
                      AND A.DFLT_STRT_TMST <= TO_DATE(                          
                            :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')          
                      AND A.LAST_UPD_TMST <=                                    
                     TO_DATE(:CC-LAST-UPD-TMST,'YYYY-MM-DD-HH24.MI.SS')         
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C250-DECLARE-PROMO-CLRNC-CSR                                  *         
      * ==> PROCESSES:                                                *         
      *  - DECLARE CSR THAT RETRIEVES CLRNC PROMO INFO FROM           *         
      *    PMT_CLRNC_PROMO                                            *         
      *  - MUST USE ' '  INSTEAD OF '' WHEN DEFAULTING THE VALUE      *         
      *    BECAUSE OTHERWISE A -1405 ERROR WILL OCCUR IN THE FETCH    *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C250-DECLARE-PROMO-CLRNC-CSR.                                            
                                                                                
           EXEC SQL                                                             
                DECLARE PROMOCLRCSR                                             
                CURSOR FOR                                                      
                    SELECT                                                      
                         CLRNC_PROMO_ID,                                        
                         TO_CHAR(STRT_TMST, 'YYYY-MM-DD-HH24.MI.SS'),           
                         TO_CHAR(END_TMST, 'YYYY-MM-DD-HH24.MI.SS'),            
                         TO_CHAR(STRT_TMST, 'HH24.MI'),                         
                         TO_CHAR(END_TMST, 'HH24.MI'),                          
                         STAT_CDE,                                              
                         TO_CHAR(LAST_UPD_TMST,                                 
                                             'YYYY-MM-DD-HH24.MI.SS')           
                    FROM                                                        
                         PMT_CLRNC_PROMO                                        
                    WHERE                                                       
                          END_TMST >= TO_DATE(                                  
                             :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')        
                      AND STRT_TMST <= TO_DATE(                                 
                             :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')         
                      AND LAST_UPD_TMST <=                                      
                     TO_DATE(:CC-LAST-UPD-TMST,'YYYY-MM-DD-HH24.MI.SS')         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
               CONTINUE                                                         
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE               TO PV-SQL                             
               MOVE 'PMT_CLRNC_PROMO'     TO PV-TABLE-NAME                      
               MOVE 'C250-DECLARE-PROMO-CLRNC-CSR' TO PV-PARAGRAPH-NAME         
               STRING 'CURSOR DECLARE ERROR - SQLCODE= '                        
                       PV-SQL DELIMITED BY SIZE INTO                            
                       PV-OPERATION-MSG                                         
               END-STRING                                                       
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      ***************************************************************           
      * C300-OPEN-PROMO-REG-CSR.                                   *            
      * ==> PROCESSES:                                                *         
      *  - OPEN PROMOREGCSR                                           *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL OPEN                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      ***************************************************************           
       C300-OPEN-PROMO-REG-CSR.                                                 
                                                                                
           EXEC SQL                                                             
                 OPEN PROMOREGCSR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C300-OPEN-PROMO-REG-CSR      '                         
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON PROMOREGCSR'                             
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ***************************************************************           
      * C350-OPEN-PROMO-CLRNC-CSR.                                 *            
      * ==> PROCESSES:                                                *         
      *  - OPEN PROMOCLRCSR                                           *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL OPEN                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      ***************************************************************           
       C350-OPEN-PROMO-CLRNC-CSR.                                               
                                                                                
           EXEC SQL                                                             
                 OPEN PROMOCLRCSR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C300-OPEN-PROMO-CLRNC-CSR      '                       
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON PROMOCLRCSR'                             
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C400-FETCH-PROMO-REG-CSR.                                     *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      *  - THEN MOVE TO THE COPYBOOK                                  *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE, C500-PROCESS-PROMO-REG-CSR *         
      *****************************************************************         
       C400-FETCH-PROMO-REG-CSR.                                                
                                                                                
           EXEC SQL                                                             
               FETCH PROMOREGCSR                                                
                INTO  :PV-PROMO-ID                                              
                    , :PV-AD-EVNT-ID                                            
                    , :PV-PROMO-NBR-TXT                                         
                    , :PV-CPLT-NM                                               
                    , :PV-ABBR-NM                                               
                    , :PV-PROMO-REG-TYP-CDE                                     
                    , :PV-PROMO-REG-ADV-TYP-CDE                                 
                    , :PV-STRT-TMST                                             
                    , :PV-END-TMST                                              
                    , :PV-ACT-START-TIME                                        
                    , :PV-ACT-END-TIME                                          
                    , :PV-ESL-TOPR-TYP        :PV-ESL-TOPR-TYP-NULL             
                    , :PV-STAT-CDE                                              
                    , :PV-LAST-UPD-TMST                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-PROMO-REG-CNT                                    
                   SET PS-REG-TYP TO TRUE                                       
                   PERFORM D100-MOVE-TO-CPYBK                                   
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'PROMO REG COUNT FROM FETCH = '                      
                                                      PA-PROMO-REG-CNT          
                   DISPLAY 'END OF PROMO REG CSR'                               
                   SET PS-END-OF-PROMO-REG-CSR TO TRUE                          
               WHEN OTHER                                                       
                   DISPLAY 'PV-PROMO-ID = ' PV-PROMO-ID                         
                   MOVE 'C400-FETCH-PROMO-REG-CSR        '                      
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON PROMO-CSR   '                           
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C450-FETCH-PROMO-CLRNC-CSR.                                   *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      *  - THEN MOVE TO THE COPYBOOK                                  *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE,C550-PROCESS-PROMO-CLRNC-CSR*         
      *****************************************************************         
       C450-FETCH-PROMO-CLRNC-CSR.                                              
                                                                                
           EXEC SQL                                                             
               FETCH PROMOCLRCSR                                                
                INTO  :PV-PROMO-ID                                              
                    , :PV-STRT-TMST                                             
                    , :PV-END-TMST                                              
                    , :PV-ACT-START-TIME                                        
                    , :PV-ACT-END-TIME                                          
                    , :PV-STAT-CDE                                              
                    , :PV-LAST-UPD-TMST                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-PROMO-CLR-CNT                                    
                   SET PS-CLRNC-TYP TO TRUE                                     
                   PERFORM D100-MOVE-TO-CPYBK                                   
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'PROMO CLR COUNT FROM FETCH = '                      
                                                      PA-PROMO-CLR-CNT          
                   DISPLAY 'END OF PROMO CLR CSR'                               
                   SET PS-END-OF-PROMO-CLR-CSR TO TRUE                          
               WHEN OTHER                                                       
                   DISPLAY 'PROMO ID = ' PV-PROMO-ID                            
                   MOVE 'C450-FETCH-PROMO-CLRNC-CSR        '                    
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON PROMO-CSR   '                           
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ****************************************************************          
      * C500-PROCESS-PROMO-REG-CSR.                                  *          
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DATA TO THE FILE                                 *         
      *  - REINITALIZE THE VARIABLES TO START FROM SCRATCH            *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT                  *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      ****************************************************************          
       C500-PROCESS-PROMO-REG-CSR.                                              
                                                                                
           PERFORM W100-WRITE-DETAIL.                                           
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C400-FETCH-PROMO-REG-CSR.                                    
                                                                                
      ****************************************************************          
      * C550-PROCESS-PROMO-CLRNC-CSR.                                *          
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DATA TO THE FILE                                 *         
      *  - REINITALIZE THE VARIABLES TO START FROM SCRATCH            *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT                  *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      ****************************************************************          
       C550-PROCESS-PROMO-CLRNC-CSR.                                            
                                                                                
           PERFORM W100-WRITE-DETAIL.                                           
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C450-FETCH-PROMO-CLRNC-CSR.                                  
                                                                                
      ****************************************************************          
      * C600-CLOSE-PROMO-REG-CSR                                     *          
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE PROMOREGCSR                                      *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = CLOSE WAS SUCCESSFULL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      ****************************************************************          
       C600-CLOSE-PROMO-REG-CSR.                                                
                                                                                
           EXEC SQL                                                             
                CLOSE PROMOREGCSR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C600-CLOSE-PROMO-REG-CSR      '                         
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON PROMO-REG-CSR  '                         
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ****************************************************************          
      * C650-CLOSE-PROMO-CLRNC-CSR                                   *          
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE PROMOCLRCSR                                      *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = CLOSE WAS SUCCESSFULL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      ****************************************************************          
       C650-CLOSE-PROMO-CLRNC-CSR.                                              
                                                                                
           EXEC SQL                                                             
                CLOSE PROMOCLRCSR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C650-CLOSE-PROMO-CLRNC-CSR      '                       
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON PROMOCLRCSR     '                        
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * D100-MOVE-TO-CPYBK.                                           *         
      * ==> PROCESSES:                                                *         
      *  - MOVE THE VARIABLES TO THE COPYBOOK                         *         
      * ==> PERFORMED FROM:                                           *         
      *       C400-FETCH-PROMO-REG-CSR, C450-FETCH-PROMO-CLRNC-CSR    *         
      *****************************************************************         
       D100-MOVE-TO-CPYBK.                                                      
                                                                                
           INITIALIZE PM702-PROMOTION-RECORD.                                   
           MOVE PC-BUSINESS-EVNT-ID    TO PM702-BUSINESS-EVNT-ID.               
           MOVE PC-ACTION-CDE          TO PM702-ACTION-CDE.                     
           MOVE PV-SYS-DATE-XS         TO PM702-BUSINESS-EVNT-CRE-TMST.         
           MOVE PV-PROMO-ID            TO PM702-PROMO-ID.                       
           STRING PV-STRT-TMST  DELIMITED BY SPACE                              
                  '.000000'     DELIMITED BY SIZE                               
                  INTO PM702-STRT-TMST.                                         
           STRING PV-END-TMST  DELIMITED BY SPACE                               
                  '.000000'     DELIMITED BY SIZE                               
                  INTO PM702-END-TMST.                                          
           MOVE PV-STAT-CDE            TO PM702-STAT-CDE.                       
           STRING PV-LAST-UPD-TMST  DELIMITED BY SPACE                          
                  '.000000'     DELIMITED BY SIZE                               
                  INTO PM702-LAST-UPD-TMST.                                     
                                                                                
           IF PV-ACT-START-TIME = '00.00'                                       
               PERFORM E100-FIND-ACTUAL-START-TIME                              
           ELSE                                                                 
               STRING PV-STRT-TMST DELIMITED BY SPACE                           
                      '.000000'     DELIMITED BY SIZE                           
                      INTO PM702-ACT-STRT-TMST                                  
           END-IF.                                                              
                                                                                
           IF PV-ACT-END-TIME = '23.59'                                         
               PERFORM E200-FIND-ACTUAL-END-TIME                                
           ELSE                                                                 
               STRING PV-END-TMST DELIMITED BY SPACE                            
                      '.000000'     DELIMITED BY SIZE                           
                      INTO PM702-ACT-END-TMST                                   
           END-IF.                                                              
                                                                                
           EVALUATE TRUE                                                        
               WHEN PS-REG-TYP                                                  
                   MOVE PV-AD-EVNT-ID      TO PM702-AD-EVNT-ID                  
                   MOVE PC-REG-PROMO       TO PM702-PROMO-TYP-CDE               
                   MOVE PV-PROMO-NBR-TXT   TO PM702-PROMO-NBR-TXT               
                   MOVE PV-CPLT-NM         TO PM702-CPLT-NM                     
                   MOVE PV-ABBR-NM         TO PM702-ABBR-NM                     
                   MOVE PV-PROMO-REG-TYP-CDE TO PM702-REG-PROMO-TYP-CDE         
                   MOVE PV-PROMO-REG-ADV-TYP-CDE                                
                                         TO PM702-SPECIFIC-PROMO-TYP-CDE        
                   MOVE PV-ESL-TOPR-TYP    TO PM702-ESL-TOPR-TYP                
               WHEN PS-CLRNC-TYP                                                
                   MOVE PC-CLRNC-PROMO     TO PM702-PROMO-TYP-CDE               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * E100-FIND-ACTUAL-START-TIME.                                  *         
      * ==> PROCESSES:                                                *         
      *  - SINCE NO TABLE IS ACTUALLY USED WE USE THE DUAL TABLE      *         
      *  - ADD 3 HOURS TO THE START TIME.                             *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      * ==> PERFORMED FROM: D100-MOVE-TO-CPYBK                        *         
      *****************************************************************         
       E100-FIND-ACTUAL-START-TIME.                                             
           EXEC SQL                                                             
               SELECT                                                           
                   TO_CHAR(TO_DATE(:PV-STRT-TMST) +                             
                             (:PC-THREE / 24), 'YYYY-MM-DD-HH24.MI.SS')         
               INTO                                                             
                   :PM702-ACT-STRT-TMST                                         
               FROM                                                             
                   DUAL                                                         
            END-EXEC.                                                           
                                                                                
            EVALUATE TRUE                                                       
                WHEN SQLCODE = ZERO                                             
                    STRING PM702-ACT-STRT-TMST DELIMITED BY SPACE               
                           '.000000'     DELIMITED BY SIZE                      
                            INTO PM702-ACT-STRT-TMST                            
                WHEN OTHER                                                      
                    DISPLAY 'START TMST: ' PV-STRT-TMST                         
                    DISPLAY 'ACTUAL TMST: ' PM702-ACT-STRT-TMST                 
                    MOVE SQLCODE               TO PV-SQL                        
                    MOVE 'DUAL'                TO PV-TABLE-NAME                 
                    MOVE 'E100-FIND-ACTUAL-START-TIME'                          
                                             TO PV-PARAGRAPH-NAME               
                    PERFORM Z100-ABEND                                          
            END-EVALUATE.                                                       
                                                                                
      *****************************************************************         
      * E200-FIND-ACTUAL-END-TIME.                                    *         
      * ==> PROCESSES:                                                *         
      *  - SINCE NO TABLE IS ACTUALLY USED WE USE THE DUAL TABLE      *         
      *  - ADD 3 HOURS TO THE END TIME.                               *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      * ==> PERFORMED FROM: D100-MOVE-TO-CPYBK                        *         
      *****************************************************************         
       E200-FIND-ACTUAL-END-TIME.                                               
           EXEC SQL                                                             
               SELECT                                                           
                   TO_CHAR(TO_DATE(:PV-END-TMST) +                              
                             (:PC-THREE / 24), 'YYYY-MM-DD-HH24.MI.SS')         
               INTO                                                             
                   :PM702-ACT-END-TMST                                          
               FROM                                                             
                   DUAL                                                         
            END-EXEC.                                                           
                                                                                
            EVALUATE TRUE                                                       
                WHEN SQLCODE = ZERO                                             
                    STRING PM702-ACT-END-TMST DELIMITED BY SPACE                
                           '.000000'     DELIMITED BY SIZE                      
                            INTO PM702-ACT-END-TMST                             
                WHEN OTHER                                                      
                    MOVE SQLCODE               TO PV-SQL                        
                    MOVE 'DUAL'                TO PV-TABLE-NAME                 
                    MOVE 'E200-FIND-ACTUAL-END-TIME'                            
                                             TO PV-PARAGRAPH-NAME               
                    PERFORM Z100-ABEND                                          
            END-EVALUATE.                                                       
                                                                                
      *****************************************************************         
      * W100-WRITE-DETAIL.                                            *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DETAIL LINE TO THE PROMO OUTPUT FILE             *         
      * ==> PERFORMED FROM:                                           *         
      *     C500-PROCESS-PROMO-REG-CSR, C550-PROCESS-PROMO-CLRNC-CSR  *         
      *****************************************************************         
       W100-WRITE-DETAIL.                                                       
                                                                                
           WRITE PROMO-LINE FROM PM702-PROMOTION-RECORD.                        
                                                                                
           ADD +1                    TO PA-RECS-WRITTEN.                        
                                                                                
      *****************************************************************         
      * W200-WRITE-TOTAL-LINE                                         *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE TOTAL LINE TO THE PROMO OUTPUT FILE              *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       W200-WRITE-TOTAL-LINE.                                                   
                                                                                
           MOVE PC-BUSINESS-EVNT-ID TO PM700-BUSINESS-EVNT-ID.                  
           MOVE PA-RECS-WRITTEN TO PM700-TOTAL-NBR-OF-RECS.                     
           WRITE PROMO-LINE FROM PM700-TOTAL-RECORD.                            
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT     *        
      * LOGONS TO ORACLE.                                              *        
      ******************************************************************        
070941*    COPY PMPD700.                                                        
                                                                                
      *****************************************************************         
      * Z100-ABEND                                                    *         
      *  - PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE            *         
      *****************************************************************         
       Z100-ABEND.                                                              
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-CURRENT-PROGRAM-NAME.           
           DISPLAY '** SQLCODE        ** = ' SQLCODE.                           
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *        
      ******************************************************************        
       Z910-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
