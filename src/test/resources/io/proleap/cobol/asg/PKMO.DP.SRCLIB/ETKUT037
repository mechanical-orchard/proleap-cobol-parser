00001  ID  DIVISION.                                                    05/31/95
00002  PROGRAM-ID.         ETKUT037.                                    ETKUT037
00003  AUTHOR.             STEVE FLUNKER.                                  LV019
00004  INSTALLATION.       KOHLS DEPARTMENT STORES.                     ETKUT037
00005  DATE-WRITTEN.       MAY 1995.                                    ETKUT037
00006  DATE-COMPILED.                                                   ETKUT037
00007                                                                   ETKUT037
00008 ******************************************************************ETKUT037
00009 *  ETKUT037 - INVOICE RECYCLCE UTILITY (TRAN PREP FOR INSERT)     ETKUT037
00010 *  WORK REQUEST #  - 3044                                         ETKUT037
00011 ******************************************************************ETKUT037
00012 *  THIS PROGRAM WILL CREATE A FILE WHICH HAS DATA TO BE INSERTED  ETKUT037
00013 *  BY THE NEXT PROGRAM.  THIS WILL ALLOW FOR CHECKPOINT RESTART   ETKUT037
00014 *  ON THE INSERT.                                                 ETKUT037
00015 ******************************************************************ETKUT037
JF1002* DATE  10/04/02                                                          
JF1002*     CHANGE MADE: ADDED LOGIC TO NOT HOLD COLBY INVOICES.                
JF1002*     MADE BY: JON FIEDLER JF1002      WR/IR #: 24084                     
JF1002******************************************************************        
C94517*  CHANGED 11/05/2014    BY COGNIZANT       WR: CHG0094517                
C94517*                                                                         
C94517* EXTENDED PO# FROM 7 TO 8 DIGITS WHERE NEEDED.                           
C94517* RECOMPILED FOR APRD050 8 DIGITS PO# EXPANDED.                           
C94517*-----------------------------------------------------------------        
260107*  CHANGED 08/12/2021    BY JIM HUNTER      WR: CHG0260107                
260107*                                                                         
260107* ADDED COPYBOOK DPWS990 TO CHANGE CALL TO DPKUT900 FROM STATIC           
260107* TO DYNAMIC.                                                             
260107*-----------------------------------------------------------------        
00016 *                                                                 ETKUT037
00017  ENVIRONMENT DIVISION.                                            ETKUT037
00018 *                                                                 ETKUT037
00019  CONFIGURATION SECTION.                                           ETKUT037
00020 *                                                                 ETKUT037
00021  SOURCE-COMPUTER.        IBM-3090.                                ETKUT037
00022  OBJECT-COMPUTER.        IBM-3090.                                ETKUT037
00023 *                                                                 ETKUT037
00024  INPUT-OUTPUT SECTION.                                            ETKUT037
00025  FILE-CONTROL.                                                    ETKUT037
00026      SELECT  INV-INPUT-FILE                                       ETKUT037
00027          ASSIGN TO INP01.                                         ETKUT037
00028                                                                   ETKUT037
00029      SELECT  INV-OUTPUT-FILE                                      ETKUT037
00030          ASSIGN TO OUT02.                                         ETKUT037
00031                                                                   ETKUT037
00032  DATA DIVISION.                                                   ETKUT037
00033  FILE SECTION.                                                    ETKUT037
00034                                                                   ETKUT037
00035  FD  INV-INPUT-FILE                                               ETKUT037
00036      LABEL RECORDS ARE STANDARD                                   ETKUT037
00037      RECORDING MODE IS F                                          ETKUT037
00038      BLOCK CONTAINS 0 RECORDS                                     ETKUT037
00039      RECORD CONTAINS 350 CHARACTERS                               ETKUT037
00040      DATA RECORD IS INV-INPUT-RECORD.                             ETKUT037
00041                                                                   ETKUT037
00042  01  INV-INPUT-RECORD.                                               CL**7
00043      COPY APRD050.                                                   CL*19
00044                                                                   ETKUT037
00045  FD  INV-OUTPUT-FILE                                              ETKUT037
00046      LABEL RECORDS ARE STANDARD                                   ETKUT037
00047      RECORDING MODE IS F                                          ETKUT037
00048      BLOCK CONTAINS 0 RECORDS                                     ETKUT037
00049      RECORD CONTAINS 350 CHARACTERS                               ETKUT037
00050      DATA RECORD IS INV-OUTPUT-RECORD.                            ETKUT037
00051                                                                   ETKUT037
00052  01  INV-OUTPUT-RECORD            PIC X(350).                     ETKUT037
00053                                                                   ETKUT037
00054  WORKING-STORAGE SECTION.                                         ETKUT037
00055 *                                                                 ETKUT037
00056  01  INV-INSERT-RECORD.                                              CL**2
00057      05 I-TABLE-NAME              PIC X(07).                      ETKUT037
00058      05 I-TP-DKEY                 PIC S9(9) COMP.                 ETKUT037
00059      05 I-DATA-AREA               PIC X(354).                     ETKUT037
00060      05 I-TFGTRHL-AREA  REDEFINES I-DATA-AREA.                    ETKUT037
00061         10  I-TRN-SET-CDE         PIC X(3).                       ETKUT037
00062         10  I-TRN-DTL-TXT         PIC X(22).                         CL**9
00063         10  I-RLSE-IND            PIC X(01).                      ETKUT037
00064         10  I-RLSE-DTE            PIC X(10).                      ETKUT037
00065         10  I-USER-ID             PIC X(08).                      ETKUT037
00066         10  FILLER                PIC X(310).                        CL**9
00067      05 I-TTRPOHL-AREA REDEFINES I-DATA-AREA.                        CL**2
00068         10  I-PO-NBR              PIC 9(9).                       ETKUT037
00069         10  I-DEPT-NBR            PIC X(03).                      ETKUT037
00070         10  I-PO-RLSE-IND         PIC X(01).                      ETKUT037
00071         10  FILLER                PIC X(341).                     ETKUT037
00072      05 I-TTRPOHL-AREA  REDEFINES I-DATA-AREA.                    ETKUT037
00073         10  I-TRN-SET-SEQ-NBR     PIC S9(9) COMP.                 ETKUT037
00074         10  I-TRN-SET-DATA        PIC X(350).                     ETKUT037
00075                                                                   ETKUT037
00076  01  WS-WORKING-STORAGE.                                          ETKUT037
00077      05  WS-NOSKU.                                                ETKUT037
00078          10  FILLER                 PIC X(05) VALUE 'NOSKU'.      ETKUT037
00079          10  WS-NOSKU-CNT           PIC 9(03) VALUE ZERO.         ETKUT037
00080                                                                   ETKUT037
00081      05  WS-HEADER-RECORD.                                        ETKUT037
00082          10  WS-PO-NBR              PIC 9(09) VALUE ZERO.         ETKUT037
00083          10  WS-LINE-NBR            PIC 9(06) VALUE ZERO.         ETKUT037
00084          10  WS-DEPT-NBR            PIC X(03) VALUE SPACES.       ETKUT037
00085          10  WS-STATUS              PIC X(01) VALUE SPACE.        ETKUT037
00086          10  FILLER                 PIC X(28) VALUE SPACES.       ETKUT037
00087          10  WS-SHIP-DATE           PIC X(06) VALUE SPACES.       ETKUT037
00088          10  WS-INTERCHANGE-ID-QUAL PIC X(02) VALUE SPACES.       ETKUT037
00089          10  WS-INTERCHANGE-ID      PIC X(15) VALUE SPACES.       ETKUT037
00090          10  FILLER                 PIC X(280) VALUE SPACES.      ETKUT037
00091      05  WS-TP-ID-QUAL              PIC X(02) VALUE SPACES.       ETKUT037
00092      05  WS-TP-ID                   PIC X(15) VALUE SPACES.       ETKUT037
00093      05  WS-SEQ-NBR                 PIC S9(9) COMP.               ETKUT037
00094      05  WS-UNUPCS-UPC              PIC S9(15)V COMP-3.              CL**6
00095 *                                                                 ETKUT037
00096  01  PV-PROGRAM-VARIABLES.                                        ETKUT037
00097      05  PV-CHECKPOINT-TYPE           PIC  X     VALUE SPACE.        CL**2
00098          88  CONTROL-BREAK                       VALUE 'C'.       ETKUT037
00099          88  LOGICAL-TRANSACTION                 VALUE 'L'.       ETKUT037
00100          88  TIME-INTERVAL                       VALUE 'T'.       ETKUT037
00101          88  REQUESTED-BY-PROGRAM                VALUE 'R'.       ETKUT037
00102          88  NO-COMMIT                           VALUE ' '.          CL*16
00103      05  PV-DB2-KEYS.                                             ETKUT037
00104          10  PV-DB2-STR-NBR           PIC  X(03).                 ETKUT037
00105          10  PV-DB2-DOC-NBR           PIC S9(07) COMP-3.          ETKUT037
00106          10  PV-DB2-DEPT-NBR          PIC  X(03).                 ETKUT037
00107          10  PV-DB2-CL-NBR            PIC  X(03).                 ETKUT037
00108          10  FILLER                   REDEFINES                   ETKUT037
00109              PV-DB2-CL-NBR.                                       ETKUT037
00110              15  PV-DB2-CL-NBR-1      PIC  X.                     ETKUT037
00111              15  PV-DB2-CL-NBR-2-3    PIC  X(02).                 ETKUT037
00112          10  PV-DB2-SEQ-NBR           PIC  X(03).                 ETKUT037
00113          10  PV-DB2-ROW-NBR           PIC S9(04) COMP.            ETKUT037
00114          10  PV-DB2-INV-DKEY          PIC S9(09) COMP.            ETKUT037
00115      05  PV-SQL-SUB                   PIC  9(03) VALUE ZERO.      ETKUT037
00116      05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACE.     ETKUT037
00117      05  PV-DB2-OPER-ATTEMPTED        PIC  X(30) VALUE SPACE.     ETKUT037
00118                                                                   ETKUT037
00119 ******************************************************************ETKUT037
00120 * TRANSACTION PREPARATION MODULE LINKAGE SECTION.                 ETKUT037
00121 ******************************************************************ETKUT037
00122                                                                   ETKUT037
260107     COPY DPWS990.                                                ETKUT037
00122                                                                   ETKUT037
00123      COPY DPLS000.                                                ETKUT037
00124                                                                   ETKUT037
00125 *                                                                 ETKUT037
00126  01  PC-PROGRAM-CONSTANTS.                                        ETKUT037
00127      05  PC-MAX-RETRIES          PIC S9(03)   VALUE +03           ETKUT037
00128                                               COMP-3.             ETKUT037
00129      05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'ETKUT037'.ETKUT037
00130      05  PC-TRAN-PREP-FUNC-CODES.                                 ETKUT037
00131          10  PC-TRAN-PREP-FUNC-OPEN   PIC  X(05) VALUE 'OPEN '.   ETKUT037
00132          10  PC-TRAN-PREP-FUNC-WRITE  PIC  X(05) VALUE 'WRITE'.   ETKUT037
00133          10  PC-TRAN-PREP-FUNC-CLOSE  PIC  X(05) VALUE 'CLOSE'.   ETKUT037
00134      05  PC-JOB-NAME                  PIC  X(08) VALUE 'ETK140  '.   CL*11
00135      05  PC-CKPT-PLAN-NAME            PIC  X(08) VALUE 'ETKUP037'.   CL**2
00136      05  PC-OPERCO-NBR                PIC  X(02) VALUE '03'.      ETKUT037
00137 *                                                                 ETKUT037
00138  01  PS-PROGRAM-SWITCHES.                                         ETKUT037
00139      05  PS-END-OF-FILE             PIC X     VALUE 'N'.          ETKUT037
00140          88  PS-EOF                           VALUE 'Y'.          ETKUT037
00141      05  PS-ONE-DEPARTMENT          PIC X     VALUE 'Y'.          ETKUT037
00142          88  ONE-DEPT                         VALUE 'Y'.          ETKUT037
00143          88  MULTIPLE-DEPTS                   VALUE 'N'.          ETKUT037
00144      05  PS-CATALOG-ERROR           PIC X     VALUE 'N'.          ETKUT037
00145          88  CATALOG-ERROR                    VALUE 'Y'.          ETKUT037
00146          88  CATALOG-IS-OK                    VALUE 'N'.          ETKUT037
00147      05  PS-KEEP-THIS-INV           PIC X     VALUE 'Y'.             CL**2
00148          88  KEEP-THIS-INV                    VALUE 'Y'.             CL**2
00149          88  KICK-OUT-THIS-INV                VALUE 'N'.             CL**2
00150      05  PS-ALL-GOOD-UPCS           PIC X     VALUE 'Y'.          ETKUT037
00151          88  ALL-GOOD-UPCS                    VALUE 'Y'.          ETKUT037
00152          88  NOT-ALL-GOOD-UPCS                VALUE 'N'.          ETKUT037
00153 *                                                                 ETKUT037
00154 ******************************************************************ETKUT037
00155 *  SQL ABEND WORKING STORAGE                                      ETKUT037
00156 ******************************************************************ETKUT037
00157      COPY DPWS004.                                                ETKUT037
00158      EJECT                                                        ETKUT037
00159 *                                                                 ETKUT037
00160 ***************************************************************** ETKUT037
00161 *****  DB2 UPC LOOKUP TABLE DCLGEN                                ETKUT037
00162 ***************************************************************** ETKUT037
00163 *                                                                 ETKUT037
00164      EXEC SQL                                                     ETKUT037
00165          INCLUDE TUNUPCS                                          ETKUT037
00166      END-EXEC.                                                    ETKUT037
00167 *                                                                 ETKUT037
00168 ***************************************************************** ETKUT037
00169 *****  DB2 PO ON HOLD TABLE DCLGEN                                ETKUT037
00170 ***************************************************************** ETKUT037
00171 *                                                                 ETKUT037
00172      EXEC SQL                                                     ETKUT037
00173          INCLUDE TTRPOHL                                          ETKUT037
00174      END-EXEC.                                                    ETKUT037
00175 *                                                                 ETKUT037
00176 ***************************************************************** ETKUT037
00177 *****  DB2 EDI FUNCTIONAL GROUP ON HOLD TABLE                     ETKUT037
00178 ***************************************************************** ETKUT037
00179 *                                                                 ETKUT037
00180      EXEC SQL                                                     ETKUT037
00181          INCLUDE TFGTRHL                                          ETKUT037
00182      END-EXEC.                                                    ETKUT037
00183 *                                                                 ETKUT037
00184 ***************************************************************** ETKUT037
00185 *****  DB2 EDI FUNCTIONAL GROUP ON HOLD DETAIL TABLE              ETKUT037
00186 ***************************************************************** ETKUT037
00187 *                                                                 ETKUT037
00188      EXEC SQL                                                     ETKUT037
00189          INCLUDE TFGTRDT                                          ETKUT037
00190      END-EXEC.                                                    ETKUT037
00191 *                                                                 ETKUT037
00192 ***************************************************************** ETKUT037
00193 *****  DB2 EDI TRADING PARTNER TABLE DCLGEN                       ETKUT037
00194 ***************************************************************** ETKUT037
00195 *                                                                 ETKUT037
00196      EXEC SQL                                                     ETKUT037
00197          INCLUDE TTRDPRT                                          ETKUT037
00198      END-EXEC.                                                    ETKUT037
00199 *                                                                 ETKUT037
00200 ***************************************************************** ETKUT037
00201 *****  DB2 EDI TRADING PARTNER TRANSMISSION TABLE DCLGEN          ETKUT037
00202 ***************************************************************** ETKUT037
00203 *                                                                 ETKUT037
00204      EXEC SQL                                                     ETKUT037
00205          INCLUDE TTRDTRN                                          ETKUT037
00206      END-EXEC.                                                    ETKUT037
00207 *                                                                 ETKUT037
00208 ***************************************************************** ETKUT037
00209 * DB2 COMMUNICATIONS AREA                                         ETKUT037
00210 ***************************************************************** ETKUT037
00211                                                                   ETKUT037
00212      EXEC SQL                                                     ETKUT037
00213          INCLUDE SQLCA                                            ETKUT037
00214      END-EXEC.                                                    ETKUT037
00215 *                                                                 ETKUT037
00216      COPY SQLCA2.                                                 ETKUT037
00217                                                                   ETKUT037
00218  01  ABEND-CODE               PIC S9(04)  VALUE ZERO              ETKUT037
00219                                           COMP SYNC.              ETKUT037
00220      EJECT                                                        ETKUT037
00221  LINKAGE SECTION.                                                 ETKUT037
00222 *                                                                 ETKUT037
00223  PROCEDURE DIVISION.                                              ETKUT037
00224 *                                                                 ETKUT037
00225 ******************************************************************ETKUT037
00226 * MAIN-LINE LOGIC.                                                ETKUT037
00227 ******************************************************************ETKUT037
00228  A100-MAIN-MODULE.                                                ETKUT037
00229 *                                                                 ETKUT037
00230      OPEN INPUT  INV-INPUT-FILE.                                     CL**2
00231      OPEN OUTPUT INV-OUTPUT-FILE.                                    CL**2
00232 *                                                                 ETKUT037
00233      MOVE PC-TRAN-PREP-FUNC-OPEN TO DP000-FUNC-CODE.              ETKUT037
00234      PERFORM Z400-BUILD-COMM-AREA-TRAN-PREP.                      ETKUT037
00235      MOVE DP000-CKPT-TYPE-CODE TO PV-CHECKPOINT-TYPE.             ETKUT037
00236                                                                   ETKUT037
00237      PERFORM Z100-READ-INPUT-FILE.                                ETKUT037
00238 *                                                                 ETKUT037
00239      PERFORM B100-PROCESS-INV-FILE                                   CL**2
00240              UNTIL PS-EOF.                                        ETKUT037
00241 *                                                                 ETKUT037
00242      CLOSE INV-INPUT-FILE                                            CL**2
00243            INV-OUTPUT-FILE.                                          CL**2
00244                                                                   ETKUT037
00245      MOVE PC-TRAN-PREP-FUNC-CLOSE TO DP000-FUNC-CODE.             ETKUT037
00246      PERFORM Z400-BUILD-COMM-AREA-TRAN-PREP.                      ETKUT037
00247 *                                                                    CL*10
00248      IF CATALOG-ERROR                                                CL*10
00249         MOVE 4060 TO RETURN-CODE                                     CL*10
00250      END-IF.                                                         CL*10
00251                                                                   ETKUT037
00252      STOP RUN.                                                    ETKUT037
00253 *                                                                 ETKUT037
00254 ******************************************************************ETKUT037
00255 *  PROCESS ALL INV RECORDS FROM INPUT FILE.                          CL**2
00256 ******************************************************************ETKUT037
00257  B100-PROCESS-INV-FILE.                                              CL**2
00258 *                                                                 ETKUT037
00259       IF AP050-INV-INTRCHG                                           CL**2
00260          SET KEEP-THIS-INV TO TRUE                                   CL**2
JF1002         IF AP050-NEW-INV-GOOD OR                                    CL**2
JF1002         (AP050-EDI-SENDER-ID-QUAL = 'ZZ' AND                             
JF1002          AP050-EDI-SENDER-ID = 'COLBYC5')                                
00262             PERFORM Z200-WRITE-OUTPUT-FILE                        ETKUT037
00263             PERFORM Z100-READ-INPUT-FILE                          ETKUT037
00264             PERFORM UNTIL PS-EOF OR AP050-INV-INTRCHG                CL**7
00265                PERFORM Z200-WRITE-OUTPUT-FILE                     ETKUT037
00266                PERFORM Z100-READ-INPUT-FILE                       ETKUT037
00267             END-PERFORM                                           ETKUT037
00268          ELSE                                                     ETKUT037
00269             MOVE AP050-EDI-SENDER-ID-QUAL TO WS-TP-ID-QUAL           CL**6
00270             MOVE AP050-EDI-SENDER-ID      TO WS-TP-ID                CL**6
00271             PERFORM C100-FIND-DATALESS-KEY                        ETKUT037
00272             IF SQLCODE = +000 OR -811                                CL*15
00273                CONTINUE                                           ETKUT037
00274             ELSE                                                  ETKUT037
00275                IF SQLCODE = +100                                     CL*15
00276                   SET KICK-OUT-THIS-INV TO TRUE                      CL**2
00277                   DISPLAY '**** '                                 ETKUT037
00278                   DISPLAY '**** INV WAS KICKED OUT DUE TO'           CL**2
00279                   DISPLAY '**** INVALID TRADING PARTNERSHIP'      ETKUT037
00280                   DISPLAY '**** INV: ' AP050-VENDOR-INVOICE-NBR      CL**2
00281                   DISPLAY '****  PO: ' AP050-KOHLS-PO-NBR            CL**2
00282                   DISPLAY '**** '                                 ETKUT037
00283                END-IF                                             ETKUT037
00284             END-IF                                                ETKUT037
00285             IF KEEP-THIS-INV                                         CL**2
00286                SET LOGICAL-TRANSACTION TO TRUE                       CL*17
00287                PERFORM C200-INSERT-TFGTRHL-ROW                    ETKUT037
00288                PERFORM Z300-WRITE-INSERT-RECORD                   ETKUT037
00289                                                                      CL*17
00290                SET NO-COMMIT TO TRUE                                 CL*17
00291                                                                      CL*17
00292                PERFORM C300-INSERT-TFGPOHL-ROW                    ETKUT037
00293                PERFORM Z300-WRITE-INSERT-RECORD                   ETKUT037
00294                MOVE +1 TO WS-SEQ-NBR                              ETKUT037
00295                PERFORM C400-INSERT-TFGTRDT-ROW                    ETKUT037
00296                PERFORM Z300-WRITE-INSERT-RECORD                   ETKUT037
00297                PERFORM Z100-READ-INPUT-FILE                       ETKUT037
00298                                                                      CL*18
00299                PERFORM UNTIL AP050-INV-INTRCHG OR                    CL**3
00300                              PS-EOF                               ETKUT037
00301                   IF AP050-INV-DETAILS                               CL**3
00302                      IF AP050-KOHLS-SKU NOT = '        '             CL*14
00303                         CONTINUE                                  ETKUT037
00304                      ELSE                                         ETKUT037
00305                         IF AP050-UPC                                 CL**4
00306                            MOVE AP050-UPC-CODE TO WS-UNUPCS-UPC      CL**4
00307                         ELSE                                         CL**4
00308                            MOVE AP050-EAN-CODE TO WS-UNUPCS-UPC      CL**4
00309                         END-IF                                       CL**4
00310                         PERFORM C500-SELECT-TUNUPCS               ETKUT037
00311                         IF SQLCODE = +000                         ETKUT037
00312                            IF UNUPCS-TRANS-IND = 'Y'              ETKUT037
00313                               SET CATALOG-ERROR TO TRUE           ETKUT037
00314                            END-IF                                 ETKUT037
00315                         END-IF                                    ETKUT037
00316                      END-IF                                       ETKUT037
00317                   END-IF                                          ETKUT037
00318                   ADD +1 TO WS-SEQ-NBR                            ETKUT037
00319                   PERFORM C400-INSERT-TFGTRDT-ROW                 ETKUT037
00320                   PERFORM Z300-WRITE-INSERT-RECORD                ETKUT037
00321                   PERFORM Z100-READ-INPUT-FILE                    ETKUT037
00322                END-PERFORM                                        ETKUT037
00323             ELSE                                                  ETKUT037
00324                PERFORM Z100-READ-INPUT-FILE                       ETKUT037
00325                PERFORM UNTIL AP050-INV-INTRCHG OR PS-EOF             CL**7
00326                      PERFORM Z100-READ-INPUT-FILE                 ETKUT037
00327                END-PERFORM                                        ETKUT037
00328             END-IF                                                ETKUT037
00329          END-IF                                                   ETKUT037
00330       ELSE                                                        ETKUT037
00331          DISPLAY '**  ABEND     **'                               ETKUT037
00332          DISPLAY '**  RECORDS IN THE INPUT FILE WERE'             ETKUT037
00333          DISPLAY '**  OUT OF SEQUENCE.    '                       ETKUT037
00334          DISPLAY '**  PROGRAM   **  =  ETKUT037'                  ETKUT037
00335          DISPLAY '**  PARAGRAPH **  = B100-PROCESS-FILE'          ETKUT037
00336          MOVE +4007          TO ABEND-CODE                        ETKUT037
00337          CALL 'ILBOABN0'  USING ABEND-CODE                        ETKUT037
00338       END-IF.                                                     ETKUT037
00339 *                                                                 ETKUT037
00340 ******************************************************************ETKUT037
00341 *  FIND THE DATALESS KEY FOR THIS TRADING PARTNER                 ETKUT037
00342 ******************************************************************ETKUT037
00343  C100-FIND-DATALESS-KEY.                                          ETKUT037
00344 *                                                                 ETKUT037
00345      PERFORM                                                      ETKUT037
00346          WITH TEST AFTER                                          ETKUT037
00347          VARYING PV-SQL-SUB                                       ETKUT037
00348          FROM 1 BY 1                                              ETKUT037
00349          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       ETKUT037
00350                    OR SQLCODE = 0                                 ETKUT037
00351                    OR SQLCODE = +100                              ETKUT037
00352                    OR SQLCODE = -811)                             ETKUT037
00353 *                                                                 ETKUT037
00354          EXEC SQL                                                 ETKUT037
00355             SELECT A.TP_DKEY                                      ETKUT037
00356             INTO :TRDPRT-TP-DKEY                                  ETKUT037
00357             FROM TTRDPRT A, TTRDTRN B                             ETKUT037
00358             WHERE A.VEND_INCHG_ID      = :WS-TP-ID                   CL*12
00359               AND A.VEND_INCHG_ID_DESC = :WS-TP-ID-QUAL              CL*12
00360               AND A.INACTV_DTE         > CURRENT DATE             ETKUT037
00361               AND B.TP_DKEY            = A.TP_DKEY                ETKUT037
00362               AND B.TRAN_SET_CDE       = '810'                       CL*12
00363               AND B.INACTV_DTE         > CURRENT DATE             ETKUT037
00364          END-EXEC                                                 ETKUT037
00365 *                                                                 ETKUT037
00366          EVALUATE TRUE                                            ETKUT037
00367              WHEN SQLCODE = -904                                  ETKUT037
00368              WHEN SQLCODE = -911                                  ETKUT037
00369              WHEN SQLCODE = -811                                  ETKUT037
00370              WHEN SQLCODE = 100                                   ETKUT037
00371              WHEN SQLCODE = 0                                     ETKUT037
00372                  CONTINUE                                         ETKUT037
00373              WHEN SQLWARN NOT = SPACES                            ETKUT037
00374              WHEN SQLCODE NOT = ZERO                              ETKUT037
00375                  MOVE 'C100-FIND-DATALESS-KEY'                    ETKUT037
00376                                TO  PV-PARAGRAPH-NAME              ETKUT037
00377                  MOVE 'FIND DATALESS KEY'                         ETKUT037
00378                                TO  PV-DB2-OPER-ATTEMPTED          ETKUT037
00379                  PERFORM Z900-SQL-ABEND                           ETKUT037
00380          END-EVALUATE                                             ETKUT037
00381      END-PERFORM.                                                 ETKUT037
00382 *                                                                 ETKUT037
00383      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                        ETKUT037
00384          MOVE 'C100-FIND-DATALESS-KEY'                            ETKUT037
00385                                TO  PV-PARAGRAPH-NAME              ETKUT037
00386          MOVE 'VERIFY RETRIES EXCEEDED'                           ETKUT037
00387                                TO  PV-DB2-OPER-ATTEMPTED          ETKUT037
00388          PERFORM Z900-SQL-ABEND                                   ETKUT037
00389      END-IF.                                                      ETKUT037
00390 *                                                                 ETKUT037
00391 ******************************************************************ETKUT037
00392 *  WRITE RECORD TO INPUT FILE FOR INSERTION INTO THE TFGTRHL TABLEETKUT037
00393 ******************************************************************ETKUT037
00394  C200-INSERT-TFGTRHL-ROW.                                         ETKUT037
00395 *                                                                 ETKUT037
00396      MOVE SPACES                   TO INV-INSERT-RECORD.             CL**7
00397      MOVE 'TFGTRHL'                TO I-TABLE-NAME.                  CL**2
00398      MOVE TRDPRT-TP-DKEY           TO I-TP-DKEY.                     CL**2
00399      MOVE '810'                    TO I-TRN-SET-CDE.                 CL**2
00400      MOVE AP050-VENDOR-INVOICE-NBR TO I-TRN-DTL-TXT.                 CL**2
00401      MOVE 'N'                      TO I-RLSE-IND.                    CL**2
00402      MOVE '9999-09-09'             TO I-RLSE-DTE.                    CL**2
00403      MOVE 'ETKUP037'               TO I-USER-ID.                     CL**2
00404 *                                                                 ETKUT037
00405 ******************************************************************ETKUT037
00406 *  WRITE RECORD TO INPUT FILE FOR INSERTION INTO THE TFGTPOL TABLEETKUT037
00407 ******************************************************************ETKUT037
00408  C300-INSERT-TFGPOHL-ROW.                                         ETKUT037
00409 *                                                                 ETKUT037
00410      MOVE SPACES               TO I-DATA-AREA.                       CL**2
00411      MOVE 'TTRPOHL'            TO I-TABLE-NAME.                      CL**2
00412      MOVE AP050-KOHLS-PO-NBR   TO I-PO-NBR.                          CL**2
00413      MOVE AP050-DEPT-NBR-ON-PO TO I-DEPT-NBR.                        CL*13
00414      MOVE 'N'                  TO I-PO-RLSE-IND.                     CL**2
00415 *                                                                 ETKUT037
00416 ******************************************************************ETKUT037
00417 *  WRITE RECORD TO INPUT FILE FOR INSERTION INTO THE TFGTRDT TABLEETKUT037
00418 ******************************************************************ETKUT037
00419  C400-INSERT-TFGTRDT-ROW.                                         ETKUT037
00420 *                                                                 ETKUT037
00421      MOVE SPACES              TO I-DATA-AREA.                     ETKUT037
00422      MOVE 'TFGTRDT'           TO I-TABLE-NAME.                    ETKUT037
00423      MOVE WS-SEQ-NBR          TO I-TRN-SET-SEQ-NBR.               ETKUT037
00424      MOVE INV-INPUT-RECORD    TO I-TRN-SET-DATA.                     CL**2
00425 *                                                                 ETKUT037
00426 ******************************************************************ETKUT037
00427 *  FIND IF THE UPC REQUEST IS BACK FROM THE CATALOG               ETKUT037
00428 ******************************************************************ETKUT037
00429  C500-SELECT-TUNUPCS.                                             ETKUT037
00430 *                                                                 ETKUT037
00431      PERFORM                                                      ETKUT037
00432          WITH TEST AFTER                                          ETKUT037
00433          VARYING PV-SQL-SUB                                       ETKUT037
00434          FROM 1 BY 1                                              ETKUT037
00435          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       ETKUT037
00436                    OR SQLCODE = 0                                 ETKUT037
00437                    OR SQLCODE = +100)                             ETKUT037
00438 *                                                                 ETKUT037
00439          EXEC SQL                                                 ETKUT037
00440              SELECT A.TRANS_IND                                   ETKUT037
00441              INTO :UNUPCS-TRANS-IND                               ETKUT037
00442              FROM TUNUPCS A                                       ETKUT037
00443              WHERE A.UPC_CDE = :WS-UNUPCS-UPC                        CL**4
00444          END-EXEC                                                 ETKUT037
00445 *                                                                 ETKUT037
00446          EVALUATE TRUE                                            ETKUT037
00447              WHEN SQLCODE = -904                                  ETKUT037
00448              WHEN SQLCODE = -911                                  ETKUT037
00449              WHEN SQLCODE = 100                                   ETKUT037
00450              WHEN SQLCODE = 0                                     ETKUT037
00451                  CONTINUE                                         ETKUT037
00452              WHEN SQLWARN NOT = SPACES                            ETKUT037
00453              WHEN SQLCODE NOT = ZERO                              ETKUT037
00454                  MOVE 'C500-SELECT-TUNUPCS'                       ETKUT037
00455                                TO  PV-PARAGRAPH-NAME              ETKUT037
00456                  MOVE 'FIND UPC ON CATALOG'                       ETKUT037
00457                                TO  PV-DB2-OPER-ATTEMPTED          ETKUT037
00458                  PERFORM Z900-SQL-ABEND                           ETKUT037
00459          END-EVALUATE                                             ETKUT037
00460      END-PERFORM.                                                 ETKUT037
00461 *                                                                 ETKUT037
00462      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                        ETKUT037
00463          MOVE 'C500-SELECT-TUNUPCS'                               ETKUT037
00464                                TO  PV-PARAGRAPH-NAME              ETKUT037
00465          MOVE 'VERIFY RETRIES EXCEEDED'                           ETKUT037
00466                                TO  PV-DB2-OPER-ATTEMPTED          ETKUT037
00467          PERFORM Z900-SQL-ABEND                                   ETKUT037
00468      END-IF.                                                      ETKUT037
00469 *                                                                 ETKUT037
00470 ******************************************************************ETKUT037
00471 * Z100-READ-INPUT-FILE                                            ETKUT037
00472 ******************************************************************ETKUT037
00473  Z100-READ-INPUT-FILE.                                            ETKUT037
00474 *                                                                 ETKUT037
00475      READ INV-INPUT-FILE                                             CL**2
00476             AT END SET PS-EOF TO TRUE.                            ETKUT037
00477 *                                                                 ETKUT037
00478 ******************************************************************ETKUT037
00479 * Z200-WRITE-OUTPUT-FILE                                          ETKUT037
00480 ******************************************************************ETKUT037
00481  Z200-WRITE-OUTPUT-FILE.                                          ETKUT037
00482 *                                                                 ETKUT037
00483      WRITE INV-OUTPUT-RECORD FROM                                    CL**7
00484           INV-INPUT-RECORD.                                          CL**8
00485 *                                                                 ETKUT037
00486 ******************************************************************ETKUT037
00487 * Z300-WRITE-INSERT-RECORD                                        ETKUT037
00488 ******************************************************************ETKUT037
00489  Z300-WRITE-INSERT-RECORD.                                        ETKUT037
00490 *                                                                 ETKUT037
00491 *    *--------------------------------------------------------    ETKUT037
00492 *    * THERE ARE NO CONTROL BREAKS IN THIS PROGRAM.  TAKING       ETKUT037
00493 *    * CHECKPOINTS BY CONTROL BREAK HAS THE SAME EFFECT AS        ETKUT037
00494 *    * TAKING CHECKPOINTS BY LOGICAL TRANSACTION.                 ETKUT037
00495 *    *--------------------------------------------------------    ETKUT037
00496      IF LOGICAL-TRANSACTION                                       ETKUT037
00497      OR CONTROL-BREAK                                             ETKUT037
00498          MOVE 'Y' TO DP000-CHECKPOINT-IND                         ETKUT037
00499      ELSE                                                         ETKUT037
00500          MOVE 'N' TO DP000-CHECKPOINT-IND                         ETKUT037
00501      END-IF.                                                      ETKUT037
00502                                                                   ETKUT037
00503      MOVE LENGTH OF INV-INSERT-RECORD                                CL**2
00504                                   TO DP000-TRANS-REC-LENGTH.      ETKUT037
00505      MOVE INV-INSERT-RECORD       TO DP000-TRANS-REC.                CL**2
00506      MOVE PC-TRAN-PREP-FUNC-WRITE TO DP000-FUNC-CODE.             ETKUT037
00507      PERFORM Z400-BUILD-COMM-AREA-TRAN-PREP.                      ETKUT037
00508                                                                   ETKUT037
00509 ******************************************************************ETKUT037
00510 * BUILD THE COMMUNICATION AREA FOR A CALL TO THE TRANSACTION      ETKUT037
00511 * PREPARATION MODULE.                                             ETKUT037
00512 ******************************************************************ETKUT037
00513  Z400-BUILD-COMM-AREA-TRAN-PREP.                                  ETKUT037
00514                                                                   ETKUT037
00515      MOVE PC-JOB-NAME             TO DP000-JOB-NAME.              ETKUT037
00516      MOVE PC-CKPT-PLAN-NAME       TO DP000-PLAN-NAME.             ETKUT037
00517      PERFORM Z900-CALL-TRANS-PREP-MODULE.                         ETKUT037
00518                                                                   ETKUT037
00519 ******************************************************************ETKUT037
00520 *     TRANSACTION PREPARATION MODULE CALL AND ERROR ROUTINES.     ETKUT037
00521 ******************************************************************ETKUT037
00522                                                                   ETKUT037
00523      COPY DPPD000.                                                ETKUT037
00524                                                                   ETKUT037
00525 *                                                                 ETKUT037
00526 ******************************************************************ETKUT037
00527 *  STANDARD DB2 ERROR ABEND ROUTINE                               ETKUT037
00528 *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             ETKUT037
00529 ******************************************************************ETKUT037
00530  Z900-SQL-ABEND.                                                  ETKUT037
00531                                                                   ETKUT037
00532      MOVE +4013                 TO ABEND-CODE.                    ETKUT037
00533      DISPLAY '**  ABEND     **'.                                  ETKUT037
00534      DISPLAY '**  PROGRAM   **  =  ETKUT037'.                     ETKUT037
00535      DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            ETKUT037
00536      DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.        ETKUT037
00537                                                                   ETKUT037
00538 *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         ETKUT037
00539 *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        ETKUT037
00540                                                                   ETKUT037
00541      COPY DPPD004.                                                ETKUT037
00542                                                                   ETKUT037
00543      CALL 'ILBOABN0'  USING ABEND-CODE.                           ETKUT037
