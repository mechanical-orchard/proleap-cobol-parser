00001  ID DIVISION.                                                     06/29/93
00002                                                                   SNKUP020
00003  PROGRAM-ID.    SNKUP020.                                            LV003
00004                                                                   SNKUP020
00005  AUTHOR.        ANNE YEKO.                                        SNKUP020
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          SNKUP020
00007  DATE-WRITTEN.  NOVEMBER, 1992.                                   SNKUP020
00008  DATE-COMPILED.                                                   SNKUP020
00009                                                                   SNKUP020
00010 ******************************************************************SNKUP020
00011 *   THIS PROGRAM WILL READ AN INPUT FILE CREATED IN EITHER      **SNKUP020
00012 *   SNKUP007 OR SNKUP008.  THE KEY INFORMATION FROM THE INPUT   **SNKUP020
00013 *   FILE IS USED TO DELETE ROWS FROM THE ASHADR TABLE.          **SNKUP020
00014 ******************************************************************SNKUP020
00015 * CHANGED __/__/__ BY ____________                              **SNKUP020
00016 *   P_________  ________________________________________________**SNKUP020
00017 *               ________________________________________________**SNKUP020
00018 *                                                               **SNKUP020
00019 ******************************************************************SNKUP020
00020                                                                   SNKUP020
00021      EJECT                                                        SNKUP020
00022                                                                   SNKUP020
00023  ENVIRONMENT DIVISION.                                            SNKUP020
00024                                                                   SNKUP020
00025  INPUT-OUTPUT SECTION.                                            SNKUP020
00026                                                                   SNKUP020
00027  FILE-CONTROL.                                                    SNKUP020
00028                                                                   SNKUP020
00029      SELECT ASNHDR-DELETE-FILE                                    SNKUP020
00030             ASSIGN TO INP01.                                      SNKUP020
00031                                                                   SNKUP020
00032  DATA DIVISION.                                                   SNKUP020
00033                                                                   SNKUP020
00034  FILE SECTION.                                                    SNKUP020
00035                                                                   SNKUP020
00036  FD  ASNHDR-DELETE-FILE                                           SNKUP020
00037      RECORDING MODE IS F                                          SNKUP020
00038      LABEL RECORDS ARE STANDARD                                   SNKUP020
00039      RECORD CONTAINS 05 CHARACTERS                                   CL**3
00040      BLOCK CONTAINS 0 RECORDS.                                    SNKUP020
00041                                                                   SNKUP020
00042  01  ASNHDR-DELETE-RECORD        PIC X(05).                          CL**3
00043                                                                   SNKUP020
00044      EJECT                                                        SNKUP020
00045  WORKING-STORAGE SECTION.                                         SNKUP020
00046                                                                   SNKUP020
00047  01  FILLER.                                                      SNKUP020
00048      05  FILLER                  PIC  X(36)  VALUE                SNKUP020
00049          ' ***** WORKING STORAGE PGM=SNKUP020 '.                  SNKUP020
00050      05  PGM-COMPILED            PIC  X(20)  VALUE SPACE.         SNKUP020
00051      05  FILLER                  PIC  X(10)  VALUE                SNKUP020
00052          ' LOCATION='.                                            SNKUP020
00053      05  PGM-LOCATION            PIC  X(04)  VALUE SPACE.         SNKUP020
00054      05  FILLER                  PIC  X(06)  VALUE                SNKUP020
00055          ' *****'.                                                SNKUP020
00056                                                                   SNKUP020
00057  01  ABEND-CODE                  PIC S9(04)  VALUE ZERO           SNKUP020
00058                                              COMP SYNC.           SNKUP020
00059                                                                   SNKUP020
00060  01  ABEND-PARAGRAPH             PIC  X(30)  VALUE SPACE.         SNKUP020
00061                                                                   SNKUP020
00062      EJECT                                                        SNKUP020
00063  01  PC-PROGRAM-CONSTANTS.                                        SNKUP020
00064      05  PC-Y                    PIC  X(01)  VALUE 'Y'.           SNKUP020
00065      05  PC-N                    PIC  X(01)  VALUE 'N'.           SNKUP020
00066                                                                   SNKUP020
00067  01  PS-PROGRAM-SWITCHES.                                         SNKUP020
00068      05  PS-END-OF-INPUT-SW      PIC  X(01)  VALUE 'N'.           SNKUP020
00069          88 PS-END-OF-INPUT                  VALUE 'Y'.           SNKUP020
00070                                                                   SNKUP020
00071  01  PS-PROGRAM-VARIABLES.                                        SNKUP020
00072      05  PV-RECS-READ            PIC  S9(7)  VALUE +0             SNKUP020
00073                                              COMP-3.              SNKUP020
00074      05  PV-RECS-DELETED         PIC  S9(7)  VALUE +0             SNKUP020
00075                                              COMP-3.              SNKUP020
00076      05  PV-DELETE-COUNTER       PIC  S9(3)  VALUE +0             SNKUP020
00077                                              COMP-3.              SNKUP020
00078                                                                   SNKUP020
00079 *    ASNHDR DELETE RECORD LAYOUT                                  SNKUP020
00080                                                                   SNKUP020
00081      COPY SNRD002I.                                               SNKUP020
00082      EJECT                                                        SNKUP020
00083 ******************************************************************SNKUP020
00084 *    DB2 DATABASE AREA                                            SNKUP020
00085 ******************************************************************SNKUP020
00086                                                                   SNKUP020
00087 *    ASN HEADER NAME & ADDRESS DETAIL                             SNKUP020
00088                                                                   SNKUP020
00089      EXEC SQL                                                     SNKUP020
00090          INCLUDE TASHADR                                          SNKUP020
00091      END-EXEC.                                                    SNKUP020
00092                                                                   SNKUP020
00093      EJECT                                                        SNKUP020
00094 *    SQL COMMUNICATION AREA                                       SNKUP020
00095                                                                   SNKUP020
00096      EXEC SQL                                                     SNKUP020
00097          INCLUDE SQLCA                                            SNKUP020
00098      END-EXEC.                                                    SNKUP020
00099      EJECT                                                        SNKUP020
00100 *    SQL COMMUNICATION AREA 2                                     SNKUP020
00101                                                                   SNKUP020
00102      COPY SQLCA2.                                                 SNKUP020
00103      EJECT                                                        SNKUP020
00104                                                                   SNKUP020
00105 ***************************************************************** SNKUP020
00106  PROCEDURE DIVISION.                                              SNKUP020
00107 ***************************************************************** SNKUP020
00108                                                                   SNKUP020
00109  A000-MAIN-DRIVER.                                                SNKUP020
00110                                                                   SNKUP020
00111      MOVE 'A000'                 TO PGM-LOCATION.                 SNKUP020
00112      MOVE WHEN-COMPILED          TO PGM-COMPILED.                 SNKUP020
00113                                                                   SNKUP020
00114      OPEN INPUT ASNHDR-DELETE-FILE.                               SNKUP020
00115                                                                   SNKUP020
00116      PERFORM B200-READ-INPUT-FILE.                                SNKUP020
00117                                                                   SNKUP020
00118      PERFORM B100-DELETE-ROWS                                     SNKUP020
00119        UNTIL PS-END-OF-INPUT.                                     SNKUP020
00120                                                                   SNKUP020
00121      DISPLAY 'SNKUP020 RECORDS READ:    ' PV-RECS-READ.           SNKUP020
00122      DISPLAY 'SNKUP020 RECORDS DELETED: ' PV-RECS-DELETED.        SNKUP020
00123                                                                   SNKUP020
00124      CLOSE ASNHDR-DELETE-FILE.                                    SNKUP020
00125                                                                   SNKUP020
00126      GOBACK.                                                      SNKUP020
00127                                                                   SNKUP020
00128      EJECT                                                        SNKUP020
00129                                                                   SNKUP020
00130 ***************************************************************** SNKUP020
00131 *    THIS PARAGRAPH CONTROLS THE MAIN PROCESSING OF THE PROGRAM   SNKUP020
00132 ***************************************************************** SNKUP020
00133  B100-DELETE-ROWS.                                                SNKUP020
00134                                                                   SNKUP020
00135      MOVE 'B100'                 TO PGM-LOCATION.                 SNKUP020
00136      MOVE 'B100-DELETE-ROWS          '                            SNKUP020
00137                                  TO ABEND-PARAGRAPH.              SNKUP020
00138                                                                   SNKUP020
00139      PERFORM B300-DELETE-ASHADR.                                  SNKUP020
00140      IF PV-DELETE-COUNTER > +500                                     CL**2
00141          PERFORM B400-COMMIT                                      SNKUP020
00142          MOVE ZERO               TO PV-DELETE-COUNTER             SNKUP020
00143      END-IF.                                                      SNKUP020
00144      PERFORM B200-READ-INPUT-FILE.                                SNKUP020
00145                                                                   SNKUP020
00146      EJECT                                                        SNKUP020
00147                                                                   SNKUP020
00148 ***************************************************************** SNKUP020
00149 *    THIS PARAGRAPH WILL READ THE NEXT ASN TO BE DELETED.         SNKUP020
00150 ***************************************************************** SNKUP020
00151  B200-READ-INPUT-FILE.                                            SNKUP020
00152                                                                   SNKUP020
00153      MOVE 'B200'                 TO PGM-LOCATION.                 SNKUP020
00154      MOVE 'B200-READ-INPUT-FILE      '                            SNKUP020
00155                                  TO ABEND-PARAGRAPH.              SNKUP020
00156                                                                   SNKUP020
00157      READ ASNHDR-DELETE-FILE                                      SNKUP020
00158          INTO SN002I-ASN-DELETE-RECORD                            SNKUP020
00159          AT END                                                   SNKUP020
00160              SET PS-END-OF-INPUT TO TRUE.                         SNKUP020
00161                                                                   SNKUP020
00162      IF NOT PS-END-OF-INPUT                                       SNKUP020
00163          ADD 1                   TO PV-RECS-READ                  SNKUP020
00164      END-IF.                                                      SNKUP020
00165                                                                   SNKUP020
00166      EJECT                                                        SNKUP020
00167                                                                   SNKUP020
00168 ***************************************************************** SNKUP020
00169 *    THIS PARAGRAPH DELETES ALL ASHADR ROWS THAT ARE EQUAL TO     SNKUP020
00170 *    THE CURRENT INPUTED ASN.                                     SNKUP020
00171 ***************************************************************** SNKUP020
00172  B300-DELETE-ASHADR.                                              SNKUP020
00173                                                                   SNKUP020
00174      MOVE 'B300'                 TO PGM-LOCATION.                 SNKUP020
00175      MOVE 'B300-DELETE-ASHADR        '                            SNKUP020
00176                                  TO ABEND-PARAGRAPH.              SNKUP020
00177                                                                   SNKUP020
00178      EXEC SQL                                                     SNKUP020
00179          DELETE                                                   SNKUP020
00180          FROM   TASHADR                                           SNKUP020
00181          WHERE  ASNHDR_ID   = :SN002I-ASNHDR-ID                      CL**3
00182      END-EXEC.                                                    SNKUP020
00183                                                                   SNKUP020
00184      IF SQLCODE = ZERO                                            SNKUP020
00185          ADD SQLERRD (3)         TO PV-RECS-DELETED               SNKUP020
00186          ADD +1                  TO PV-DELETE-COUNTER             SNKUP020
00187      ELSE                                                         SNKUP020
00188          IF SQLCODE = +100                                        SNKUP020
00189              CONTINUE                                             SNKUP020
00190          ELSE                                                     SNKUP020
00191              PERFORM Z998-SQL-ERROR                               SNKUP020
00192          END-IF                                                   SNKUP020
00193      END-IF.                                                      SNKUP020
00194                                                                   SNKUP020
00195      EJECT                                                        SNKUP020
00196                                                                   SNKUP020
00197 ***************************************************************** SNKUP020
00198 *    THIS PARAGRAPH WILL COMMIT THE ROWS JUST DELETED             SNKUP020
00199 ***************************************************************** SNKUP020
00200  B400-COMMIT.                                                     SNKUP020
00201                                                                   SNKUP020
00202      MOVE 'B400'                 TO PGM-LOCATION.                 SNKUP020
00203      MOVE 'B400-COMMIT               '                            SNKUP020
00204                                  TO ABEND-PARAGRAPH.              SNKUP020
00205                                                                   SNKUP020
00206      EXEC SQL                                                     SNKUP020
00207          COMMIT                                                   SNKUP020
00208      END-EXEC.                                                    SNKUP020
00209                                                                   SNKUP020
00210      IF SQLCODE = ZERO                                            SNKUP020
00211          CONTINUE                                                 SNKUP020
00212      ELSE                                                         SNKUP020
00213          PERFORM Z998-SQL-ERROR                                   SNKUP020
00214      END-IF.                                                      SNKUP020
00215                                                                   SNKUP020
00216      EJECT                                                        SNKUP020
00217                                                                   SNKUP020
00218 ***************************************************************** SNKUP020
00219 *    THIS MODULE PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT  SNKUP020
00220 *    THAT AN SQL ERROR CODE OCCURS.                               SNKUP020
00221 ***************************************************************** SNKUP020
00222  Z998-SQL-ERROR.                                                  SNKUP020
00223                                                                   SNKUP020
00224      MOVE SQLCODE                TO SQLCODE2.                     SNKUP020
00225      MOVE SQLERRD(3)             TO SQLERRD3.                     SNKUP020
00226      DISPLAY '** ABEND **         ** = SQLERROR'.                 SNKUP020
00227      DISPLAY '** PROGRAM          ** = SNKUP020'.                 SNKUP020
00228      DISPLAY '** PARAGRAPH        ** = ' ABEND-PARAGRAPH.         SNKUP020
00229      DISPLAY '** SQLCODE          ** = ' SQLCODE2.                SNKUP020
00230      DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                SNKUP020
00231      DISPLAY '** SQLERRM          ** = ' SQLERRM.                 SNKUP020
00232      DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                SNKUP020
00233      MOVE +4013                  TO ABEND-CODE.                   SNKUP020
00234      CALL 'ILBOABN0'             USING ABEND-CODE.                SNKUP020
