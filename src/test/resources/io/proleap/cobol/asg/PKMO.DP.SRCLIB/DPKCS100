00001 ******************************************************************08/14/89
00002 *                                                                 DPKCS100
00003 * MODULE NAME = DPKCS100                                             LV001
00004 *                                                                 DPKCS100
00005 * DESCRIPTIVE NAME = C.I.C.S./VS (VTAM) Autoinstall User Program  DPKCS100
00006 *                                                                 DPKCS100
00007 *                                                                 DPKCS100
00008 * STATUS = 1.7.0                                                  DPKCS100
00009 *                                                                 DPKCS100
00010 * FUNCTION = Provide user input to Autoinstall processing         DPKCS100
00011 *                                                                 DPKCS100
00012 *            This module is a component of ZCP.                   DPKCS100
00013 *                                                                 DPKCS100
00014 *            It is called via an EXEC CICS LINK command, from     DPKCS100
00015 *            DFHZATD(the Autoinstall program).                    DPKCS100
00016 *                                                                 DPKCS100
00017 *            Input to the module is a parameter list addressed by DPKCS100
00018 *            DFHEICAP.                                            DPKCS100
00019 *                                                                 DPKCS100
00020 *            The program is invoked when:                         DPKCS100
00021 *            1) An autoinstall INSTALL is in progress             DPKCS100
00022 *            2) An autoinstall DELETE has just completed          DPKCS100
00023 *                                                                 DPKCS100
00024 *            The function to be performed is indicated via the pasDPKCS100
00025 *            PARAMETER LIST. THIS IS EVALUATED DURING COMMON INITIDPKCS100
00026 *            ISATION PROCESSING, AND CONTROL PASSED TO THE APPROPRDPKCS100
00027 *            ROUTINE.                                             DPKCS100
00028 *                                                                 DPKCS100
00029 * Function 1 - autoinstall INSTALL                                DPKCS100
00030 * --------------------------------                                DPKCS100
00031 *                                                                 DPKCS100
00032 *    Parameter list:                                              DPKCS100
00033 *                                                                 DPKCS100
00034 *    Fullword 1 =   Function field                                DPKCS100
00035 *      Byte 1     -   Request type(X'F0' for INSTALL)             DPKCS100
00036 *      Byte 2     -   Reserved                                    DPKCS100
00037 *      Byte 3     -   Reserved                                    DPKCS100
00038 *      Byte 4     -   Reserved                                    DPKCS100
00039 *    Fullword 2 ==> LS-NETNAME                                    DPKCS100
00040 *    Fullword 3 ==> Model_name list                               DPKCS100
00041 *    Fullword 4 ==> Return field                                  DPKCS100
00042 *    Fullword 5 ==> CINIT_RU                                      DPKCS100
00043 *                                                                 DPKCS100
00044 *    The format of the data is as follows:                        DPKCS100
00045 *                                                                 DPKCS100
00046 *    LS-NETNAME - |x|LS-NETNAME|                                  DPKCS100
00047 *    (where 'x' is 2 byte length of LS-NETNAME)                   DPKCS100
00048 *                                                                 DPKCS100
00049 *    Model_name list - |y|Model1|Model2|...|Modeln|               DPKCS100
00050 *    (where 'y' is a 2 byte number of names in the list; each nameDPKCS100
00051 *     ELEMENT IS 8 BYTES IN LENGTH; Y MAY BE ZERO, IN WHICH CASE NDPKCS100
00052 *     LIST FOLLOWS).                                              DPKCS100
00053 *                                                                 DPKCS100
00054 *    Return field - |Model_name|TRMIDNT|LS-PRINTTO|LS-ALTPRT |StatDPKCS100
00055 *                   |8 bytes   |4 bytes|4 bytes|4 bytes|1 byte|   DPKCS100
00056 *    (A 21 byte field to return selected items and status informatDPKCS100
00057 *                                                                 DPKCS100
00058 *    CINIT_RU - |z|CINIT_RU|                                      DPKCS100
00059 *    (where 'z' is 2 byte length of CINIT_RU)                     DPKCS100
00060 *                                                                 DPKCS100
00061 *    The purpose of this function is to select a model name, and  DPKCS100
00062 *    corresponding terminal id to be used as input for an autoinstDPKCS100
00063 *    RESOURCE 'BUILDER' REQUEST.                                  DPKCS100
00064 *    Optionally, Printer and Altprinter values may be supplied.   DPKCS100
00065 *                                                                 DPKCS100
00066 *    The dafault action of this program on this event is a followsDPKCS100
00067 *    MODEL_NAME = THE FIRST NAME IN THE SUPPLIED LIST             DPKCS100
00068 *    Terminal= the last 4 non-blank characters of the supplied NETDPKCS100
00069 *    PRINTER = NOT SUPPLIED                                       DPKCS100
00070 *    Altprinter = not supplied                                    DPKCS100
00071 *                                                                 DPKCS100
00072 *    These values are placed in the return field, and the status bDPKCS100
00073 *    SET TO ZERO TO INDICATE THAT A SELECTION HAS BEEN MADE.      DPKCS100
00074 *                                                                 DPKCS100
00075 *    Return is then made to the calling program.                  DPKCS100
00076 *                                                                 DPKCS100
00077 *    If the list contains no elements, then no action is taken.   DPKCS100
00078 *                                                                 DPKCS100
00079 *    EXIT-NORMAL =                                                DPKCS100
00080 *         Exit is via an EXEC CICS RETURN command.                DPKCS100
00081 *         Status is set to zero if all processing completes normalDPKCS100
00082 *                                                                 DPKCS100
00083 *    EXIT-ERROR =                                                 DPKCS100
00084 *         Exit is via an EXEC CICS RETURN command.                DPKCS100
00085 *         Status is non-zero on entry to this module, and is left DPKCS100
00086 *         untouched if any error occurs, hence, a non-zero return DPKCS100
00087 *         code is passed back to the calling program.             DPKCS100
00088 *                                                                 DPKCS100
00089 * Function 2 - autoinstall DELETE                                 DPKCS100
00090 * -------------------------------                                 DPKCS100
00091 *                                                                 DPKCS100
00092 *    Parameter list:                                              DPKCS100
00093 *                                                                 DPKCS100
00094 *    Fullword 1 =   Function field                                DPKCS100
00095 *      Byte 1     -   Request type(X'F1' for DELETE)              DPKCS100
00096 *      Byte 2     -   Reserved                                    DPKCS100
00097 *      Byte 3     -   Reserved                                    DPKCS100
00098 *      Byte 4     -   Reserved                                    DPKCS100
00099 *    Fullword 2 =   Terminal id of deleted terminal               DPKCS100
00100 *                                                                 DPKCS100
00101 *    This function gives the user the opportunity to perform      DPKCS100
00102 *    processing when an autoinstall'ed terminal has been deleted. DPKCS100
00103 *                                                                 DPKCS100
00104 *    The default action of this program is to establish addressabiDPKCS100
00105 *    TO THE PARAMETER LIST, AND RETURN.                           DPKCS100
00106 *                                                                 DPKCS100
00107 *    EXIT-NORMAL =                                                DPKCS100
00108 *         Exit is via an EXEC CICS RETURN command.                DPKCS100
00109 *                                                                 DPKCS100
00110 *-----------------------------------------------------------------DPKCS100
00111 *                                                                 DPKCS100
00112 * NOTES :                                                         DPKCS100
00113 *    DEPENDENCIES = S/370                                         DPKCS100
00114 *                                                                 DPKCS100
00115 *    REGISTER CONVENTIONS =                                       DPKCS100
00116 *                                                                 DPKCS100
00117 *    MODULE TYPE = Executable                                     DPKCS100
00118 *    PROCESSOR = COBOL                                            DPKCS100
00119 *    ATTRIBUTES = Read only, Serially Reusable                    DPKCS100
00120 *                                                                 DPKCS100
00121 *-----------------------------------------------------------------DPKCS100
00122 *                                                                 DPKCS100
00123 * ENTRY POINT = DPKCS100                                          DPKCS100
00124 *                                                                 DPKCS100
00125 *     PURPOSE = All Functions                                     DPKCS100
00126 *                                                                 DPKCS100
00127 *     The request type is analysed, and control passed to the     DPKCS100
00128 *     appropriate routine.                                        DPKCS100
00129 *                                                                 DPKCS100
00130 *                                                                 DPKCS100
00131 *-----------------------------------------------------------------DPKCS100
00132 *                                                                 DPKCS100
00133 * EXTERNAL REFERENCES = None                                      DPKCS100
00134 *                                                                 DPKCS100
00135 *     ROUTINES =                                                  DPKCS100
00136 *         EXEC CICS RETURN - return to calling program            DPKCS100
00137 *                                                                 DPKCS100
00138 *                                                                 DPKCS100
00139 *     CONTROL BLOCKS =                                            DPKCS100
00140 *         See FUNCTION section for description of input parametersDPKCS100
00141 *                                                                 DPKCS100
00142 *-----------------------------------------------------------------DPKCS100
00143 *                                                                 DPKCS100
00144 * DESCRIPTION                                                     DPKCS100
00145 *                                                                 DPKCS100
00146 * A check is made to ensure the presence of the input parameters  DPKCS100
00147 * (passed via COMMAREA). If these do not exist, then return is madDPKCS100
00148 * TO THE CALLING PROGRAM.                                         DPKCS100
00149 *                                                                 DPKCS100
00150 * The type of request(INSTALL|DELETE) is then determined, and a   DPKCS100
00151 * branch taken to the appropriate function routine(see 'FUNCTION' DPKCS100
00152 * above for details).                                             DPKCS100
00153 *                                                                 DPKCS100
00154 *                                                                 DPKCS100
00155 ******************************************************************DPKCS100
00156   IDENTIFICATION DIVISION.                                        DPKCS100
00157   PROGRAM-ID.     DPKCS100.                                       DPKCS100
00158   AUTHOR.         BRUCE BORCHARDT.                                DPKCS100
00159   INSTALLATION.   KOHLS DEPARTMENT STORES.                        DPKCS100
00160   DATE-WRITTEN.   04/25/89.                                       DPKCS100
00161   DATE-COMPILED.                                                  DPKCS100
00162 *    TRANSLATED BY MHTRAN-2  3/29/89                              DPKCS100
00163                                                                   DPKCS100
00164   ENVIRONMENT DIVISION.                                           DPKCS100
00165                                                                   DPKCS100
00166   DATA DIVISION.                                                  DPKCS100
00167   WORKING-STORAGE SECTION.                                        DPKCS100
00168 *                                                                 DPKCS100
00169 * CODES SUPPLIED BY COMMAREA:                                     DPKCS100
00170 *                                                                 DPKCS100
00171  01  WC-WORKING-CODES.                                            DPKCS100
00172      05  WC-INSTALL-CODE PIC X(1) VALUE IS '0'.                   DPKCS100
00173      05  WC-DELETE-CODE  PIC X(1) VALUE IS '1'.                   DPKCS100
00174  01  WM-WRITEQ-MESSAGE1.                                          DPKCS100
00175      05  FILLER          PIC X(15) VALUE '***** TERMINAL '.       DPKCS100
00176      05  WM-TERMINAL     PIC X(4) VALUE SPACES.                   DPKCS100
00177      05  FILLER          PIC X(22) VALUE ' HAS NO ENTRY IN TABLE'.DPKCS100
00178      05  FILLER          PIC X(13) VALUE ' FOR DPKCS100'.         DPKCS100
00179  01  WM-WRITEQ-MESSAGE2.                                          DPKCS100
00180      05  FILLER          PIC X(16) VALUE '***** CNTL UNIT '.      DPKCS100
00181      05  WM-CNTLUNIT     PIC X(2)  VALUE SPACES.                  DPKCS100
00182      05  FILLER          PIC X(22) VALUE ' HAS NO ENTRY IN TABLE'.DPKCS100
00183      05  FILLER          PIC X(13) VALUE ' FOR DPKCS100'.         DPKCS100
00184 *                                                                 DPKCS100
00185 * STRUCTURE TO ALLOW THE LAST FOUR CHARACTERS TO BE USED AS       DPKCS100
00186 * THE LS-NETNAME.                                                 DPKCS100
00187 *                                                                 DPKCS100
00188  01  NB-LS-NETNAME-BITS.                                          DPKCS100
00189      05  NB-FIRST-HALF.                                           DPKCS100
00190          10 NB-CHR-1     PIC X(1) VALUE SPACES.                   DPKCS100
00191          10 NB-CHR-2     PIC X(1) VALUE SPACES.                   DPKCS100
00192          10 NB-CHR-3     PIC X(1) VALUE SPACES.                   DPKCS100
00193          10 NB-CHR-4     PIC X(1) VALUE SPACES.                   DPKCS100
00194      05  NB-SECOND-HALF.                                          DPKCS100
00195          10 NB-CHR-5     PIC X(1) VALUE SPACES.                   DPKCS100
00196          10 NB-CHR-6     PIC X(1) VALUE SPACES.                   DPKCS100
00197          10 NB-CHR-7     PIC X(1) VALUE SPACES.                   DPKCS100
00198          10 NB-CHR-8     PIC X(1) VALUE SPACES.                   DPKCS100
00199 *                                                                 DPKCS100
00200 * TERMINAL IDENTIFIER IS BUILT HERE BEFORE BEING PLACED IN THE    DPKCS100
00201 * RETURN FIELD.                                                   DPKCS100
00202 *                                                                 DPKCS100
00203  01  TI-TERM-IDNT.                                                DPKCS100
00204      05 TI-TERM-CHR1        PIC X(1) VALUE SPACES.                DPKCS100
00205      05 TI-TERM-CHR2        PIC X(1) VALUE SPACES.                DPKCS100
00206      05 TI-TERM-CHR3        PIC X(1) VALUE SPACES.                DPKCS100
00207      05 TI-TERM-CHR4        PIC X(1) VALUE SPACES.                DPKCS100
00208  01  TI-TERM-IDNT-ALT REDEFINES TI-TERM-IDNT.                     DPKCS100
00209      05 TI-TERM-CHR1-CHR2   PIC X(2).                             DPKCS100
00210      05 FILLER              PIC X(2).                             DPKCS100
00211  01  DT-DUMMY-TERM.                                               DPKCS100
00212      05 DT-CNTL-UNIT-ID     PIC X(2) VALUE SPACES.                DPKCS100
00213      05 FILLER              PIC X(2) VALUE 'XX'.                  DPKCS100
00214  01  XB-XNVT-BLL-RESOLUTION.                                      DPKCS100
00215      05 XB-XNVT-BLL-AS-COMP PIC S9(8) COMP.                       DPKCS100
00216      05 XB-XNVT-BLL-AS-POINTER                                    DPKCS100
00217              REDEFINES XB-XNVT-BLL-AS-COMP POINTER.               DPKCS100
00218  01  DE-DB2-ERROR-MSG.                                            DPKCS100
00219      05 FILLER              PIC X(34) VALUE                       DPKCS100
00220              'DB2 ERROR - DPKCS100 - SQLCODE = ('.                DPKCS100
00221      05 DE-SQLCODE          PIC Z(8)9- VALUE ZERO.                DPKCS100
00222      05 FILLER              PIC X VALUE ')'.                      DPKCS100
00223      05 FILLER              PIC X(8) VALUE                        DPKCS100
00224              'ERRMC = '.                                          DPKCS100
00225      05 DE-SQLERRMC         PIC X(72) VALUE SPACES.               DPKCS100
00226 ******************************************************************DPKCS100
00227 *    STANDARD SQLCA2 COPY MEMBER                                  DPKCS100
00228 ******************************************************************DPKCS100
00229      COPY SQLCA2.                                                 DPKCS100
00230      EXEC SQL                                                     DPKCS100
00231          INCLUDE TTRM000                                          DPKCS100
00232      END-EXEC.                                                    DPKCS100
00233      EXEC SQL                                                     DPKCS100
00234          INCLUDE SQLCA                                            DPKCS100
00235      END-EXEC.                                                    DPKCS100
00236   LINKAGE SECTION.                                                DPKCS100
00237 *                                                                 DPKCS100
00238 * COMMAREA FORMAT:                                                DPKCS100
00239 *   FULLWORD 1 - FUNCTION FIELD                                   DPKCS100
00240 *                BYTE 1 - REQUEST TYPE                            DPKCS100
00241 *                BYTE 2 - RESERVED                                DPKCS100
00242 *                BYTE 3 - RESERVED                                DPKCS100
00243 *                BYTE 4 - RESERVED.                               DPKCS100
00244 *   FULLWORD 2 - POINTER TO LS-NETNAME.                           DPKCS100
00245 *   FULLWORD 3 - POINTER TO MODEL NAME LIST.                      DPKCS100
00246 *   FULLWORD 4 - POINTER TO RETURN FIELD.                         DPKCS100
00247 *   FULLWORD 5 - POINTER TO CINIT_RU.                             DPKCS100
00248 *                                                                 DPKCS100
00249   01  DFHCOMMAREA.                                                DPKCS100
00250       05  LS-FUNCTION-FIELD.                                      DPKCS100
00251           10  LS-REQUEST-TYPE PIC X(1) VALUE SPACES.              DPKCS100
00252           10  LS-REST         PIC X(3) VALUE SPACES.              DPKCS100
00253       05  LS-NET-PTR          PIC S9(8) COMP.                     DPKCS100
00254       05  LS-MOD-PTR          PIC S9(8) COMP.                     DPKCS100
00255       05  LS-RET-FLD-PTR      PIC S9(8) COMP.                     DPKCS100
00256       05  LS-CINRU-PTR        PIC S9(8) COMP.                     DPKCS100
00257 *                                                                 DPKCS100
00258 * PARMLIST ENABLES THE DATA AREAS POINTED TO BY THE COMMAREA      DPKCS100
00259 * TO BE ACCESSED                                                  DPKCS100
00260 *                                                                 DPKCS100
00261   01  LS-NETNAME.                                                 DPKCS100
00262       05  LS-NETNAME-LENGTH   PIC S9(2) COMP.                     DPKCS100
00263       05  LS-NETNAME-NAME     PIC X(8)  VALUE SPACES.             DPKCS100
00264 *                                                                 DPKCS100
00265 * MODELNAME IS NOT USED IN THIS PROGRAM AS THE IBM SUPPLIED       DPKCS100
00266 * AUTOINSTALL MODELS ARE NOT BEING USED. MODELNAME IS A LIST      DPKCS100
00267 * OF AUTOINSTALL MODELS SO IF MORE THAN THE FIRST MODEL IS TO     DPKCS100
00268 * BE USED A LONGER DEFINITION WILL BE REQUIRED:                   DPKCS100
00269 *                                                                 DPKCS100
00270   01  LS-MODELNAME.                                               DPKCS100
00271       05  LS-NO-MODELS       PIC S9(2) COMP.                      DPKCS100
00272       05  LS-MODELNAME-NAME  PIC X(8)  VALUE SPACES.              DPKCS100
00273 *                                                                 DPKCS100
00274 * LS-RETURN-FIELD WILL CONTAIN THE DATA TO BE RETURNED TO CICS    DPKCS100
00275 *                                                                 DPKCS100
00276   01  LS-RETURN-FIELD.                                            DPKCS100
00277       05  LS-MOD-NAME        PIC X(8) VALUE SPACES.               DPKCS100
00278       05  LS-INSTANCE-NAME   PIC X(4) VALUE SPACES.               DPKCS100
00279       05  LS-PRINTTO         PIC X(4) VALUE SPACES.               DPKCS100
00280       05  LS-ALTPRT          PIC X(4) VALUE SPACES.               DPKCS100
00281       05  LS-RET-STATUS      PIC X    VALUE SPACES.               DPKCS100
00282   01  LS-CINIT-AREA.                                              DPKCS100
00283       05  LS-CINITRU-LENGTH  PIC S9(4).                           DPKCS100
00284       05  LS-CINITRU         PIC X(256) VALUE SPACES.             DPKCS100
00285  PROCEDURE DIVISION.                                              DPKCS100
00286 *                                                                 DPKCS100
00287 * SET UP ADDRESSABILITY TO THE PARMLIST.                          DPKCS100
00288 *                                                                 DPKCS100
00289 *                                                                 DPKCS100
00290 * CHECK THAT WE HAVE A COMMAREA AND SOME MODELS TO USE, IF NOT    DPKCS100
00291 * THEN EXIT                                                       DPKCS100
00292 *                                                                 DPKCS100
00293  0000-MAINLINE.                                                   DPKCS100
00294      IF EIBCALEN EQUAL 0                                          DPKCS100
00295         PERFORM 1000-RETURN-LINE.                                 DPKCS100
00296 *                                                                 DPKCS100
00297 * SET UP ADDRESSABILITY TO THE COMMAREA - MOVE POINTERS AND       DPKCS100
00298 * THEN RELOAD THE DATA.                                           DPKCS100
00299 *                                                                 DPKCS100
00300      MOVE LS-NET-PTR TO XB-XNVT-BLL-AS-COMP                       DPKCS100
00301      SET ADDRESS OF LS-NETNAME TO XB-XNVT-BLL-AS-POINTER.         DPKCS100
00302 *                                                                 DPKCS100
00303      MOVE LS-MOD-PTR TO XB-XNVT-BLL-AS-COMP                       DPKCS100
00304      SET ADDRESS OF LS-MODELNAME TO XB-XNVT-BLL-AS-POINTER.       DPKCS100
00305 *                                                                 DPKCS100
00306      MOVE LS-RET-FLD-PTR TO XB-XNVT-BLL-AS-COMP                   DPKCS100
00307      SET ADDRESS OF LS-RETURN-FIELD TO XB-XNVT-BLL-AS-POINTER.    DPKCS100
00308 *                                                                 DPKCS100
00309      MOVE LS-CINRU-PTR TO XB-XNVT-BLL-AS-COMP                     DPKCS100
00310      SET ADDRESS OF LS-CINIT-AREA TO XB-XNVT-BLL-AS-POINTER.      DPKCS100
00311 *                                                                 DPKCS100
00312 * EXECUTE THE APPROPRIATE PARAGRAPH FOR INSTALL OR DELETE:        DPKCS100
00313 *                                                                 DPKCS100
00314      IF LS-NO-MODELS EQUAL 0                                      DPKCS100
00315         PERFORM 1000-RETURN-LINE.                                 DPKCS100
00316      IF LS-REQUEST-TYPE EQUAL WC-INSTALL-CODE                     DPKCS100
00317         PERFORM 2000-INSTALL-PARAGRAPH THRU 2500-MOVE.            DPKCS100
00318 *                                                                 DPKCS100
00319 * IF THE REQUEST WAS AN INSTALL REQUEST THEN THE NEXT TEST        DPKCS100
00320 * WILL FAIL ANYWAY, IE. FANCY LOGIC NOT REQUIRED!                 DPKCS100
00321 *                                                                 DPKCS100
00322      IF LS-REQUEST-TYPE EQUAL WC-DELETE-CODE                      DPKCS100
00323         PERFORM 3000-DELETE-PARAGRAPH.                            DPKCS100
00324 *                                                                 DPKCS100
00325 * RETURN TO CICS.                                                 DPKCS100
00326 *                                                                 DPKCS100
00327  1000-RETURN-LINE.                                                DPKCS100
00328      EXEC CICS RETURN END-EXEC.                                   DPKCS100
00329 *                                                                 DPKCS100
00330 *                                                                 DPKCS100
00331  2000-INSTALL-PARAGRAPH.                                          DPKCS100
00332 *                                                                 DPKCS100
00333 * MOVE THE LS-NETNAME SO THAT IT CAN BE DEALT WITH ON A CHARACTER DPKCS100
00334 * CHARACTER BASIS.                                                DPKCS100
00335 *                                                                 DPKCS100
00336      MOVE LS-NETNAME-NAME TO NB-LS-NETNAME-BITS.                  DPKCS100
00337 *                                                                 DPKCS100
00338 * RESET LS-NETNAME LENGTH IF THERE ARE TRAILING SPACES.           DPKCS100
00339 *                                                                 DPKCS100
00340      IF (LS-NETNAME-LENGTH = 8 AND NB-CHR-8 = ' ')                DPKCS100
00341         MOVE 7 TO LS-NETNAME-LENGTH.                              DPKCS100
00342      IF (LS-NETNAME-LENGTH = 7 AND NB-CHR-7 = ' ')                DPKCS100
00343         MOVE 6 TO LS-NETNAME-LENGTH.                              DPKCS100
00344      IF (LS-NETNAME-LENGTH = 6 AND NB-CHR-6 = ' ')                DPKCS100
00345         MOVE 5 TO LS-NETNAME-LENGTH.                              DPKCS100
00346      IF (LS-NETNAME-LENGTH = 5 AND NB-CHR-5 = ' ')                DPKCS100
00347         MOVE 4 TO LS-NETNAME-LENGTH.                              DPKCS100
00348 *                                                                 DPKCS100
00349 * MAKE UP TERMINAL IDENTIFIER FROM LS-NETNAME.                    DPKCS100
00350 *                                                                 DPKCS100
00351      IF LS-NETNAME-LENGTH < 5                                     DPKCS100
00352         MOVE NB-FIRST-HALF TO TI-TERM-IDNT.                       DPKCS100
00353      IF LS-NETNAME-LENGTH = 5                                     DPKCS100
00354         MOVE NB-CHR-2 TO TI-TERM-CHR1                             DPKCS100
00355         MOVE NB-CHR-3 TO TI-TERM-CHR2                             DPKCS100
00356         MOVE NB-CHR-4 TO TI-TERM-CHR3                             DPKCS100
00357         MOVE NB-CHR-5 TO TI-TERM-CHR4.                            DPKCS100
00358      IF LS-NETNAME-LENGTH = 6                                     DPKCS100
00359         MOVE NB-CHR-3 TO TI-TERM-CHR1                             DPKCS100
00360         MOVE NB-CHR-4 TO TI-TERM-CHR2                             DPKCS100
00361         MOVE NB-CHR-5 TO TI-TERM-CHR3                             DPKCS100
00362         MOVE NB-CHR-6 TO TI-TERM-CHR4.                            DPKCS100
00363      IF LS-NETNAME-LENGTH = 7                                     DPKCS100
00364         MOVE NB-CHR-4 TO TI-TERM-CHR1                             DPKCS100
00365         MOVE NB-CHR-5 TO TI-TERM-CHR2                             DPKCS100
00366         MOVE NB-CHR-6 TO TI-TERM-CHR3                             DPKCS100
00367         MOVE NB-CHR-7 TO TI-TERM-CHR4.                            DPKCS100
00368      IF LS-NETNAME-LENGTH = 8                                     DPKCS100
00369         MOVE NB-SECOND-HALF TO TI-TERM-IDNT.                      DPKCS100
00370 *                                                                 DPKCS100
00371 * PLACE TI-TERM-IDNT IN RETURN FIELD                              DPKCS100
00372 *                                                                 DPKCS100
00373      MOVE TI-TERM-IDNT TO LS-INSTANCE-NAME                        DPKCS100
00374                           TTRM000-TERMINAL-NMBR.                  DPKCS100
00375 *                                                                 DPKCS100
00376 * GET PRINTER INFO FROM TABLE                                     DPKCS100
00377 *                                                                 DPKCS100
00378      EXEC CICS HANDLE ABEND LABEL(2500-MOVE) END-EXEC.            DPKCS100
00379      EXEC SQL                                                     DPKCS100
00380          SELECT TERMINAL_NMBR,                                    DPKCS100
00381                 PRINTER_NMBR                                      DPKCS100
00382          INTO :TTRM000-TERMINAL-NMBR,                             DPKCS100
00383               :TTRM000-PRINTER-NMBR                               DPKCS100
00384          FROM TTRM000                                             DPKCS100
00385          WHERE TERMINAL_NMBR = :TTRM000-TERMINAL-NMBR             DPKCS100
00386      END-EXEC.                                                    DPKCS100
00387      MOVE SQLCODE TO DE-SQLCODE                                   DPKCS100
00388      MOVE SQLERRMC TO DE-SQLERRMC                                 DPKCS100
00389      IF SQLCODE = 0                                               DPKCS100
00390          MOVE TTRM000-PRINTER-NMBR TO LS-PRINTTO                  DPKCS100
00391      ELSE                                                         DPKCS100
00392          IF SQLCODE = +100                                        DPKCS100
00393              MOVE TI-TERM-IDNT TO WM-TERMINAL                     DPKCS100
00394              EXEC CICS                                            DPKCS100
00395                   WRITEQ TD                                       DPKCS100
00396                   QUEUE('CSML')                                   DPKCS100
00397                   FROM (WM-WRITEQ-MESSAGE1)                       DPKCS100
00398                   LENGTH(54)                                      DPKCS100
00399              END-EXEC                                             DPKCS100
00400              MOVE TI-TERM-CHR1-CHR2 TO DT-CNTL-UNIT-ID            DPKCS100
00401              MOVE DT-DUMMY-TERM TO TTRM000-TERMINAL-NMBR          DPKCS100
00402              EXEC SQL                                             DPKCS100
00403                  SELECT TERMINAL_NMBR,                            DPKCS100
00404                         PRINTER_NMBR                              DPKCS100
00405                  INTO :TTRM000-TERMINAL-NMBR,                     DPKCS100
00406                       :TTRM000-PRINTER-NMBR                       DPKCS100
00407                  FROM TTRM000                                     DPKCS100
00408                  WHERE TERMINAL_NMBR = :TTRM000-TERMINAL-NMBR     DPKCS100
00409              END-EXEC                                             DPKCS100
00410              IF (SQLCODE < 0 OR SQLCODE > +99)                    DPKCS100
00411                  MOVE TI-TERM-CHR1-CHR2 TO WM-CNTLUNIT            DPKCS100
00412                  EXEC CICS                                        DPKCS100
00413                       WRITEQ TD                                   DPKCS100
00414                       QUEUE('CSML')                               DPKCS100
00415                       FROM (WM-WRITEQ-MESSAGE2)                   DPKCS100
00416                       LENGTH(53)                                  DPKCS100
00417                  END-EXEC                                         DPKCS100
00418                  MOVE SQLCODE TO DE-SQLCODE                       DPKCS100
00419                  MOVE SQLERRMC TO DE-SQLERRMC                     DPKCS100
00420                  EXEC CICS                                        DPKCS100
00421                      WRITEQ TD                                    DPKCS100
00422                      QUEUE('CSML')                                DPKCS100
00423                      FROM (DE-DB2-ERROR-MSG)                      DPKCS100
00424                      LENGTH(125)                                  DPKCS100
00425                  END-EXEC                                         DPKCS100
00426              ELSE                                                 DPKCS100
00427                  MOVE TTRM000-PRINTER-NMBR TO LS-PRINTTO          DPKCS100
00428              END-IF                                               DPKCS100
00429          ELSE                                                     DPKCS100
00430              MOVE SQLCODE TO DE-SQLCODE                           DPKCS100
00431              MOVE SQLERRMC TO DE-SQLERRMC                         DPKCS100
00432              EXEC CICS                                            DPKCS100
00433                   WRITEQ TD                                       DPKCS100
00434                   QUEUE('CSML')                                   DPKCS100
00435                   FROM (DE-DB2-ERROR-MSG)                         DPKCS100
00436                   LENGTH(125)                                     DPKCS100
00437              END-EXEC                                             DPKCS100
00438          END-IF                                                   DPKCS100
00439      END-IF.                                                      DPKCS100
00440  2500-MOVE.                                                       DPKCS100
00441 *                                                                 DPKCS100
00442 * SELECT THE MODEL FROM THE LIST SUPPLIED (THE FIRST MODEL IS     DPKCS100
00443 * SELECTED).                                                      DPKCS100
00444 *                                                                 DPKCS100
00445 *                                                                 DPKCS100
00446      MOVE LS-MODELNAME-NAME TO LS-MOD-NAME.                       DPKCS100
00447 *                                                                 DPKCS100
00448 * PUT RETURN CODE 0 INTO THE STATUS BYTE.                         DPKCS100
00449 *                                                                 DPKCS100
00450      MOVE LOW-VALUES TO LS-RET-STATUS.                            DPKCS100
00451 *                                                                 DPKCS100
00452  3000-DELETE-PARAGRAPH.                                           DPKCS100
00453 *                                                                 DPKCS100
00454 * DELETE CODE IS PLACED HERE.                                     DPKCS100
00455 *                                                                 DPKCS100
00456 * RETURN TO CICS                                                  DPKCS100
00457 *                                                                 DPKCS100
00458      EXEC CICS RETURN END-EXEC.                                   DPKCS100
