       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    MLKUP703                                          00030002
       AUTHOR.        DAVE METZGER.                                     00040002
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  5-19-1999.                                        00060002
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *   MLKUP703 - DEPARTMENT, ENT-ID LEVEL                          *00100002
      *              M/L VENDOR ANALYSIS UPDATE (TVADPT)               *00110002
      *                                                                 00110117
      *CHANGE HISTORY:                                                  00110317
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00110417
      * ------- ---------- ----------- -------------------------------- 00110517
      *    10587      12/06/1999   MKUP_INIT_AMT AND MKUP_NET_AMT CALCS*00111016
W26603* W26603  03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION     00116017
      ******************************************************************00120000
                                                                        00140000
       ENVIRONMENT DIVISION.                                            00150000
                                                                        00160000
       CONFIGURATION SECTION.                                           00170000
       SOURCE-COMPUTER.        IBM-3090.                                00180000
       OBJECT-COMPUTER.        IBM-3090.                                00190000
       INPUT-OUTPUT SECTION.                                            00200000
       FILE-CONTROL.                                                    00210000
                                                                        00211002
           SELECT CONDENSED-FILE                                        00220000
               ASSIGN TO INP01.                                         00230000
                                                                        00240000
       DATA DIVISION.                                                   00260000
       FILE SECTION.                                                    00270000
                                                                        00280000
       FD  CONDENSED-FILE                                               00290000
           LABEL RECORDS ARE STANDARD                                   00300000
           RECORDING MODE IS F                                          00310000
           BLOCK CONTAINS 0 RECORDS.                                    00320000
W26603 01  CONDENSED-RECORD              PIC X(200).                    00330017
                                                                        00350000
       WORKING-STORAGE SECTION.                                         00360000
                                                                        00370000
       01  FILLER                        PIC X(28)  VALUE               00380009
                'BEGINNING OF WORKING STORAGE'.                         00390000
                                                                        00400000
       01  ABEND-CODE                    PIC S9(04) VALUE ZERO          00410009
                                                    COMP SYNC.          00411009
                                                                        00420000
       01  PC-PROGRAM-CONSTANTS.                                        00430000
           05  PC-JOB-NAME               PIC  X(06) VALUE 'MLK415'.     00440009
           05  PC-PROGRAM-NAME           PIC  X(08) VALUE 'MLKUP703'.   00450009
           05  PC-MAX-RETRIES            PIC S9(03) VALUE +3 COMP-3.    00460109
                                                                        00470000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00480000
           05  PE-MSG-01                 PIC X(50)  VALUE               00490009
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED  '.     00500000
           05  PE-MSG-02                 PIC X(50)  VALUE               00510009
                'ATTEMPT TO UPDATE ROW ON TABLE TVADPT FAILED    '.     00520002
           05  PE-MSG-03                 PIC X(50)  VALUE               00530009
                'ATTEMPT TO INSERT ROW ON TABLE TVADPT FAILED    '.     00540002
           05  PE-MSG-04                 PIC X(50)  VALUE               00550009
                'ERROR IN SELECT FROM TCKRSTCNTL                 '.     00560000
           05  PE-MSG-05                 PIC X(50)  VALUE               00570009
                'UPDATE TO TCKRSTINF FAILED                      '.     00580000
           05  PE-MSG-06                 PIC X(50)  VALUE               00590009
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT'.     00600000
           05  PE-MSG-07                 PIC X(50)  VALUE               00610009
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00620000
                                                                        00630002
       01  PS-PROGRAM-SWITCHES.                                         00640000
           05  PS-EOF-CONDENSED-FILE-SW  PIC X(01)  VALUE 'N'.          00650009
               88  PS-EOF-CONDENSED-FILE            VALUE 'Y'.          00660009
           05  PS-VADPT-CURSOR-EOF-SW    PIC X(01)  VALUE 'N'.          00670009
               88  PS-END-OF-TABLE-RECS             VALUE 'Y'.          00680009
           05  PS-FIRST-COMMIT-SW        PIC X(01)  VALUE 'N'.          00690009
               88  PS-FIRST-COMMIT                  VALUE 'Y'.          00700009
                                                                        00730000
       01  PI-PROGRAM-INDICATORS.                                       00740000
           05  PI-PROCESSING-OPTIONS-IND PIC X(01)  VALUE 'P'.          00750009
               88  PI-UPDATE-VADPT-ROW              VALUE 'U'.          00760009
               88  PI-INSERT-VADPT-ROW              VALUE 'I'.          00770009
                                                                        00780002
       01  PROGRAM-VARIABLES.                                           00790000
           05  PV-RETRY-COUNT            PIC S9(03) VALUE +0 COMP-3.    00800009
           05  PV-ENT-ID                 PIC 9(09)  VALUE 0.            00820009
                                                                        00830002
           05  PV-PARAGRAPH-NAME         PIC  X(30) VALUE SPACES.       00840009
           05  PV-OPERATION-ATTEMPTED    PIC  X(50) VALUE SPACES.       00860009
           05  PV-CURRENT-TIMESTAMP      PIC  X(26) VALUE SPACES.       00870009
           05  PV-COMMIT-TIMESTAMP       PIC  X(26) VALUE SPACES.       00880009
W26603     05  PV-MKUP-NET-AMT           PIC S9(11)V99 VALUE +0 COMP-3. 00890017
W26603     05  PV-MKUP-INIT-AMT          PIC S9(11)V99 VALUE +0 COMP-3. 00900017
W26603     05  PV-NET-COST               PIC S9(11)V99 VALUE +0 COMP-3. 00900117
W26603     05  PV-NET-RETAIL             PIC S9(11)V99 VALUE +0 COMP-3. 00901017
                                                                        00910002
       01  PA-PROGRAM-ACCUMULATORS.                                     00920000
           05  PA-RECORD-COUNTS.                                        00930000
               10  PA-INPUT-COUNT        PIC  9(09)    VALUE ZEROES.    00940009
               10  PA-UPDATE-COUNT       PIC  9(09)    VALUE ZEROES.    00950009
               10  PA-INSERT-COUNT       PIC  9(09)    VALUE ZEROES.    00960009
               10  PA-COMMIT-COUNT       PIC  9(09)    VALUE ZEROES.    00980009
                                                                        00990000
       01  SAVE-AREA.                                                   01020000
           05  SV-UPDATE-FIELDS.                                        01080000
W26603         10  SV-RCPT-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01090017
W26603         10  SV-RCPT-NET-COST      PIC S9(11)V99  COMP-3 VALUE +0.01100017
W26603         10  SV-PADJ-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01110017
W26603         10  SV-PADJ-NET-COST      PIC S9(11)V99  COMP-3 VALUE +0.01120017
W26603         10  SV-SLS-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.01130017
W26603         10  SV-SLS-REG-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01140017
W26603         10  SV-SLS-PROMO-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01150017
W26603         10  SV-SLS-PROCLR-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01162017
W26603         10  SV-SLS-CLR-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01170017
W26603         10  SV-SLS-OTH-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01180017
W26603         10  SV-MKDN-PLU-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01190017
W26603         10  SV-MKDN-RGST-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01200017
W26603         10  SV-MKDN-UMTCH-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01210017
W26603         10  SV-MKDN-BYR-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01220017
W26603         10  SV-MKDN-STR-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01230017
W26603         10  SV-MKDN-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01240017
W26603         10  SV-MKUP-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01250017
W26603         10  SV-MKUP-NET-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01260017
W26603         10  SV-MKUP-INIT-AMT      PIC S9(11)V99  COMP-3 VALUE +0.01270017
W26603         10  SV-XFER-IN-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01280017
W26603         10  SV-XFER-OUT-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01290017
W26603         10  SV-RCPT-GRO-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01300017
W26603         10  SV-PADJ-FRGT-COST-AMT PIC S9(11)V99  COMP-3 VALUE +0.01310017
W26603         10  SV-PADJ-DISC-COST-AMT PIC S9(11)V99  COMP-3 VALUE +0.01320017
W26603         10  SV-DR-ALW-RTL-AMT     PIC S9(11)V99  COMP-3 VALUE +0.01330017
W26603         10  SV-RTV-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.01340017
W26603         10  SV-INV-ADJ-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01350017
      *                                                                 01360000
      ******************************************************************01370000
      * MLRD203 - M/L MONTHLY TVADPT DB2 TABLE UPDATE TXN LAYOUT        01380014
      ******************************************************************01390000
W26603     COPY MLRD203.                                                01400017
                                                                        01410002
      ******************************************************************01420000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01430000
      ******************************************************************01440000
           COPY DPWS004.                                                01450000
                                                                        01460002
      ******************************************************************01470000
      *  USED FOR DB2 ERRORS                                            01480000
      ******************************************************************01490000
           EXEC SQL                                                     01500000
               INCLUDE SQLCA                                            01510000
           END-EXEC.                                                    01520000
                                                                        01530002
      ******************************************************************01540000
      *  MONTHLY M/L VENDOR ANALYSIS DEPARTMENT, ENT-ID TABLE           01550002
      ******************************************************************01560000
           EXEC SQL                                                     01570007
               INCLUDE TVADPT                                           01580007
           END-EXEC.                                                    01590007
                                                                        01600007
      ***************************************************************** 01610000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01620000
      ***************************************************************** 01630000
           EXEC SQL                                                     01640000
               INCLUDE TCKRSTCN                                         01650000
           END-EXEC.                                                    01660000
                                                                        01670002
           EXEC SQL                                                     01680000
               INCLUDE TCKRSTIN                                         01690000
           END-EXEC.                                                    01700000
                                                                        01710002
      ******************************************************************01720000
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01730000
      ******************************************************************01740000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01750000
           05  CR-AREA-LEN                  PIC S9(04) VALUE +35  COMP. 01760000
           05  CR-CHECKPOINT-RESTART-VAR.                               01770000
               10  CR-PREV-DTL-KEY          PIC  X(24) VALUE SPACES.    01780005
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    01790000
                   15  CR-FISCAL-NBR        PIC  X(02).                 01800000
                   15  CR-FISCAL-MN-END-DTE PIC  X(10).                 01810000
                   15  CR-DEPT-NBR          PIC  X(03).                 01820000
                   15  CR-ENT-ID            PIC  9(09).                 01840000
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01850000
                                                                        01860002
       01  CK-CHECKPOINT-SAVE-AREA.                                     01870000
           05  CK-SAVE-PREV-KEY         PIC  X(24) VALUE SPACES.        01880005
           05  FILLER REDEFINES CK-SAVE-PREV-KEY.                       01890000
               10  CK-FISCAL-NBR        PIC  X(02).                     01900000
               10  CK-FISCAL-MN-END-DTE PIC  X(10).                     01910000
               10  CK-DEPT-NBR          PIC  X(03).                     01920000
               10  CK-ENT-ID            PIC  9(09).                     01940000
                                                                        01950000
                                                                        01960000
      *================================================================*01970009
       PROCEDURE DIVISION.                                              01980000
      *================================================================*01981009
      ******************************************************************01981109
      * PROGRAM LOGIC                                                   01982002
      ******************************************************************01983002
       0000-MLKUP703.                                                   02010002
                                                                        02020000
           PERFORM 1000-INITIALIZATION.                                 02030000
                                                                        02040002
           PERFORM 2000-PROCESS-INPUT                                   02050000
             UNTIL PS-EOF-CONDENSED-FILE.                               02060009
                                                                        02070002
           PERFORM 8000-EOJ-ROUTINE.                                    02080000
                                                                        02090000
           STOP RUN.                                                    02100000
                                                                        02110000
      ******************************************************************02120000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02130000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02140000
      ******************************************************************02150000
       1000-INITIALIZATION.                                             02170000
                                                                        02180002
           OPEN INPUT CONDENSED-FILE.                                   02190000
                                                                        02200000
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02210000
                                                                        02220000
      ***  CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION           02230002
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02240000
               PERFORM 7500-SELECT-RESTART-INFO                         02250000
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT                       02260009
                                          TO CR-CHECKPOINT-RESTART-VAR  02270009
               MOVE CR-PREV-DTL-KEY       TO CK-SAVE-PREV-KEY           02280009
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                02290000
               DISPLAY 'MONTH NBR       ' CR-FISCAL-NBR                 02300000
               DISPLAY 'MONTH END DATE  ' CR-FISCAL-MN-END-DTE          02310000
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   02320000
               DISPLAY 'ENT-ID          ' CR-ENT-ID                     02340000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           02350000
           ELSE                                                         02360000
               SET PS-FIRST-COMMIT TO TRUE                              02370000
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02380000
           END-IF.                                                      02390000
                                                                        02400002
           PERFORM 4000-READ-CONDENSED-FILE                             02410009
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT               02420009
                  OR PS-EOF-CONDENSED-FILE.                             02430009
                                                                        02440002
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02450000
               DISPLAY 'INPUT FILE RESTARTED ON RECORD '                02460000
                        PA-INPUT-COUNT                                  02470000
               DISPLAY 'FISCAL MONTH NBR      ' ML203-FSCL-MN-NBR       02480014
               DISPLAY 'FISCAL MONTH END DATE ' ML203-FSCL-MN-END-DTE   02490014
               DISPLAY 'DEPARTMENT NBR        ' ML203-DEPT-NBR          02500014
               MOVE ML203-ENT-ID   TO PV-ENT-ID                         02520014
               DISPLAY 'ENT-ID                ' PV-ENT-ID               02530000
           END-IF.                                                      02540000
                                                                        02550000
           IF PS-EOF-CONDENSED-FILE                                     02560000
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02570000
                   CONTINUE                                             02580000
               ELSE                                                     02590000
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02600000
           ELSE                                                         02610000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02620000
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02630000
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02640000
           END-IF.                                                      02650000
                                                                        02660000
      ******************************************************************02670000
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02680000
      ******************************************************************02690000
       2000-PROCESS-INPUT.                                              02700000
                                                                        02710002
           PERFORM 7000-SELECT-DB-ROW.                                  02740000
                                                                        02750002
           EVALUATE TRUE                                                02790008
               WHEN PI-UPDATE-VADPT-ROW                                 02800008
                   PERFORM 2100-CALCULATE-UPDATES                       02801015
                   PERFORM 7100-UPDATE-VADPT-ROW                        02810008
               WHEN PI-INSERT-VADPT-ROW                                 02820008
                   PERFORM 2200-CALCULATE-INSERTS                       02821015
                   PERFORM 7200-INSERT-VADPT-ROW                        02830008
               WHEN OTHER                                               02840008
                   MOVE '7000-SELECT-DB-ROW' TO PV-PARAGRAPH-NAME       02850008
                   MOVE PE-MSG-01            TO PV-OPERATION-ATTEMPTED  02860008
                   PERFORM 9999-SQL-ABEND                               02870008
           END-EVALUATE.                                                02880008
                                                                        02880108
           PERFORM 7700-COMMIT-PROCESSING.                              02881008
           PERFORM 4000-READ-CONDENSED-FILE.                            02882008
                                                                        02900002
      ******************************************************************02990000
      * CALCULATE FIELDS FOR UPDATE TO DATABASE                         03000015
      ******************************************************************03010000
       2100-CALCULATE-UPDATES.                                          03020000
                                                                        03021002
      ***  ADD DB2 TABLE ROW VALUES TO CURRENT INPUT RECORD ROW VALUES  03030002
           COMPUTE SV-SLS-REG-RTL-AMT =                                 03050000
                   VADPT-SLS-REG-RTL-AMT    + ML203-SLS-REG-RTL-AMT.    03060014
           COMPUTE SV-SLS-PROMO-RTL-AMT =                               03070000
                   VADPT-SLS-PROMO-RTL-AMT  + ML203-SLS-PROMO-RTL-AMT.  03080014
           COMPUTE SV-SLS-PROCLR-RTL-AMT =                              03090000
                   VADPT-SLS-PROCLR-RTL-AMT + ML203-SLS-PROCLR-RTL-AMT. 03100014
           COMPUTE SV-SLS-CLR-RTL-AMT =                                 03110000
                   VADPT-SLS-CLR-RTL-AMT    + ML203-SLS-CLR-RTL-AMT.    03120014
           COMPUTE SV-SLS-OTH-RTL-AMT =                                 03130000
                   VADPT-SLS-OTH-RTL-AMT    + ML203-SLS-OTH-RTL-AMT.    03140014
                                                                        03150000
           COMPUTE SV-MKDN-PLU-RTL-AMT  =                               03160000
                   VADPT-MKDN-PLU-RTL-AMT   + ML203-MKDN-PLU-RTL-AMT.   03170014
           COMPUTE SV-MKDN-RGST-RTL-AMT =                               03180007
                   VADPT-MKDN-RGST-RTL-AMT  + ML203-MKDN-RGST-RTL-AMT.  03190014
           COMPUTE SV-MKDN-UMTCH-RTL-AMT =                              03200000
                   VADPT-MKDN-UMTCH-RTL-AMT + ML203-MKDN-UMTCH-RTL-AMT. 03210014
           COMPUTE SV-MKDN-BYR-RTL-AMT  =                               03220000
                   VADPT-MKDN-BYR-RTL-AMT   + ML203-MKDN-BYR-RTL-AMT.   03230014
           COMPUTE SV-MKDN-STR-RTL-AMT  =                               03240000
                   VADPT-MKDN-STR-RTL-AMT   + ML203-MKDN-STR-RTL-AMT.   03250014
                                                                        03260000
           COMPUTE SV-MKUP-RTL-AMT      =                               03270000
                   VADPT-MKUP-RTL-AMT       + ML203-MKUP-RTL-AMT.       03280014
           COMPUTE SV-XFER-IN-RTL-AMT   =                               03290000
                   VADPT-XFER-IN-RTL-AMT    + ML203-XFER-IN-RTL-AMT.    03300014
           COMPUTE SV-XFER-OUT-RTL-AMT  =                               03310000
                   VADPT-XFER-OUT-RTL-AMT   + ML203-XFER-OUT-RTL-AMT.   03320014
           COMPUTE SV-RCPT-RTL-AMT      =                               03330000
                   VADPT-RCPT-RTL-AMT       + ML203-RCPT-RTL-AMT.       03340014
           COMPUTE SV-RCPT-NET-COST     =                               03350000
                   VADPT-RCPT-NET-COST-AMT  + ML203-RCPT-NET-COST-AMT.  03360014
           COMPUTE SV-RCPT-GRO-COST-AMT =                               03370000
                   VADPT-RCPT-GRO-COST-AMT  + ML203-RCPT-GRO-COST-AMT.  03380014
           COMPUTE SV-PADJ-FRGT-COST-AMT =                              03390000
                   VADPT-PADJ-FRGT-COST-AMT + ML203-PADJ-FRGT-COST-AMT. 03400014
           COMPUTE SV-PADJ-DISC-COST-AMT =                              03410000
                   VADPT-PADJ-DISC-COST-AMT + ML203-PADJ-DISC-COST-AMT. 03420014
           COMPUTE SV-PADJ-RTL-AMT      =                               03430000
                   VADPT-PADJ-RTL-AMT       + ML203-PADJ-RTL-AMT.       03440014
           COMPUTE SV-PADJ-NET-COST     =                               03450000
                   VADPT-PADJ-NET-COST-AMT  + ML203-PADJ-NET-COST-AMT.  03460014
           COMPUTE SV-DR-ALW-RTL-AMT    =                               03470000
                   VADPT-DR-ALW-RTL-AMT     + ML203-DR-ALW-RTL-AMT.     03480014
           COMPUTE SV-RTV-RTL-AMT       =                               03490000
                   VADPT-RTV-RTL-AMT        + ML203-RTV-RTL-AMT.        03500014
           COMPUTE SV-INV-ADJ-RTL-AMT   =                               03510000
                    VADPT-INV-ADJ-RTL-AMT  + ML203-INV-ADJ-RTL-AMT.     03520014
                                                                        03530000
      *--- INITIAL MARKUP AMOUNT                                        03531016
           COMPUTE PV-MKUP-INIT-AMT =                                   03540000
1299RM             ML203-RCPT-RTL-AMT - ML203-RCPT-NET-COST-AMT.        03541016
1299RM*            ML203-RCPT-RTL-AMT - ML203-RCPT-GRO-COST-AMT.        03550016
           COMPUTE SV-MKUP-INIT-AMT   =                                 03560000
                   VADPT-MKUP-INIT-AMT + PV-MKUP-INIT-AMT.              03570004
                                                                        03580000
      *--- NET MARKUP AMOUNT                                            03581016
1299RM*    COMPUTE PV-MKUP-NET-AMT =                                    03590016
1299RM*           (ML203-RCPT-RTL-AMT -                                 03600016
1299RM*           (ML203-RCPT-GRO-COST-AMT +                            03610016
1299RM*            ML203-PADJ-FRGT-COST-AMT) -                          03620016
1299RM*            ML203-PADJ-DISC-COST-AMT).                           03630016
1299RM     COMPUTE PV-NET-RETAIL =                                      03630116
1299RM            (ML203-RCPT-RTL-AMT +                                 03630216
1299RM             ML203-PADJ-RTL-AMT +                                 03630316
1299RM             ML203-MKUP-RTL-AMT +                                 03630416
1299RM            (ML203-XFER-IN-RTL-AMT -                              03630516
1299RM             ML203-XFER-OUT-RTL-AMT))                             03630616
1299RM     END-COMPUTE.                                                 03630716
1299RM     COMPUTE PV-NET-COST = ML203-RCPT-NET-COST-AMT +              03630816
1299RM                           ML203-PADJ-NET-COST-AMT                03630916
1299RM     END-COMPUTE.                                                 03631016
                                                                        03631116
1299RM     COMPUTE PV-MKUP-NET-AMT = PV-NET-RETAIL - PV-NET-COST        03631216
1299RM     END-COMPUTE.                                                 03632016
           COMPUTE SV-MKUP-NET-AMT     =                                03640000
                   VADPT-MKUP-NET-AMT  + PV-MKUP-NET-AMT.               03650004
                                                                        03660000
           COMPUTE SV-INV-ADJ-RTL-AMT  =                                03670000
                   VADPT-INV-ADJ-RTL-AMT  + ML203-INV-ADJ-RTL-AMT.      03680014
                                                                        03691002
      ******************************************************************03692015
      * CALCULATE FIELDS FOR INSERT TO DATABASE                         03693015
      ******************************************************************03694015
       2200-CALCULATE-INSERTS.                                          03695015
                                                                        03696015
           COMPUTE PV-MKUP-INIT-AMT =                                   03698015
1299RM             ML203-RCPT-RTL-AMT - ML203-RCPT-NET-COST-AMT.        03698116
1299RM*            ML203-RCPT-RTL-AMT - ML203-RCPT-GRO-COST-AMT.        03699016
                                                                        03699115
1299RM*    COMPUTE PV-MKUP-NET-AMT =                                    03699216
1299RM*           (ML203-RCPT-RTL-AMT -                                 03699316
1299RM*           (ML203-RCPT-GRO-COST-AMT +                            03699416
1299RM*            ML203-PADJ-FRGT-COST-AMT) -                          03699516
1299RM*            ML203-PADJ-DISC-COST-AMT).                           03699616
1299RM     COMPUTE PV-NET-RETAIL =                                      03699716
1299RM            (ML203-RCPT-RTL-AMT +                                 03699816
1299RM             ML203-PADJ-RTL-AMT +                                 03699916
1299RM             ML203-MKUP-RTL-AMT +                                 03700016
1299RM            (ML203-XFER-IN-RTL-AMT -                              03700116
1299RM             ML203-XFER-OUT-RTL-AMT))                             03700216
1299RM     END-COMPUTE.                                                 03700316
1299RM     COMPUTE PV-NET-COST = ML203-RCPT-NET-COST-AMT +              03700416
1299RM                           ML203-PADJ-NET-COST-AMT                03700516
1299RM     END-COMPUTE.                                                 03700616
                                                                        03700716
1299RM     COMPUTE PV-MKUP-NET-AMT = PV-NET-RETAIL - PV-NET-COST        03700816
1299RM     END-COMPUTE.                                                 03700916
                                                                        03701015
      ******************************************************************03702000
      * READS THE CONDENSED FILE INTO THE TVADPT LAYOUT                 03710002
      ******************************************************************03720000
       4000-READ-CONDENSED-FILE.                                        03730000
                                                                        03731002
           READ CONDENSED-FILE INTO ML203-UPDATE-REC                    03740014
              AT END                                                    03750000
                MOVE 'Y' TO PS-EOF-CONDENSED-FILE-SW.                   03760000
                                                                        03770002
           IF PS-EOF-CONDENSED-FILE                                     03780000
              CONTINUE                                                  03790008
           ELSE                                                         04020008
              ADD 1      TO PA-INPUT-COUNT                              04030008
            END-IF.                                                     04040000
                                                                        04050002
      ******************************************************************04060000
      * SELECT A ROW FROM THE MONTHLY MCL  TABLE THAT MATCHES INPUT KEY 04070000
      *   SQLCODE FROM 'SELECT' IS EVALUATED IN 2000-PROCESS-INPUT      04080000
      ******************************************************************04090000
       7000-SELECT-DB-ROW.                                              04100000
                                                                        04110002
           PERFORM WITH TEST AFTER                                      04110111
              VARYING PV-RETRY-COUNT                                    04110211
              FROM 1 BY 1                                               04110309
              UNTIL PV-RETRY-COUNT > PC-MAX-RETRIES                     04110511
                 OR SQLCODE = +0                                        04110611
                 OR SQLCODE = +100                                      04110711
                                                                        04111008
              EXEC SQL                                                  04120008
                  SELECT SLS_REG_RTL_AMT                                04130008
                        ,SLS_PROMO_RTL_AMT                              04140008
                        ,SLS_PROCLR_RTL_AMT                             04150008
                        ,SLS_CLR_RTL_AMT                                04160008
                        ,SLS_OTH_RTL_AMT                                04170008
                        ,MKDN_PLU_RTL_AMT                               04180008
                        ,MKDN_RGST_RTL_AMT                              04190008
                        ,MKDN_UMTCH_RTL_AMT                             04200008
                        ,MKDN_BYR_RTL_AMT                               04210008
                        ,MKDN_STR_RTL_AMT                               04220008
                        ,MKUP_RTL_AMT                                   04230008
                        ,XFER_IN_RTL_AMT                                04240008
                        ,XFER_OUT_RTL_AMT                               04250008
                        ,RCPT_RTL_AMT                                   04260008
                        ,RCPT_NET_COST_AMT                              04270008
                        ,RCPT_GRO_COST_AMT                              04280008
                        ,PADJ_FRGT_COST_AMT                             04290008
                        ,PADJ_DISC_COST_AMT                             04300008
                        ,PADJ_RTL_AMT                                   04310008
                        ,PADJ_NET_COST_AMT                              04320008
                        ,DR_ALW_RTL_AMT                                 04330008
                        ,RTV_RTL_AMT                                    04340008
                        ,INV_ADJ_RTL_AMT                                04350008
                        ,MKUP_INIT_AMT                                  04351008
                        ,MKUP_NET_AMT                                   04352008
                    INTO :VADPT-SLS-REG-RTL-AMT                         04353008
                        ,:VADPT-SLS-PROMO-RTL-AMT                       04354008
                        ,:VADPT-SLS-PROCLR-RTL-AMT                      04355008
                        ,:VADPT-SLS-CLR-RTL-AMT                         04356008
                        ,:VADPT-SLS-OTH-RTL-AMT                         04357008
                        ,:VADPT-MKDN-PLU-RTL-AMT                        04358008
                        ,:VADPT-MKDN-RGST-RTL-AMT                       04359008
                        ,:VADPT-MKDN-UMTCH-RTL-AMT                      04360008
                        ,:VADPT-MKDN-BYR-RTL-AMT                        04370008
                        ,:VADPT-MKDN-STR-RTL-AMT                        04380008
                        ,:VADPT-MKUP-RTL-AMT                            04390008
                        ,:VADPT-XFER-IN-RTL-AMT                         04400008
                        ,:VADPT-XFER-OUT-RTL-AMT                        04410008
                        ,:VADPT-RCPT-RTL-AMT                            04420008
                        ,:VADPT-RCPT-NET-COST-AMT                       04430008
                        ,:VADPT-RCPT-GRO-COST-AMT                       04440008
                        ,:VADPT-PADJ-FRGT-COST-AMT                      04450008
                        ,:VADPT-PADJ-DISC-COST-AMT                      04460008
                        ,:VADPT-PADJ-RTL-AMT                            04470008
                        ,:VADPT-PADJ-NET-COST-AMT                       04480008
                        ,:VADPT-DR-ALW-RTL-AMT                          04490008
                        ,:VADPT-RTV-RTL-AMT                             04500008
                        ,:VADPT-INV-ADJ-RTL-AMT                         04510008
                        ,:VADPT-MKUP-INIT-AMT                           04520008
                        ,:VADPT-MKUP-NET-AMT                            04530008
                  FROM TVADPT                                           04540008
                 WHERE   FSCL_MN_NBR         = :ML203-FSCL-MN-NBR       04550014
                   AND   FSCL_MN_END_DTE     = :ML203-FSCL-MN-END-DTE   04560014
                   AND   DEPT_NBR            = :ML203-DEPT-NBR          04570014
                   AND   ENT_ID              = :ML203-ENT-ID            04590014
              END-EXEC                                                  04600010
                                                                        04820002
              EVALUATE SQLCODE                                          04820108
                  WHEN +0                                               04820208
                      SET PI-UPDATE-VADPT-ROW     TO TRUE               04820308
                  WHEN +100                                             04820408
                      SET PI-INSERT-VADPT-ROW     TO TRUE               04820508
                  WHEN -904                                             04820608
                  WHEN -911                                             04820708
                  WHEN -913                                             04820808
                      CONTINUE                                          04820908
                  WHEN OTHER                                            04821408
                    MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME    04821508
                    MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED            04821608
                    PERFORM 9999-SQL-ABEND                              04821708
              END-EVALUATE                                              04822208
           END-PERFORM.                                                 04823008
                                                                        04831008
           IF PV-RETRY-COUNT > PC-MAX-RETRIES                           04832008
              DISPLAY '*** DEADLOCK OCCURRED ON SELECT ***'             04832108
              MOVE '7000-SELECT-DB-ROW' TO PV-PARAGRAPH-NAME            04832408
              MOVE PE-MSG-07            TO PV-OPERATION-ATTEMPTED       04832508
              PERFORM 9999-SQL-ABEND                                    04832708
           END-IF.                                                      04832808
                                                                        04833008
      ***************************************************************** 04840000
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON FSCL INFO, DEPT  04850000
      * AND ENT ID. CURRENT TABLE VALUES UPDATED TO INCLUDE VALUES      04860002
      * FROM INPUT REC MLRD203                                          04870002
      ***************************************************************** 04880000
       7100-UPDATE-VADPT-ROW.                                           04890004
                                                                        04900002
           PERFORM WITH TEST AFTER                                      04920111
              VARYING PV-RETRY-COUNT                                    04920211
              FROM 1 BY 1                                               04922009
              UNTIL PV-RETRY-COUNT > PC-MAX-RETRIES                     04923009
                 OR SQLCODE = +0                                        04924010
                                                                        04926008
              EXEC SQL                                                  04930008
                  UPDATE TVADPT                                         04940008
                      SET SLS_REG_RTL_AMT     = :SV-SLS-REG-RTL-AMT     04950008
                        , SLS_PROMO_RTL_AMT   = :SV-SLS-PROMO-RTL-AMT   04960008
                        , SLS_PROCLR_RTL_AMT  = :SV-SLS-PROCLR-RTL-AMT  04970008
                        , SLS_CLR_RTL_AMT     = :SV-SLS-CLR-RTL-AMT     04980008
                        , SLS_OTH_RTL_AMT     = :SV-SLS-OTH-RTL-AMT     04990008
                        , MKDN_PLU_RTL_AMT    = :SV-MKDN-PLU-RTL-AMT    05000008
                        , MKDN_RGST_RTL_AMT   = :SV-MKDN-RGST-RTL-AMT   05010008
                        , MKDN_UMTCH_RTL_AMT  = :SV-MKDN-UMTCH-RTL-AMT  05020008
                        , MKDN_BYR_RTL_AMT    = :SV-MKDN-BYR-RTL-AMT    05030008
                        , MKDN_STR_RTL_AMT    = :SV-MKDN-STR-RTL-AMT    05040008
                        , MKUP_RTL_AMT        = :SV-MKUP-RTL-AMT        05050008
                        , XFER_IN_RTL_AMT     = :SV-XFER-IN-RTL-AMT     05060008
                        , XFER_OUT_RTL_AMT    = :SV-XFER-OUT-RTL-AMT    05070008
                        , RCPT_RTL_AMT        = :SV-RCPT-RTL-AMT        05080008
                        , RCPT_NET_COST_AMT   = :SV-RCPT-NET-COST       05090008
                        , RCPT_GRO_COST_AMT   = :SV-RCPT-GRO-COST-AMT   05100008
                        , PADJ_FRGT_COST_AMT  = :SV-PADJ-FRGT-COST-AMT  05110008
                        , PADJ_DISC_COST_AMT  = :SV-PADJ-DISC-COST-AMT  05120008
                        , PADJ_RTL_AMT        = :SV-PADJ-RTL-AMT        05130008
                        , PADJ_NET_COST_AMT   = :SV-PADJ-NET-COST       05140008
                        , DR_ALW_RTL_AMT      = :SV-DR-ALW-RTL-AMT      05150008
                        , RTV_RTL_AMT         = :SV-RTV-RTL-AMT         05160008
                        , INV_ADJ_RTL_AMT     = :SV-INV-ADJ-RTL-AMT     05170008
                        , MKUP_INIT_AMT       = :SV-MKUP-INIT-AMT       05180008
                        , MKUP_NET_AMT        = :SV-MKUP-NET-AMT        05190008
                 WHERE   FSCL_MN_NBR          = :ML203-FSCL-MN-NBR      05200014
                   AND   FSCL_MN_END_DTE      = :ML203-FSCL-MN-END-DTE  05210014
                   AND   DEPT_NBR             = :ML203-DEPT-NBR         05220014
                   AND   ENT_ID               = :ML203-ENT-ID           05240014
              END-EXEC                                                  05250008
                                                                        05260002
              EVALUATE SQLCODE                                          05441008
                  WHEN +0                                               05442008
                      CONTINUE                                          05443008
                  WHEN -904                                             05446008
                  WHEN -911                                             05447008
                  WHEN -913                                             05448008
                      CONTINUE                                          05449008
                  WHEN OTHER                                            05449108
                   MOVE '7100-UPDATE-VADPT-ROW' TO PV-PARAGRAPH-NAME    05449208
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED05449308
                   PERFORM 9999-SQL-ABEND                               05449408
              END-EVALUATE                                              05449808
           END-PERFORM.                                                 05449908
                                                                        05450102
           IF PV-RETRY-COUNT > PC-MAX-RETRIES                           05451008
              DISPLAY '*** DEADLOCK OCCURRED ON SELECT ***'             05451108
              MOVE '7100-UPDATE-VADPT-ROW' TO PV-PARAGRAPH-NAME         05452108
              MOVE PE-MSG-07               TO PV-OPERATION-ATTEMPTED    05452208
              PERFORM 9999-SQL-ABEND                                    05452408
           ELSE                                                         05456008
              ADD 1 TO PA-UPDATE-COUNT                                  05460008
           END-IF.                                                      05461008
                                                                        05470002
      ***************************************************************** 05480000
      * THERE WAS NO MATCHING ROW ON THE MONTHLY DEPT TABLE. INITIAL  * 05490002
      * - ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW            * 05500000
      * - CALCULATED FIELDS ARE INITIALLY SET TO 0 AND UPDATED IN     * 05510002
      *   THE RECALC PROGRAM (MLKUP704)                               * 05511002
      ***************************************************************** 05520000
       7200-INSERT-VADPT-ROW.                                           05530004
                                                                        05549208
           PERFORM WITH TEST AFTER                                      05549311
              VARYING PV-RETRY-COUNT                                    05549411
              FROM 1 BY 1                                               05549609
              UNTIL PV-RETRY-COUNT > PC-MAX-RETRIES                     05549709
                 OR SQLCODE = +0                                        05549810
                                                                        05550002
             EXEC SQL                                                   05560008
                 INSERT INTO TVADPT                                     05570008
                 (  FSCL_MN_NBR                                         05580008
                  , FSCL_MN_END_DTE                                     05590008
                  , DEPT_NBR                                            05600008
                  , ENT_ID                                              05620008
                  , SLS_REG_RTL_AMT                                     05630008
                  , SLS_PROMO_RTL_AMT                                   05640008
                  , SLS_PROCLR_RTL_AMT                                  05650008
                  , SLS_CLR_RTL_AMT                                     05660008
                  , SLS_OTH_RTL_AMT                                     05670008
                  , MKDN_PLU_RTL_AMT                                    05680008
                  , MKDN_RGST_RTL_AMT                                   05690008
                  , MKDN_UMTCH_RTL_AMT                                  05700008
                  , MKDN_BYR_RTL_AMT                                    05710008
                  , MKDN_STR_RTL_AMT                                    05720008
                  , MKUP_RTL_AMT                                        05730008
                  , XFER_IN_RTL_AMT                                     05740008
                  , XFER_OUT_RTL_AMT                                    05750008
                  , RCPT_RTL_AMT                                        05760008
                  , RCPT_NET_COST_AMT                                   05770008
                  , RCPT_GRO_COST_AMT                                   05780008
                  , PADJ_FRGT_COST_AMT                                  05790008
                  , PADJ_DISC_COST_AMT                                  05800008
                  , PADJ_RTL_AMT                                        05810008
                  , PADJ_NET_COST_AMT                                   05820008
                  , DR_ALW_RTL_AMT                                      05830008
                  , RTV_RTL_AMT                                         05840008
                  , INV_ADJ_RTL_AMT                                     05850008
                  , SHNK_RTL_AMT                                        05860008
                  , MKUP_INIT_AMT                                       05870008
                  , MKUP_NET_AMT                                        05880008
                  , END_INV_RTL_AMT                                     05890008
                  , CUM_MKUP_PCT                                        05900008
                  , END_INV_COST_AMT                                    05910008
                  , SLS_COST_AMT                                        05920008
                  , GRO_MRGN_AMT      )                                 05930008
            VALUES                                                      05940008
                (   :ML203-FSCL-MN-NBR                                  05950014
                  , :ML203-FSCL-MN-END-DTE                              05960014
                  , :ML203-DEPT-NBR                                     05970014
                  , :ML203-ENT-ID                                       05990014
                  , :ML203-SLS-REG-RTL-AMT                              06000014
                  , :ML203-SLS-PROMO-RTL-AMT                            06010014
                  , :ML203-SLS-PROCLR-RTL-AMT                           06020014
                  , :ML203-SLS-CLR-RTL-AMT                              06030014
                  , :ML203-SLS-OTH-RTL-AMT                              06040014
                  , :ML203-MKDN-PLU-RTL-AMT                             06050014
                  , :ML203-MKDN-RGST-RTL-AMT                            06060014
                  , :ML203-MKDN-UMTCH-RTL-AMT                           06070014
                  , :ML203-MKDN-BYR-RTL-AMT                             06080014
                  , :ML203-MKDN-STR-RTL-AMT                             06090014
                  , :ML203-MKUP-RTL-AMT                                 06100014
                  , :ML203-XFER-IN-RTL-AMT                              06110014
                  , :ML203-XFER-OUT-RTL-AMT                             06120014
                  , :ML203-RCPT-RTL-AMT                                 06130014
                  , :ML203-RCPT-NET-COST-AMT                            06140014
                  , :ML203-RCPT-GRO-COST-AMT                            06150014
                  , :ML203-PADJ-FRGT-COST-AMT                           06160014
                  , :ML203-PADJ-DISC-COST-AMT                           06170014
                  , :ML203-PADJ-RTL-AMT                                 06180014
                  , :ML203-PADJ-NET-COST-AMT                            06190014
                  , :ML203-DR-ALW-RTL-AMT                               06200014
                  , :ML203-RTV-RTL-AMT                                  06210014
                  , :ML203-INV-ADJ-RTL-AMT                              06220014
                  , 0                                                   06230008
                  , :PV-MKUP-INIT-AMT                                   06240008
                  , :PV-MKUP-NET-AMT                                    06250008
                  , 0                                                   06260008
                  , 0                                                   06270008
                  , 0                                                   06280008
                  , 0                                                   06290008
                  , 0                       )                           06300008
              END-EXEC                                                  06310008
                                                                        06320002
              EVALUATE SQLCODE                                          06330008
                 WHEN +0                                                06340008
                     CONTINUE                                           06350008
                 WHEN -904                                              06360008
                 WHEN -911                                              06370008
                 WHEN -913                                              06380008
                     CONTINUE                                           06390008
                 WHEN OTHER                                             06400008
                    MOVE '7200-INSERT-VADPT-ROW' TO PV-PARAGRAPH-NAME   06410008
                    MOVE PE-MSG-02            TO PV-OPERATION-ATTEMPTED 06420008
                    PERFORM 9999-SQL-ABEND                              06430008
              END-EVALUATE                                              06440008
           END-PERFORM.                                                 06450008
                                                                        06511008
           IF PV-RETRY-COUNT > PC-MAX-RETRIES                           06512008
              DISPLAY '*** DEADLOCK OCCURRED ON SELECT ***'             06513008
              MOVE '7200-INSERT-VADPT-ROW' TO PV-PARAGRAPH-NAME         06513108
              MOVE PE-MSG-07               TO PV-OPERATION-ATTEMPTED    06513208
              PERFORM 9999-SQL-ABEND                                    06513408
           ELSE                                                         06517008
              ADD 1 TO PA-INSERT-COUNT                                  06517108
           END-IF.                                                      06519008
                                                                        06530000
      ******************************************************************06540000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *06550000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *06560000
      *            CONTROL TABLE                                       *06570000
      ******************************************************************06580000
       7300-GET-COMMIT-TIMESTAMP.                                       06590000
                                                                        06600000
           EXEC SQL                                                     06610000
      ***  CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL     06620002
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       06630000
               INTO :PV-COMMIT-TIMESTAMP                                06640000
               FROM TCKRSTCNTL                                          06650000
              WHERE JOB_NAME  = :PC-JOB-NAME                            06660000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        06670000
           END-EXEC.                                                    06680000
                                                                        06690000
           EVALUATE TRUE                                                06700000
              WHEN SQLCODE = 0                                          06710002
      ***     DISPLAY 'NEXT COMMIT AT TIMESTAMP: ', PV-COMMIT-TIMESTAMP 06720002
                  CONTINUE                                              06730002
              WHEN SQLCODE NOT EQUAL ZERO                               06740002
                  MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME 06750002
                  PERFORM 9999-SQL-ABEND                                06760002
           END-EVALUATE.                                                06770000
                                                                        06790002
      ******************************************************************06800000
      * 7400-INIT-CHECKPOINT-SELECT                                    *06810000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *06820000
      *            IF WE ARE IN A RESTART CONDITION.                   *06830000
      ******************************************************************06840000
       7400-INIT-CHECKPOINT-SELECT.                                     06850000
                                                                        06860000
           EXEC SQL                                                     06870000
             SELECT  CKPT_STAT_CODE                                     06880000
                    ,CKPT_FRQNCY_QTY                                    06890000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         06900000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        06910000
               FROM  TCKRSTCNTL                                         06920000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06930000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06940000
           END-EXEC.                                                    06950000
                                                                        06960000
           IF SQLCODE = 0                                               06970000
               CONTINUE                                                 06980000
           ELSE                                                         06990000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  07000000
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     07010000
               PERFORM 9999-SQL-ABEND                                   07020000
           END-IF.                                                      07030000
                                                                        07050008
      ******************************************************************07060000
      * 7500-SELECT-RESTART-INFO                                       *07070000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *07080000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *07090000
      ******************************************************************07100000
       7500-SELECT-RESTART-INFO.                                        07110000
                                                                        07120000
           EXEC SQL                                                     07130000
             SELECT  WORK_STRG_DESC                                     07140000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          07150000
               FROM  TCKRSTINF                                          07160000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07170000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07180000
           END-EXEC.                                                    07190000
                                                                        07200000
           IF SQLCODE = 0                                               07210000
               CONTINUE                                                 07220000
           ELSE                                                         07230000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   07240000
               PERFORM 9999-SQL-ABEND                                   07250000
           END-IF.                                                      07260000
                                                                        07270000
      ******************************************************************07280000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *07290000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *07300000
      ******************************************************************07310000
       7600-SET-CURRENT-TIMESTAMP.                                      07320000
                                                                        07330000
           EXEC SQL                                                     07340000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           07350000
           END-EXEC.                                                    07360000
                                                                        07370000
           IF SQLCODE = 0                                               07380000
               CONTINUE                                                 07390000
           ELSE                                                         07400000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 07410000
               PERFORM 9999-SQL-ABEND                                   07420000
           END-IF.                                                      07430000
                                                                        07450002
      ******************************************************************07460000
      * 7700-COMMIT-PROCESSING.                                        *07470000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *07480000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*07490000
      ******************************************************************07500000
       7700-COMMIT-PROCESSING.                                          07510000
                                                                        07511008
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          07540000
                                                                        07550000
      ***  PREPARE COMMIT RECORD FOR UPDATE                             07560002
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                07570000
               MOVE ML203-FSCL-MN-NBR         TO CR-FISCAL-NBR          07580014
               MOVE ML203-FSCL-MN-END-DTE     TO CR-FISCAL-MN-END-DTE   07590014
               MOVE ML203-DEPT-NBR            TO CR-DEPT-NBR            07600014
               MOVE ML203-ENT-ID              TO PV-ENT-ID              07620014
               MOVE PV-ENT-ID                 TO CR-ENT-ID              07630000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    07640000
      ***      MOVE COMMIT INFO TO WORKING STORAGE ROW                  07650002
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       07660000
                                             TCKRSTINF-WORK-STRG-DESC   07670000
               IF PS-FIRST-COMMIT                                       07680000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                07690000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                07700000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       07710000
               END-IF                                                   07720000
               PERFORM 7800-COMMIT-CHANGES                              07730000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        07740000
           END-IF.                                                      07750000
                                                                        07770002
      ******************************************************************07780000
      * 7800-COMMIT-CHANGES.                                            07790000
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      07800000
      *            TAKE A COMMIT.                                       07810000
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    07820000
      ******************************************************************07830000
       7800-COMMIT-CHANGES.                                             07840000
                                                                        07850000
           EXEC SQL                                                     07860000
             UPDATE TCKRSTINF                                           07870000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          07880000
              WHERE JOB_NAME       = :PC-JOB-NAME                       07890000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   07900000
           END-EXEC.                                                    07910000
                                                                        07920000
           IF SQLCODE = 0                                               07930000
               CONTINUE                                                 07941000
           ELSE                                                         07950000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED07960000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     07970000
               PERFORM 9999-SQL-ABEND                                   07980000
           END-IF.                                                      07990000
                                                                        08000000
           EXEC SQL                                                     08010000
               COMMIT                                                   08020000
           END-EXEC.                                                    08030000
                                                                        08040000
           IF SQLCODE = 0                                               08050000
               ADD 1 TO PA-COMMIT-COUNT                                 08051000
           ELSE                                                         08070000
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED08080000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     08090000
               PERFORM 9999-SQL-ABEND                                   08100000
           END-IF.                                                      08110000
                                                                        08120002
      ******************************************************************08130000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   08140000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   08150000
      ******************************************************************08160000
       7900-UPDATE-CHECKPOINT-STATUS.                                   08170000
                                                                        08180000
           EXEC SQL                                                     08190000
             UPDATE  TCKRSTCNTL                                         08200000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        08210000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           08220000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       08230000
           END-EXEC.                                                    08240000
                                                                        08250000
           IF SQLCODE = 0                                               08260000
               CONTINUE                                                 08270000
           ELSE                                                         08280000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME08290000
               PERFORM 9999-SQL-ABEND                                   08300000
           END-IF.                                                      08310000
                                                                        08320000
      ******************************************************************08880000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *08890000
      ******************************************************************08900000
       8000-EOJ-ROUTINE.                                                08910000
                                                                        08910108
           MOVE ML203-FSCL-MN-NBR         TO CR-FISCAL-NBR.             08911014
           MOVE ML203-FSCL-MN-END-DTE     TO CR-FISCAL-MN-END-DTE.      08912014
           MOVE ML203-DEPT-NBR            TO CR-DEPT-NBR.               08913014
           MOVE ML203-ENT-ID              TO PV-ENT-ID.                 08914014
           MOVE PV-ENT-ID                 TO CR-ENT-ID.                 08915008
           MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT.       08916008
      ***      MOVE COMMIT INFO TO WORKING STORAGE ROW                  08917008
           MOVE CR-CHECKPOINT-RESTART-AREA TO                           08918008
                                         TCKRSTINF-WORK-STRG-DESC.      08919008
           EXEC SQL                                                     08919108
             UPDATE TCKRSTINF                                           08919208
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          08919308
              WHERE JOB_NAME   = :PC-JOB-NAME                           08919408
                AND PLAN_NAME  = :PC-PROGRAM-NAME                       08919508
           END-EXEC.                                                    08919608
                                                                        08919708
           IF SQLCODE = 0                                               08919908
               CONTINUE                                                 08920008
           ELSE                                                         08920108
               MOVE PE-MSG-05              TO PV-OPERATION-ATTEMPTED    08920208
               MOVE '* 8000-EOJ-ROUTINE *' TO PV-PARAGRAPH-NAME         08920308
               PERFORM 9999-SQL-ABEND                                   08920408
           END-IF.                                                      08920508
                                                                        08921002
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       08930000
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       08940000
                                                                        08960002
           DISPLAY SPACES.                                              08970000
           DISPLAY 'INPUT RECORDS PROCESSED  ', PA-INPUT-COUNT.         08980000
           DISPLAY 'ROWS UPDATED             ', PA-UPDATE-COUNT.        08990000
           DISPLAY 'ROWS INSERTED            ', PA-INSERT-COUNT.        09000000
           DISPLAY 'COMMITS TAKEN            ', PA-COMMIT-COUNT.        09001009
           DISPLAY SPACES.                                              09010000
                                                                        09020002
           CLOSE CONDENSED-FILE.                                        09030000
                                                                        09050002
      ******************************************************************09060008
      *  STANDARD DB2 ERROR ABEND ROUTINE                               09160000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             09170000
      ******************************************************************09180000
       9999-SQL-ABEND.                                                  09190000
                                                                        09200000
           MOVE +4013                 TO ABEND-CODE.                    09210000
           DISPLAY '**  ABEND     **'.                                  09220000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              09230000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            09240000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       09250000
                                                                        09260000
      ***  THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         09270002
      ***  DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        09280002
                                                                        09290000
           COPY DPPD004.                                                09300000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           09310000
