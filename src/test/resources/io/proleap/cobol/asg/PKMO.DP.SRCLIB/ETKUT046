00001  IDENTIFICATION DIVISION.                                         07/13/95
00002  PROGRAM-ID.    ETKUT046.                                         ETKUT046
00003  AUTHOR.        STEVE FLUNKER.                                       LV001
00004  INSTALLATION.  KOHLS DEPARTMENT STORES.                          ETKUT046
00005  DATE-WRITTEN.  APRIL-95.                                         ETKUT046
00006  DATE-COMPILED.                                                   ETKUT046
00007 ******************************************************************ETKUT046
00008 *     ETKUT046 - EXTRACT EDI INVOICES TO BE REVALIDATED           ETKUT046
00009 *                                                                 ETKUT046
00010 *     THIS PROGRAM WILL EXTRACT ALL INVOICES THAT ARE FLAGGED     ETKUT046
00011 *     NOT TO BE RELEASED AND WRITE THE DATA FOR THESE DOCUMENTS   ETKUT046
00012 *     TO A FLAT FILE. THIS FILE WILL THEN BE FED IN TO THE        ETKUT046
00013 *     REVALIDATION PROGRAM TO EDIT ANY CHANGES MADE ONLINE.       ETKUT046
00014 ******************************************************************ETKUT046
00015 *                                                                 ETKUT046
00016  ENVIRONMENT DIVISION.                                            ETKUT046
00017 *                                                                 ETKUT046
00018  CONFIGURATION SECTION.                                           ETKUT046
00019 *                                                                 ETKUT046
00020  SOURCE-COMPUTER.        IBM-3090.                                ETKUT046
00021  OBJECT-COMPUTER.        IBM-3090.                                ETKUT046
00022 *                                                                 ETKUT046
00023 *                                                                 ETKUT046
00024  INPUT-OUTPUT SECTION.                                            ETKUT046
00025  FILE-CONTROL.                                                    ETKUT046
00026 *                                                                 ETKUT046
00027      SELECT INV-OUTPUT-FILE                                       ETKUT046
00028          ASSIGN TO OUT01.                                         ETKUT046
00029 *                                                                 ETKUT046
00030 ******************************************************************ETKUT046
00031  DATA DIVISION.                                                   ETKUT046
00032 *                                                                 ETKUT046
00033 *                                                                 ETKUT046
00034  FILE SECTION.                                                    ETKUT046
00035                                                                   ETKUT046
00036 *                                                                 ETKUT046
00037  FD  INV-OUTPUT-FILE                                              ETKUT046
00038      RECORDING MODE IS F                                          ETKUT046
00039      BLOCK CONTAINS 0 RECORDS.                                    ETKUT046
00040  01  INV-OUTPUT-RECORD               PIC  X(384).                 ETKUT046
00041 *                                                                 ETKUT046
00042                                                                   ETKUT046
00043 ******************************************************************ETKUT046
00044  WORKING-STORAGE SECTION.                                         ETKUT046
00045 ******************************************************************ETKUT046
00046 *                                                                 ETKUT046
00047 *                                                                 ETKUT046
00048  01  ABEND-CODE                      PIC S9(04) VALUE ZERO        ETKUT046
00049                                                 COMP SYNC.        ETKUT046
00050 *                                                                 ETKUT046
00051  01  PV-PROGRAM-VARIABLES.                                        ETKUT046
00052      05  PV-OUT-DATA-AREA.                                        ETKUT046
00053          10  FILLER                  PIC S9(04) COMP.             ETKUT046
00054          10  PV-OUT-DATA             PIC X(350).                  ETKUT046
00055 *                                                                 ETKUT046
00056  01  WS-OUTPUT-RECORD.                                            ETKUT046
00057      05  WS-OUT-TP-DKEY              PIC S9(09) COMP.             ETKUT046
00058      05  WS-OUT-DKEY-TMST            PIC X(26).                   ETKUT046
00059      05  WS-OUT-SEQ-NBR              PIC S9(09) COMP.             ETKUT046
00060      05  WS-OUT-DATA                 PIC X(350).                  ETKUT046
00061                                                                   ETKUT046
00062 *  DOCUMENTS ON HOLD TABLE                                        ETKUT046
00063      EXEC SQL                                                     ETKUT046
00064          INCLUDE TFGTRHL                                          ETKUT046
00065      END-EXEC.                                                    ETKUT046
00066 *                                                                 ETKUT046
00067 *  DOCUMENT ON HOLD DETAIL DATA TABLE                             ETKUT046
00068      EXEC SQL                                                     ETKUT046
00069          INCLUDE TFGTRDT                                          ETKUT046
00070      END-EXEC.                                                    ETKUT046
00071 *                                                                 ETKUT046
00072 *  DB2 COMMUNICATION AREA                                         ETKUT046
00073 *                                                                 ETKUT046
00074      EXEC SQL                                                     ETKUT046
00075         INCLUDE SQLCA                                             ETKUT046
00076      END-EXEC.                                                    ETKUT046
00077 *                                                                 ETKUT046
00078 *  DB2 COMMUNICATION AREA 2                                       ETKUT046
00079 *                                                                 ETKUT046
00080      COPY SQLCA2.                                                 ETKUT046
00081 *                                                                 ETKUT046
00082 *  DB2 DOCUMENT CURSOR                                            ETKUT046
00083 *  SELECTS DOCUMENTS AND THE DATA THAT IS TO BE RELEASED          ETKUT046
00084 *                                                                 ETKUT046
00085      EXEC SQL                                                     ETKUT046
00086         DECLARE DOC_CURSOR CURSOR FOR                             ETKUT046
00087           SELECT A.TP_DKEY,                                       ETKUT046
00088                  A.TP_DKEY_TMST,                                  ETKUT046
00089                  A.TRN_SET_CDE,                                   ETKUT046
00090                  B.TRN_SET_SEQ_NBR,                               ETKUT046
00091                  B.TRN_SET_DATA                                   ETKUT046
00092             FROM TFGTRHL A, TFGTRDT B                             ETKUT046
00093             WHERE A.RLSE_IND     = 'N'            AND             ETKUT046
00094                   B.TP_DKEY      = A.TP_DKEY      AND             ETKUT046
00095                   B.TP_DKEY_TMST = A.TP_DKEY_TMST AND             ETKUT046
00096                   A.TRN_SET_CDE  = '810'                          ETKUT046
00097             ORDER BY A.TP_DKEY,                                   ETKUT046
00098                      A.TP_DKEY_TMST,                              ETKUT046
00099                      B.TRN_SET_SEQ_NBR                            ETKUT046
00100      END-EXEC.                                                    ETKUT046
00101                                                                   ETKUT046
00102 *                                                                 ETKUT046
00103 *                                                                 ETKUT046
00104  PROCEDURE DIVISION.                                              ETKUT046
00105                                                                   ETKUT046
00106 *                                                                 ETKUT046
00107  A100-MAIN-MODULE.                                                ETKUT046
00108 ******************************************************************ETKUT046
00109 *    OPEN CURSOR TO FIND ALL DOCUMENTS TO BE REVALIDATED.         ETKUT046
00110 *    DO SOME HOUSEKEEPING.                                        ETKUT046
00111 *    PROCESS ALL EXTRACTED DOCUMENTS AND WRITE THEM TO            ETKUT046
00112 *    OUTPUT FILE.                                                 ETKUT046
00113 *    CLOSE FILE AND CURSOR.                                       ETKUT046
00114 ******************************************************************ETKUT046
00115                                                                   ETKUT046
00116      PERFORM Z000-OPEN-CURSOR.                                    ETKUT046
00117                                                                   ETKUT046
00118      PERFORM B100-INITIALIZE-DUTIES.                              ETKUT046
00119                                                                   ETKUT046
00120      PERFORM B200-PROCESS-DOCUMENT                                ETKUT046
00121          UNTIL SQLCODE = +100                                     ETKUT046
00122                                                                   ETKUT046
00123      PERFORM Z200-CLOSE-FILE.                                     ETKUT046
00124      PERFORM Z300-CLOSE-CURSOR.                                   ETKUT046
00125                                                                   ETKUT046
00126      STOP RUN.                                                    ETKUT046
00127                                                                   ETKUT046
00128  B100-INITIALIZE-DUTIES.                                          ETKUT046
00129                                                                   ETKUT046
00130 ******************************************************************ETKUT046
00131 *    OPEN OUTPUT FILE                                             ETKUT046
00132 *    FETCH FIRST DOCUMENT TO BE REVALIDATED                       ETKUT046
00133 *                                                                 ETKUT046
00134 *    PERFORMED BY A100-MAIN-MODULE.                               ETKUT046
00135 ******************************************************************ETKUT046
00136                                                                   ETKUT046
00137      OPEN OUTPUT INV-OUTPUT-FILE.                                 ETKUT046
00138                                                                   ETKUT046
00139      PERFORM Z100-FETCH-DOC-CSR.                                  ETKUT046
00140                                                                   ETKUT046
00141  B200-PROCESS-DOCUMENT.                                           ETKUT046
00142                                                                   ETKUT046
00143 ******************************************************************ETKUT046
00144 *    DETERMINE WHICH EDI DOCUMENT IT IS AND WRITE THE DATA FOR    ETKUT046
00145 *    THAT DOCUMENT TO THE APPROPRIATE OUTPUT FILE.                ETKUT046
00146 *                                                                 ETKUT046
00147 *    PERFORMED BY A100-MAIN-MODULE.                               ETKUT046
00148 ******************************************************************ETKUT046
00149                                                                   ETKUT046
00150      MOVE FGTRHL-TP-DKEY            TO  WS-OUT-TP-DKEY.           ETKUT046
00151      MOVE FGTRHL-TP-DKEY-TMST       TO  WS-OUT-DKEY-TMST.         ETKUT046
00152      MOVE FGTRDT-TRN-SET-SEQ-NBR    TO  WS-OUT-SEQ-NBR.           ETKUT046
00153      MOVE FGTRDT-TRN-SET-DATA       TO  PV-OUT-DATA-AREA.         ETKUT046
00154      MOVE PV-OUT-DATA               TO  WS-OUT-DATA.              ETKUT046
00155                                                                   ETKUT046
00156      PERFORM C100-WRITE-INVOICE.                                  ETKUT046
00157                                                                   ETKUT046
00158      PERFORM Z100-FETCH-DOC-CSR.                                  ETKUT046
00159                                                                   ETKUT046
00160  C100-WRITE-INVOICE.                                              ETKUT046
00161                                                                   ETKUT046
00162 ******************************************************************ETKUT046
00163 *    WRITE A RECORD TO THE INVOICE OUTPUT FILE                    ETKUT046
00164 *                                                                 ETKUT046
00165 *    PERFORMED BY B200-PROCESS-DOCUMENT.                          ETKUT046
00166 ******************************************************************ETKUT046
00167                                                                   ETKUT046
00168      WRITE INV-OUTPUT-RECORD                                      ETKUT046
00169          FROM WS-OUTPUT-RECORD.                                   ETKUT046
00170                                                                   ETKUT046
00171  Z000-OPEN-CURSOR.                                                ETKUT046
00172                                                                   ETKUT046
00173 ******************************************************************ETKUT046
00174 *    OPEN THE DOCUMENT CURSOR                                     ETKUT046
00175 *                                                                 ETKUT046
00176 *    PERFORMED BY A100-MAIN-MODULE.                               ETKUT046
00177 ******************************************************************ETKUT046
00178                                                                   ETKUT046
00179      EXEC SQL                                                     ETKUT046
00180          OPEN DOC_CURSOR                                          ETKUT046
00181      END-EXEC.                                                    ETKUT046
00182                                                                   ETKUT046
00183      IF SQLCODE < 0                                               ETKUT046
00184          DISPLAY '** ABEND **'                                    ETKUT046
00185          DISPLAY '** OPEN CSR ERROR FOR TFGTRHL/TFGTRDT TABLE **' ETKUT046
00186          PERFORM Z900-SQL-ERROR                                   ETKUT046
00187      ELSE                                                         ETKUT046
00188          IF SQLCODE > +100                                        ETKUT046
00189              DISPLAY '** ABEND **'                                ETKUT046
00190              DISPLAY '** OPEN CSR WARNING TFGTRHL/TFGTRDT TABLE'  ETKUT046
00191              PERFORM Z900-SQL-WARNING                             ETKUT046
00192          END-IF                                                   ETKUT046
00193      END-IF.                                                      ETKUT046
00194                                                                   ETKUT046
00195  Z100-FETCH-DOC-CSR.                                              ETKUT046
00196                                                                   ETKUT046
00197 ******************************************************************ETKUT046
00198 *    FETCH THE DOCUMENTS USING THE DOC CURSOR                     ETKUT046
00199 *                                                                 ETKUT046
00200 *    PERFORMED BY B100-INITIALIZE-DUTIES.                         ETKUT046
00201 *    PERFORMED BY B200-PROCESS-DOCUMENT.                          ETKUT046
00202 ******************************************************************ETKUT046
00203                                                                   ETKUT046
00204      EXEC SQL                                                     ETKUT046
00205          FETCH DOC_CURSOR                                         ETKUT046
00206          INTO  :FGTRHL-TP-DKEY,                                   ETKUT046
00207                :FGTRHL-TP-DKEY-TMST,                              ETKUT046
00208                :FGTRHL-TRN-SET-CDE,                               ETKUT046
00209                :FGTRDT-TRN-SET-SEQ-NBR,                           ETKUT046
00210                :FGTRDT-TRN-SET-DATA                               ETKUT046
00211      END-EXEC.                                                    ETKUT046
00212                                                                   ETKUT046
00213      IF SQLCODE < 0                                               ETKUT046
00214          DISPLAY '** ABEND **'                                    ETKUT046
00215          DISPLAY '** FETCH ERROR ON TFGTRHL/TFGTRDT TABLE'        ETKUT046
00216          PERFORM Z900-SQL-ERROR                                   ETKUT046
00217      ELSE                                                         ETKUT046
00218          IF SQLCODE > +100                                        ETKUT046
00219              DISPLAY '** ABEND **'                                ETKUT046
00220              DISPLAY '** FETCH WARNING ON TFGTRHL/TFGTRDT TABLE'  ETKUT046
00221              PERFORM Z900-SQL-WARNING                             ETKUT046
00222          END-IF                                                   ETKUT046
00223      END-IF.                                                      ETKUT046
00224                                                                   ETKUT046
00225  Z200-CLOSE-FILE.                                                 ETKUT046
00226                                                                   ETKUT046
00227 ******************************************************************ETKUT046
00228 *    CLOSE THE OUTPUT FILE                                        ETKUT046
00229 *                                                                 ETKUT046
00230 *    PERFORMED BY A100-MAIN-MODULE.                               ETKUT046
00231 ******************************************************************ETKUT046
00232                                                                   ETKUT046
00233      CLOSE INV-OUTPUT-FILE.                                       ETKUT046
00234                                                                   ETKUT046
00235  Z300-CLOSE-CURSOR.                                               ETKUT046
00236                                                                   ETKUT046
00237 ******************************************************************ETKUT046
00238 *    OPEN THE DOCUMENT CURSOR                                     ETKUT046
00239 *                                                                 ETKUT046
00240 *    PERFORMED BY A100-MAIN-MODULE.                               ETKUT046
00241 ******************************************************************ETKUT046
00242                                                                   ETKUT046
00243      EXEC SQL                                                     ETKUT046
00244          CLOSE DOC_CURSOR                                         ETKUT046
00245      END-EXEC.                                                    ETKUT046
00246                                                                   ETKUT046
00247      IF SQLCODE < 0                                               ETKUT046
00248          DISPLAY '** ABEND **'                                    ETKUT046
00249          DISPLAY '** CLOSE CSR ERROR FOR TFGTRHL/TFGTRDT TABLES'  ETKUT046
00250          PERFORM Z900-SQL-ERROR                                   ETKUT046
00251      ELSE                                                         ETKUT046
00252          IF SQLCODE > +100                                        ETKUT046
00253              DISPLAY '** ABEND **'                                ETKUT046
00254              DISPLAY '** CLOSE CSR WARNING TFGTRHL/TFGTRDT TABLES'ETKUT046
00255              PERFORM Z900-SQL-WARNING                             ETKUT046
00256          END-IF                                                   ETKUT046
00257      END-IF.                                                      ETKUT046
00258                                                                   ETKUT046
00259  Z900-SQL-WARNING.                                                ETKUT046
00260                                                                   ETKUT046
00261 ******************************************************************ETKUT046
00262 *     SQL WARNING PROCESSING.                                    *ETKUT046
00263 ******************************************************************ETKUT046
00264                                                                   ETKUT046
00265      MOVE SQLERRD(3) TO SQLERRD3.                                 ETKUT046
00266      MOVE SQLCODE    TO SQLCODE2.                                 ETKUT046
00267      DISPLAY '** ABEND **       ** = SQLWARNING'.                 ETKUT046
00268      DISPLAY '** PROGRAM        ** = ETKUT046'.                   ETKUT046
00269      DISPLAY '** SQLCODE        ** = ' SQLCODE2.                  ETKUT046
00270      DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                  ETKUT046
00271      DISPLAY '** SQLWARN0       ** = ' SQLWARN0.                  ETKUT046
00272      DISPLAY '** SQLWARN1       ** = ' SQLWARN1.                  ETKUT046
00273      DISPLAY '** SQLWARN2       ** = ' SQLWARN2.                  ETKUT046
00274      DISPLAY '** SQLWARN3       ** = ' SQLWARN3.                  ETKUT046
00275      DISPLAY '** SQLWARN4       ** = ' SQLWARN4.                  ETKUT046
00276      DISPLAY '** SQLWARN5       ** = ' SQLWARN5.                  ETKUT046
00277      DISPLAY '** SQLWARN6       ** = ' SQLWARN6.                  ETKUT046
00278      DISPLAY '** SQLWARN7       ** = ' SQLWARN7.                  ETKUT046
00279                                                                   ETKUT046
00280      MOVE +4013 TO ABEND-CODE.                                    ETKUT046
00281      CALL 'ILBOABN0' USING ABEND-CODE.                            ETKUT046
00282                                                                   ETKUT046
00283                                                                   ETKUT046
00284  Z900-SQL-ERROR.                                                  ETKUT046
00285                                                                   ETKUT046
00286 ******************************************************************ETKUT046
00287 *     SQL ERROR PROCESSING.                                       ETKUT046
00288 ******************************************************************ETKUT046
00289                                                                   ETKUT046
00290      MOVE SQLCODE    TO SQLCODE2.                                 ETKUT046
00291      MOVE SQLERRD(3) TO SQLERRD3.                                 ETKUT046
00292      DISPLAY '** ABEND **       ** = SQLERROR'.                   ETKUT046
00293      DISPLAY '** PROGRAM        ** = ETKUT046'.                   ETKUT046
00294      DISPLAY '** SQLCODE        ** = ' SQLCODE2.                  ETKUT046
00295      DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                  ETKUT046
00296      DISPLAY '** SQLERRM        ** = ' SQLERRM.                   ETKUT046
00297      DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                  ETKUT046
00298                                                                   ETKUT046
00299      MOVE +4013 TO ABEND-CODE.                                    ETKUT046
00300      CALL 'ILBOABN0' USING ABEND-CODE.                            ETKUT046
00301                                                                   ETKUT046
