000100 IDENTIFICATION DIVISION.                                                 
000200                                                                          
000300 TITLE 'TRAILER INVALID STATUS FOR STORES TO RECEIVE'.                    
000310                                                                          
000400 PROGRAM-ID.             SUKUT202.                                        
000500 AUTHOR.                 DENISE HAHN.                                     
000600 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000700 DATE-WRITTEN            JANUARY 2020.                                    
000800 DATE-COMPILED.                                                           
000900                                                                          
001000*----------------------------------------------------------------*        
001200*                                                                *        
001300* THIS PROGRAM WILL BE CALLED WHEN A STORE IS TRYING TO OPEN A   *        
001400* TRAILER AND THE TRAILER IS NOT IN A DS (DC SHIPPED) STATUS.    *        
001500*                                                                *        
001600* THE JOB WILL SEND AN EMAIL TO COT (CENTRAL OUTBOUND TRANSPORT),*        
001700* DC LP GROUPS AND LOGISTICS DC APPS SUPPORT TEAM.               *        
001800*                                                                *        
001900* THIS IS TO GET QUICKER NOTIFICATION WHEN A TRAILER IS AT A     *        
002000* STORE BUT WAS NOT SYSTEMICALLY SHIPPED BY THE DC YET.  THERE   *        
002100* IS AN ISSUE WITH THE AUTOMATED CONVEYOR APPLICATION THAT DOES  *        
002200* NOT SET THE TRIP TO SHIPPED.                                   *        
002300*                                                                *        
002400* THE JOB IS STARTED BY WEBSERVICE SUKCS801                      *        
002500* THE DATA IN THE CONTROL CARD (SUCC096) IS IN FORMAT SURD167    *        
002600*                                                                *        
003400*----------------------------------------------------------------*        
005400 ENVIRONMENT DIVISION.                                                    
005500 INPUT-OUTPUT SECTION.                                                    
005600                                                                          
005700 FILE-CONTROL.                                                            
005800                                                                          
005900     SELECT TRAILER-INPUT-FILE                                            
006000         ASSIGN TO INP01.                                                 
006100                                                                          
006200     SELECT ERROR-OUT-FILE                                                
006300         ASSIGN TO OUT01.                                                 
006400                                                                          
006500*****************************************************************         
006600 DATA DIVISION.                                                           
006700*****************************************************************         
006800 FILE SECTION.                                                            
006900                                                                          
007000 FD  TRAILER-INPUT-FILE                                                   
007100     RECORDING MODE IS F                                                  
007200     BLOCK CONTAINS 0 RECORDS                                             
007300     LABEL RECORDS ARE STANDARD.                                          
007400                                                                          
007500 01  INPUT-REC                    PIC X(80).                              
007600                                                                          
007700 FD  ERROR-OUT-FILE                                                       
007800     RECORDING MODE IS F                                                  
007900     BLOCK CONTAINS 0 RECORDS                                             
008000     LABEL RECORDS ARE STANDARD.                                          
008100                                                                          
008200 01  ERROR-OUT-RECORD             PIC X(100).                             
008300                                                                          
008400                                                                          
008500*****************************************************************         
008600 WORKING-STORAGE SECTION.                                                 
008700                                                                          
008701* INPUT RECORD LAYOUT                                                     
008710     COPY SURD167.                                                        
009400                                                                          
009410* OUTPUT RECORD LAYOUT                                                    
009420     COPY SURD168.                                                        
009430                                                                          
009500 01  PS-PROGRAM-SWITCHES.                                                 
009600     05  PS-EOF-ERROR-CSR-SW          PIC X(01)  VALUE 'N'.               
009700         88  PS-EOF-ERROR-CSR-YES                VALUE 'Y'.               
009800         88  PS-EOF-ERROR-CSR-NO                 VALUE 'N'.               
009900                                                                          
010000 01  PC-PROGRAM-CONSTANTS.                                                
010100     05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'SUKUT202'.        
010200                                                                          
010300 01  PV-PROGRAM-VARIABLES.                                                
010310     05  PV-SQL-RETURN-CD             PIC -9(04).                         
011300                                                                          
011400 01  PV-ABEND-FIELDS.                                                     
011500     05  PV-ABEND-PARAGRAPH           PIC  X(50) VALUE SPACES.            
011600     05  PV-ABEND-TABLE               PIC  X(12) VALUE SPACES.            
011700     05  PV-DB2-MESSAGE               PIC  X(50) VALUE SPACES.            
011800     05  PV-ABEND-CODE                PIC S9(04) COMP SYNC                
011900                                                 VALUE +0.                
011910     05  PV-STATUS-DATE               PIC  X(10) VALUE SPACES.            
011920     05  PV-STATUS-TIME               PIC  X(08) VALUE SPACES.            
011930     05  PV-DO-OPER                   PIC S9(4) COMP.                     
011940     05  PV-RETURN-CODE               PIC S9(9) COMP.                     
012000                                                                          
012100 01  HEADING-OUT.                                                         
012200     05  HEAD-REC                     PIC X(100)  VALUE SPACES.           
012300*01  RECORD-OUT.                                                          
012400*    05  FILLER                       PIC X(1)    VALUE SPACES.           
012500*    05  REC-OPER                     PIC ZZZ9    VALUE ZEROES.           
012600*    05  FILLER                       PIC X(1)    VALUE SPACES.           
012700*    05  FILLER                       PIC X(08)   VALUE                   
012800*        'ERR_CDE '.                                                      
012900*    05  REC-ERR-CDE                  PIC 999.                            
013000*    05  FILLER                       PIC X(01)   VALUE SPACES.           
013100*    05  REC-CNT                      PIC ZZZ,ZZ9.                        
013200*    05  FILLER                       PIC X(01)   VALUE SPACES.           
013300*    05  REC-MESSAGE                  PIC X(50).                          
013400*    05  FILLER                       PIC X(04)   VALUE SPACES.           
013500                                                                          
013600* * * COMMUNICATIONS AREA FOR DB2                                         
013700     EXEC SQL                                                             
013800          INCLUDE SQLCA                                                   
013900     END-EXEC.                                                            
013910                                                                          
014410* * * TSUOBTR - OUTBOUND TRIP TABLE                                       
014420     EXEC SQL                                                             
014430         INCLUDE TSUOBTR                                                  
014440     END-EXEC.                                                            
014450                                                                          
014460* * * TSUOBTS - OUTBOUND TRIP STATUS TABLE                                
014470     EXEC SQL                                                             
014480         INCLUDE TSUOBTS                                                  
014490     END-EXEC.                                                            
014491                                                                          
014492* * * TSUOBSC - OUTBOUND TRIP STATUS CODE TABLE                           
014493     EXEC SQL                                                             
014494         INCLUDE TSUOBSC                                                  
014495     END-EXEC.                                                            
014496                                                                          
014497* * * TKRPRPM - PARM TABLE                                                
014498     EXEC SQL                                                             
014499         INCLUDE TKRPRPM                                                  
014500     END-EXEC.                                                            
014501                                                                          
014510**DECLARE THE TRIP-STATUS-CSR                                             
014600     EXEC SQL                                                             
014700          DECLARE TRIP-STATUS-CSR CURSOR FOR                              
015000           SELECT A.ORGN_OPER_UNT_ID                                      
015100                 ,A.DEST_OPER_UNT_ID                                      
015200                 ,A.TRLR_SRL_NBR                                          
015300                 ,B.STAT_OPER_UNT_ID                                      
015310                 ,B.TRIP_STAT_CDE                                         
015320                 ,B.OTBND_STAT_USR_ID                                     
015400                 ,DATE(B.OTBND_STAT_TMST)                                 
015410                 ,TIME(B.OTBND_STAT_TMST)                                 
015500                 ,C.STAT_SHRT_DESC                                        
016710            FROM  TSUOBTR A                                               
016800                , TSUOBTS B                                               
016900                , TSUOBSC C                                               
017200           WHERE  A.OTBND_TRIP_ID = :SUOBTR-OTBND-TRIP-ID                 
017300             AND  A.OTBND_TRIP_ID = B.OTBND_TRIP_ID                       
017400             AND  B.TRIP_STAT_CDE = C.TRIP_STAT_CDE                       
017500           ORDER BY B.OTBND_STAT_TMST                                     
018200            WITH UR                                                       
018300     END-EXEC.                                                            
018400                                                                          
018500     COPY DPWS004.                                                        
018600                                                                          
018700                                                                          
018800*****************************************************************         
018900 PROCEDURE DIVISION.                                                      
019000*****************************************************************         
019100                                                                          
019200     OPEN INPUT  TRAILER-INPUT-FILE                                       
019300          OUTPUT ERROR-OUT-FILE.                                          
019400                                                                          
019500     PERFORM 1000-READ-INPUT.                                             
019600                                                                          
019610     SET  PS-EOF-ERROR-CSR-NO  TO  TRUE.                                  
019620                                                                          
019700     PERFORM 2000-OPEN-ERROR-CSR.                                         
019800     IF  PS-EOF-ERROR-CSR-NO                                              
019900         PERFORM 2010-FETCH-ERROR-CSR                                     
019910         UNTIL PS-EOF-ERROR-CSR-YES                                       
020000     END-IF                                                               
020100     PERFORM 2020-CLOSE-ERROR-CSR.                                        
020200                                                                          
020300     MOVE SPACES  TO  HEAD-REC.                                           
020400     DISPLAY HEADING-OUT.                                                 
020500     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
020600                                                                          
020700     CLOSE TRAILER-INPUT-FILE                                             
020800           ERROR-OUT-FILE.                                                
020900                                                                          
021000*    IF ANY ERRORS WERE FOUND, THEN SET THE RETURN-CODE, SO               
021100*    THAT THE EMAIL WILL GET SENT OUT IN THE NEXT STEP                    
021200                                                                          
021210     IF  PV-DO-OPER  > 0                                                  
021220         PERFORM 3000-GET-RETURN-CODE                                     
021221         MOVE  PV-RETURN-CODE  TO  RETURN-CODE                            
021222     ELSE                                                                 
021223         MOVE 4095 TO  RETURN-CODE                                        
021230     END-IF.                                                              
021240                                                                          
021600                                                                          
021700     GOBACK.                                                              
021800                                                                          
021900*****************************************************************         
022000*                                                                         
022100*****************************************************************         
022200 1000-READ-INPUT.                                                         
022300                                                                          
022400     READ TRAILER-INPUT-FILE                                              
022500     INTO SU167-EXTRACT-RECORD                                            
022600            AT END                                                        
022700               MOVE 'MISSING INPUT FILE     '                             
022800                                TO PV-DB2-MESSAGE                         
022900               MOVE '1000-READ-INPUT   '                                  
023000                                TO PV-ABEND-PARAGRAPH                     
023100               MOVE 'INP01'     TO PV-ABEND-TABLE                         
023200               PERFORM 9100-SQL-ABEND                                     
023300     END-READ.                                                            
023400                                                                          
023500                                                                          
023600     MOVE '*****************************************'                     
023700                                        TO HEAD-REC.                      
023800     DISPLAY HEAD-REC.                                                    
023900     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
024000                                                                          
024100                                                                          
024110     MOVE   SPACES                      TO HEAD-REC.                      
024200     STRING  'FROM:           SUK950/SUKUT202'                            
024400                                           DELIMITED BY SIZE              
024500                                      INTO HEAD-REC.                      
024600     DISPLAY HEAD-REC.                                                    
024700     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
024800                                                                          
024900                                                                          
025000     MOVE   SPACES                      TO HEAD-REC.                      
025100     DISPLAY HEAD-REC.                                                    
025200     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
025300                                                                          
025400                                                                          
025500     STRING  'TRLR SEAL ID:  '  SU167-TRLR-SEAL-ID                        
025700                                           DELIMITED BY SIZE              
025800                                      INTO HEAD-REC.                      
025900     DISPLAY HEAD-REC.                                                    
026000     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
026100                                                                          
026101     MOVE SPACES TO HEAD-REC.                                             
026110     STRING  'OUTBOUND TRIP: '  SU167-OTBND-TRIP-ID                       
026120                                           DELIMITED BY SIZE              
026130                                      INTO HEAD-REC.                      
026140     DISPLAY HEAD-REC.                                                    
026150     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
026160                                                                          
026161     MOVE SPACES TO HEAD-REC.                                             
026170     STRING  'DC NUMBER:     '  SU167-ORGN-OPER-TRIP-ID                   
026180                                           DELIMITED BY SIZE              
026190                                      INTO HEAD-REC.                      
026191     DISPLAY HEAD-REC.                                                    
026192     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
026193                                                                          
026194     MOVE SPACES TO HEAD-REC.                                             
026195     STRING  'STORE NUMBER:  '  SU167-DEST-OPER-TRIP-ID                   
026196                                           DELIMITED BY SIZE              
026197                                      INTO HEAD-REC.                      
026198     DISPLAY HEAD-REC.                                                    
026199     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
026200                                                                          
026201     MOVE SPACES TO HEAD-REC.                                             
026202     STRING  'TRAILER SERIAL:'  SU167-TRLR-SRL-NBR                        
026203                                           DELIMITED BY SIZE              
026204                                      INTO HEAD-REC.                      
026205     DISPLAY HEAD-REC.                                                    
026206     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
026207                                                                          
026208     MOVE SPACES TO HEAD-REC.                                             
026209     DISPLAY HEAD-REC.                                                    
026210     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
026211                                                                          
026212     MOVE SPACES TO HEAD-REC.                                             
026213     STRING  'TRIP STATUS:   '  SU167-TRIP-STAT-CDE                       
026214                                           DELIMITED BY SIZE              
026215                                      INTO HEAD-REC.                      
026216     DISPLAY HEAD-REC.                                                    
026217     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
026218                                                                          
026219     MOVE SPACES TO HEAD-REC.                                             
026220     DISPLAY HEAD-REC.                                                    
026221     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
026223                                                                          
026224     MOVE SPACES TO HEAD-REC.                                             
026225     STRING  'SCAC:          '  SU167-SCAC-CDE                            
026226                                           DELIMITED BY SIZE              
026227                                      INTO HEAD-REC.                      
026228     DISPLAY HEAD-REC.                                                    
026229     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
026230                                                                          
026231     MOVE SPACES TO HEAD-REC.                                             
026232     STRING  'TRLR:          '  SU167-TRLR-ID                             
026233                                           DELIMITED BY SIZE              
026234                                      INTO HEAD-REC.                      
026235     DISPLAY HEAD-REC.                                                    
026236     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
026237                                                                          
026238     MOVE SPACES                        TO HEAD-REC.                      
026239     DISPLAY HEAD-REC.                                                    
026240     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
026241                                                                          
026250                                                                          
026300     MOVE '*****************************************'                     
026400                                        TO HEAD-REC.                      
026500     DISPLAY HEAD-REC.                                                    
026600     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
026700                                                                          
026800                                                                          
026900     MOVE SPACES                        TO HEAD-REC.                      
027000     DISPLAY HEAD-REC.                                                    
027100     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
027200                                                                          
027300*                                                                         
027310     MOVE SPACES TO HEAD-REC.                                             
027400     MOVE 'OUTBOUND TRIP STATUS ERROR               '                     
027500                                        TO HEAD-REC.                      
027600     DISPLAY HEAD-REC.                                                    
027700     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
027800                                                                          
028000     MOVE SPACES                        TO HEAD-REC.                      
028100     DISPLAY HEAD-REC.                                                    
028200     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
028201                                                                          
028210     MOVE SPACES TO HEAD-REC.                                             
028220     MOVE 'STORE ENTERED THE SEAL NUMBER LISTED ABOVE.'                   
028230                                        TO HEAD-REC.                      
028240     DISPLAY HEAD-REC.                                                    
028250     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
028251                                                                          
028260     MOVE SPACES TO HEAD-REC.                                             
028270     MOVE                                                                 
028271      'THEY RECEIVED AN ERROR INDICATING INVALID TRAILER STATUS.'         
028280                                        TO HEAD-REC.                      
028290     DISPLAY HEAD-REC.                                                    
028291     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
028292                                                                          
028293     MOVE SPACES                        TO HEAD-REC.                      
028294     DISPLAY HEAD-REC.                                                    
028295     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
028296                                                                          
028297     MOVE SPACES TO HEAD-REC.                                             
028298     MOVE 'PLEASE CHECK TO SEE IF THE STATUS OF THIS TRAILER'             
028299                                        TO HEAD-REC.                      
028300     DISPLAY HEAD-REC.                                                    
028301     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
028302                                                                          
028303     MOVE SPACES TO HEAD-REC.                                             
028304     MOVE 'NEEDS TO BE UPDATED TO DS (DC SHIPPED) STATUS.  '              
028305                                        TO HEAD-REC.                      
028306     DISPLAY HEAD-REC.                                                    
028307     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
028308                                                                          
028309     MOVE SPACES                        TO HEAD-REC.                      
028310     DISPLAY HEAD-REC.                                                    
028311     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
028312                                                                          
028313     MOVE SPACES TO HEAD-REC.                                             
028314     MOVE 'PLEASE REACH OUT TO THE STORE ONCE THE TRAILER   '             
028315                                        TO HEAD-REC.                      
028316     DISPLAY HEAD-REC.                                                    
028317     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
028318                                                                          
028319     MOVE SPACES TO HEAD-REC.                                             
028320     MOVE 'STATUS HAS BEEN UPDATED.                        '              
028321                                        TO HEAD-REC.                      
028322     DISPLAY HEAD-REC.                                                    
028323     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
028324                                                                          
028325     MOVE SPACES                        TO HEAD-REC.                      
028326     DISPLAY HEAD-REC.                                                    
028330     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
028400                                                                          
028500     MOVE '*****************************************'                     
028600                                        TO HEAD-REC.                      
028700     DISPLAY HEAD-REC.                                                    
028800     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
028900                                                                          
029000                                                                          
029100     MOVE SPACES                        TO HEAD-REC.                      
029200     DISPLAY HEAD-REC.                                                    
029300     WRITE ERROR-OUT-RECORD FROM HEAD-REC.                                
029400                                                                          
029500                                                                          
029600*****************************************************************         
029700*  OPEN THE ERROR REC CURSOR                                              
029800*****************************************************************         
029900 2000-OPEN-ERROR-CSR.                                                     
030000                                                                          
030100     MOVE SU167-OTBND-TRIP-ID       TO SUOBTR-OTBND-TRIP-ID.              
030110                                                                          
030120     SET PS-EOF-ERROR-CSR-NO        TO TRUE.                              
030200                                                                          
030300     EXEC SQL                                                             
030400          OPEN TRIP-STATUS-CSR                                            
030500     END-EXEC.                                                            
030600                                                                          
030700     EVALUATE TRUE                                                        
030800         WHEN SQLCODE = ZERO                                              
030900              CONTINUE                                                    
031000         WHEN SQLCODE = +100                                              
031100              SET PS-EOF-ERROR-CSR-YES   TO TRUE                          
031200         WHEN SQLCODE = -904                                              
031300         WHEN SQLCODE = -913                                              
031400         WHEN SQLCODE = -911                                              
031500             MOVE SQLCODE               TO PV-SQL-RETURN-CD               
031600             STRING 'SQL='                                                
031700                  PV-SQL-RETURN-CD                                        
031800                  DELIMITED BY SIZE                                       
031900                            INTO PV-DB2-MESSAGE                           
032000             MOVE '2000-OPEN-ERROR-CSR'                                   
032100                              TO PV-ABEND-PARAGRAPH                       
032200             MOVE 'TSUOBTS'   TO PV-ABEND-TABLE                           
032300             PERFORM 9100-SQL-ABEND                                       
032400         WHEN SQLWARN0 NOT = SPACES                                       
032500         WHEN SQLCODE NOT = ZERO                                          
032600             MOVE SQLCODE               TO PV-SQL-RETURN-CD               
032700             STRING 'SQL='                                                
032800                  PV-SQL-RETURN-CD                                        
032900                  DELIMITED BY SIZE                                       
033000                            INTO PV-DB2-MESSAGE                           
033100             MOVE '2000-OPEN-ERROR-CSR'                                   
033200                              TO PV-ABEND-PARAGRAPH                       
033300             MOVE 'TSUOBTS'   TO PV-ABEND-TABLE                           
033400             PERFORM 9100-SQL-ABEND                                       
033500     END-EVALUATE.                                                        
033600                                                                          
033700                                                                          
033800*****************************************************************         
033900*  FETCH THE ERROR REC CURSOR RECORDS                                     
034000*****************************************************************         
034100 2010-FETCH-ERROR-CSR.                                                    
034200                                                                          
034300     EXEC SQL                                                             
034400          FETCH TRIP-STATUS-CSR                                           
034500          INTO  :SUOBTR-ORGN-OPER-UNT-ID                                  
034600               ,:SUOBTR-DEST-OPER-UNT-ID                                  
034700               ,:SUOBTR-TRLR-SRL-NBR                                      
034800               ,:SUOBTS-STAT-OPER-UNT-ID                                  
034810               ,:SUOBTS-TRIP-STAT-CDE                                     
034811               ,:SUOBTS-OTBND-STAT-USR-ID                                 
034820               ,:PV-STATUS-DATE                                           
034821               ,:PV-STATUS-TIME                                           
034830               ,:SUOBSC-STAT-SHRT-DESC                                    
034900     END-EXEC.                                                            
035000                                                                          
035100     EVALUATE TRUE                                                        
035200         WHEN SQLCODE = ZERO                                              
035210              IF  SUOBTS-TRIP-STAT-CDE  =  'DO'                           
035220                  MOVE SUOBTS-STAT-OPER-UNT-ID TO PV-DO-OPER              
035230              END-IF                                                      
035240                                                                          
035300              PERFORM 2100-WRITE-RECORD                                   
035400         WHEN SQLCODE = +100                                              
035500              SET PS-EOF-ERROR-CSR-YES   TO TRUE                          
035600         WHEN SQLCODE = -904                                              
035700         WHEN SQLCODE = -913                                              
035800         WHEN SQLCODE = -911                                              
035900              CONTINUE                                                    
036000         WHEN SQLWARN0 NOT = SPACES                                       
036100         WHEN SQLCODE NOT = ZERO                                          
036200             MOVE SQLCODE               TO PV-SQL-RETURN-CD               
036300             STRING 'SQL='                                                
036400                  PV-SQL-RETURN-CD                                        
036500                  DELIMITED BY SIZE                                       
036600                            INTO PV-DB2-MESSAGE                           
036700             MOVE '2010-FETCH-ERROR-CSR'                                  
036800                              TO PV-ABEND-PARAGRAPH                       
036900             MOVE 'TSUOBTS'   TO PV-ABEND-TABLE                           
037000             PERFORM 9100-SQL-ABEND                                       
037100     END-EVALUATE.                                                        
037200                                                                          
037300                                                                          
037400*****************************************************************         
037500*  CLOSE THE ERROR REC CURSOR                                             
037600*****************************************************************         
037700 2020-CLOSE-ERROR-CSR.                                                    
037800                                                                          
037900     EXEC SQL                                                             
038000          CLOSE TRIP-STATUS-CSR                                           
038100     END-EXEC.                                                            
038200                                                                          
038300     EVALUATE TRUE                                                        
038400         WHEN SQLCODE = ZERO                                              
038500         WHEN SQLCODE = -904                                              
038600         WHEN SQLCODE = -913                                              
038700         WHEN SQLCODE = -911                                              
038800             CONTINUE                                                     
038900         WHEN SQLWARN0 NOT = SPACES                                       
039000         WHEN SQLCODE NOT = ZERO                                          
039100             MOVE SQLCODE               TO PV-SQL-RETURN-CD               
039200             STRING 'SQL='                                                
039300                  PV-SQL-RETURN-CD                                        
039400                  DELIMITED BY SIZE                                       
039500                            INTO PV-DB2-MESSAGE                           
039600             MOVE '2020-CLOSE-ERROR-CSR'                                  
039700                              TO PV-ABEND-PARAGRAPH                       
039800             MOVE 'TSUOBTS'   TO PV-ABEND-TABLE                           
039900             PERFORM 9100-SQL-ABEND                                       
040000     END-EVALUATE.                                                        
040100                                                                          
040200                                                                          
040300*****************************************************************         
040400*  WRITE A RMS RECORD TO THE ERROR FILE                                   
040500*****************************************************************         
040600 2100-WRITE-RECORD.                                                       
040601                                                                          
040608     INITIALIZE SU168-EXTRACT-RECORD.                                     
040614     MOVE SUOBTS-STAT-OPER-UNT-ID  TO  SU168-STATUS-OPER.                 
040615     MOVE SUOBTS-TRIP-STAT-CDE     TO  SU168-TRIP-STAT-CDE.               
040618     MOVE PV-STATUS-DATE           TO  SU168-STATUS-DATE.                 
040619     MOVE PV-STATUS-TIME           TO  SU168-STATUS-TIME.                 
040620     MOVE SUOBTS-OTBND-STAT-USR-ID TO  SU168-STATUS-USERID.               
040630                                                                          
048500                                                                          
048600     DISPLAY SU168-EXTRACT-RECORD.                                        
048700     WRITE ERROR-OUT-RECORD FROM SU168-EXTRACT-RECORD.                    
048800                                                                          
049900                                                                          
049901******************************************************************        
049902*  GET THE RETURN CODE - BASED ON THE LAST DC THAT OPENED THE             
049903*  TRAILER                                                                
049904******************************************************************        
049910 3000-GET-RETURN-CODE.                                                    
049911                                                                          
049920     EXEC SQL                                                             
049930          SELECT  FUNC_QTY                                                
049940            INTO :PV-RETURN-CODE                                          
049980            FROM  TKRPRPM                                                 
049990           WHERE  LOC_ID             = :PV-DO-OPER                        
049991             AND  INTFC_SYS_CDE      = 'KHL'                              
049992             AND  SYS_PRC_FUNC_CDE   = 'SUOBRC'                           
049993             AND  FUNC_PRC_STAT_CDE  = 'AC'                               
049994             AND  FUNC_BEG_TMST     <= CURRENT TIMESTAMP                  
049995             AND  FUNC_END_TMST     >= CURRENT TIMESTAMP                  
049996            WITH UR                                                       
049997     END-EXEC.                                                            
049998                                                                          
049999     EVALUATE TRUE                                                        
050000         WHEN SQLCODE = ZERO                                              
050001              CONTINUE                                                    
050004                                                                          
050006         WHEN SQLCODE = +100                                              
050007              MOVE 4060  TO  PV-RETURN-CODE                               
050008                                                                          
050009         WHEN SQLCODE = -904                                              
050010         WHEN SQLCODE = -913                                              
050011         WHEN SQLCODE = -911                                              
050014         WHEN SQLWARN0 NOT = SPACES                                       
050015         WHEN SQLCODE NOT = ZERO                                          
050016             MOVE SQLCODE               TO PV-SQL-RETURN-CD               
050017             STRING 'SQL='                                                
050018                  PV-SQL-RETURN-CD                                        
050019                  DELIMITED BY SIZE                                       
050020                            INTO PV-DB2-MESSAGE                           
050021             MOVE '3000-GET-RETURN-CODE'                                  
050022                              TO PV-ABEND-PARAGRAPH                       
050023             MOVE 'TKRPRPM'   TO PV-ABEND-TABLE                           
050024             PERFORM 9100-SQL-ABEND                                       
050025     END-EVALUATE.                                                        
050026                                                                          
050030                                                                          
050100 9100-SQL-ABEND.                                                          
050200******************************************************************        
050300*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
050400*  ERROR OR WARNING CODE OCCURS.                                          
050500******************************************************************        
050600     DISPLAY '9100-SQL-ABEND'.                                            
050700     DISPLAY '** ABEND     **'.                                           
050800     DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                        
050900     DISPLAY '** PARAGRAPH ** = ' PV-ABEND-PARAGRAPH.                     
051000     DISPLAY '** TABLE     ** = ' PV-ABEND-TABLE.                         
051100     DISPLAY '** MESSAGE   ** = ' PV-DB2-MESSAGE.                         
051200     COPY DPPD004.                                                        
051300                                                                          
051400     MOVE +4013                  TO PV-ABEND-CODE.                        
051500     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
051600                                                                          
