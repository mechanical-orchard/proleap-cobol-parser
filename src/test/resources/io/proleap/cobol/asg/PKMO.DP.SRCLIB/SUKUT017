000010*****************************************************************         
000020 IDENTIFICATION DIVISION.                                                 
000030*****************************************************************         
000040                                                                          
000050 PROGRAM-ID.             SUKUT017.                                        
000060 AUTHOR.                 MICHAEL WALTERS.                                 
000070 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000080 DATE-WRITTEN            OCTOBER, 1997.                                   
000090 DATE-COMPILED.                                                           
000091*****************************************************************         
000092*****************************************************************         
000094*  THIS PROGRAM BUMP THE CARTON AND AUDIT FILES TOGETHER.  IN   *         
000095*  THE PROCESS, RECEIVER OVER/SHORT EXCEPTIONS WILL                       
 00095*  ALSO BE FOUND *                                                        
000097*****************************************************************         
000120*****************************************************************         
000130 ENVIRONMENT DIVISION.                                                    
000140*****************************************************************         
000150                                                                          
000160 CONFIGURATION SECTION.                                                   
000170                                                                          
000180 SOURCE-COMPUTER.        IBM-3090.                                        
000190 OBJECT-COMPUTER.        IBM-3090.                                        
000191                                                                          
000192 INPUT-OUTPUT SECTION.                                                    
000193                                                                          
000194 FILE-CONTROL.                                                            
000198*                                                                         
000199     SELECT AUDIT-EXTRACT-FILE                                            
000200         ASSIGN TO INP01.                                                 
000201                                                                          
000202     SELECT RECEIVER-EXTRACT-FILE                                         
000203         ASSIGN TO INP02.                                                 
000201                                                                          
000202     SELECT RECEIVER-MERGE-FILE                                           
000203         ASSIGN TO OUT01.                                                 
000210*                                                                         
000220*****************************************************************         
000230 DATA DIVISION.                                                           
000240*****************************************************************         
000250*                                                                         
000260 FILE SECTION.                                                            
000270*                                                                         
000296 FD  AUDIT-EXTRACT-FILE                                                   
000297     RECORDING MODE IS F                                                  
000298     BLOCK CONTAINS 0  RECORDS.                                           
000299 01  AUDIT-EXTRACT-RECORD            PIC  X(200).                         
000300                                                                          
000301 FD  RECEIVER-EXTRACT-FILE                                                
000302     RECORDING MODE IS F                                                  
000303     BLOCK CONTAINS 0  RECORDS.                                           
000304 01  RECEIVER-EXTRACT-RECORD         PIC  X(200).                         
000300                                                                          
000301 FD  RECEIVER-MERGE-FILE                                                  
000302     RECORDING MODE IS F                                                  
000303     BLOCK CONTAINS 0  RECORDS.                                           
000304 01  RECEIVER-MERGE-RECORD           PIC  X(200).                         
000305*                                                                         
000310                                                                          
000320 WORKING-STORAGE SECTION.                                                 
000321*                                                                         
000322 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
000323                                                   COMP SYNC.             
000331*                                                                         
000340 01  PROGRAM-SWITCHES.                                                    
000362     05  PS-EOF-AUDIT-EXTRACT-SW     PIC  X(01)    VALUE 'N'.             
000363         88  PS-EOF-AUDIT-EXTRACT                  VALUE 'Y'.             
000364         88  PS-NOT-EOF-AUDIT-EXTRACT             VALUE 'N'.              
000362     05  PS-EOF-RECEIVER-EXTRACT-SW  PIC  X(01)    VALUE 'N'.             
000363         88  PS-EOF-RECEIVER-EXTRACT               VALUE 'Y'.             
000364         88  PS-NOT-EOF-RECEIVER-EXTRACT           VALUE 'N'.             
000362     05  PS-PROCESS-SW               PIC  X(01)    VALUE ' '.             
000363         88  PS-PROCESS-RECEIVER-EXTRACT           VALUE 'V'.             
000364         88  PS-PROCESS-AUDIT-EXTRACT              VALUE 'A'.             
000364         88  PS-PROCESS-BOTH                       VALUE 'B'.             
000409*                                                                         
000410 01  PC-CONSTANTS.                                                        
000411     05  PC-1                        PIC S9(03)    VALUE +1               
000412                                                   COMP-3.                
000413     05  PC-A                        PIC  X(01)    VALUE 'A'.             
000414     05  PC-Y                        PIC  X(01)    VALUE 'Y'.             
000420     05  PC-RCV                      PIC  X(03)    VALUE 'RCV'.           
000431     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
000432         'SUKUT017'.                                                      
000440*                                                                         
000450 01  PV-MISC-FIELDS.                                                      
000492     05  PV-WRITE-COUNT              PIC S9(09)    COMP.                  
000492     05  PV-PBS-IND-COUNT            PIC S9(09)    COMP.                  
000492     05  PV-HOLD-PO-NBR              PIC S9(09)    COMP.                  
000493     05  PV-LINE-SUB                 PIC S9(03)    VALUE ZERO             
000494                                                   COMP-3.                
000495     05  PV-ABEND-CODE               PIC S9(04)    VALUE ZERO             
000496                                                   COMP SYNC.             
000497     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
000498     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
000499     05  PV-ABEND-PARAGRAPH          PIC  X(30)    VALUE SPACES.          
000500     05  PV-AUDIT-NBR-X              PIC  X(09).                          
000501     05  PV-AUDIT-NBR REDEFINES PV-AUDIT-NBR-X                            
000502                                     PIC  9(09).                          
000503     05  PV-SKU-COUNT                    PIC S9(09)  VALUE +0             
000504                                                     COMP-3.              
000554*                                                                         
000555******************************************************************        
000556*  RECORD LAYOUT FOR THE AUDIT EXTRACT INPUT FILE                         
000557******************************************************************        
044800 01  AUDIT-EXTRACT.                                                       
000530     COPY SURD004.                                                        
000511                                                                          
000554*                                                                         
000555******************************************************************        
000556*  RECORD LAYOUT FOR THE RECEIVER EXTRACT INPUT FILE                      
000557******************************************************************        
044800 01  RECEIVER-EXTRACT.                                                    
000530     COPY SURD004.                                                        
000511                                                                          
000554*                                                                         
000555******************************************************************        
000556*  RECORD LAYOUT FOR THE RECEIVER MERGE OUTPUT FILE                       
000557******************************************************************        
044800 01  RECEIVER-MERGE.                                                      
000530     COPY SURD004.                                                        
000511                                                                          
001196 EJECT                                                                    
001499*                                                                         
001500 PROCEDURE DIVISION.                                                      
001510*                                                                         
001520 0000-MAINLINE-PROCESSING.                                                
001530******************************************************************        
001540**  THIS IS THE MAIN DRIVER FOR THE PROGRAM.                              
001550******************************************************************        
001580*                                                                         
001590     PERFORM 1000-INITIALIZATION.                                         
001591*                                                                         
001592     PERFORM 2000-BUMP-FILES                                              
00             UNTIL PS-EOF-AUDIT-EXTRACT                                       
00               AND PS-EOF-RECEIVER-EXTRACT.                                   
001596*                                                                         
001590     PERFORM 9000-EOJ-ROUTINE.                                            
001596*                                                                         
001612     GOBACK.                                                              
001613*                                                                         
001614*                                                                         
001615 1000-INITIALIZATION.                                                     
001620******************************************************************        
001630**  PERFORM THE INITIAL PROCESSING FOR THE PROGRAM.                       
001640******************************************************************        
001650                                                                          
001652     OPEN INPUT  AUDIT-EXTRACT-FILE                                       
                       RECEIVER-EXTRACT-FILE                                    
001653          OUTPUT RECEIVER-MERGE-FILE.                                     
001690                                                                          
           SET PS-NOT-EOF-AUDIT-EXTRACT                                         
               PS-NOT-EOF-RECEIVER-EXTRACT  TO TRUE                             
                                                                                
001691     PERFORM 7000-READ-AUDIT-EXTRACT-FILE.                                
001691     PERFORM 7001-READ-RCVR-EXTRACT-FILE.                                 
001692                                                                          
001693     IF PS-EOF-AUDIT-EXTRACT                                              
001695         DISPLAY '** AUDIT EXTRACT IS EMPTY **'                           
001698     END-IF.                                                              
001692                                                                          
001693     IF PS-EOF-RECEIVER-EXTRACT                                           
001695         DISPLAY '** RECEIVER EXTRACT IS EMPTY **'                        
001698     END-IF.                                                              
002150*                                                                         
002150****************************************************************          
002160*  2000-BUMP-FILES.                                                       
002160*  MATCH THE RECORDS OF WHAT WAS AUDITED WITH WHAT WAS TRANSCRBE          
002160*  ON THE RECEIVER                                                        
002160*  MATCH THE RECORDS AT THE FOLLOWING LEVEL:               .              
002160*  AUDIT/PO/RECEIVER/DEST-OPER-UNT-ID/SKU-NBR                             
002150****************************************************************          
002160 2000-BUMP-FILES.                                                         
                                                                                
           PERFORM 2001-COMPARE-KEYS.                                           
                                                                                
           INITIALIZE SU004-EXTRACT-RECORD OF RECEIVER-MERGE.                   
                                                                                
           EVALUATE TRUE                                                        
      *      IF BOTH FILES ARE ON MATCHING RECORDS, MOVE THE AUDIT EXTRACT      
      *      RECORD TO THE RECEIVER MERGE RECORD, AND MOVE THE QUANTITY         
      *      RECEIVED FROM RECEIVER EXTRACT RECORD TO THE EXPECTED QTY          
      *      ON THE RECEIVER MERGE RECORD                                       
000364       WHEN  PS-PROCESS-BOTH                                              
               MOVE SU004-EXTRACT-RECORD OF AUDIT-EXTRACT                       
                 TO SU004-EXTRACT-RECORD OF RECEIVER-MERGE                      
                                                                                
               MOVE SU004-QTY-RECV       OF RECEIVER-EXTRACT                    
                 TO SU004-QTY-RECV       OF RECEIVER-MERGE                      
                                                                                
               PERFORM 7000-READ-AUDIT-EXTRACT-FILE                             
               PERFORM 7001-READ-RCVR-EXTRACT-FILE                              
                                                                                
      *      IF THE RECEIVER EXTRACT IS BEHIND THE AUDIT EXTRACT IT IS A        
      *      SHORTAGE.  MOVE THE EXTRACT RECORD FROM THE RECEIVER EXTRACT TO    
      *      THE RECEIVER MERGE RECORD, AND SET THE ERROR QUANTITIES TO +1.     
000363       WHEN  PS-PROCESS-RECEIVER-EXTRACT                                  
               MOVE SU004-EXTRACT-RECORD OF RECEIVER-EXTRACT                    
                 TO SU004-EXTRACT-RECORD OF RECEIVER-MERGE                      
               MOVE +1 TO SU004-PICK-ERR-CNT      OF RECEIVER-MERGE             
                          SU004-UNT-ERR-CNT-COMBO OF RECEIVER-MERGE             
                          SU004-TOT-ERR-QTY       OF RECEIVER-MERGE             
                                                                                
               PERFORM 7001-READ-RCVR-EXTRACT-FILE                              
                                                                                
      *      IF THE AUDIT EXTRACT IS BEHIND THE RECEIVER EXTRACT IT IS AN       
      *      OVERAGE.  MOVE THE EXTRACT RECORD FROM THE AUDIT EXTRACT TO THE    
      *      RECEIVER MERGE RECORD, AND SET ALL ERROR QUANTITIES TO +1          
000364       WHEN  PS-PROCESS-AUDIT-EXTRACT                                     
               MOVE SU004-EXTRACT-RECORD OF AUDIT-EXTRACT                       
                 TO SU004-EXTRACT-RECORD OF RECEIVER-MERGE                      
               MOVE +1 TO SU004-PICK-ERR-CNT      OF RECEIVER-MERGE             
                          SU004-UNT-ERR-CNT-COMBO OF RECEIVER-MERGE             
                          SU004-TOT-ERR-QTY       OF RECEIVER-MERGE             
                                                                                
               PERFORM 7000-READ-AUDIT-EXTRACT-FILE                             
           END-EVALUATE.                                                        
                                                                                
           PERFORM 8000-WRITE-RECEIVER-MERGE-FILE.                              
                                                                                
                                                                                
002150****************************************************************          
002160*  2001-COMPARE-KEYS.                                                     
002160*  DETERMINE IF THE FILES MATCH.   IF THEY DON'T, DETERMINE WHICH         
002160*  FILE TO PROCESS                                                        
002150****************************************************************          
002160 2001-COMPARE-KEYS.                                                       
                                                                                
           EVALUATE TRUE                                                        
      *        WHEN THE AUDIT NUMBER IS DIFFERENT                               
002161         WHEN SU004-AUD-CNTL-NBR  OF AUDIT-EXTRACT <                      
002161              SU004-AUD-CNTL-NBR  OF RECEIVER-EXTRACT                     
                   SET PS-PROCESS-AUDIT-EXTRACT      TO TRUE                    
002161         WHEN SU004-AUD-CNTL-NBR  OF AUDIT-EXTRACT >                      
002161              SU004-AUD-CNTL-NBR  OF RECEIVER-EXTRACT                     
                   SET PS-PROCESS-RECEIVER-EXTRACT   TO TRUE                    
                                                                                
      *        PO NUMBER                                                        
               WHEN SU004-PO-NBR        OF AUDIT-EXTRACT <                      
                    SU004-PO-NBR        OF RECEIVER-EXTRACT                     
                   SET PS-PROCESS-AUDIT-EXTRACT      TO TRUE                    
               WHEN SU004-PO-NBR        OF AUDIT-EXTRACT >                      
                    SU004-PO-NBR        OF RECEIVER-EXTRACT                     
                   SET PS-PROCESS-RECEIVER-EXTRACT   TO TRUE                    
                                                                                
      *        RECEIVER NUMBER                                                  
               WHEN SU004-REC-SEQ-NBR   OF AUDIT-EXTRACT <                      
                    SU004-REC-SEQ-NBR   OF RECEIVER-EXTRACT                     
                   SET PS-PROCESS-AUDIT-EXTRACT      TO TRUE                    
               WHEN SU004-REC-SEQ-NBR   OF AUDIT-EXTRACT >                      
                    SU004-REC-SEQ-NBR   OF RECEIVER-EXTRACT                     
                   SET PS-PROCESS-RECEIVER-EXTRACT   TO TRUE                    
                                                                                
      *        DEST OPER UNT ID (STORE)                                         
               WHEN SU004-DEST-OPER-UNT-ID OF AUDIT-EXTRACT <                   
                    SU004-DEST-OPER-UNT-ID OF RECEIVER-EXTRACT                  
                   SET PS-PROCESS-AUDIT-EXTRACT      TO TRUE                    
               WHEN SU004-DEST-OPER-UNT-ID OF AUDIT-EXTRACT >                   
                    SU004-DEST-OPER-UNT-ID OF RECEIVER-EXTRACT                  
                   SET PS-PROCESS-RECEIVER-EXTRACT   TO TRUE                    
                                                                                
      *        SKU-NBR NUMBER                                                   
               WHEN SU004-SKU-NBR OF AUDIT-EXTRACT <                            
                    SU004-SKU-NBR OF RECEIVER-EXTRACT                           
                   SET PS-PROCESS-AUDIT-EXTRACT      TO TRUE                    
               WHEN SU004-SKU-NBR OF AUDIT-EXTRACT >                            
                    SU004-SKU-NBR OF RECEIVER-EXTRACT                           
                   SET PS-PROCESS-RECEIVER-EXTRACT   TO TRUE                    
                                                                                
      *        IF IT IS THE SAME ITEM, PROCESS BOTH RECORDS                     
               WHEN OTHER                                                       
                   SET PS-PROCESS-BOTH               TO TRUE                    
           END-EVALUATE.                                                        
068513                                                                          
068514                                                                          
068514****************************************************************          
068514* 7000-READ-AUDIT-EXTRACT-FILE                                 *          
068514* READ THE NEXT RECORD OF THE AUDIT EXTRACT FILE               *          
068514****************************************************************          
068515 7000-READ-AUDIT-EXTRACT-FILE.                                            
068517                                                                          
068518     READ AUDIT-EXTRACT-FILE                                              
           INTO SU004-EXTRACT-RECORD OF AUDIT-EXTRACT                           
068519        AT END                                                            
068520           SET PS-EOF-AUDIT-EXTRACT TO TRUE                               
068520           MOVE +999999999 TO SU004-AUD-CNTL-NBR OF AUDIT-EXTRACT         
068521     END-READ.                                                            
068522                                                                          
068514****************************************************************          
068514* 7001-READ-RCVR-EXTRACT-FILE                                  *          
068514* READ THE NEXT RECORD OF THE RECEIVER EXTRACT FILE            *          
068514****************************************************************          
068515 7001-READ-RCVR-EXTRACT-FILE.                                             
068517                                                                          
068518     READ RECEIVER-EXTRACT-FILE                                           
           INTO SU004-EXTRACT-RECORD OF RECEIVER-EXTRACT                        
068519        AT END                                                            
068520           SET PS-EOF-RECEIVER-EXTRACT TO TRUE                            
068520           MOVE +999999999 TO SU004-AUD-CNTL-NBR                          
068520                              OF RECEIVER-EXTRACT                         
068521     END-READ.                                                            
068523                                                                          
068524*                                                                         
068514****************************************************************          
068514* 8000-WRITE-RECEIVER-MERGE-FILE                               *          
068514* WRITE THE NEXT RECROD OF THE RECEIVER MERGE FILE             *          
068514****************************************************************          
068525 8000-WRITE-RECEIVER-MERGE-FILE.                                          
068526     WRITE RECEIVER-MERGE-RECORD                                          
068527       FROM SU004-EXTRACT-RECORD OF RECEIVER-MERGE.                       
068528*                                                                         
068529 9000-EOJ-ROUTINE.                                                        
068531                                                                          
068532     CLOSE AUDIT-EXTRACT-FILE                                             
068532           RECEIVER-EXTRACT-FILE                                          
068533           RECEIVER-MERGE-FILE.                                           
068534                                                                          
