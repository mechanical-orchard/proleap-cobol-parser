000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    RCKUT122.                                                 
000600 AUTHOR.        GERMAN BANDA.                                             
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   JULY, 2003.                                               
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM WILL READ THE PACK BY STORE ERROR FILE TO CHECK FOR       
001300*  VERIFIED RECEIVERS. VERIFIED RECEIVERS WILL BE FORMATTED TO CALL       
001310*  THE VENDOR VIOLATION UPDATE MODULE(RCKUP008)                           
001400******************************************************************        
001807* CHANGE HISTORY                                                *         
001300*     MADE BY: PRITHIVIRAJ SUBRAMANIAN (COGNIZANT)              *         
001300*     DATE:    11/12/2014                                       *         
001300*      RECOMPILE THE PROGRAM AS THE PO NUMBER FIELD IN THE      *         
001300*      COPYBOOK RCRD052 AND RCRD053 IS INCREASED TO 9 FROM 7.   *         
001400*****************************************************************         
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
003420     SELECT PACK-BY-STORE-ERROR-FILE                                      
003430         ASSIGN TO IN01.                                                  
003431     SELECT OUTPUT-FILE                                                   
003432         ASSIGN TO OUT01.                                                 
003433                                                                          
003500******************************************************************        
003600 DATA DIVISION.                                                           
003700******************************************************************        
003800                                                                          
003900 FILE SECTION.                                                            
004000                                                                          
005410 FD  PACK-BY-STORE-ERROR-FILE                                             
005420     RECORDING MODE IS F                                                  
005430     LABEL RECORDS ARE STANDARD                                           
005440     BLOCK CONTAINS 0 RECORDS.                                            
005450                                                                          
005460 01  PACK-BY-STORE-ERROR-RECORD PIC  X(300).                              
005470                                                                          
005480 FD  OUTPUT-FILE                                                          
005490     RECORDING MODE IS F                                                  
005500     LABEL RECORDS ARE STANDARD                                           
005501     RECORD CONTAINS 300                                                  
005502     BLOCK CONTAINS 0 RECORDS                                             
005503     DATA RECORD IS OUTPUT-RECORD.                                        
005504 01  OUTPUT-RECORD               PIC X(300).                              
005505*                                                                         
005510*                                                                         
005600******************************************************************        
005700 WORKING-STORAGE SECTION.                                                 
007400******************************************************************        
007500*  DB2 FORMATTED MESSAGE AREA                                             
007600******************************************************************        
007700     COPY DPWS004.                                                        
007800*                                                                         
007900******************************************************************        
008000*  ASN RECEIVER PACKSLIP EXTRACT ERROR FILE LAYOUT                        
008010******************************************************************        
008020     COPY RCRD053.                                                        
008030*                                                                         
008040******************************************************************        
008050*  LAYOUT FOR DATA PASSED TO VENDOR VIOLATION CREATE MODULE               
008060******************************************************************        
008070     COPY RCRD068.                                                        
008080*                                                                         
008100******************************************************************        
008200*  COMMUNICATIONS AREA FOR DB2                                            
008300******************************************************************        
008400     EXEC SQL                                                             
008500          INCLUDE SQLCA                                                   
008600     END-EXEC.                                                            
008800*                                                                         
011180******************************************************************        
011190*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE RECEIVER TABLE             
011192******************************************************************        
011193     EXEC SQL                                                             
011194          INCLUDE TRECEIV                                                 
011195     END-EXEC.                                                            
011196                                                                          
011197******************************************************************        
011198*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE PACKSLIP TABLE             
011199******************************************************************        
011200     EXEC SQL                                                             
011210          INCLUDE TPKSLIP                                                 
011211     END-EXEC.                                                            
011212                                                                          
011213******************************************************************        
011215*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE PURCHASE TABLE             
011216******************************************************************        
011217     EXEC SQL                                                             
011218          INCLUDE TPURCHS                                                 
011219     END-EXEC.                                                            
011220                                                                          
011221******************************************************************        
011222*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE SHIP UNT LOCATION          
011223*  TABLE                                                                  
011224******************************************************************        
011225     EXEC SQL                                                             
011226          INCLUDE TSHUNLO                                                 
011227     END-EXEC.                                                            
011228                                                                          
011230******************************************************************        
011250*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE SHIP UNT LOCATION          
011251*  TABLE                                                                  
011252******************************************************************        
011253     EXEC SQL                                                             
011254          INCLUDE TINPKSU                                                 
011255     END-EXEC.                                                            
011256                                                                          
011257******************************************************************        
011259*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE VENDOR LOGISTICS           
011260*  VIOLATION TABLE                                                        
011261******************************************************************        
011262     EXEC SQL                                                             
011263          INCLUDE TVILN                                                   
011264     END-EXEC.                                                            
011265                                                                          
011266*****************************************************************         
011267*  DB2 DCLGEN FOR PARAMETER TABLE                         *               
011268***********************************************************               
011269     EXEC SQL                                                             
011270          INCLUDE TKRPRPM                                                 
011271     END-EXEC.                                                            
011272                                                                          
011273******************************************************************        
011274                                                                          
011280 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
011300                                                   COMP SYNC.             
011400                                                                          
011500 01  PS-PROGRAM-SWITCHES.                                                 
011600     05  PS-EOF-PBS-ERROR-FILE-SW    PIC  X(01)    VALUE 'N'.             
011700         88  PS-EOF-PBS-ERROR-FILE                 VALUE 'Y'.             
011800         88  PS-MORE-PBS-ERROR-FILE                VALUE 'N'.             
011810     05  PS-VRFD-REC-SEQ-FND-SW      PIC  X(01)    VALUE 'N'.             
011820         88  PS-VRFD-REC-SEQ-FND                   VALUE 'Y'.             
011830         88  PS-NO-VRFD-REC-SEQ-FND                VALUE 'N'.             
011840     05  PS-VILN-NBR-EXISTS-IND      PIC  X(01)    VALUE 'N'.             
011850         88  PS-VILN-NBR-EXISTS                    VALUE 'N'.             
011860         88  PS-VILN-NBR-NOT-EXISTS                VALUE 'Y'.             
011891     05  PS-CREATE-EXCEPTION-TYP-SW  PIC  X(02)    VALUE 'AP'.            
011892         88  PS-CREATE-APP-TO-DEBIT                VALUE 'AP'.            
011893         88  PS-CREATE-APP-TO-WAIVE                VALUE 'WA'.            
011894     05  PS-VEND-EXCP-LEVEL-SW        PIC X(03)    VALUE '   '.           
011896         88  PS-VVLCDX-VEND-EXCPT-FOUND            VALUE 'CDX'.           
011897         88  PS-VVLGPX-VEND-EXCPT-FOUND            VALUE 'GPX'.           
011898         88  PS-VNLVLX-VEND-EXCPT-FOUND            VALUE 'VLX'.           
011899     05  PS-PBSA-ERR-CTN-FOUND-SW     PIC X(01)    VALUE 'N'.             
011900         88  PS-PBSA-ERR-CTN-FOUND                 VALUE 'Y'.             
011910         88  PS-PBSA-ERR-CTN-NOT-FOUND             VALUE 'N'.             
011920     05  PS-PBSVLN-PROCESS-SW         PIC X(01)    VALUE 'N'.             
011930         88  PS-PBSVLN-PROCESS-YES                 VALUE 'Y'.             
011940         88  PS-PBSVLN-PROCESS-NO                  VALUE 'N'.             
012000                                                                          
012100 01  PC-PROGRAM-CONSTANTS.                                                
012200     05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100             
012300                                                   COMP SYNC.             
012400     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
012500         'RCKUT122'.                                                      
012510     05  PC-VENDOR-VILN-CRE-PROGRAM  PIC  X(08)    VALUE                  
012520         'RCKUP008'.                                                      
012600     05  PC-CURRENT-OPERATING-COMPANY                                     
012700                                     PIC  X(02)    VALUE '03'.            
012800     05  PC-MAX-RETRIES              PIC S9(04)    VALUE +3               
012900                                                   COMP SYNC.             
013000     05  PC-CURRENT-TIMESTAMP        PIC  X(26)    VALUE SPACES.          
013010     05  PC-COMPLETE-TIMESTAMP       PIC  X(26)    VALUE SPACES.          
013020     05  PC-INPROCESS-TIMESTAMP      PIC  X(26)    VALUE SPACES.          
013030     05  PC-COMMIT-HOLD              PIC 9(3)      VALUE 020.             
013031     05  PC-DEFAULT-BEG-TMST             PIC  X(26)  VALUE                
013032                                 '0001-01-01-01.01.01.000000'.            
013040**CONSTANTS FOR INSERT TO TABLES                                          
013050     05  PC-BDGT-AREA-CDE-COMP       PIC X(4)      VALUE 'COMP'.          
013060     05  PC-GP-CDE-PBS               PIC X(3)      VALUE 'PBS'.           
013070     05  PC-VILN-RESP-ID             PIC 9(02)     VALUE 03.              
013080     05  PC-VILN-RPT-CDE             PIC X(01)     VALUE 'V'.             
013090     05  PC-CURR-STAT-CDE            PIC X(02)     VALUE 'AP'.            
013100                                                                          
013200 01  PV-PROGRAM-VARIABLES.                                                
013300     05  PV-PO-NBR                   PIC  S9(9) COMP.                     
013301     05  PV-PREV-PO-NBR              PIC  S9(9) COMP.                     
013302     05  PV-PREV-DC-LOC              PIC   9(4).                          
013310     05  PV-ITEM-NBR                 PIC  9(15)    VALUE ZERO.            
013400     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
013500     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
013600     05  PV-OPEN-COUNTER             PIC S9(04)    VALUE ZERO             
013700                                                   COMP SYNC.             
014200     05  PV-WRITE-COUNTER            PIC S9(09)    VALUE ZERO             
014300                                                   COMP SYNC.             
014301     05  PV-VILN-CRE-COUNTER         PIC S9(09)    VALUE ZERO             
014302                                                   COMP SYNC.             
014310     05  PV-READ-COUNTER             PIC S9(09)    VALUE ZERO             
014320                                                   COMP SYNC.             
014400     05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                     
014401     05  PV-DISP-INPKSU-CTNS         PIC ZZ,ZZ9.                          
014402                                                                          
014410     05  PV-DISPLAY-CARTON           PIC 9(18).                           
014500                                                                          
014510                                                                          
014600     05  PV-VILN-DB2-COUNT           PIC S9(09)    VALUE ZERO             
014601                                                   COMP-3.                
014610     05  PV-DB2-COUNT                PIC S9(09)    VALUE ZERO             
014700                                                   COMP-3.                
014710     05  PV-COMMIT-COUNT             PIC S9(09)    VALUE +0               
014720                                                   COMP SYNC.             
014730     05  PV-COMMIT-COUNTER           PIC 9(03)     VALUE ZERO.            
014740                                                                          
014750     05  PV-STATUS-COUNTER           PIC S9(09)    VALUE ZERO             
014760                                                   COMP SYNC.             
014761     05  PV-CRE-DATE-TO-CURRENT      PIC S9(09)    VALUE ZERO             
014762                                                   COMP SYNC.             
014763     05  PV-SHUNLO-CRE-DTE           PIC X(10)     VALUE SPACE.           
014764     05  PV-RECEIV-DATE              PIC X(10)     VALUE SPACE.           
014765     05  PV-RECORDS-WRITTEN          PIC S9(9)     VALUE +0.              
014766                                                                          
014767                                                                          
014770 01  DK-DB2-KEYS.                                                         
014780     05  DK-VILN-GP-CDE              PIC  X(03).                          
014791     05  DK-VILN-GP-SEQ-NBR          PIC  S9(4) COMP.                     
014800                                                                          
014900                                                                          
018440                                                                          
018500******************************************************************        
018600 PROCEDURE DIVISION.                                                      
018700******************************************************************        
018800                                                                          
018900 0000-PROGRAM-MAINLINE.                                                   
019000******************************************************************        
019100* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE             
019200*      PROGRAM.                                                           
019300******************************************************************        
019400                                                                          
019500     PERFORM 1000-INITIALIZE.                                             
019510     MOVE ZEROES       TO PV-PREV-DC-LOC.                                 
019520     IF PS-MORE-PBS-ERROR-FILE                                            
019700         PERFORM 2000-PROCESS-VRFD-PBS                                    
019800            UNTIL PS-EOF-PBS-ERROR-FILE                                   
019900     END-IF.                                                              
019901                                                                          
020000     PERFORM 9000-EOJ-ROUTINE.                                            
020100                                                                          
020200     STOP RUN.                                                            
020300                                                                          
020400 1000-INITIALIZE.                                                         
020500******************************************************************        
020600*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
020700*    -- OPEN OUTPUT FILE                                                  
020800*    -- OPEN CURSOR                                                       
020900*    -- FETCH CURSOR                                                      
021000******************************************************************        
021310     OPEN INPUT  PACK-BY-STORE-ERROR-FILE                                 
021320          OUTPUT OUTPUT-FILE.                                             
021600                                                                          
021700     PERFORM 7000-READ-PBS-ERROR-FILE.                                    
021800                                                                          
022000                                                                          
022100******************************************************************        
025410 2000-PROCESS-VRFD-PBS.                                                   
025500******************************************************************        
025910     MOVE RC053-PO-NBR  TO     PKSLIP-PO-NBR.                             
025920     MOVE RC053-SEQ-NBR TO     PKSLIP-SEQ-NBR.                            
025921                                                                          
025930     IF RC053-DC =  PV-PREV-DC-LOC                                        
025931        CONTINUE                                                          
025932     ELSE                                                                 
025940        PERFORM 3000-CHECK-TKRPRPM-DC-PBSVLN                              
025950     END-IF                                                               
025951                                                                          
025960     IF PS-PBSVLN-PROCESS-YES                                             
026000        PERFORM 2200-SELECT-PKSLIP-RCVR                                   
026103        IF PS-VRFD-REC-SEQ-FND                                            
026104           PERFORM 2250-CHECK-FOR-PREV-RVCR-VILN                          
026105           IF PS-VILN-NBR-NOT-EXISTS                                      
026106              PERFORM 2300-SELECT-PO-TYP-CDE                              
026107              PERFORM 2400-SELECT-OCCRNC-DTE                              
026108              IF PS-PBSA-ERR-CTN-FOUND                                    
026109                  PERFORM 2500-COUNT-RCVR-ERR-CTN                         
026110                  IF RC053-TINPKSU-PKG-COUNT                              
026111                                      =  RC053-TPKSLIP-EXP-QTY            
026112                    IF KRPRPM-FUNC-BEG-TMST > PC-DEFAULT-BEG-TMST         
026113                       PERFORM 4050-DISPLAY-VIL-DATA                      
026114                    END-IF                                                
026115                    IF (RC053-UPC-NBR > 0) AND                            
026116                       (RC053-PKSL-LINE-NBR > 0)                          
026117                       PERFORM 4000-LOAD-VILN-DATA                        
026118                       PERFORM 4100-LINK-TO-VILN-CREATE-MOD               
026119                    END-IF                                                
026120                  ELSE                                                    
026122                    IF KRPRPM-FUNC-BEG-TMST > PC-DEFAULT-BEG-TMST         
026123                       PERFORM 4050-DISPLAY-VIL-DATA                      
026124                    END-IF                                                
026125                  END-IF                                                  
026126              END-IF                                                      
026127           END-IF                                                         
026128        ELSE                                                              
026129           PERFORM 2400-SELECT-OCCRNC-DTE                                 
026130           IF PS-PBSA-ERR-CTN-FOUND                                       
026131              IF PV-CRE-DATE-TO-CURRENT  <  180                           
026132                 PERFORM 8000-REWRITE-PBS-ERROR-FILE                      
026133              END-IF                                                      
026140           END-IF                                                         
026167        END-IF                                                            
026168     END-IF.                                                              
026169                                                                          
026180     PERFORM 7000-READ-PBS-ERROR-FILE.                                    
026403                                                                          
026496 2200-SELECT-PKSLIP-RCVR.                                                 
026497******************************************************************        
026498*  SELECT THE REC SEQ NBR AND ACTUAL CARTON COUNT                         
026499******************************************************************        
026500     EXEC SQL                                                             
026501         SELECT A.REC_SEQ_NBR                                             
026502              , B.ACTL_CART_CNT                                           
026503              , DATE(B.REC_TMST)                                          
026504          INTO :PKSLIP-REC-SEQ-NBR                                        
026505              ,:RECEIV-ACTL-CART-CNT                                      
026506              ,:PV-RECEIV-DATE                                            
026507          FROM    TPKSLIP A                                               
026508                , TRECEIV B                                               
026509          WHERE     A.PO_NBR         =  :PKSLIP-PO-NBR                    
026510             AND    A.SEQ_NBR        =  :PKSLIP-SEQ-NBR                   
026511             AND    A.PO_NBR         =  B.ORD_NBR                         
026512             AND    A.REC_SEQ_NBR    =  B.REC_SEQ_NBR                     
026513             AND    B.STATUS_CDE     =  'VE'                              
026514             AND    A.VER_STAT_CDE   = 'A'                                
026515             AND    B.VER_STATUS_CDE = 'A'                                
026516             AND    A.PKSL_STAT_CDE  = 'AC'                               
026517             AND    B.ACTL_CART_CNT >  0                                  
026518             AND    B.ML_POST_TMST  < '9999-09-09-09.09.09.999999'        
026519     END-EXEC.                                                            
026520                                                                          
026521     EVALUATE TRUE                                                        
026522         WHEN SQLCODE = ZERO                                              
026523              SET PS-VRFD-REC-SEQ-FND TO TRUE                             
026524         WHEN SQLCODE = +100                                              
026525             SET PS-NO-VRFD-REC-SEQ-FND TO TRUE                           
026526         WHEN SQLWARN0 NOT = SPACES                                       
026527         WHEN SQLCODE < ZERO                                              
026528         WHEN SQLCODE > ZERO                                              
026529             MOVE '2200-SELECT-PKSLIP-RCVR       '                        
026530                             TO PV-PARAGRAPH-NAME                         
026531             MOVE 'SELECT REC SEQ NBR    '                                
026532                             TO PV-DB2-OPERATION-ATTEMPTED                
026533             DISPLAY 'PURCHAS ORDER NUMBER IS '                           
026534                     PKSLIP-PO-NBR                                        
026535             DISPLAY 'SEQUENCE NUMBER IS    '                             
026536                     PKSLIP-SEQ-NBR                                       
026537             PERFORM 9100-SQL-ABEND                                       
026538     END-EVALUATE.                                                        
026539                                                                          
026540 2250-CHECK-FOR-PREV-RVCR-VILN.                                           
026541******************************************************************        
026542*  SELECT THE REC SEQ NBR AND ACTUAL CARTON COUNT                         
026543******************************************************************        
026544                                                                          
026545     MOVE ZEROES TO PV-VILN-DB2-COUNT.                                    
026546                                                                          
026547     EXEC SQL                                                             
026548        SELECT  COUNT(*)                                                  
026549          INTO  :PV-VILN-DB2-COUNT                                        
026550          FROM  TVILN                                                     
026551          WHERE PO_NBR        =  :PKSLIP-PO-NBR                           
026552            AND REC_SEQ_NBR   =  :PKSLIP-REC-SEQ-NBR                      
026553            AND VILN_GP_CDE   =  'PBS'                                    
026559     END-EXEC.                                                            
026560                                                                          
026561     EVALUATE TRUE                                                        
026562         WHEN SQLCODE = ZERO                                              
026564              IF PV-VILN-DB2-COUNT > 0                                    
026565                 SET PS-VILN-NBR-EXISTS TO TRUE                           
026566              ELSE                                                        
026567                 SET PS-VILN-NBR-NOT-EXISTS TO TRUE                       
026568              END-IF                                                      
026569         WHEN SQLWARN0 NOT = SPACES                                       
026570         WHEN SQLCODE < ZERO                                              
026571         WHEN SQLCODE > ZERO                                              
026572             MOVE '2250-CHECK-FOR-PREV-RVCR-VILN '                        
026573                             TO PV-PARAGRAPH-NAME                         
026574             MOVE 'SELECT REC SEQ NBR    '                                
026575                             TO PV-DB2-OPERATION-ATTEMPTED                
026576             DISPLAY 'PURCHAS ORDER NUMBER IS '                           
026577                     PKSLIP-PO-NBR                                        
026578             DISPLAY 'SEQUENCE NUMBER IS    '                             
026579                     PKSLIP-SEQ-NBR                                       
026580             PERFORM 9100-SQL-ABEND                                       
026581     END-EVALUATE.                                                        
026582                                                                          
026583 2300-SELECT-PO-TYP-CDE.                                                  
026584******************************************************************        
026585*  SELECT THE REC SEQ NBR AND ACTUAL CARTON COUNT                         
026586******************************************************************        
026587     EXEC SQL                                                             
026588         SELECT A.PO_TYP_CDE                                              
026590          INTO :PURCHS-PO-TYP-CDE                                         
026700          FROM  TPURCHS A                                                 
026810          WHERE  A.PO_NBR         =  :PKSLIP-PO-NBR                       
026820           AND   A.VER_STATUS_CDE =  'A'                                  
026874     END-EXEC.                                                            
026875                                                                          
026876     EVALUATE TRUE                                                        
026877         WHEN SQLCODE = ZERO                                              
026879         WHEN SQLCODE = +100                                              
026880              CONTINUE                                                    
026882         WHEN SQLWARN0 NOT = SPACES                                       
026883         WHEN SQLCODE < ZERO                                              
026884         WHEN SQLCODE > ZERO                                              
026885             MOVE '2300-SELECT-PO-TYP-CDE        '                        
026886                             TO PV-PARAGRAPH-NAME                         
026887             MOVE 'SELECT PO TYPE CODE   '                                
026888                             TO PV-DB2-OPERATION-ATTEMPTED                
026889             DISPLAY 'PURCHAS ORDER NUMBER IS '                           
026890                     PKSLIP-PO-NBR                                        
026891             DISPLAY 'SEQUENCE NUMBER IS    '                             
026892                     PKSLIP-SEQ-NBR                                       
026893             PERFORM 9100-SQL-ABEND                                       
026894     END-EVALUATE.                                                        
026895                                                                          
026896 2400-SELECT-OCCRNC-DTE.                                                  
026897******************************************************************        
026898*  SELECT THE CREATE TMST FROM TSHUNLO WHICH IS THE TMSTP THAT            
026899*  THE VIOLATION WAS IDENTIFIED                                           
026900******************************************************************        
026901     MOVE RC053-SHIPT-UNT-ID  TO SHUNLO-SHIPT-UNT-ID.                     
026902                                                                          
026903     EXEC SQL                                                             
026904         SELECT DISTINCT                                                  
026905                DATE(CRE_TMST)                                            
026906              , DATE(CURRENT TIMESTAMP) - DATE(CRE_TMST)                  
026907          INTO :PV-SHUNLO-CRE-DTE                                         
026908              ,:PV-CRE-DATE-TO-CURRENT                                    
026909          FROM  TSHUNLO A                                                 
026910          WHERE SHIPT_UNT_ID     =  :SHUNLO-SHIPT-UNT-ID                  
026911           AND  SCN_LOC_ID      IN ('PBSA')                               
026912                                                                          
026913     END-EXEC.                                                            
026914                                                                          
026915     EVALUATE TRUE                                                        
026916         WHEN SQLCODE = ZERO                                              
026917         WHEN SQLCODE = -811                                              
026918              SET PS-PBSA-ERR-CTN-FOUND TO TRUE                           
026920         WHEN SQLCODE = +100                                              
026921              SET PS-PBSA-ERR-CTN-NOT-FOUND TO TRUE                       
026922         WHEN SQLWARN0 NOT = SPACES                                       
026923         WHEN SQLCODE < ZERO                                              
026924         WHEN SQLCODE > ZERO                                              
026925             MOVE SHUNLO-SHIPT-UNT-ID  TO PV-DISPLAY-CARTON               
026926             MOVE '2400-SELECT-OCCRNC-DTE        '                        
026927                             TO PV-PARAGRAPH-NAME                         
026928             MOVE 'SELECT OCCURENCE DATE '                                
026929                             TO PV-DB2-OPERATION-ATTEMPTED                
026930             DISPLAY 'PURCHAS ORDER NUMBER IS '                           
026931                     PKSLIP-PO-NBR                                        
026932             DISPLAY 'SEQUENCE NUMBER IS    '                             
026933                     PKSLIP-SEQ-NBR                                       
026934             DISPLAY 'CARTON ID      IS    '                              
026935                     PV-DISPLAY-CARTON                                    
026936             PERFORM 9100-SQL-ABEND                                       
026937     END-EVALUATE.                                                        
026938                                                                          
026939 2500-COUNT-RCVR-ERR-CTN.                                                 
026940******************************************************************        
026941*  COUNT OF THE NUMBER OF ERROR CARTONS INSERTED TO TSHUNLO               
026942******************************************************************        
026943                                                                          
026944     MOVE  ZEROES             TO PV-DB2-COUNT.                            
026945                                                                          
026946     EXEC SQL                                                             
026947         SELECT DISTINCT(COUNT(*))                                        
026948          INTO  :PV-DB2-COUNT                                             
026949          FROM  TSHUNLO A                                                 
026950              , TINPKSU B                                                 
026951              , TPKSLIP C                                                 
026952          WHERE A.SHIPT_UNT_ID   =  B.SHIPT_UNT_ID                        
026953           AND  B.PO_NBR         = :PKSLIP-PO-NBR                         
026954           AND  B.PO_NBR         = C.PO_NBR                               
026955           AND  B.SEQ_NBR        = C.SEQ_NBR                              
026956           AND  C.REC_SEQ_NBR    = :PKSLIP-REC-SEQ-NBR                    
026957           AND  A.SCN_LOC_ID     = 'PBSA'                                 
026958           AND  C.VER_STAT_CDE   = 'A'                                    
026959     END-EXEC.                                                            
026960                                                                          
026961     EVALUATE TRUE                                                        
026962         WHEN SQLCODE = ZERO                                              
026963              CONTINUE                                                    
026964         WHEN SQLWARN0 NOT = SPACES                                       
026965         WHEN SQLCODE < ZERO                                              
026966         WHEN SQLCODE > ZERO                                              
026967             MOVE '2500-COUNT-RCVR-ERR-CTN       '                        
026968                             TO PV-PARAGRAPH-NAME                         
026969             MOVE 'COUNT ERROR CTNS ON TSHUNLO'                           
026970                             TO PV-DB2-OPERATION-ATTEMPTED                
026971             DISPLAY 'PURCHAS ORDER NUMBER IS '                           
026972                     PKSLIP-PO-NBR                                        
026973             DISPLAY 'RECEIVER NUMBER IS    '                             
026974                     PKSLIP-REC-SEQ-NBR                                   
026975             PERFORM 9100-SQL-ABEND                                       
026976     END-EVALUATE.                                                        
026977                                                                          
026978*----------------------------------------------------------------*        
026979* PARM VALUE TO DETERMINE IF DC IS TURNED ON TO CALL VENDOR      *        
026980* VIOLATON INSERT MODULE                                         *        
026981*----------------------------------------------------------------*        
026982 3000-CHECK-TKRPRPM-DC-PBSVLN.                                            
026983     MOVE RC053-DC              TO KRPRPM-LOC-ID                          
026984                                   PV-PREV-DC-LOC.                        
026985     EXEC SQL                                                             
026986          SELECT FUNC_BEG_TMST                                            
026987          INTO  :KRPRPM-FUNC-BEG-TMST                                     
026988          FROM   TKRPRPM                                                  
026989          WHERE  SYS_PRC_FUNC_CDE  = 'PBSVLN'                             
026990            AND  LOC_ID            = :KRPRPM-LOC-ID                       
026991            AND  INTFC_SYS_CDE     = 'KHL'                                
026992            AND  FUNC_PRC_STAT_CDE = 'AC'                                 
026993            AND  FUNC_TXT          = 'Y'                                  
026994     END-EXEC.                                                            
026995                                                                          
026996     EVALUATE TRUE                                                        
026997         WHEN SQLCODE = +0                                                
026998             SET PS-PBSVLN-PROCESS-YES TO TRUE                            
026999         WHEN SQLCODE = +100                                              
027000             SET PS-PBSVLN-PROCESS-NO  TO TRUE                            
027001         WHEN SQLWARN0 NOT = SPACE                                        
027002         WHEN SQLCODE  NOT = +0                                           
027003             MOVE '3000-CHECK-TKRPRPM-DC-PBSVLN  '                        
027004                             TO PV-PARAGRAPH-NAME                         
027005             MOVE 'CHECK DC PARM PBSVLN  '                                
027006                             TO PV-DB2-OPERATION-ATTEMPTED                
027007             DISPLAY 'DC LOCATION IS    '                                 
027008                     PV-PREV-DC-LOC                                       
027009             PERFORM 9100-SQL-ABEND                                       
027014     END-EVALUATE.                                                        
027015                                                                          
027016                                                                          
027017 4000-LOAD-VILN-DATA.                                                     
027018     SET RC068-PBS-EDIT-CREATE     TO TRUE.                               
027019     MOVE RC053-PO-NBR             TO RC068-PO-NUMBER                     
027020     MOVE RC053-SEQ-NBR            TO RC068-PKSLIP-SEQ-NBR                
027021     MOVE 'PBS'                    TO RC068-VILN-GROUP-MST                
027022     MOVE 'PBS'                    TO RC068-VILN-GROUP-CDE                
027023     MOVE  1                       TO RC068-GP-SEQ-NBR                    
027024     MOVE RC053-DEPT-NBR           TO RC068-DEPT-NBR                      
027025     MOVE RC053-DC                 TO RC068-OPER-UNT-ID                   
027026     MOVE RC053-DEST-STORE         TO RC068-DEST-STORE                    
027027     MOVE PKSLIP-REC-SEQ-NBR       TO RC068-REC-SEQ-NBR                   
027028     MOVE RC053-ASN-NBR            TO RC068-ASN-NBR                       
027029     MOVE RC053-TPURCHS-ENT-ID     TO RC068-ENT-ID                        
027030     MOVE 'RCKUT122'               TO RC068-USER-ID                       
027031     MOVE  'Y'                     TO RC068-PACK-BY-STORE-IND             
027032     MOVE   03                     TO RC068-VILN-RESP-ID                  
027033     MOVE  'OC'                    TO RC068-FCLTY-AREA-ID                 
027034     MOVE  PURCHS-PO-TYP-CDE       TO RC068-PO-TYP-CDE                    
027035     MOVE  3                       TO RC068-OCRNC-SHFT-CDE                
027036     MOVE PV-SHUNLO-CRE-DTE        TO RC068-OCRNC-DTE                     
027037     MOVE PV-RECEIV-DATE           TO RC068-RECEIV-DTE                    
027038                                                                          
027039     MOVE RECEIV-ACTL-CART-CNT     TO RC068-VERFD-CRTN-COUNT.             
027040                                                                          
027041     IF PV-DB2-COUNT   >   RECEIV-ACTL-CART-CNT                           
027042        MOVE RECEIV-ACTL-CART-CNT  TO RC068-ERROR-CRTN-COUNT              
027043     ELSE                                                                 
027044        MOVE PV-DB2-COUNT          TO RC068-ERROR-CRTN-COUNT              
027045     END-IF                                                               
027046                                                                          
027047     MOVE RC053-SHIPT-UNT-ID       TO RC068-SHIPT-UNT-ID.                 
027048     MOVE RC053-UPC-NBR            TO RC068-UPC-NBR.                      
027049     MOVE RC053-PKSL-LINE-NBR      TO RC068-PKSL-LINE-NBR.                
027050     MOVE 100                      TO RC068-CDE-ID.                       
027051     MOVE 'AP'                     TO RC068-CDVL-CDE.                     
027060     MOVE RC053-TSHUUPC-SHP-QTY    TO RC068-TSHUUPC-SHP-QTY.              
027096     MOVE RC053-TPKSLIP-EXP-QTY    TO RC068-TPKSLIP-EXP-QTY.              
027097     MOVE RC053-TPKITEM-EXP-QTY    TO RC068-TPKITEM-EXP-QTY.              
027098 4050-DISPLAY-VIL-DATA.                                                   
027099                                                                          
027100     MOVE RC053-PO-NBR             TO RC068-PO-NUMBER                     
027110     DISPLAY 'PURCHAS ORDER NUMBER IS ' RC068-PO-NUMBER                   
027112     MOVE RC053-DEPT-NBR           TO RC068-DEPT-NBR                      
027113     DISPLAY 'DEPT NBR ' RC068-DEPT-NBR                                   
027114     MOVE RC053-DC                 TO RC068-OPER-UNT-ID                   
027115     DISPLAY 'DC NUMBER '  RC068-OPER-UNT-ID                              
027116     MOVE RC053-DEST-STORE         TO RC068-DEST-STORE                    
027117     DISPLAY 'STORE  '    RC068-DEST-STORE                                
027118     MOVE PKSLIP-REC-SEQ-NBR       TO RC068-REC-SEQ-NBR                   
027119     DISPLAY 'REC SEQ NBR ' RC068-REC-SEQ-NBR                             
027122     MOVE RC053-TPURCHS-ENT-ID     TO RC068-ENT-ID                        
027123     DISPLAY 'ENT ID '  RC068-ENT-ID                                      
027128     MOVE  PURCHS-PO-TYP-CDE       TO RC068-PO-TYP-CDE                    
027129     DISPLAY 'PURCHS PO TYP  '    RC068-PO-TYP-CDE                        
027131     MOVE PV-SHUNLO-CRE-DTE        TO RC068-OCRNC-DTE                     
027132     DISPLAY 'SHUNLO CRE DTE '     RC068-OCRNC-DTE                        
027133     MOVE PV-RECEIV-DATE           TO RC068-RECEIV-DTE                    
027134     DISPLAY 'RECEIV DATE '     RC068-RECEIV-DTE                          
027135                                                                          
027136     MOVE RECEIV-ACTL-CART-CNT     TO RC068-VERFD-CRTN-COUNT.             
027137     DISPLAY 'VRFD CRTN CNT '    RC068-VERFD-CRTN-COUNT                   
027138                                                                          
027139     IF PV-DB2-COUNT   >   RECEIV-ACTL-CART-CNT                           
027140        MOVE RECEIV-ACTL-CART-CNT  TO RC068-ERROR-CRTN-COUNT              
027141     ELSE                                                                 
027142        MOVE PV-DB2-COUNT          TO RC068-ERROR-CRTN-COUNT              
027143     END-IF                                                               
027144                                                                          
027145     DISPLAY 'ERR CRTN COUNT '   RC068-ERROR-CRTN-COUNT                   
027146     MOVE RC053-SHIPT-UNT-ID       TO RC068-SHIPT-UNT-ID.                 
027147     DISPLAY 'SHIPT UNT ID  '   RC068-SHIPT-UNT-ID                        
027148     MOVE RC053-UPC-NBR            TO RC068-UPC-NBR.                      
027149     DISPLAY 'UPC NUMBER  '   RC068-UPC-NBR                               
027150     MOVE RC053-PKSL-LINE-NBR      TO RC068-PKSL-LINE-NBR.                
027151     DISPLAY 'PKSLI LINE NBR '    RC068-PKSL-LINE-NBR                     
027154     MOVE RC053-TSHUUPC-SHP-QTY    TO RC068-TSHUUPC-SHP-QTY.              
027155     DISPLAY 'SHUUPC SHP QTY '    RC068-TSHUUPC-SHP-QTY                   
027156     MOVE RC053-TPKSLIP-EXP-QTY    TO RC068-TPKSLIP-EXP-QTY.              
027157     DISPLAY 'PKSLIP EXP QTY '    RC068-TPKSLIP-EXP-QTY.                  
027158     MOVE RC053-TPKITEM-EXP-QTY    TO RC068-TPKITEM-EXP-QTY.              
027159     DISPLAY 'PKITEM EXP QTY '    RC068-TPKITEM-EXP-QTY.                  
027160     MOVE RC053-TINPKSU-PKG-COUNT  TO PV-DISP-INPKSU-CTNS.                
027161     DISPLAY 'INPKSU CART CNT'    PV-DISP-INPKSU-CTNS.                    
027162*----------------------------------------------------------------*        
027163*  LINK TO THE MODULE THAT WILL PERFORM INSERTS TO THE VIOLATION *        
027164*  TABLES TO CREATE ENTRIES IN VCS                                        
027165*----------------------------------------------------------------*        
027166 4100-LINK-TO-VILN-CREATE-MOD.                                            
027167                                                                          
027168     CALL PC-VENDOR-VILN-CRE-PROGRAM                                      
027169        USING RC068-VEND-VILN-CRE-RECORD.                                 
027170                                                                          
027171                                                                          
027172     EVALUATE TRUE                                                        
027173       WHEN RC068-MOD-SUCCESSFUL                                          
027174              ADD +1 TO PV-VILN-CRE-COUNTER                               
027175       WHEN RC068-MOD-NO-VILN-CREATE                                      
027176            DISPLAY 'NO VIOLATION CREATED'                                
027177            DISPLAY 'PURCHAS ORDER NUMBER IS '                            
027178                    PKSLIP-PO-NBR                                         
027179            DISPLAY 'SEQUENCE NUMBER IS    '                              
027180                    PKSLIP-SEQ-NBR                                        
027181       WHEN OTHER                                                         
027182            MOVE '4100-LINK-TO-VILN-CREATE-MOD  '                         
027183                            TO PV-PARAGRAPH-NAME                          
027184            MOVE 'LINK TO VILN CREATE MOD'                                
027185                            TO PV-DB2-OPERATION-ATTEMPTED                 
027186            DISPLAY 'PURCHAS ORDER NUMBER IS '                            
027187                    PKSLIP-PO-NBR                                         
027188            DISPLAY 'SEQUENCE NUMBER IS    '                              
027189                    PKSLIP-SEQ-NBR                                        
027190            PERFORM 9100-SQL-ABEND                                        
027191            DISPLAY 'SQLCODE         IS    '                              
027192                    RC068-DB2-SQLCODE                                     
027193            PERFORM 9100-SQL-ABEND                                        
027194     END-EVALUATE.                                                        
027195                                                                          
027196 7000-READ-PBS-ERROR-FILE.                                                
027197******************************************************************        
027198*  READ THE INPUT FILE                                                    
027199******************************************************************        
027200                                                                          
027201      READ PACK-BY-STORE-ERROR-FILE INTO                                  
027202            RC053-ASN-RECVR-PKSLIP-ERRREC                                 
027203            AT END                                                        
027204               SET PS-EOF-PBS-ERROR-FILE TO TRUE.                         
027205                                                                          
027206      IF PS-EOF-PBS-ERROR-FILE                                            
027207         CONTINUE                                                         
027208      ELSE                                                                
027210         ADD +1 TO PV-READ-COUNTER                                        
027300      END-IF.                                                             
030200                                                                          
030201******************************************************************        
030202**RECORDS THAT HAVE NOT BEEN VERIFIED WILL BE REWRITTEN TO THE ***        
030203**ERROR FILE. THE VERIFIED RECEIVERS WILL DROP OFF THE FILE               
030204******************************************************************        
030210 8000-REWRITE-PBS-ERROR-FILE.                                             
030300       WRITE OUTPUT-RECORD                                                
030400           FROM RC053-ASN-RECVR-PKSLIP-ERRREC                             
030500           ADD +1 TO PV-RECORDS-WRITTEN.                                  
030600                                                                          
035800 9000-EOJ-ROUTINE.                                                        
035900******************************************************************        
036000*  CLOSE OUTPUT FILE.                                                     
036100******************************************************************        
036200     CLOSE PACK-BY-STORE-ERROR-FILE                                       
036400           OUTPUT-FILE.                                                   
036500     MOVE PV-READ-COUNTER TO PV-DISPLAY-COUNT.                            
036600     DISPLAY 'TOTAL INPUT RECORDS READ '                                  
036601     PV-DISPLAY-COUNT.                                                    
036610     MOVE PV-VILN-CRE-COUNTER TO PV-DISPLAY-COUNT.                        
036620     DISPLAY 'NUMBER OF VIOLATIONS CREATED   ' PV-DISPLAY-COUNT.          
040292                                                                          
040300 9100-SQL-ABEND.                                                          
040400******************************************************************        
040500*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
040600*  ERROR OR WARNING CODE OCCURS.                                          
040700******************************************************************        
040800     DISPLAY '9100-SQL-ABEND'.                                            
040900     DISPLAY '** ABEND     **'.                                           
041000     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
041100     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
041200     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
041300                                                                          
041400******************************************************************        
041500* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
041600* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
041700* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
041800* FORMAT.                                                                 
041900******************************************************************        
042000     COPY DPPD004.                                                        
042100                                                                          
042200     MOVE +4013                  TO ABEND-CODE.                           
042300     CALL 'ILBOABN0' USING ABEND-CODE.                                    
