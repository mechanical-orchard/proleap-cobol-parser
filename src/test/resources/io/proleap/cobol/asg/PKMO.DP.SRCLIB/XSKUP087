       IDENTIFICATION DIVISION.                                         00010001
       PROGRAM-ID.      XSKUP087.                                       00020001
       AUTHOR.          KOHLS DEPARTMENT STORES.                        00030001
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040001
       DATE-WRITTEN.     9/23/2004.                                     00050001
       DATE-COMPILED.                                                   00060001
      ******************************************************************00070001
      **XS DOMAIN TABLE LOAD - INITIAL LOAD/RECOVERY FOR TABLE          00080001
      **                       XST_PRCHG                                00090001
      ******************************************************************00100001
      **THIS PROGRAM USES THE NEW PERM SYSTEM EXTRACT TO LOAD THE       00110001
      **XS DOMAIN TABLE.                                                00120001
      ******************************************************************00130001
      *DATE: 9/24/2004   MADE BY:NANCY MARTIN  INITIAL PROGRAM WRITE    00140001
      ******************************************************************00150001
       ENVIRONMENT DIVISION.                                            00160001
       CONFIGURATION SECTION.                                           00170001
       SOURCE-COMPUTER.        IBM-3090.                                00180001
       OBJECT-COMPUTER.        IBM-3090.                                00190001
       INPUT-OUTPUT SECTION.                                            00200001
       FILE-CONTROL.                                                    00210001
                                                                        00220001
           SELECT INPUT-FILE                                            00230001
                  ASSIGN TO INP01.                                      00240001
                                                                        00250001
       DATA DIVISION.                                                   00290001
       FILE SECTION.                                                    00300001
                                                                        00310001
       FD  INPUT-FILE                                                   00320001
           RECORDING MODE F                                             00330001
           BLOCK CONTAINS 0 RECORDS                                     00340001
           LABEL RECORDS ARE OMITTED                                    00350001
           DATA RECORD IS INPUT-RECORD.                                 00360001
       01  INPUT-RECORD                    PIC X(200).                  00370001
                                                                        00380001
       WORKING-STORAGE SECTION.                                         00380801
      ******************************************************************00380901
      **SWITCHES                                                        00381001
      ******************************************************************00382001
       01  PS-PROGRAM-SWITCHES.                                         00383001
           05  PS-END-OF-INPUT-FILE-SW         PIC X(01)   VALUE 'N'.   00384001
               88  PS-END-OF-INPUT-FILE                    VALUE 'Y'.   00385001
               88  PS-MORE-INPUT-FILE                      VALUE 'Y'.   00386001
      ******************************************************************00390001
      **TIMESTAMP VARIABLES                                             00400001
      ******************************************************************00410001
       01  SD-TIMESTAMP                        PIC  X(26)  VALUE SPACES.00420001
       01  FILLER REDEFINES SD-TIMESTAMP.                               00430001
           05  SD-CURR-DTE                     PIC  X(10).              00440001
           05  FILLER                          PIC  X(01).              00450001
           05  SD-HOUR                         PIC  X(02).              00460001
           05  FILLER                          PIC  X(01).              00470001
           05  SD-MINUTE                       PIC  X(02).              00480001
           05  FILLER                          PIC  X(01).              00490001
           05  SD-SECOND                       PIC  X(02).              00500001
           05  FILLER                          PIC  X(01).              00510001
           05  SD-MICROSEC                     PIC  X(06).              00520001
      ******************************************************************00530001
      **PROGRAM CONSTANTS                                               00540001
      ******************************************************************00550001
       01  PC-PROGRAM-CONSTANTS.                                        00560001
           05  PC-CURRENT-PROGRAM-NAME    PIC X(08)   VALUE 'XSKUP087'. 00570001
      ******************************************************************00580001
      **COPYBOOKS                                                       00590001
      ******************************************************************00600001
      ***INPUT FILE LAYOUT - NEW PERM EXTRACT                           00610001
           COPY PERD103.                                                00620001
                                                                        00630001
      ***VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        00640001
           COPY DPWS004.                                                00650001
      ***DB2 ABEND COPYBOOK                                             00660001
           COPY DPWS900.                                                00670001
      ***DB2 MESSAGE AREA                                               00680001
           COPY SQLCA2.                                                 00690001
      ******************************************************************00720001
      **TABLE INCLUSIONS                                                00730001
      ******************************************************************00740001
                                                                        00750001
      ***DB2 COMMUNICATIONS AREA                                        00760001
           EXEC SQL                                                     00770001
                INCLUDE SQLCA                                           00780001
           END-EXEC.                                                    00790001
                                                                        00800001
      ***TABLE XST_PRCHG                                                00801001
           EXEC SQL                                                     00802001
                INCLUDE XST00063                                        00803001
           END-EXEC.                                                    00804001
                                                                        00805001
      * PC- PROGRAM CONSTANTS                                           00960001
                                                                        00961001
      *----------------------------------------------------------       00962001
      * PV- PROGRAM VARIABLES                                           00963001
      *----------------------------------------------------------       00964001
       01  PV-DB2-FIELDS.                                               00965002
           05  PV-DB2-PRCHG-ID              PIC S9(11) COMP   VALUE 0.  00967002
      ******************************************************************00970301
      **NULL INDICATORS                                                 00970401
      ******************************************************************00970501
       01  PV-NULL-INDICATORS.                                          00970602
           05  PV-PRCHG-DESC-NULL              PIC S9(4) COMP.          00970705
           05  PV-PRCHG-DEPT-NULL              PIC S9(4) COMP.          00970805
           05  PV-ALL-RMNG-NULL                PIC S9(4) COMP.          00970905
           05  PV-APP-TMST-NULL                PIC S9(4) COMP.          00971005
      ******************************************************************00971201
      **PROGRAM COUNTERS                                                00971301
      ******************************************************************00971401
       01  PV-PROGRAM-COUNTER.                                          00971501
           05  PV-INPUT-RECORDS-READ      PIC 9(09)   VALUE ZEROS.      00971601
           05  PV-PRCHG-CNT               PIC 9(09)   VALUE ZEROS.      00971701
           05  PV-COMMIT-COUNTER          PIC 9(09)   VALUE ZEROS.      00972101
      ******************************************************************00973001
      **ERROR HANDLING                                                  00974001
      ******************************************************************00975001
       01  PV-MISC-ABEND-FIELDS.                                        00976001
           05  PV-PARAGRAPH-NAME            PIC X(30) VALUE SPACE.      00977001
           05  PV-OPERATION-MSG-1           PIC X(40) VALUE SPACE.      00978001
           05  PV-OPERATION-MSG-2           PIC X(40) VALUE SPACE.      00979001
           05  PV-DB2-OPERATION             PIC X(30) VALUE SPACE.      00980001
       01  ABEND-CODE                  PIC S9(4) VALUE ZEROS COMP SYNC. 00990001
           88  ABEND-4001-WRITE-ERROR            VALUE +4001.           01000001
           88  ABEND-4002-READ-ERROR             VALUE +4002.           01010001
           88  ABEND-4003-CTRL-CARD-ERROR        VALUE +4003.           01020001
           88  ABEND-4004-TABLE-ERROR            VALUE +4004.           01030001
           88  ABEND-4005-OPEN-ERROR             VALUE +4005.           01040001
           88  ABEND-4006-CLOSE-ERROR            VALUE +4006.           01050001
           88  ABEND-4007-SEQUENCE-ERROR         VALUE +4007.           01060001
           88  ABEND-4008-DATA-ERROR             VALUE +4008.           01070001
           88  ABEND-4009-KEY-DATA-ERROR         VALUE +4009.           01080001
           88  ABEND-4013-DB2-ERROR              VALUE +4013.           01090001
           88  ABEND-4019-CAL-SUB-ERROR          VALUE +4019.           01100001
           88  ABEND-4444-FORCED-ABEND           VALUE +4444.           01110001
                                                                        01120001
       PROCEDURE DIVISION.                                              01130001
      ***************************************************************** 01140001
                                                                        01150001
           PERFORM 1000-INITIALIZATION.                                 01160001
                                                                        01170001
           PERFORM 2000-PROCESS-RECORDS                                 01180001
             UNTIL PS-END-OF-INPUT-FILE.                                01190004
                                                                        01200001
           PERFORM 9999-WRAPUP.                                         01210001
           GOBACK.                                                      01220001
                                                                        01230001
       EJECT.                                                           01240001
      ******************************************************************01250001
      **INITIALIZATION AREA                                             01260001
      ******************************************************************01270001
       1000-INITIALIZATION.                                             01280001
                                                                        01290001
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             01300001
                                                                        01310001
           OPEN INPUT INPUT-FILE                                        01320001
                                                                        01340001
           PERFORM 1100-SELECT-DB2-TIME.                                01350001
           DISPLAY ' XSKUP087 START CURRENT TMST-', SD-TIMESTAMP.       01400002
                                                                        01410001
      *    * READ FIRST EXTRACT RECORD *                                01475001
           PERFORM 9999-READ-INPUT-FILE.                                01476001
           IF PS-END-OF-INPUT-FILE                                      01476105
              DISPLAY '** ABEND           **'                           01476205
              DISPLAY '** EMPTY LOAD FILE FOR PRCHG ---'                01476304
              PERFORM 9999-DISPLAY-STATISTICS                           01476404
              PERFORM 9999-SQL-ABEND-ROUTINE                            01476504
           END-IF.                                                      01476604
                                                                        01477001
       EJECT.                                                           01478001
      ******************************************************************01479001
      **RETRIEVE TIME FROM DB2                                          01480001
      ******************************************************************01490001
       1100-SELECT-DB2-TIME.                                            01500001
                                                                        01510001
           MOVE '1100-SELECT-DB2-TIME' TO PV-PARAGRAPH-NAME.            01520001
                                                                        01530001
           EXEC SQL                                                     01540001
                SELECT CURRENT TIMESTAMP                                01550001
                INTO  :SD-TIMESTAMP                                     01560001
                FROM   SYSIBM.SYSDUMMY1                                 01570001
           END-EXEC.                                                    01580001
                                                                        01590001
           IF  SQLCODE NOT EQUAL ZERO                                   01600001
               PERFORM 9999-SQL-ABEND-ROUTINE                           01610001
           END-IF.                                                      01620001
                                                                        01630001
       EJECT.                                                           01640001
      ******************************************************************02010001
      **PROCESSES INPUT FILE                                            02020001
      ******************************************************************02030001
       2000-PROCESS-RECORDS.                                            02040001
                                                                        02050001
           MOVE '2000-PROCESS-RECORDS' TO PV-PARAGRAPH-NAME.            02060001
           INITIALIZE PV-NULL-INDICATORS.                               02061001
                                                                        02061101
           INITIALIZE DCLXST00063                                       02061401
           MOVE PE103-PRCHG-ID         TO PV-DB2-PRCHG-ID               02061502
           MOVE PV-DB2-PRCHG-ID        TO XST00063-PRCHG-ID             02061602
           MOVE PE103-PRCHG-DESC       TO XST00063-PRCHG-DESC           02062402
           MOVE PE103-PRCHG-DESC-NI    TO PV-PRCHG-DESC-NULL            02062505
                                                                        02062601
           MOVE PE103-DEPT-NBR         TO XST00063-DEPT-NBR             02062801
           MOVE PE103-DEPT-NBR-NI      TO PV-PRCHG-DEPT-NULL            02062905
           MOVE PE103-EVNT-CTG-CDE     TO XST00063-EVNT-CTG-CDE         02063101
           MOVE PE103-PRCHG-TYP-CDE    TO XST00063-PRCHG-TYP-CDE        02063201
           MOVE PE103-PRCHG-ST-CDE     TO XST00063-PRCHG-ST-CDE         02063301
           MOVE PE103-ALL-RMNG-STR-IND TO XST00063-ALL-RMNG-STR-IND     02063401
           MOVE PE103-ALL-RMNG-STR-IND-NI TO PV-ALL-RMNG-NULL           02063505
           MOVE PE103-APP-TMST         TO XST00063-APP-TMST             02063605
           MOVE PE103-APP-TMST-NI      TO PV-APP-TMST-NULL              02063705
                                                                        02065001
NEMNTE***  XST00063-LAST-UPD-TMST RECIEVES CURRENT TIMESTAMP            02090003
           MOVE PE103-SOR-LAST-UPD-TMST TO XST00063-SOR-LAST-UPD-TMST   02091002
                                                                        02110001
           PERFORM 2100-INSERT-PRCHG-ROW                                02120001
                                                                        02140001
                                                                        02305201
           IF  PV-COMMIT-COUNTER >= 3000                                02305301
               PERFORM 9999-COMMIT-DATA                                 02305401
               INITIALIZE PV-COMMIT-COUNTER                             02305501
           END-IF.                                                      02305601
                                                                        02305701
           PERFORM 9999-READ-INPUT-FILE.                                02305801
                                                                        02305901
       EJECT.                                                           02306001
      ***************************************************************** 02307001
      **INSERT ROW INTO TABLE XST_PRCHG                                 02308001
      ***************************************************************** 02309001
       2100-INSERT-PRCHG-ROW.                                           02310001
                                                                        02320001
           MOVE '2100-INSERT-PRCHG-ROW' TO PV-PARAGRAPH-NAME.           02330001
                                                                        02340001
           EXEC SQL                                                     02350001
           INSERT INTO XST_PRCHG  (  PRCHG_ID                           02360001
                                   , PRCHG_DESC                         02370001
                                   , DEPT_NBR                           02390001
                                   , EVNT_CTG_CDE                       02400001
                                   , PRCHG_TYP_CDE                      02410001
                                   , PRCHG_ST_CDE                       02411001
                                   , ALL_RMNG_STR_IND                   02420001
                                   , APP_TMST                           02470101
                                   , SOR_LAST_UPD_TMST                  02471001
                                   , LAST_UPD_TMST                      02480001
                                   , LAST_ACTN_CDE)                     02491001
           VALUES        ( :XST00063-PRCHG-ID                           02500002
                         , :XST00063-PRCHG-DESC:PV-PRCHG-DESC-NULL      02501005
                         , :XST00063-DEPT-NBR:PV-PRCHG-DEPT-NULL        02510005
                         , :XST00063-EVNT-CTG-CDE                       02520002
                         , :XST00063-PRCHG-TYP-CDE                      02540002
                         , :XST00063-PRCHG-ST-CDE                       02550002
                         , :XST00063-ALL-RMNG-STR-IND                   02570002
                                             :PV-ALL-RMNG-NULL          02570105
                         , :XST00063-APP-TMST:PV-APP-TMST-NULL          02571005
                         , :XST00063-SOR-LAST-UPD-TMST                  02572002
                         , CURRENT TIMESTAMP                            02620002
                         , 'A')                                         02631002
           END-EXEC.                                                    02640001
                                                                        02650001
           EVALUATE TRUE                                                02660001
              WHEN SQLCODE = ZERO                                       02670001
                   ADD +1                       TO PV-PRCHG-CNT         02680001
                   ADD +1                       TO PV-COMMIT-COUNTER    02690001
              WHEN OTHER                                                02700001
                 DISPLAY '** NON ZERO SQL CODE ON INSERT FOR ---'       02710001
                 DISPLAY 'PRICE CHANGE ID:  '                           02710101
                                 XST00063-PRCHG-ID                      02711001
                 DISPLAY 'PRICE CHANGE DESC:'                           02712001
                                 XST00063-PRCHG-DESC                    02713001
                 DISPLAY 'PRCHG DESC NULL: ' PV-PRCHG-DESC-NULL         02713105
                 DISPLAY 'DEPARTMENT NUMBER:'                           02720001
                                 XST00063-DEPT-NBR                      02722001
                 DISPLAY 'PRCHG DEPT NULL: ' PV-PRCHG-DEPT-NULL         02722105
                 DISPLAY 'EVENT CTG CODE:   '                           02722201
                                 XST00063-EVNT-CTG-CDE                  02723001
                 DISPLAY 'PRCHG TYPE CODE:  '                           02723101
                                 XST00063-PRCHG-TYP-CDE                 02724001
                 DISPLAY 'PRCHG STAT CODE:  '                           02724101
                                 XST00063-PRCHG-ST-CDE                  02725001
                 DISPLAY 'ALL RMNG STR IND: '                           02725101
                                 XST00063-ALL-RMNG-STR-IND              02726001
                 DISPLAY 'ALL RMNG STR NULL:' PV-ALL-RMNG-NULL          02727005
                 DISPLAY 'APP TIMESTAMP:    '                           02740001
                                 XST00063-APP-TMST                      02741001
                 DISPLAY 'APP TIMESTAMP NULL:' PV-APP-TMST-NULL         02750005
                 DISPLAY 'SOR LAST MSG TMST:'                           02860001
                                 XST00063-SOR-LAST-UPD-TMST             02861001
                 DISPLAY 'LAST UPD TMST:    '                           02862001
                                 XST00063-LAST-UPD-TMST                 02862101
                                 '--- DB2 TMST VALUE'                   02862202
                 DISPLAY 'LAST ACTION CODE: ', 'A',                     02863002
                                 '--- HARD CODED VALUE'                 02864002
                 PERFORM 9999-DISPLAY-STATISTICS                        02870001
                 PERFORM 9999-SQL-ABEND-ROUTINE                         02880001
           END-EVALUATE.                                                02890001
                                                                        02900001
       EJECT.                                                           02910001
      ***************************************************************** 03240501
      **READ INPUT FILE                                                 03240601
      ***************************************************************** 03240701
       9999-READ-INPUT-FILE.                                            03240801
                                                                        03240901
           READ INPUT-FILE INTO PE103-RECORD                            03241001
                AT END                                                  03242001
                   SET PS-END-OF-INPUT-FILE TO TRUE                     03243001
           END-READ.                                                    03244001
                                                                        03245001
           IF PS-MORE-INPUT-FILE                                        03246001
              ADD +1 TO PV-INPUT-RECORDS-READ                           03247001
           END-IF.                                                      03248001
                                                                        03249001
       EJECT.                                                           03250001
      ****************************************************************  03352001
      *END OF PROGRAM ROUTINE                                           03353001
      ****************************************************************  03354001
       9999-WRAPUP.                                                     03355001
                                                                        03356001
           PERFORM 9999-COMMIT-DATA.                                    03357001
                                                                        03358001
           CLOSE INPUT-FILE.                                            03359001
                                                                        03360001
           PERFORM 9999-DISPLAY-STATISTICS.                             03370001
                                                                        03380001
       EJECT.                                                           03390001
      ****************************************************************  03400001
      *COMMIT DATA TO DB                                                03410001
      ****************************************************************  03420001
       9999-COMMIT-DATA.                                                03430001
                                                                        03440001
           EXEC SQL                                                     03450001
                COMMIT                                                  03460001
           END-EXEC.                                                    03470001
                                                                        03480001
103002     EVALUATE TRUE                                                03490001
103002        WHEN SQLCODE = ZERO                                       03500001
103002             CONTINUE                                             03510001
103002        WHEN OTHER                                                03520001
103002             PERFORM 9999-DISPLAY-STATISTICS                      03530001
103002             PERFORM 9999-SQL-ABEND-ROUTINE                       03540001
103002     END-EVALUATE.                                                03550001
                                                                        03560001
       EJECT.                                                           03570001
      ****************************************************************  03580001
      *DISPLAY PROGRAM STATISTICS                                       03590001
      ****************************************************************  03600001
       9999-DISPLAY-STATISTICS.                                         03610001
                                                                        03620001
           DISPLAY '*************************************************'. 03630001
           DISPLAY '* PROGRAM...............: ' PC-CURRENT-PROGRAM-NAME.03640001
           DISPLAY '* DATE/TIMESTAMP........: ' SD-TIMESTAMP.           03650001
           DISPLAY '* TOTAL RECORDS READ....: ' PV-INPUT-RECORDS-READ.  03660001
           DISPLAY '* PR CHG  INSERTS.......: ' PV-PRCHG-CNT.           03670001
           DISPLAY '*************************************************'. 03700001
                                                                        03710001
       EJECT.                                                           03720001
                                                                        03730001
      *----------------------------------------------------------       03740001
      * 9999-ABEND-ROUTINE.                                             03750001
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                  03760001
      *----------------------------------------------------------       03770001
       9999-ABEND-ROUTINE.                                              03780001
                                                                        03781001
           DISPLAY '** ABEND           **'.                             03782001
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  03783001
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        03784001
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       03785001
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       03786001
           CALL 'ILBOABN0' USING ABEND-CODE.                            03787001
                                                                        03788001
      *9999-EXIT.                                                       03789001
       EJECT.                                                           03789101
                                                                        03789201
      ******************************************************************03789301
      **SQL ABEND ROUTINE                                               03789401
      ******************************************************************03789501
       9999-SQL-ABEND-ROUTINE.                                          03789601
                                                                        03789701
           DISPLAY '** ABEND     **'.                                   03789801
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        03789901
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              03790001
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               03800001
      ******************************************************************03810001
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     03820001
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      03830001
      ******************************************************************03840001
           COPY DPPD004.                                                03850001
           SET ABEND-4013-DB2-ERROR TO TRUE.                            03860001
           CALL 'ILBOABN0' USING ABEND-CODE.                            03870001
                                                                        03880001
       EJECT.                                                           03890001
