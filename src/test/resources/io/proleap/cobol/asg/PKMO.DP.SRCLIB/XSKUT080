       IDENTIFICATION DIVISION.                                         00010003
       PROGRAM-ID.      XSKUT080.                                       00020003
       AUTHOR.          KOHLS DEPARTMENT STORES.                        00030003
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040003
       DATE-WRITTEN.    09/10/2004.                                     00050003
       DATE-COMPILED.                                                   00060003
      ******************************************************************00070003
      **XS DOMAIN TABLE INTEGRITY PROCESS - FOR TABLE XST_MAJ_CL        00080003
      ******************************************************************00090003
      **THIS PROGRAM VALIDATES THE DATA IN THE XS DOMAIN IS THE SAME    00100003
      **AS THE DATA IN THE SOR - RMS.                                   00110005
      ******************************************************************00120003
      *DATE:09/10/2004   MADE BY:JULIE TEWES INITIAL PROGRAM WRITE      00130003
      ******************************************************************00140003
       ENVIRONMENT DIVISION.                                            00150003
       CONFIGURATION SECTION.                                           00160003
       SOURCE-COMPUTER.        IBM-3090.                                00170003
       OBJECT-COMPUTER.        IBM-3090.                                00180003
       INPUT-OUTPUT SECTION.                                            00190003
       FILE-CONTROL.                                                    00200003
                                                                        00210003
           SELECT CONTROL-CARD                                          00220003
                  ASSIGN TO INP00.                                      00230003
           SELECT RMS-EXTRACT                                           00240005
                  ASSIGN TO INP01.                                      00250003
           SELECT XS-EXTRACT                                            00260003
                  ASSIGN TO INP02.                                      00270003
           SELECT DIFFERENCES-FILE                                      00280003
                  ASSIGN TO OUT01.                                      00290003
                                                                        00300003
       DATA DIVISION.                                                   00310003
       FILE SECTION.                                                    00320003
                                                                        00330003
       FD  CONTROL-CARD                                                 00340003
           RECORDING MODE F                                             00350003
           BLOCK CONTAINS 0 RECORDS                                     00360003
           LABEL RECORDS ARE OMITTED                                    00370003
           DATA RECORD IS CONTROL-CARD-RECORD.                          00380003
       01  CONTROL-CARD-RECORD             PIC X(080).                  00390003
                                                                        00400003
       FD  RMS-EXTRACT                                                  00410005
           RECORDING MODE F                                             00420003
           BLOCK CONTAINS 0 RECORDS                                     00430003
           LABEL RECORDS ARE OMITTED                                    00440003
           DATA RECORD IS RMS-RECORD.                                   00450005
       01  RMS-RECORD                      PIC X(042).                  00460006
                                                                        00470003
       FD  XS-EXTRACT                                                   00480003
           RECORDING MODE F                                             00490003
           BLOCK CONTAINS 0 RECORDS                                     00500003
           LABEL RECORDS ARE OMITTED                                    00510003
           DATA RECORD IS XS-RECORD.                                    00520003
       01  XS-RECORD                       PIC X(026).                  00530005
                                                                        00540003
       FD  DIFFERENCES-FILE                                             00550003
           RECORDING MODE F                                             00560003
           BLOCK CONTAINS 0 RECORDS                                     00570003
           LABEL RECORDS ARE OMITTED                                    00580003
           DATA RECORD IS DIFFERENCES-RECORD.                           00590003
       01  DIFFERENCES-RECORD              PIC X(100).                  00600005
                                                                        00610003
       WORKING-STORAGE SECTION.                                         00620003
      ******************************************************************00630003
      **SWITCHES                                                        00640003
      ******************************************************************00650003
       01  PS-PROGRAM-SWITCHES.                                         00660003
           05  PS-END-OF-RMS-EXTRACT-SW        PIC X(01) VALUE 'N'.     00670005
               88  PS-END-OF-RMS-EXTRACT                 VALUE 'Y'.     00680005
               88  PS-NOT-END-OF-RMS-EXTRACT             VALUE 'N'.     00690005
           05  PS-END-OF-XS-EXTRACT-SW         PIC X(01) VALUE 'N'.     00700003
               88  PS-END-OF-XS-EXTRACT                  VALUE 'Y'.     00710003
               88  PS-NOT-END-OF-XS-EXTRACT              VALUE 'N'.     00720003
           05  PS-ERROR-FOUND-SW               PIC X(01) VALUE 'N'.     00730003
               88  PS-ERROR-FOUND                        VALUE 'Y'.     00740003
               88  PS-ERROR-NOT-FOUND                    VALUE 'N'.     00750003
      ******************************************************************00760003
      **CONTROL CARD LAYOUT                                             00770003
      ******************************************************************00780003
       01  CONTROL-CARD-LAYOUT.                                         00790003
           05  CC-START-POINT                  PIC 9(05) VALUE ZEROS.   00800003
           05  FILLER                          PIC X(01) VALUE SPACES.  00810003
           05  CC-READ-FREQUENCY               PIC 9(05) VALUE ZEROS.   00820003
           05  FILLER                          PIC X(69) VALUE SPACES.  00830003
      ******************************************************************00840003
      **TIMESTAMP VARIABLES                                             00850003
      ******************************************************************00860003
       01  SD-TIMESTAMP                        PIC X(26) VALUE SPACES.  00870003
       01  FILLER REDEFINES SD-TIMESTAMP.                               00880003
           05  SD-CURR-DTE                     PIC X(10).               00890003
           05  FILLER                          PIC X(01).               00900003
           05  SD-HOUR                         PIC X(02).               00910003
           05  FILLER                          PIC X(01).               00920003
           05  SD-MINUTE                       PIC X(02).               00930003
           05  FILLER                          PIC X(01).               00940003
           05  SD-SECOND                       PIC X(02).               00950003
           05  FILLER                          PIC X(01).               00960003
           05  SD-MICROSEC                     PIC X(06).               00970003
      ******************************************************************00980003
      **PROGRAM CONSTANTS                                               00990003
      ******************************************************************01000003
       01  PC-PROGRAM-CONSTANTS.                                        01010003
           05  PC-CURRENT-PROGRAM-NAME         PIC X(08)                01020003
                                                       VALUE 'XSKUT080'.01030003
      ******************************************************************01040003
      **PROGRAM COUNTERS                                                01050003
      ******************************************************************01060003
       01  PC-PROGRAM-COUNTER.                                          01070003
           05  PC-RECORD-COUNT                 PIC S9(09) VALUE ZEROS   01080003
                                                          COMP-3.       01090003
           05  PC-RMS-EXTRACT-RECS-READ        PIC 9(09) VALUE ZEROS.   01100005
           05  PC-XS-EXTRACT-RECS-READ         PIC 9(09) VALUE ZEROS.   01110003
           05  PC-ERROR-RECORD-WRITTEN         PIC 9(09) VALUE ZEROS.   01120003
      ******************************************************************01130003
      **ERROR HANDLING                                                  01140003
      ******************************************************************01150003
       01  PV-MISC-ABEND-FIELDS.                                        01160003
           05  PV-PARAGRAPH-NAME            PIC X(30) VALUE SPACE.      01170003
           05  PV-OPERATION-MSG-1           PIC X(40) VALUE SPACE.      01180003
           05  PV-OPERATION-MSG-2           PIC X(40) VALUE SPACE.      01190003
           05  PV-DB2-OPERATION             PIC X(30) VALUE SPACE.      01200003
       01  ABEND-CODE                  PIC S9(4) VALUE ZEROS COMP SYNC. 01210003
           88  ABEND-4013-DB2-ERROR              VALUE +4013.           01220003
      ******************************************************************01230003
      **COPYBOOKS                                                       01240003
      ******************************************************************01250003
      ***RMS EXTRACT FILE LAYOUT                                        01260005
           COPY HMRD009.                                                01270003
      ***XS EXTRACT FILE LAYOUT                                         01280003
           COPY XSRD067.                                                01290003
      ***XS ERROR FILE LAYOUT                                           01300003
           COPY XSRD068.                                                01310003
      ***VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        01320003
           COPY DPWS004.                                                01330003
      ***DB2 ABEND COPYBOOK                                             01340003
           COPY DPWS900.                                                01350003
      ***DB2 MESSAGE AREA                                               01360003
           COPY SQLCA2.                                                 01370003
      ******************************************************************01380003
      **TABLE INCLUSIONS                                                01390003
      ******************************************************************01400003
      ***DB2 COMMUNICATIONS AREA                                        01410003
           EXEC SQL                                                     01420003
                INCLUDE SQLCA                                           01430003
           END-EXEC.                                                    01440003
      ***TABLE XST_MAJ_CL                                               01450003
           EXEC SQL                                                     01460003
                INCLUDE XST00071                                        01470005
           END-EXEC.                                                    01480003
                                                                        01490003
       PROCEDURE DIVISION.                                              01500003
      ***************************************************************** 01510003
       000-MAINLINE.                                                    01520003
                                                                        01530003
           PERFORM 1000-INITIALIZATION.                                 01540003
           PERFORM 2000-PROCESS-RECORDS                                 01550003
                   UNTIL PS-END-OF-RMS-EXTRACT                          01560005
                      OR PS-END-OF-XS-EXTRACT.                          01570003
           PERFORM 3000-PROCESS-EXTRA-RECORDS                           01580003
                   UNTIL PS-END-OF-RMS-EXTRACT                          01590005
                     AND PS-END-OF-XS-EXTRACT.                          01600003
           PERFORM 9999-WRAPUP.                                         01610003
           GOBACK.                                                      01620003
                                                                        01630003
       EJECT.                                                           01640003
      ******************************************************************01650003
      **INITIALIZATION AREA                                             01660003
      ******************************************************************01670003
       1000-INITIALIZATION.                                             01680003
                                                                        01690003
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             01700003
                                                                        01710003
           INITIALIZE PC-PROGRAM-COUNTER.                               01720003
                                                                        01730003
           OPEN INPUT  CONTROL-CARD                                     01740003
                       RMS-EXTRACT                                      01750005
                       XS-EXTRACT                                       01760003
                OUTPUT DIFFERENCES-FILE.                                01770003
                                                                        01780003
           PERFORM 1100-SELECT-DB2-TIME.                                01790003
           PERFORM 1200-SELECT-RECORD-COUNT.                            01800003
                                                                        01810003
           PERFORM 9999-READ-CONTROL-CARD.                              01820003
           PERFORM 9999-READ-RMS-EXTRACT CC-START-POINT TIMES.          01830005
           PERFORM 9999-READ-XS-EXTRACT  CC-START-POINT TIMES.          01840003
                                                                        01850003
       EJECT.                                                           01860003
      ******************************************************************01870003
      **RETRIEVE TIME FROM DB2                                          01880003
      ******************************************************************01890003
       1100-SELECT-DB2-TIME.                                            01900003
                                                                        01910003
           MOVE '1100-SELECT-DB2-TIME' TO PV-PARAGRAPH-NAME.            01920003
                                                                        01930003
           EXEC SQL                                                     01940003
                SELECT CURRENT TIMESTAMP                                01950003
                INTO  :SD-TIMESTAMP                                     01960003
                FROM   SYSIBM.SYSDUMMY1                                 01970003
           END-EXEC.                                                    01980003
                                                                        01990003
           EVALUATE TRUE                                                02000003
              WHEN SQLCODE = ZERO                                       02010003
                   CONTINUE                                             02020003
              WHEN OTHER                                                02030003
               PERFORM 9999-ABEND-ROUTINE                               02040003
           END-EVALUATE.                                                02050003
                                                                        02060003
       EJECT.                                                           02070003
      ******************************************************************02080003
      **RETRIEVE TIME FROM DB2                                          02090003
      ******************************************************************02100003
       1200-SELECT-RECORD-COUNT.                                        02110003
                                                                        02120003
           MOVE '1200-SELECT-RECORD-COUNT' TO PV-PARAGRAPH-NAME.        02130003
                                                                        02140003
           EXEC SQL                                                     02150003
                SELECT COUNT(*)                                         02160003
                INTO  :PC-RECORD-COUNT                                  02170003
                FROM   XST_MAJ_CL                                       02180003
           END-EXEC.                                                    02190003
                                                                        02200003
           EVALUATE TRUE                                                02210003
              WHEN SQLCODE = ZERO                                       02220003
                   CONTINUE                                             02230003
              WHEN OTHER                                                02240003
                   PERFORM 9999-ABEND-ROUTINE                           02250003
           END-EVALUATE.                                                02260003
                                                                        02270003
       EJECT.                                                           02280003
      ******************************************************************02290003
      **PROCESSES INPUT FILE                                            02300003
      ******************************************************************02310003
       2000-PROCESS-RECORDS.                                            02320003
                                                                        02330003
           MOVE '2000-PROCESS-RECORDS' TO PV-PARAGRAPH-NAME.            02340003
                                                                        02350003
           INITIALIZE XSRD068-MAJCLASS-ERR-RECORD.                      02360004
                                                                        02370003
           EVALUATE TRUE                                                02380003
              WHEN HM009-DEPT-NBR = XSRD067-DEPT-NBR                    02390003
                   PERFORM 2100-COMPARE-MAJ-CLASS                       02400003
              WHEN HM009-DEPT-NBR > XSRD067-DEPT-NBR                    02410003
                   PERFORM 9999-MOVE-XS-VALUES                          02420003
                   PERFORM 9999-WRITE-ERROR-RECORD                      02430003
                   PERFORM 9999-READ-XS-EXTRACT                         02440003
              WHEN HM009-DEPT-NBR < XSRD067-DEPT-NBR                    02450003
                   PERFORM 9999-MOVE-RMS-VALUES                         02460005
                   PERFORM 9999-WRITE-ERROR-RECORD                      02470003
                   PERFORM 9999-READ-RMS-EXTRACT                        02480005
           END-EVALUATE.                                                02490003
                                                                        02500003
       EJECT.                                                           02510003
      ******************************************************************02520003
      **COMPARES RECORDS MAJOR CLASS FIELDS                             02530003
      ******************************************************************02540003
       2100-COMPARE-MAJ-CLASS.                                          02550003
                                                                        02560003
           MOVE '2100-COMPARE-MAJ-CLASS' TO PV-PARAGRAPH-NAME.          02570003
                                                                        02580003
           EVALUATE TRUE                                                02590003
              WHEN HM009-MAJ-CL-NBR = XSRD067-MAJ-CL-NBR                02600003
                   PERFORM 2300-COMPARE-FIELDS                          02610003
                   PERFORM 9999-READ-XS-EXTRACT  CC-READ-FREQUENCY TIMES02620003
                   PERFORM 9999-READ-RMS-EXTRACT CC-READ-FREQUENCY TIMES02630005
              WHEN HM009-MAJ-CL-NBR > XSRD067-MAJ-CL-NBR                02640003
                   PERFORM 9999-MOVE-XS-VALUES                          02650003
                   PERFORM 9999-WRITE-ERROR-RECORD                      02660003
                   PERFORM 9999-READ-XS-EXTRACT                         02670003
              WHEN HM009-MAJ-CL-NBR < XSRD067-MAJ-CL-NBR                02680003
                   PERFORM 9999-MOVE-RMS-VALUES                         02690005
                   PERFORM 9999-WRITE-ERROR-RECORD                      02700003
                   PERFORM 9999-READ-RMS-EXTRACT                        02710005
           END-EVALUATE.                                                02720003
                                                                        02730003
       EJECT.                                                           02740003
      ******************************************************************02750003
      **COMPARES FIELDS BETWEEN FILES TO SEE IF ANY DIFFERENCES EXIST   02760003
      ******************************************************************02770003
       2300-COMPARE-FIELDS.                                             02780003
                                                                        02790003
           MOVE '2300-COMPARE-FIELDS' TO PV-PARAGRAPH-NAME.             02800003
                                                                        02810003
           INITIALIZE PS-ERROR-FOUND-SW.                                02820003
                                                                        02830003
           IF  XSRD067-MAJ-CL-NM    NOT EQUAL HM009-MAJ-CL-NM           02840003
               SET PS-ERROR-FOUND     TO TRUE                           02850003
           END-IF.                                                      02860003
                                                                        02870003
           IF  PS-ERROR-FOUND                                           02880003
               PERFORM 9999-MOVE-XS-VALUES                              02890003
               PERFORM 9999-MOVE-RMS-VALUES                             02900005
               PERFORM 9999-WRITE-ERROR-RECORD                          02910003
           END-IF.                                                      02920003
                                                                        02930003
       EJECT.                                                           02940003
      ******************************************************************02950003
      **COMPARES FIELDS BETWEEN FILES TO SEE IF ANY DIFFERENCES EXIST   02960003
      ******************************************************************02970003
       3000-PROCESS-EXTRA-RECORDS.                                      02980003
                                                                        02990003
           MOVE '3000-PROCESS-EXTRA-RECORDS' TO PV-PARAGRAPH-NAME.      03000003
                                                                        03010003
           INITIALIZE DIFFERENCES-RECORD.                               03020003
                                                                        03030003
           IF  PS-NOT-END-OF-RMS-EXTRACT                                03040005
               PERFORM 9999-MOVE-RMS-VALUES                             03050005
               PERFORM 9999-WRITE-ERROR-RECORD                          03060003
               PERFORM 9999-READ-RMS-EXTRACT                            03070005
           END-IF.                                                      03080003
                                                                        03090003
           IF  PS-NOT-END-OF-XS-EXTRACT                                 03100003
               PERFORM 9999-MOVE-XS-VALUES                              03110003
               PERFORM 9999-WRITE-ERROR-RECORD                          03120003
               PERFORM 9999-READ-XS-EXTRACT                             03130003
           END-IF.                                                      03140003
                                                                        03150003
       EJECT.                                                           03160003
      ***************************************************************** 03170003
      **READ CONTROL CARD FOR FREQUENCY OF COMPARE                      03180003
      ***************************************************************** 03190003
       9999-READ-CONTROL-CARD.                                          03200003
                                                                        03210003
           READ CONTROL-CARD INTO CONTROL-CARD-LAYOUT.                  03220003
                                                                        03230003
       EJECT.                                                           03240003
      ***************************************************************** 03250003
      **READ RMS EXTRACT FILE                                           03260005
      ***************************************************************** 03270003
       9999-READ-RMS-EXTRACT.                                           03280005
                                                                        03290003
           IF  PS-NOT-END-OF-RMS-EXTRACT                                03300005
               READ RMS-EXTRACT INTO HMRD009-RECORD                     03310005
                    AT END                                              03320003
                       SET PS-END-OF-RMS-EXTRACT TO TRUE                03330005
               END-READ                                                 03340003
           END-IF.                                                      03350003
                                                                        03360003
           IF  PS-NOT-END-OF-RMS-EXTRACT                                03370005
               ADD +1                       TO PC-RMS-EXTRACT-RECS-READ 03380005
           END-IF.                                                      03390003
                                                                        03400003
       EJECT.                                                           03410003
      ***************************************************************** 03420003
      **READ XS EXTRACT FILE                                            03430003
      ***************************************************************** 03440003
       9999-READ-XS-EXTRACT.                                            03450003
                                                                        03460003
           IF  PS-NOT-END-OF-XS-EXTRACT                                 03470003
               READ XS-EXTRACT INTO XSRD067-MAJCLASS-UNLOAD-FILE        03480004
                    AT END                                              03490003
                       SET PS-END-OF-XS-EXTRACT TO TRUE                 03500003
               END-READ                                                 03510003
           END-IF.                                                      03520003
                                                                        03530003
           IF  PS-NOT-END-OF-XS-EXTRACT                                 03540003
               ADD +1                       TO PC-XS-EXTRACT-RECS-READ  03550003
           END-IF.                                                      03560003
                                                                        03570003
       EJECT.                                                           03580003
      ***************************************************************** 03590003
      **MOVES XS EXTRACT VALUES TO ERROR RECORD                         03600003
      ***************************************************************** 03610003
       9999-MOVE-XS-VALUES.                                             03620003
                                                                        03630003
           MOVE XSRD067-DEPT-NBR      TO XSRD068-XS-DEPT-NBR.           03640003
           MOVE XSRD067-MAJ-CL-NBR    TO XSRD068-XS-MAJ-CL-NBR.         03650003
           MOVE XSRD067-MAJ-CL-NM     TO XSRD068-XS-MAJ-CL-NM.          03660004
                                                                        03670003
       EJECT.                                                           03680003
      ***************************************************************** 03690003
      **MOVES RMS EXTRACT VALUES TO ERROR RECORD                        03700005
      ***************************************************************** 03710003
       9999-MOVE-RMS-VALUES.                                            03720005
                                                                        03730003
           MOVE HM009-DEPT-NBR        TO XSRD068-RMS-DEPT-NBR.          03740005
           MOVE HM009-MAJ-CL-NBR      TO XSRD068-RMS-MAJ-CL-NBR.        03750005
           MOVE HM009-MAJ-CL-NM       TO XSRD068-RMS-MAJ-CL-NM.         03760005
                                                                        03770003
       EJECT.                                                           03780003
      ***************************************************************** 03790003
      **ERROR FILE                                                      03800003
      ***************************************************************** 03810003
       9999-WRITE-ERROR-RECORD.                                         03820003
                                                                        03830003
           WRITE DIFFERENCES-RECORD FROM XSRD068-MAJCLASS-ERR-RECORD.   03840004
                                                                        03850003
           ADD +1                       TO PC-ERROR-RECORD-WRITTEN.     03860003
                                                                        03870003
       EJECT.                                                           03880003
      ****************************************************************  03890003
      *END OF PROGRAM ROUTINE                                           03900003
      ****************************************************************  03910003
       9999-WRAPUP.                                                     03920003
                                                                        03930003
           CLOSE CONTROL-CARD                                           03940003
                 RMS-EXTRACT                                            03950005
                 XS-EXTRACT                                             03960003
                 DIFFERENCES-FILE.                                      03970003
                                                                        03980003
           PERFORM 9999-DISPLAY-STATISTICS.                             03990003
                                                                        04000003
           IF  PC-ERROR-RECORD-WRITTEN > 0                              04010003
               MOVE 04               TO RETURN-CODE                     04020003
           END-IF.                                                      04030003
                                                                        04040003
       EJECT.                                                           04050003
      ****************************************************************  04060003
      *DISPLAY PROGRAM STATISTICS                                       04070003
      ****************************************************************  04080003
       9999-DISPLAY-STATISTICS.                                         04090003
                                                                        04100003
           DISPLAY '*************************************************'. 04110003
           DISPLAY '* PROGRAM..............: ' PC-CURRENT-PROGRAM-NAME. 04120003
           DISPLAY '* DATE/TIMESTAMP.......: ' SD-TIMESTAMP.            04130003
           DISPLAY '* RECORDS IN TABLE.....: ' PC-RECORD-COUNT.         04140003
           DISPLAY '* RMS EXTRACT RECORDS..: ' PC-RMS-EXTRACT-RECS-READ.04150005
           DISPLAY '* XS EXTRACT RECORDS...: ' PC-XS-EXTRACT-RECS-READ. 04160003
           DISPLAY '* ERROR RECORDS WRITTEN: ' PC-ERROR-RECORD-WRITTEN. 04170003
           DISPLAY '*************************************************'. 04180003
                                                                        04190003
       EJECT.                                                           04200003
      ******************************************************************04210003
      **SQL ABEND ROUTINE                                               04220003
      ******************************************************************04230003
       9999-ABEND-ROUTINE.                                              04240003
                                                                        04250003
           DISPLAY '** ABEND     **'.                                   04260003
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        04270003
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              04280003
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               04290003
      ******************************************************************04300003
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     04310003
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      04320003
      ******************************************************************04330003
           COPY DPPD004.                                                04340003
           SET ABEND-4013-DB2-ERROR TO TRUE.                            04350003
           CALL 'ILBOABN0' USING ABEND-CODE.                            04360003
                                                                        04370003
       EJECT                                                            04380003
