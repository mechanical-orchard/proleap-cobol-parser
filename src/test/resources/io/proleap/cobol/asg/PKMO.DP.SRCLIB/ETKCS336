00001 *-----------------------*                                         06/22/94
00002  IDENTIFICATION DIVISION.                                         ETKCS336
00003 *-----------------------*                                            LV001
00004  PROGRAM-ID.    ETKCS336.                                         ETKCS336
00005  AUTHOR.        TESS BESTE.                                       ETKCS336
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          ETKCS336
00007  DATE-WRITTEN.  01-27-94.                                         ETKCS336
00008  DATE-COMPILED.                                                   ETKCS336
00009                                                                   ETKCS336
00010 *---------------------------------------------------------------* ETKCS336
00011 *            E336 - EDI NETWORK INQUIRY                           ETKCS336
00012 *                                                                 ETKCS336
00013 * THIS PROGRAM DISPLAYS ALL NETWORK INFORMATION ON THE TNETWRK    ETKCS336
00014 * DB2 TABLE FOR THE SELECTED EDI NETWORK.                         ETKCS336
00015 *---------------------------------------------------------------* ETKCS336
CICS  *  CHANGED 10/28/2022    BY BARB SCHULTZ    WR: CHG0277210      * ETKCS336
CICS  *                                                               * ETKCS336
CICS  *  ADDED COPYBOOK DPWS930 AND MADE THE CICS ARCHITECTURE CALL   * ETKCS336
CICS  *  DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.    * ETKCS336
00016                                                                   ETKCS336
00015 *---------------------------------------------------------------* ETKCS336
00016                                                                   ETKCS336
00017  ENVIRONMENT DIVISION.                                            ETKCS336
00018                                                                   ETKCS336
00019  DATA DIVISION.                                                   ETKCS336
00020                                                                   ETKCS336
00021  WORKING-STORAGE SECTION.                                         ETKCS336
00022                                                                   ETKCS336
00023  01  PC-PROGRAM-CONSTANTS.                                        ETKCS336
00024      05  PC-CURRENT-MAP-NAME          PIC  X(08)    VALUE         ETKCS336
00025                                                          'ET336A'.ETKCS336
00026      05  PC-CURRENT-MAPSET-NAME       PIC  X(08)    VALUE         ETKCS336
00027                                                         'ETKM336'.ETKCS336
00028      05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)    VALUE         ETKCS336
00029                                                        'ETKCS336'.ETKCS336
00030      05  PC-TYPE-CDE-DIALUP           PIC  X        VALUE '0'.    ETKCS336
00031      05  PC-TYPE-CDE-LEASE-LINE       PIC  X        VALUE '1'.    ETKCS336
00032      05  PC-TYPE-CDE-INTCONNECT       PIC  X        VALUE '2'.    ETKCS336
00033      05  PC-TYPE-CDE-DESC-DIALUP      PIC  X(12)    VALUE         ETKCS336
00034                                                         'DIAL UP'.ETKCS336
00035      05  PC-TYPE-CDE-DESC-LEASE-LINE  PIC  X(12)    VALUE         ETKCS336
00036                                                      'LEASE LINE'.ETKCS336
00037      05  PC-TYPE-CDE-DESC-INTCONNECT  PIC  X(12)    VALUE         ETKCS336
00038                                                    'INTERCONNECT'.ETKCS336
00039                                                                   ETKCS336
00040  01  PC-TSYMSG-NUMBERS.                                           ETKCS336
00041      05  PC-TSYMSG-00069              PIC 9(5)      VALUE 00069.  ETKCS336
00042      05  PC-TSYMSG-00070              PIC 9(5)      VALUE 00070.  ETKCS336
00043      05  PC-TSYMSG-00148              PIC 9(5)      VALUE 00148.  ETKCS336
00044                                                                   ETKCS336
00045  01  PS-PROGRAM-SUBSCRIPTS.                                       ETKCS336
00046      05  PS-SUB                       PIC S9(04)    VALUE +0      ETKCS336
00047                                                     COMP SYNC.    ETKCS336
00048                                                                   ETKCS336
00049  01  PS-PROGRAM-SWITCHES.                                         ETKCS336
00050      05  PS-LIST-END-SW               PIC X         VALUE 'N'.    ETKCS336
00051          88  PS-LIST-END                            VALUE 'Y'.    ETKCS336
00052          88  PS-NO-LIST-END                         VALUE 'N'.    ETKCS336
00053                                                                   ETKCS336
00054  01  PV-PROGRAM-VARIABLES.                                        ETKCS336
00055      05  PV-CICS-RESP                 PIC S9(04)    VALUE +0      ETKCS336
00056                                                     COMP SYNC.    ETKCS336
00057      05  PV-PHONE-NBR.                                            ETKCS336
00058          10  PV-PHONE-NBR-POS1        PIC X.                      ETKCS336
00059          10  PV-PHONE-NBR-POS2-4      PIC X(3).                   ETKCS336
00060          10  PV-PHONE-NBR-POS5-7      PIC X(3).                   ETKCS336
00061          10  PV-PHONE-NBR-POS8-11     PIC X(4).                   ETKCS336
00062                                                                   ETKCS336
00063      05  PV-FMTD-PHONE-NBR.                                       ETKCS336
00064          10  PV-FMTD-PHONE-NBR-POS1   PIC X.                      ETKCS336
00065          10  FILLER                   PIC X         VALUE '-'.    ETKCS336
00066          10  PV-FMTD-PHONE-NBR-POS2-4 PIC X(3).                   ETKCS336
00067          10  FILLER                   PIC X         VALUE '-'.    ETKCS336
00068          10  PV-FMTD-PHONE-NBR-POS5-7 PIC X(3).                   ETKCS336
00069          10  FILLER                   PIC X         VALUE '-'.    ETKCS336
00070          10  PV-FMTD-PHONE-NBR-POS8-11                            ETKCS336
00071                                       PIC X(4).                   ETKCS336
00072                                                                   ETKCS336
00073      05  PV-LINE-SPEED                PIC X(7).                   ETKCS336
00074      05  PV-LINE-SPEED-DIGIT          REDEFINES                   ETKCS336
00075          PV-LINE-SPEED                PIC X                       ETKCS336
00076                                       OCCURS 7.                   ETKCS336
00077                                                                   ETKCS336
00078      05  PV-SUB                       PIC S9(04)    COMP SYNC     ETKCS336
00079                                                     VALUE ZERO.   ETKCS336
00080      05  PV-SUB2                      PIC S9(04)    COMP SYNC.    ETKCS336
00081                                                                   ETKCS336
00082  01  PV-DB2-KEYS.                                                 ETKCS336
00083      15  PV-DB2-NETWK-ID              PIC  X.                     ETKCS336
00084                                                                   ETKCS336
00085 *---------------------------------------------------------------* ETKCS336
CICS  *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          * ETKCS336
00087 *---------------------------------------------------------------* ETKCS336
00088      COPY DPWS030.                                                ETKCS336
CICS       COPY DPWS930.                                                ETKCS336
00089                                                                   ETKCS336
00090 *---------------------------------------------------------------* ETKCS336
00091 *    STANDARD COMMAREA.                                         * ETKCS336
00092 *---------------------------------------------------------------* ETKCS336
00093      COPY DPWS020.                                                ETKCS336
00094      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                ETKCS336
00095                                                                   ETKCS336
00096 *---------------------------------------------------------------* ETKCS336
00097 *    INTER-APPLICATION COMMAREA.                                * ETKCS336
00098 *---------------------------------------------------------------* ETKCS336
00099          10  INTER-APPL-COMM-AREA       PIC X(200).               ETKCS336
00100      COPY ETWS336.                                                ETKCS336
00101                                                                   ETKCS336
00102 *---------------------------------------------------------------* ETKCS336
00103 *    SPECIFIC COMMMAREA FOR ETKCS336.                           * ETKCS336
00104 *---------------------------------------------------------------* ETKCS336
00105          10  ASC-SPECIFIC-COMMAREA.                               ETKCS336
00106              15  ASC-MAP-FIELDS.                                  ETKCS336
00107                  20  ASC-NETWORK-ID   PIC  X.                     ETKCS336
00108                  20  ASC-NETWORK-NAME PIC  X(15).                 ETKCS336
00109                  20  ASC-TYPE-CDE     PIC  X.                     ETKCS336
00110                  20  ASC-TYPE-CDE-DESC                            ETKCS336
00111                                       PIC  X(12).                 ETKCS336
00112                  20  ASC-LINE-SPEED   PIC  9(7).                  ETKCS336
00113                  20  ASC-LINE-SPEED-DIGIT                         ETKCS336
00114                                       REDEFINES                   ETKCS336
00115                      ASC-LINE-SPEED   PIC  X                      ETKCS336
00116                                       OCCURS 7.                   ETKCS336
00117                  20  ASC-USER-ID      PIC  X(8).                  ETKCS336
00118                  20  ASC-PASSWORD     PIC  X(8).                  ETKCS336
00119                  20  ASC-DIALUP-PHONE-NBR                         ETKCS336
00120                                       PIC  X(11).                 ETKCS336
00121                  20  ASC-CONTACT-PHONE-NBR                        ETKCS336
00122                                       PIC  X(11).                 ETKCS336
00123                  20  ASC-DFLT-INTCHG-ID-QUAL                      ETKCS336
00124                                       PIC  X(2).                  ETKCS336
00125                  20  ASC-DFLT-INTCHG-ID                           ETKCS336
00126                                       PIC  X(15).                 ETKCS336
00127              15  ASC-SEL-SUB          PIC S9(04)    COMP.         ETKCS336
00128              15  ASC-SEL-ITEM         PIC S9(04)    COMP.         ETKCS336
00129              15  ASC-NUMBER-OF-TS-ITEMS                           ETKCS336
00130                                       PIC S9(05)    COMP-3.       ETKCS336
00131              15  ASC-USE-SELECTION-QUEUE-IND                      ETKCS336
00132                                       PIC X.                      ETKCS336
00133                  88  ASC-USE-SELECTION-QUEUE        VALUE 'Y'.    ETKCS336
00134              15  ASC-SEL-QUEUE-NAME.                              ETKCS336
00135                  20  ASC-SEL-TERM-ID  PIC  X(4).                  ETKCS336
00136                  20  ASC-SEL-TASK-NUMBER                          ETKCS336
00137                                       PIC S9(5)           COMP-3. ETKCS336
00138                  20  ASC-SEL-SEQ      PIC  9(1).                  ETKCS336
00139          10  FILLER                   PIC  X(2758).               ETKCS336
00140                                                                   ETKCS336
00141 *---------------------------------------------------------------* ETKCS336
00142 *    SHEET SELECTION TSQ B QUEUE RECORD LAYOUT.                 * ETKCS336
00143 *---------------------------------------------------------------* ETKCS336
00144      COPY ETWS335.                                                ETKCS336
00145                                                                   ETKCS336
00146 *---------------------------------------------------------------* ETKCS336
00147 *    ABEND PROCESSING COPYBOOK.                                 * ETKCS336
00148 *---------------------------------------------------------------* ETKCS336
00149      COPY DPWS013.                                                ETKCS336
00150                                                                   ETKCS336
00151 *---------------------------------------------------------------* ETKCS336
00152 *    ATTRIBUTE SETTINGS COPYBOOK.                               * ETKCS336
00153 *---------------------------------------------------------------* ETKCS336
00154      COPY DPWS015.                                                ETKCS336
00155                                                                   ETKCS336
00156 *---------------------------------------------------------------* ETKCS336
00157 *    FUNCTION KEYS COPYBOOK.                                    * ETKCS336
00158 *---------------------------------------------------------------* ETKCS336
00159      COPY DPWS016.                                                ETKCS336
00160                                                                   ETKCS336
00161 *---------------------------------------------------------------* ETKCS336
00162 *    MAP LAYOUT                                                 * ETKCS336
00163 *---------------------------------------------------------------* ETKCS336
00164      COPY ETKM336.                                                ETKCS336
00165                                                                   ETKCS336
00166 *---------------------------------------------------------------* ETKCS336
00167 *    DB2 AREA FOR EDI NETWORK TABLE                             * ETKCS336
00168 *---------------------------------------------------------------* ETKCS336
00169      EXEC SQL                                                     ETKCS336
00170           INCLUDE TNETWRK                                         ETKCS336
00171      END-EXEC.                                                    ETKCS336
00172                                                                   ETKCS336
00173 *---------------------------------------------------------------* ETKCS336
00174 *    DB2 AREA FOR COMMUNICATIONS                                * ETKCS336
00175 *---------------------------------------------------------------* ETKCS336
00176      EXEC SQL                                                     ETKCS336
00177           INCLUDE SQLCA                                           ETKCS336
00178      END-EXEC.                                                    ETKCS336
00179  EJECT                                                            ETKCS336
00180  LINKAGE SECTION.                                                 ETKCS336
00181                                                                   ETKCS336
00182  01  DFHCOMMAREA.                                                 ETKCS336
00183      05  FILLER                       OCCURS  1 TO 4072 TIMES     ETKCS336
00184                                       DEPENDING ON EIBCALEN.      ETKCS336
00185          10  FILLER                   PIC X.                      ETKCS336
00186                                                                   ETKCS336
00187  EJECT                                                            ETKCS336
00188  PROCEDURE DIVISION.                                              ETKCS336
00189                                                                   ETKCS336
00190  0000-MAIN-MODULE.                                                ETKCS336
00191                                                                   ETKCS336
00192      INITIALIZE DP030-CICS-API-FIELDS.                            ETKCS336
00193      MOVE +1                      TO DP030-NUMBER-OF-MAPS.        ETKCS336
00194      MOVE PC-CURRENT-MAPSET-NAME  TO DP030-MAPSET-NAME.           ETKCS336
00195      MOVE PC-CURRENT-MAP-NAME     TO DP030-MAP-NAME (1).          ETKCS336
00196      SET DP030-RECEIVE-APPL-MAP   TO TRUE.                        ETKCS336
00197      MOVE LENGTH OF ET336AI       TO DP030-MAP-LENGTH (1).        ETKCS336
00198      MOVE 'PROGRAM ENTRY CALL'    TO DP013-MESSAGE-TEXT (1).      ETKCS336
00199      PERFORM 0001-CALL-CICS-ARCH-API.                             ETKCS336
00200                                                                   ETKCS336
00201      PERFORM 1000-CONTROL-PROCESSING.                             ETKCS336
00202                                                                   ETKCS336
00203      MOVE 'PROGRAM EXIT CALL'     TO DP013-MESSAGE-TEXT (1).      ETKCS336
00204      PERFORM 0001-CALL-CICS-ARCH-API.                             ETKCS336
00205                                                                   ETKCS336
00206                                                                   ETKCS336
00207 *----------------------------------------------------------------*ETKCS336
00208 * THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE AND   ETKCS336
00209 * WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.                 ETKCS336
00210 *----------------------------------------------------------------*ETKCS336
00211  0001-CALL-CICS-ARCH-API.                                         ETKCS336
00212                                                                   ETKCS336
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                      ETKCS336
00214                                     DFHCOMMAREA                   ETKCS336
00215                                     DP030-CICS-API-FIELDS         ETKCS336
00216                                     DP020-STANDARD-COMMAREA       ETKCS336
00217                                     ET336AI.                      ETKCS336
00218                                                                   ETKCS336
00219      IF  DP030-RC-CALL-SUCCESSFUL                                 ETKCS336
00220          CONTINUE                                                 ETKCS336
00221      ELSE                                                         ETKCS336
00222          SET DP013-NO-ROLLBACK                                    ETKCS336
00223              DP013-XCTL-DISPLAY-RESTART                           ETKCS336
00224              DP013-CICS-ABEND           TO TRUE                   ETKCS336
CICS           MOVE '0001-CALL-CICS-ARCH-API'                           ETKCS336
CICS                                     TO DP013-PARAGRAPH             ETKCS336
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE OETKCS336
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)      ETKCS336
00228          MOVE DP030-RETURN-CODE                                   ETKCS336
00229                                TO DP013-MESSAGE-TEXT (3)          ETKCS336
00230          PERFORM DP013-0000-PROCESS-ABEND                         ETKCS336
00231      END-IF.                                                      ETKCS336
00232                                                                   ETKCS336
00233                                                                   ETKCS336
00234 *----------------------------------------------------------------*ETKCS336
00235 * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *ETKCS336
00236 * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *ETKCS336
00237 *----------------------------------------------------------------*ETKCS336
00238  1000-CONTROL-PROCESSING.                                         ETKCS336
00239                                                                   ETKCS336
00240      EVALUATE TRUE                                                ETKCS336
00241          WHEN DP020-NEXT-ACT-INITIAL                              ETKCS336
00242              PERFORM 1100-PROCESS-INTER-APPL-COMM                 ETKCS336
00243              IF  DP020-NEXT-ACT-APPL-ERROR                        ETKCS336
00244                  CONTINUE                                         ETKCS336
00245              ELSE                                                 ETKCS336
00246                  PERFORM 4000-BUILD-INITIAL-PANEL                 ETKCS336
00247              END-IF                                               ETKCS336
00248                                                                   ETKCS336
00249          WHEN DP020-NEXT-ACT-READ-MAP                             ETKCS336
00250              PERFORM 2000-PROCESS-PANEL                           ETKCS336
00251                                                                   ETKCS336
00252          WHEN DP020-NEXT-ACT-RETURN                               ETKCS336
00253              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 ETKCS336
00254                                                                   ETKCS336
00255          WHEN OTHER                                               ETKCS336
00256              SET DP013-LOGIC-ABEND                                ETKCS336
00257                  DP013-NO-ROLLBACK TO TRUE                        ETKCS336
00258              MOVE '1000-CONTROL-PROCESSING'                       ETKCS336
00259                                TO  DP013-PARAGRAPH                ETKCS336
00260              MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'    ETKCS336
00261                                TO  DP013-MESSAGE-TEXT(1)          ETKCS336
00262              PERFORM DP013-0000-PROCESS-ABEND                     ETKCS336
00263      END-EVALUATE.                                                ETKCS336
00264                                                                   ETKCS336
00265                                                                   ETKCS336
00266 *----------------------------------------------------------------*ETKCS336
00267 * DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA.       ETKCS336
00268 *----------------------------------------------------------------*ETKCS336
00269  1100-PROCESS-INTER-APPL-COMM.                                    ETKCS336
00270                                                                   ETKCS336
00271      INITIALIZE ASC-SPECIFIC-COMMAREA.                            ETKCS336
00272      SET ASC-USE-SELECTION-QUEUE   TO TRUE.                       ETKCS336
00273      MOVE ET336-NUMBER-OF-TS-ITEMS TO ASC-NUMBER-OF-TS-ITEMS.     ETKCS336
00274      MOVE ET336-SEL-QUEUE-NAME     TO ASC-SEL-QUEUE-NAME.         ETKCS336
00275      PERFORM 1120-PREPARE-LIST-ENTRY.                             ETKCS336
00276                                                                   ETKCS336
00277                                                                   ETKCS336
00278 *----------------------------------------------------------------*ETKCS336
00279 * GET READY TO START AT THE FIRST ITEM ON THE LIST.               ETKCS336
00280 *----------------------------------------------------------------*ETKCS336
00281  1120-PREPARE-LIST-ENTRY.                                         ETKCS336
00282                                                                   ETKCS336
00283      MOVE ET335-MAX-ITEMS TO ASC-SEL-SUB.                         ETKCS336
00284      MOVE ZERO            TO ASC-SEL-ITEM.                        ETKCS336
00285      PERFORM 2140-GET-NEXT-NETWORK.                               ETKCS336
00286                                                                   ETKCS336
00287                                                                   ETKCS336
00288 *----------------------------------------------------------------*ETKCS336
00289 * RECEIVE THE MAP AND PERFORM THE APPROPRIATE PARAGRAPH BASED ON  ETKCS336
00290 * THE FUNCTION KEY SELECTED.                                      ETKCS336
00291 *----------------------------------------------------------------*ETKCS336
00292  2000-PROCESS-PANEL.                                              ETKCS336
00293                                                                   ETKCS336
00294      EVALUATE TRUE                                                ETKCS336
00295          WHEN DP020-SRC-AID = DP016-CLEAR                         ETKCS336
00296              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 ETKCS336
00297                                                                   ETKCS336
00298          WHEN DP020-FK-REFRESH (DP020-SRC-AID)                    ETKCS336
00299              MOVE ASC-NETWORK-ID TO PV-DB2-NETWK-ID               ETKCS336
00300              PERFORM 4000-BUILD-INITIAL-PANEL                     ETKCS336
00301                                                                   ETKCS336
00302          WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)             ETKCS336
00303              IF  ASC-USE-SELECTION-QUEUE                          ETKCS336
00304                  PERFORM 2140-GET-NEXT-NETWORK                    ETKCS336
00305                  IF PS-LIST-END                                   ETKCS336
00306                      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN         ETKCS336
00307                      SET PS-NO-LIST-END TO TRUE                   ETKCS336
00308                  ELSE                                             ETKCS336
00309                      MOVE ASC-NETWORK-ID TO PV-DB2-NETWK-ID       ETKCS336
00310                      PERFORM 4000-BUILD-INITIAL-PANEL             ETKCS336
00311                  END-IF                                           ETKCS336
00312              ELSE                                                 ETKCS336
00313 *                ---- BOTTOM OF LIST ----                         ETKCS336
00314                  MOVE PC-TSYMSG-00070 TO DP020-MSG-NUMBER         ETKCS336
00315                  SET DP020-MSG-INFORMATIONAL TO TRUE              ETKCS336
00316              END-IF                                               ETKCS336
00317                                                                   ETKCS336
00318          WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)             ETKCS336
00319              IF  ASC-USE-SELECTION-QUEUE                          ETKCS336
00320                  PERFORM 2150-GET-PREV-NETWORK                    ETKCS336
00321                  IF PS-LIST-END                                   ETKCS336
00322                      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN         ETKCS336
00323                      SET PS-NO-LIST-END TO TRUE                   ETKCS336
00324                  ELSE                                             ETKCS336
00325                      MOVE ASC-NETWORK-ID TO PV-DB2-NETWK-ID       ETKCS336
00326                      PERFORM 4000-BUILD-INITIAL-PANEL             ETKCS336
00327                  END-IF                                           ETKCS336
00328              ELSE                                                 ETKCS336
00329 *                ---- TOP OF LIST ----                            ETKCS336
00330                  MOVE PC-TSYMSG-00069 TO DP020-MSG-NUMBER         ETKCS336
00331                  SET DP020-MSG-INFORMATIONAL TO TRUE              ETKCS336
00332              END-IF                                               ETKCS336
00333                                                                   ETKCS336
00334          WHEN DP020-SRC-AID = DP016-ENTER                         ETKCS336
00335              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 ETKCS336
00336                                                                   ETKCS336
00337          WHEN OTHER                                               ETKCS336
00338              SET DP013-NO-ROLLBACK                                ETKCS336
00339                  DP013-XCTL-DISPLAY-RESTART                       ETKCS336
00340                  DP013-CICS-ABEND           TO TRUE               ETKCS336
00341              MOVE '2000-PROCESS-PANEL' TO DP013-PARAGRAPH         ETKCS336
00342              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      ETKCS336
00343                                    TO DP013-MESSAGE-TEXT (1)      ETKCS336
00344              PERFORM DP013-0000-PROCESS-ABEND                     ETKCS336
00345      END-EVALUATE.                                                ETKCS336
00346                                                                   ETKCS336
00347                                                                   ETKCS336
00348  2140-GET-NEXT-NETWORK.                                           ETKCS336
00349                                                                   ETKCS336
00350      ADD +1                       TO ASC-SEL-SUB.                 ETKCS336
00351      IF  ASC-SEL-SUB > ET335-MAX-ITEMS                            ETKCS336
00352          ADD +1                   TO ASC-SEL-ITEM                 ETKCS336
00353          MOVE +1                  TO ASC-SEL-SUB                  ETKCS336
00354      END-IF.                                                      ETKCS336
00355      IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                ETKCS336
00356          PERFORM 9100-READ-SEL-QUEUE                              ETKCS336
00357          IF  ET335-ITEM (ASC-SEL-SUB) > ZERO                      ETKCS336
00358              MOVE ET335-NETWORK-ID (ASC-SEL-SUB) TO ASC-NETWORK-IDETKCS336
00359                                                    PV-DB2-NETWK-IDETKCS336
00360              SET ET335-INQ (ASC-SEL-SUB) TO TRUE                  ETKCS336
00361              PERFORM 9200-REWRITE-SEL-QUEUE                       ETKCS336
00362          ELSE                                                     ETKCS336
00363              SUBTRACT +1 FROM ASC-SEL-SUB                         ETKCS336
00364 *            ---- BOTTOM OF LIST ----                             ETKCS336
00365              MOVE PC-TSYMSG-00070 TO DP020-MSG-NUMBER             ETKCS336
00366              SET DP020-MSG-INFORMATIONAL                          ETKCS336
00367                  PS-LIST-END             TO TRUE                  ETKCS336
00368          END-IF                                                   ETKCS336
00369      ELSE                                                         ETKCS336
00370          SUBTRACT +1 FROM ASC-SEL-ITEM                            ETKCS336
00371          MOVE ET335-MAX-ITEMS     TO ASC-SEL-SUB                  ETKCS336
00372 *        ---- BOTTOM OF LIST ----                                 ETKCS336
00373          MOVE PC-TSYMSG-00070     TO DP020-MSG-NUMBER             ETKCS336
00374          SET DP020-MSG-INFORMATIONAL                              ETKCS336
00375              PS-LIST-END             TO TRUE                      ETKCS336
00376      END-IF.                                                      ETKCS336
00377                                                                   ETKCS336
00378                                                                   ETKCS336
00379  2150-GET-PREV-NETWORK.                                           ETKCS336
00380                                                                   ETKCS336
00381      SUBTRACT +1 FROM ASC-SEL-SUB.                                ETKCS336
00382      IF  ASC-SEL-SUB < +1                                         ETKCS336
00383          SUBTRACT +1 FROM ASC-SEL-ITEM                            ETKCS336
00384          MOVE ET335-MAX-ITEMS     TO ASC-SEL-SUB                  ETKCS336
00385      END-IF.                                                      ETKCS336
00386                                                                   ETKCS336
00387      IF  ASC-SEL-ITEM > ZERO                                      ETKCS336
00388          PERFORM 9100-READ-SEL-QUEUE                              ETKCS336
00389          MOVE ET335-NETWORK-ID (ASC-SEL-SUB) TO ASC-NETWORK-ID    ETKCS336
00390                                               PV-DB2-NETWK-ID     ETKCS336
00391      ELSE                                                         ETKCS336
00392          MOVE +1                  TO ASC-SEL-ITEM                 ETKCS336
00393                                      ASC-SEL-SUB                  ETKCS336
00394 *        ---- TOP OF LIST ----                                    ETKCS336
00395          MOVE PC-TSYMSG-00069     TO DP020-MSG-NUMBER             ETKCS336
00396          SET DP020-MSG-INFORMATIONAL                              ETKCS336
00397              PS-LIST-END             TO TRUE                      ETKCS336
00398      END-IF.                                                      ETKCS336
00399                                                                   ETKCS336
00400                                                                   ETKCS336
00401 *----------------------------------------------------------------*ETKCS336
00402 * READ THE DATABASE AND STORE THE INFO IN THE ASC. DISPLAY THE    ETKCS336
00403 * PANEL.                                                          ETKCS336
00404 *----------------------------------------------------------------*ETKCS336
00405  4000-BUILD-INITIAL-PANEL.                                        ETKCS336
00406                                                                   ETKCS336
00407      INITIALIZE ASC-MAP-FIELDS.                                   ETKCS336
00408                                                                   ETKCS336
00409      PERFORM 4310-READ-TNETWRK.                                   ETKCS336
00410                                                                   ETKCS336
00411      EVALUATE TRUE                                                ETKCS336
00412          WHEN SQLCODE = ZERO                                      ETKCS336
00413              MOVE NETWRK-NETWK-ID     TO ASC-NETWORK-ID           ETKCS336
00414              MOVE NETWRK-NETWK-NME    TO ASC-NETWORK-NAME         ETKCS336
00415              MOVE NETWRK-NETWK-TYPE-CDE                           ETKCS336
00416                                       TO ASC-TYPE-CDE             ETKCS336
00417              EVALUATE TRUE                                        ETKCS336
00418                  WHEN ASC-TYPE-CDE = PC-TYPE-CDE-DIALUP           ETKCS336
00419                      MOVE PC-TYPE-CDE-DESC-DIALUP                 ETKCS336
00420                                       TO ASC-TYPE-CDE-DESC        ETKCS336
00421                  WHEN ASC-TYPE-CDE = PC-TYPE-CDE-LEASE-LINE       ETKCS336
00422                      MOVE PC-TYPE-CDE-DESC-LEASE-LINE             ETKCS336
00423                                       TO ASC-TYPE-CDE-DESC        ETKCS336
00424                  WHEN ASC-TYPE-CDE = PC-TYPE-CDE-INTCONNECT       ETKCS336
00425                      MOVE PC-TYPE-CDE-DESC-INTCONNECT             ETKCS336
00426                                       TO ASC-TYPE-CDE-DESC        ETKCS336
00427              END-EVALUATE                                         ETKCS336
00428              MOVE NETWRK-LNE-SPEED-NBR                            ETKCS336
00429                                       TO ASC-LINE-SPEED           ETKCS336
00430              MOVE NETWRK-KOHLS-UID    TO ASC-USER-ID              ETKCS336
00431              MOVE NETWRK-KOHLS-PASSWORD-ID                        ETKCS336
00432                                       TO ASC-PASSWORD             ETKCS336
00433              MOVE NETWRK-COMM-PHN-NBR TO ASC-DIALUP-PHONE-NBR     ETKCS336
00434              MOVE NETWRK-TECH-CNTCT-PHN-NBR                       ETKCS336
00435                                       TO ASC-CONTACT-PHONE-NBR    ETKCS336
00436              MOVE NETWRK-DFLT-INCHG-ID-DESC                       ETKCS336
00437                                       TO ASC-DFLT-INTCHG-ID-QUAL  ETKCS336
00438              MOVE NETWRK-DFLT-INCHG-ID                            ETKCS336
00439                                       TO ASC-DFLT-INTCHG-ID       ETKCS336
00440              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 ETKCS336
00441          WHEN SQLCODE = +100                                      ETKCS336
00442              SET DP020-NEXT-ACT-APPL-ERROR                        ETKCS336
00443                  DP020-MSG-FATAL           TO TRUE                ETKCS336
00444 *            ---- DATA DOES NOT EXIST ----                        ETKCS336
00445              MOVE PC-TSYMSG-00148 TO DP020-MSG-NUMBER             ETKCS336
00446      END-EVALUATE.                                                ETKCS336
00447                                                                   ETKCS336
00448                                                                   ETKCS336
00449  4310-READ-TNETWRK.                                               ETKCS336
00450                                                                   ETKCS336
00451      EXEC SQL                                                     ETKCS336
00452           SELECT NETWK_ID                                         ETKCS336
00453                , NETWK_NME                                        ETKCS336
00454                , NETWK_TYPE_CDE                                   ETKCS336
00455                , COMM_PHN_NBR                                     ETKCS336
00456                , LNE_SPEED_NBR                                    ETKCS336
00457                , KOHLS_UID                                        ETKCS336
00458                , KOHLS_PASSWORD_ID                                ETKCS336
00459                , DFLT_INCHG_ID                                    ETKCS336
00460                , DFLT_INCHG_ID_DESC                               ETKCS336
00461                , TECH_CNTCT_PHN_NBR                               ETKCS336
00462           INTO   :NETWRK-NETWK-ID                                 ETKCS336
00463                , :NETWRK-NETWK-NME                                ETKCS336
00464                , :NETWRK-NETWK-TYPE-CDE                           ETKCS336
00465                , :NETWRK-COMM-PHN-NBR                             ETKCS336
00466                , :NETWRK-LNE-SPEED-NBR                            ETKCS336
00467                , :NETWRK-KOHLS-UID                                ETKCS336
00468                , :NETWRK-KOHLS-PASSWORD-ID                        ETKCS336
00469                , :NETWRK-DFLT-INCHG-ID                            ETKCS336
00470                , :NETWRK-DFLT-INCHG-ID-DESC                       ETKCS336
00471                , :NETWRK-TECH-CNTCT-PHN-NBR                       ETKCS336
00472           FROM TNETWRK                                            ETKCS336
00473           WHERE NETWK_ID = :PV-DB2-NETWK-ID                       ETKCS336
00474      END-EXEC.                                                    ETKCS336
00475                                                                   ETKCS336
00476      EVALUATE TRUE                                                ETKCS336
00477          WHEN SQLCODE = ZERO                                      ETKCS336
00478          WHEN SQLCODE = +100                                      ETKCS336
00479              CONTINUE                                             ETKCS336
00480          WHEN SQLWARN0 NOT = SPACE                                ETKCS336
00481          WHEN SQLCODE  NOT = ZERO                                 ETKCS336
00482              MOVE '4310-READ-TNETWRK'   TO  DP013-PARAGRAPH       ETKCS336
00483              MOVE 'SELECT FROM TNETWRK' TO  DP013-MESSAGE-TEXT (1)ETKCS336
00484              MOVE SQLCA                 TO  DP013-SQLCA           ETKCS336
00485              SET DP013-DB2-ABEND        TO  TRUE                  ETKCS336
00486              PERFORM DP013-0000-PROCESS-ABEND                     ETKCS336
00487      END-EVALUATE.                                                ETKCS336
00488                                                                   ETKCS336
00489                                                                   ETKCS336
00490  4400-MOVE-COMMAREA-TO-SCREEN.                                    ETKCS336
00491                                                                   ETKCS336
00492      MOVE ASC-NETWORK-ID            TO ANTIDO.                    ETKCS336
00493      MOVE ASC-NETWORK-NAME          TO ANAMEO.                    ETKCS336
00494      MOVE ASC-TYPE-CDE              TO ATYPEO.                    ETKCS336
00495      MOVE ASC-TYPE-CDE-DESC         TO ATYPDSCO.                  ETKCS336
00496                                                                   ETKCS336
00497 *    *------------------------------------------------------------ETKCS336
00498 *    * REMOVE LEADING ZEROS AND LEFT JUSTIFY THE LINE SPEED.      ETKCS336
00499 *    *------------------------------------------------------------ETKCS336
00500      INSPECT ASC-LINE-SPEED                                       ETKCS336
00501          TALLYING PV-SUB FOR LEADING ZEROS.                       ETKCS336
00502                                                                   ETKCS336
00503      ADD  +1 TO PV-SUB.                                           ETKCS336
00504      MOVE +1 TO PV-SUB2.                                          ETKCS336
00505      MOVE SPACE TO PV-LINE-SPEED.                                 ETKCS336
00506                                                                   ETKCS336
00507      PERFORM                                                      ETKCS336
00508          VARYING PV-SUB FROM PV-SUB BY 1                          ETKCS336
00509          UNTIL PV-SUB > LENGTH OF ALSPEEDO                        ETKCS336
00510              MOVE ASC-LINE-SPEED-DIGIT (PV-SUB)                   ETKCS336
00511                TO  PV-LINE-SPEED-DIGIT (PV-SUB2)                  ETKCS336
00512              ADD +1 TO PV-SUB2                                    ETKCS336
00513      END-PERFORM.                                                 ETKCS336
00514                                                                   ETKCS336
00515      MOVE PV-LINE-SPEED             TO ALSPEEDO.                  ETKCS336
00516                                                                   ETKCS336
00517      MOVE ASC-USER-ID               TO AUSERIDO.                  ETKCS336
00518      MOVE ASC-PASSWORD              TO APASSWDO.                  ETKCS336
00519                                                                   ETKCS336
00520 *    *------------------------------------------------------------ETKCS336
00521 *    * FORMAT THE DIALUP PHONE NUMBER WITH HYPENS.                ETKCS336
00522 *    *------------------------------------------------------------ETKCS336
00523      MOVE ASC-DIALUP-PHONE-NBR      TO PV-PHONE-NBR.              ETKCS336
00524      MOVE PV-PHONE-NBR-POS1         TO PV-FMTD-PHONE-NBR-POS1.    ETKCS336
00525      MOVE PV-PHONE-NBR-POS2-4       TO PV-FMTD-PHONE-NBR-POS2-4.  ETKCS336
00526      MOVE PV-PHONE-NBR-POS5-7       TO PV-FMTD-PHONE-NBR-POS5-7.  ETKCS336
00527      MOVE PV-PHONE-NBR-POS8-11      TO PV-FMTD-PHONE-NBR-POS8-11. ETKCS336
00528      MOVE PV-FMTD-PHONE-NBR         TO ADIALNOO.                  ETKCS336
00529                                                                   ETKCS336
00530 *    *------------------------------------------------------------ETKCS336
00531 *    * FORMAT THE CONTACT PHONE NUMBER WITH HYPENS.               ETKCS336
00532 *    *------------------------------------------------------------ETKCS336
00533      MOVE ASC-CONTACT-PHONE-NBR     TO PV-PHONE-NBR.              ETKCS336
00534      MOVE PV-PHONE-NBR-POS1         TO PV-FMTD-PHONE-NBR-POS1.    ETKCS336
00535      MOVE PV-PHONE-NBR-POS2-4       TO PV-FMTD-PHONE-NBR-POS2-4.  ETKCS336
00536      MOVE PV-PHONE-NBR-POS5-7       TO PV-FMTD-PHONE-NBR-POS5-7.  ETKCS336
00537      MOVE PV-PHONE-NBR-POS8-11      TO PV-FMTD-PHONE-NBR-POS8-11. ETKCS336
00538      MOVE PV-FMTD-PHONE-NBR         TO ACNTCNOO.                  ETKCS336
00539                                                                   ETKCS336
00540      MOVE ASC-DFLT-INTCHG-ID-QUAL   TO AINTIDQO                   ETKCS336
00541      MOVE ASC-DFLT-INTCHG-ID        TO AINTIDO.                   ETKCS336
00542                                                                   ETKCS336
00543                                                                   ETKCS336
00544 *----------------------------------------------------------------*ETKCS336
00545 * READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *ETKCS336
00546 *----------------------------------------------------------------*ETKCS336
00547  9100-READ-SEL-QUEUE.                                             ETKCS336
00548                                                                   ETKCS336
00549      EXEC CICS                                                    ETKCS336
00550          READQ TS                                                 ETKCS336
00551              QUEUE (ASC-SEL-QUEUE-NAME)                           ETKCS336
00552              INTO  (ET335-NETWORK-ID-REC)                         ETKCS336
00553              ITEM  (ASC-SEL-ITEM)                                 ETKCS336
00554              RESP  (PV-CICS-RESP)                                 ETKCS336
00555      END-EXEC.                                                    ETKCS336
00556                                                                   ETKCS336
00557      IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                       ETKCS336
00558          CONTINUE                                                 ETKCS336
00559      ELSE                                                         ETKCS336
00560          SET DP013-LOGIC-ABEND                                    ETKCS336
00561              DP013-NO-ROLLBACK      TO TRUE                       ETKCS336
00562          MOVE '9100-READ-SEL-QUEUE' TO DP013-PARAGRAPH            ETKCS336
00563          MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'   ETKCS336
00564                                     TO DP013-MESSAGE-TEXT (1)     ETKCS336
00565          PERFORM DP013-0000-PROCESS-ABEND                         ETKCS336
00566      END-IF.                                                      ETKCS336
00567                                                                   ETKCS336
00568                                                                   ETKCS336
00569 *----------------------------------------------------------------*ETKCS336
00570 * UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *ETKCS336
00571 *----------------------------------------------------------------*ETKCS336
00572  9200-REWRITE-SEL-QUEUE.                                          ETKCS336
00573                                                                   ETKCS336
00574      EXEC CICS                                                    ETKCS336
00575           WRITEQ TS                                               ETKCS336
00576               QUEUE  (ASC-SEL-QUEUE-NAME)                         ETKCS336
00577               FROM   (ET335-NETWORK-ID-REC)                       ETKCS336
00578               ITEM   (ASC-SEL-ITEM)                               ETKCS336
00579               RESP   (PV-CICS-RESP)                               ETKCS336
00580               REWRITE                                             ETKCS336
00581      END-EXEC.                                                    ETKCS336
00582                                                                   ETKCS336
00583      IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                   ETKCS336
00584          SET DP013-CICS-ABEND                                     ETKCS336
00585              DP013-NO-ROLLBACK         TO TRUE                    ETKCS336
00586          MOVE '9200-REWRITE-SEL-QUEUE' TO DP013-PARAGRAPH         ETKCS336
00587          MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'   ETKCS336
00588                                        TO DP013-MESSAGE-TEXT (1)  ETKCS336
00589          PERFORM DP013-0000-PROCESS-ABEND                         ETKCS336
00590      END-IF.                                                      ETKCS336
00591                                                                   ETKCS336
00592                                                                   ETKCS336
00593 *----------------------------------------------------------------*ETKCS336
00594 *    ABEND PROCESSOR MODULE                                      *ETKCS336
00595 *----------------------------------------------------------------*ETKCS336
00596      COPY DPPD013.                                                ETKCS336
