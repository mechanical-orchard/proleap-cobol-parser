000100 IDENTIFICATION DIVISION.                                         00010000
000200                                                                  00020000
000300 PROGRAM-ID.      SAKUT056.                                       00030000
000400 AUTHOR.          LUKE SCHULTE.                                   00040000
000500 INSTALLATION.    KOHLS DEPARTMENT STORES.                        00050000
000600 DATE-WRITTEN.    08/18/2003.                                     00060000
000700 DATE-COMPILED.                                                   00070000
000800                                                                  00080000
000900******************************************************************00090000
001000*  MODULE TYPE:   DB2 COBOL2 BATCH.                               00100000
001300*                                                                         
001400*  DESCRIPTION:   **THIS PROGRAM IS USED TO VALIDATE              00130000
001500*                   SKU/UPC BEFORE DATA IS SENT TO EDW.           00140000
001600*                        -------                                          
001700*                                                                 00150000
001800*                 FILE    DESCRIPTION           COPYBOOK          00160000
001900*                 -----   -------------------   ---------         00170000
002700*                 DB2     TSKXREF (READ)                          00240000
002800*                 DB2     TUPC    (READ)                          00240000
003400*                 INP01   ITEM FILE             HARDCODED         00260000
004000*                 OUT01   ITEM FILE (VLDTD)     SARD024                   
003500*                 OUT02   AUDIT FILE            SARD033           00270000
004500*************************** CHANGE LOG ***************************00300000
004600* AUTH     BY       DATE      DESCRIPTION                         00310000
004700* -------  -------  --------  ------------------------------------00320000
004800* WR24707  TKMA543  08-18-03  CREATED.                            00330000
004900******************************************************************00420000
SLA001* DATE: 08/13/2008            WR/IR #: WR35157                   *       0
SLA001* WHO: DAN PAYNTER (REFER TO 'SLA001' FOR ALL CHANGES)           *       0
SLA001* WHY: IMPROVE EFFICIENCY OF THIS PROGRAM.                       *       0
SLA001******************************************************************       0
FL0902* DATE: 04/09/2009            WR/IR #: WR36454                   *       0
FL0902* WHO: JIM HUNTER  (REFER TO 'FL0902' FOR ALL CHANGES)           *       0
FL0902* WHY: INCREASE INPUT AND OUTPUT RECORD LENGTHS BY 15 BYTES TO   *       0
FL0902*      ALLOW FOR THE EXPANSION OF THE MDSE ID FROM 15 TO 30      *       0
FL0902*      DIGITS.                                                   *       0
PCIEDW******************************************************************        
PCIEDW* DATE: 09/21/2009  WR/IR: WR37364                                        
PCIEDW* WHO: STEVEN NOWAK                                                       
PCIEDW* WHY: PROGRAM WAS RENAMED FROM MKKUT035.                                 
PCIEDW******************************************************************        
C43001* DATE: 06/09/2016  CHG : CHG0143001                             *        
C43001* WHO: COGNIZANT                                                 *        
C43001* WHY: ADDED 'WITH UR' CONDITION IN THE CURSOR TO AVOID DEADLOCK *        
C43001******************************************************************        
005000* DATE: 02/14/2023  CHG : CHG0281369                             *        
005000* WHO: JEAN KURK                                                 *        
005000* WHY: CHANGE CALL OF UPC CHECK DIGIT ROUTINE TO BE DYNAMIC      *        
005000******************************************************************        
005000                                                                  00430000
005100 ENVIRONMENT DIVISION.                                            00440000
005200 CONFIGURATION SECTION.                                           00450000
005300                                                                  00460000
005400 SOURCE-COMPUTER.        IBM-3090.                                00470000
005500 OBJECT-COMPUTER.        IBM-3090.                                00480000
005600                                                                  00490000
005700 INPUT-OUTPUT SECTION.                                            00500000
005800 FILE-CONTROL.                                                    00510000
005900                                                                  00520000
006000     SELECT CW-EXTRACT-ITM-IN  ASSIGN TO INP01.                   00530000
006500     SELECT CW-EXTRACT-ITM-OUT ASSIGN TO OUT01.                   00580035
006810     SELECT CW-EXTRACT-AUD     ASSIGN TO OUT02.                   00610068
006900                                                                  00620000
007000 DATA DIVISION.                                                   00630000
007100 FILE SECTION.                                                    00640000
007200                                                                  00650000
010300 FD  CW-EXTRACT-ITM-IN                                            00960000
010400     RECORDING MODE IS F                                          00970000
010500     LABEL RECORDS ARE STANDARD                                   00980000
010600     BLOCK CONTAINS 0 CHARACTERS.                                 00990000
FL0902 01  CW-EXTRACT-ITM-IN-REC           PIC X(294).                  01000037
                                                                                
010300 FD  CW-EXTRACT-ITM-OUT                                           00960000
010400     RECORDING MODE IS F                                          00970000
010500     LABEL RECORDS ARE STANDARD                                   00980000
010600     BLOCK CONTAINS 0 CHARACTERS.                                 00990000
FL0902 01  CW-EXTRACT-ITM-OUT-REC          PIC X(294).                  01000037
                                                                                
012510 FD  CW-EXTRACT-AUD                                               01140068
012520     RECORDING MODE IS F                                          01150068
012530     LABEL RECORDS ARE STANDARD                                   01160068
012540     BLOCK CONTAINS 0 CHARACTERS.                                 01170068
012550 01  CW-EXTRACT-AUD-REC              PIC X(137).                  01180068
012700******************************************************************01200000
012800*                       WORKING STORAGE                           01210000
012900******************************************************************01220000
013000 WORKING-STORAGE SECTION.                                         01230000
013100                                                                  01240000
013200 01  W-START                         PIC  X(40)                   01250000
013300     VALUE 'SAKUT056 - WORKING STORAGE BEGINS HERE'.              01260000
013400                                                                  01270000
013500                                                                  01280000
013600******************************************************************01290000
013700*                         SWITCHES                                01300000
013800******************************************************************01310000
013900 01  PS-PROGRAM-SWITCHES.                                         01320000
014000                                                                  01330000
014700     05  PS-END-OF-ITM-IN-SW         PIC  X(01)  VALUE 'N'.       01400000
014800         88  PS-END-OF-ITM-IN                    VALUE 'Y'.       01410000
           05  PS-END-OF-SKU-UPC-SW        PIC  X(01)  VALUE 'N'.               
               88  PS-END-OF-SKU-UPC                   VALUE 'Y'.               
           05  PS-DEPT-CL-ERR-SW           PIC  X(01)  VALUE 'N'.               
               88  PS-DEPT-CL-ERR                      VALUE 'Y'.               
           05  PS-TSKXREF-FOUND-SW         PIC  X(01)  VALUE 'N'.               
               88  PS-TSKXREF-FOUND                    VALUE 'Y'.               
           05  PS-DUMMY-SKU-SW             PIC  X(01)  VALUE 'N'.               
               88  PS-DUMMY-SKU                        VALUE 'Y'.               
           05  PS-AUDIT-SW                 PIC  X(01)  VALUE SPACES.            
               88  PS-AUDIT-OLD                        VALUE 'O'.               
               88  PS-AUDIT-NEW                        VALUE 'N'.               
           05  PS-SKU-UPC-CHNG-SW          PIC  X(01)  VALUE 'N'.               
               88  PS-SKU-UPC-CHNG                     VALUE 'Y'.               
           05  PS-AUDIT-OFF-SW             PIC  X(01)  VALUE 'N'.               
               88  PS-AUDIT-OFF                        VALUE 'Y'.               
016500                                                                  01500000
016600     05  PV-DB2-TABLE-NAME           PIC  X(12)  VALUE SPACES.    01510028
017810         88  TUPC-TBL                            VALUE 'TUPC'.    01600068
018000                                                                  01620028
018100     05  PV-DB2-OPERATION-MSG        PIC  X(60)  VALUE SPACES.    01630028
018200         88  OPEN-CRSR                           VALUE            01640028
018300                                                 'CURSOR OPEN'.   01650028
018400         88  FETCH-CRSR                          VALUE            01660028
018500                                                 'CURSOR FETCH'.  01670028
018600         88  CLOSE-CRSR                          VALUE            01680028
018700                                                 'CURSOR CLOSE'.  01690028
018800         88  SELECT-TBL                          VALUE            01700061
018900                                                 'SELECT'.        01710061
019000                                                                  01720061
013600******************************************************************01290000
013700*                         VARIABLES                               01300000
013800******************************************************************01310000
024300 01  PV-PROGRAM-VARIABLES.                                        02100000
019100     05  PV-PARAGRAPH-NAME           PIC X(30)  VALUE SPACES.     01730028
024400     05  PV-SQLCODE                  PIC +9(04) VALUE ZEROES.     02110000
TJHTMP     05  PV-SKU-VALIDATE-ERROR       PIC X(1)   VALUE SPACE.      02110000
           05  PV-PREV-SKU-NBR             PIC S9(8)  VALUE ZEROES.             
           05  PV-PREV-UPC-NBR             PIC S9(15) VALUE ZEROES.             
           05  PV-SAVE-SKU-NBR             PIC S9(8)  VALUE ZEROES.             
           05  PV-SAVE-ITM-NBR             PIC S9(15) VALUE ZEROES.             
           05  PV-SAVE-UPC-NBR             PIC S9(15) VALUE ZEROES.             
           05  PV-SAVE-AUDIT-CDE           PIC X(1)   VALUE SPACES.             
           05  PV-UPC-UPC-NBR              PIC S9(15) VALUE ZEROES.             
           05  PV-SKXREF-ITM-NBR           PIC S9(15) VALUE ZEROES.             
           05  PV-SKXREF-SKU               PIC S9(8)  VALUE ZEROES.             
           05  PV-DUMMY-SKU-NBR.                                                
               10 FILLER                   PIC X(6).                            
               10 PV-DUMMY-SKU-LAST-3      PIC X(3).                            
                                                                                
019800******************************************************************01740000
019900*                        ACCUMULATORS                             01750000
020000******************************************************************01760000
020100 01  PA-PROGRAM-ACCUMULATORS.                                     01770000
020200                                                                  01780000
           05  PA-SKU-UPC-READ-COUNT       PIC S9(9) VALUE ZEROES COMP.         
           05  PA-KOHLS-SWPSTKS-ITM-COUNT  PIC S9(9) VALUE ZEROES COMP.         
           05  PA-SKU-RENUM-COUNT          PIC S9(9) VALUE ZEROES COMP.         
           05  PA-SKU-INVLD-DUMMY-COUNT    PIC S9(9) VALUE ZEROES COMP.         
           05  PA-SKU-GIFT-CARD-COUNT      PIC S9(9) VALUE ZEROES COMP.         
           05  PA-SKU-DELTD-COUNT          PIC S9(9) VALUE ZEROES COMP.         
           05  PA-UPC-INVALID-COUNT        PIC S9(9) VALUE ZEROES COMP.         
           05  PA-DEPT-CL-INVALID-COUNT    PIC S9(9) VALUE ZEROES COMP.         
021500                                                                  01880000
021700     05  PA-ITM-READ-COUNT           PIC S9(9) VALUE ZEROES COMP. 01900055
021700     05  PA-ITM-WRITE-COUNT          PIC S9(9) VALUE ZEROES COMP. 01900055
022710     05  PA-AUD-WRITE-COUNT          PIC S9(9) VALUE ZEROES COMP. 01960068
           05  PA-AUD-CHECK-COUNT          PIC S9(9) VALUE ZEROES COMP.         
           05  PA-VLDTD-UPC-COUNT          PIC S9(9) VALUE ZEROES COMP.         
023100******************************************************************01980000
023200*                          CONSTANTS                              01990000
023300******************************************************************02000000
023400 01  PC-PROGRAM-CONSTANTS.                                        02010000
023500     05  PC-PROGRAM-NAME             PIC  X(08)  VALUE 'SAKUT056'.02020000
023600                                                                  02030000
023700     05  PC-SQL-SUCCESSFUL           PIC S9(04)  VALUE ZEROES.    02040028
023800     05  PC-SQL-ROW-NOT-FOUND        PIC S9(04)  VALUE +100.      02050034
023810     05  PC-KOHLS-SWPSTKS-SKU        PIC 9(08)  VALUE 83021001.   02050034
023810     05  PC-GIFT-CARD                PIC 9(08)  VALUE 98300000.   02050034
023810     05  PC-GIFT-CERT                PIC 9(08)  VALUE 98100000.   02050034
023900                                                                  02060000
029500******************************************************************02600000
029600*               SYTEM TIME AND DATE VARIABLES                     02610000
029700******************************************************************02620000
029800 01  SYS-DATE-TIME.                                               02630000
029900     05  SYS-DATE                    PIC  X(08)  VALUE SPACES.    02640000
030000     05  SYS-TIME                    PIC  X(08)  VALUE SPACES.    02650000
030100                                                                  02660000
030200 01  ST-SYS-HHMM                     PIC  9(04)  VALUE ZEROES.    02670000
030300                                                                  02680000
030400 01  ST-BEG-TIME.                                                 02690000
030500     05  ST-BEG-HR                   PIC  9(02)  VALUE ZEROES.    02700000
030600     05  FILLER                      PIC  X(01)  VALUE ':'.       02710000
030700     05  ST-BEG-MN                   PIC  9(02)  VALUE ZEROES.    02720000
           05  FILLER                      PIC  X(01)  VALUE ':'.               
030700     05  ST-BEG-SEC                  PIC  9(02)  VALUE ZEROES.    02720000
030800                                                                  02730000
030900 01  ST-END-TIME.                                                 02740000
031000     05  ST-END-HR                   PIC  9(02)  VALUE ZEROES.    02750000
031100     05  FILLER                      PIC  X(01)  VALUE ':'.       02760000
031200     05  ST-END-MN                   PIC  9(02)  VALUE ZEROES.    02770000
           05  FILLER                      PIC  X(01)  VALUE ':'.               
030700     05  ST-END-SEC                  PIC  9(02)  VALUE ZEROES.    02720000
031300                                                                  02780000
032100 01  SD-PROC-DATE                    PIC  9(08)  VALUE ZEROES.    02860000
                                                                                
032200 01  SD-SYS-DATE.                                                 02870000
032300     05  SD-CC                       PIC  9(02)  VALUE ZEROES.    02880000
032400     05  SD-YY                       PIC  9(02)  VALUE ZEROES.    02890000
032500     05  FILLER                      PIC  X(01)  VALUE '-'.       02900000
032600     05  SD-MM                       PIC  9(02)  VALUE ZEROES.    02910000
032700     05  FILLER                      PIC  X(01)  VALUE '-'.       02920000
032800     05  SD-DD                       PIC  9(02)  VALUE ZEROES.    02930000
032900                                                                  02940000
033000******************************************************************02950000
033100*                        ERROR HANDLING                           02960000
033200******************************************************************02970000
033300 01  ABEND-CODE                  PIC S9(4) VALUE ZEROS COMP SYNC. 02980000
033400     88  ABEND-4001-WRITE-ERROR            VALUE +4001.           02990000
033500     88  ABEND-4002-READ-ERROR             VALUE +4002.           03000000
033600     88  ABEND-4003-CTRL-CARD-ERROR        VALUE +4003.           03010000
033700     88  ABEND-4004-TABLE-ERROR            VALUE +4004.           03020000
033800     88  ABEND-4005-OPEN-ERROR             VALUE +4005.           03030000
033900     88  ABEND-4006-CLOSE-ERROR            VALUE +4006.           03040000
034000     88  ABEND-4007-SEQUENCE-ERROR         VALUE +4007.           03050000
034100     88  ABEND-4008-DATA-ERROR             VALUE +4008.           03060000
034200     88  ABEND-4009-CARD-ERROR             VALUE +4009.           03070000
034500     88  ABEND-4013-DB2-ERROR              VALUE +4013.           03100000
034600     88  ABEND-4019-CAL-SUB-ERROR          VALUE +4019.           03110000
034700     88  ABEND-4444-FORCED-ABEND           VALUE +4444.           03120000
034800                                                                  03130000
035000******************************************************************03150000
035100*                       RECORD LAYOUTS                            03160000
035200******************************************************************03170000
           COPY SARD033.                                                        
           COPY SARD024.                                                        
036100                                                                  03260012
036200******************************************************************03270000
036300* DPWS004 VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004 03280000
036400******************************************************************03290000
036500     COPY DPWS004.                                                03300000
036600                                                                  03310000
036200******************************************************************03270000
036300* DPWS040I VARIABLES REQUIRED FOR DUMMY SKU ROUTINE               03280000
036400******************************************************************03290000
036500     COPY DPWS040I.                                               03300000
036600                                                                  03310000
036700******************************************************************03320000
036800* PARAMETER LIST FOR THE CALENDAR ROUTINE                         03330000
036900******************************************************************03340000
037000     COPY DPWS500.                                                03350000
037100                                                                  03360000
037200******************************************************************03370000
037300* DB2 MESSAGE AREA                                                03380000
037400******************************************************************03390000
037500     COPY SQLCA2.                                                 03400000
037600                                                                  03410000
037700******************************************************************03420000
037800* STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                 03430000
037900******************************************************************03440000
038000     EXEC SQL                                                     03450000
038100         INCLUDE SQLCA                                            03460000
038200     END-EXEC.                                                    03470000
                                                                                
048780******************************************************************04380168
048790* SKU CROSS-REFERENCE TABLE                                       04381068
048791******************************************************************04382068
048792     EXEC SQL                                                     04383068
048793         INCLUDE TSKXREF                                          04384068
048794     END-EXEC.                                                    04385068
                                                                                
048780******************************************************************04380168
048790* UPC TABLE                                                       04381068
048791******************************************************************04382068
048792     EXEC SQL                                                     04383068
048793         INCLUDE TUPC                                             04384068
048794     END-EXEC.                                                    04385068
                                                                                
068000******************************************************************06900000
068100* VLDT SKU/UPC CURSOR                                             06910000
068200******************************************************************06920000
           EXEC SQL                                                             
              DECLARE VLDT-SKU-UPC-CRSR CURSOR FOR                              
              SELECT                                                            
                 B.UPC_NBR                                                      
                ,B.UPC_TYP_CDE                                                  
                ,C.ITM_NBR                                                      
                ,C.SKU                                                          
SLA001        FROM TUPC    B,                                                   
                   TSKXREF C                                                    
SLA001        WHERE B.UPC_NBR     = :UPC-UPC-NBR                                
              AND   B.EFF_END_DTE IS NULL                                       
              AND   B.UPC_TYP_CDE IN ('I','V')                                  
              AND   B.INTR_UPC_ID = C.INTR_UPC_ID                               
              AND   C.SKU_STAT_CDE NOT IN ('00','98')                           
              ORDER BY B.UPC_TYP_CDE DESC                                       
C43001        WITH UR                                                           
SLA001     END-EXEC.                                                    04385068
                                                                                
                                                                                
070200 PROCEDURE DIVISION.                                              07120068
070300******************************************************************07130000
070400* MAIN DRIVER                                                     07140000
070500******************************************************************07150000
070600 0000-MAINLINE.                                                   07160000
070700                                                                  07170000
071200     PERFORM A1000-INITIALIZE.                                    07220001
                                                                                
           PERFORM D1000-READ-ITM-IN.                                           
                                                                                
           PERFORM B1000-PROCESS-ITM-IN                                         
071100        UNTIL PS-END-OF-ITM-IN.                                   07210000
                                                                                
085100     PERFORM E1000-CONTROLS.                                      07520038
                                                                                
071200     PERFORM A3000-CLOSE.                                         07220001
071300                                                                  07230000
071400     GOBACK.                                                      07240000
071500                                                                  07250000
071600******************************************************************07260000
071700* A1000 - PREFORMS TASK INITIATION PROCESSING                     07270000
071800******************************************************************07280000
071900 A1000-INITIALIZE.                                                07290000
072000                                                                  07300000
072100     OPEN INPUT  CW-EXTRACT-ITM-IN.                               07310000
072200     OPEN OUTPUT CW-EXTRACT-ITM-OUT                               07320000
072200                 CW-EXTRACT-AUD.                                  07320000
073000                                                                  07380000
073100     MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 07390000
073200                                                                  07400000
073300     MOVE SYS-TIME (1:4) TO ST-SYS-HHMM.                          07410000
073400     MOVE SYS-TIME (1:2) TO ST-BEG-HR.                            07420000
073500     MOVE SYS-TIME (3:2) TO ST-BEG-MN.                            07430000
073500     MOVE SYS-TIME (5:2) TO ST-BEG-SEC.                           07430000
073600                                                                  07440000
073700     MOVE SYS-DATE (1:2) TO SD-CC.                                07450000
073800     MOVE SYS-DATE (3:2) TO SD-YY.                                07460000
073900     MOVE SYS-DATE (5:2) TO SD-MM.                                07470000
074000     MOVE SYS-DATE (7:2) TO SD-DD.                                07480000
074100                                                                  07490000
082300******************************************************************07870001
082400* A4000 - CLOSE FILES                                             07880001
082500******************************************************************07890001
082600 A3000-CLOSE.                                                     07900001
082700                                                                  07910001
082800     CLOSE CW-EXTRACT-ITM-IN                                      07920001
082900           CW-EXTRACT-ITM-OUT                                     07930001
083000           CW-EXTRACT-AUD.                                        07940001
082500******************************************************************07890001
083700* B1000 - MAIN PROCESS                                            07990000
083800******************************************************************08000000
083900 B1000-PROCESS-ITM-IN.                                            08010001
                                                                                
            IF SA024-ITM-SKU-NBR = PC-KOHLS-SWPSTKS-SKU                         
               ADD 1 TO PA-KOHLS-SWPSTKS-ITM-COUNT                              
            ELSE                                                                
               IF SA024-ITM-SKU-NBR = PV-PREV-SKU-NBR AND                       
                  SA024-ITM-UPC-NBR = PV-PREV-UPC-NBR                           
                  IF PS-SKU-UPC-CHNG                                            
                     PERFORM B1200-AUDIT-VLDTD-UPC                              
                  END-IF                                                        
               ELSE                                                             
                  PERFORM B1100-VALIDATE-SKU-UPC                                
               END-IF                                                           
               IF PV-SKU-VALIDATE-ERROR EQUAL SPACE                             
                  PERFORM D2000-WRITE-ITM-OUT                                   
               END-IF                                                           
               MOVE SPACE TO PV-SKU-VALIDATE-ERROR                              
            END-IF.                                                             
                                                                                
            PERFORM D1000-READ-ITM-IN.                                          
092000                                                                          
082500******************************************************************07890001
083700* B1100 - VALIDATE SKU/UPC                                        07990000
083800******************************************************************08000000
083900 B1100-VALIDATE-SKU-UPC.                                          08010001
                                                                                
           INITIALIZE PS-SKU-UPC-CHNG-SW.                                       
           SET PS-AUDIT-OLD TO TRUE.                                            
           PERFORM D3000-WRITE-SKU-UPC-AUDIT.                                   
           PERFORM C1000-VALIDATE-UPC.                                          
                                                                                
           IF SA033-AUD-CHANGE                                                  
              SET PS-SKU-UPC-CHNG TO TRUE                                       
              SET PS-AUDIT-NEW TO TRUE                                          
              PERFORM D3000-WRITE-SKU-UPC-AUDIT                                 
097300     END-IF.                                                              
                                                                                
082500******************************************************************07890001
083700* B1200 - AUDIT UPC THAT HAS ALREADY BEEN VALIDATED               07990000
083800******************************************************************08000000
083900 B1200-AUDIT-VLDTD-UPC.                                           08010001
                                                                                
            IF SA024-ITM-SKU-NBR NOT = PV-SAVE-SKU-NBR OR                       
               SA024-ITM-UPC-NBR NOT = PV-SAVE-UPC-NBR OR                       
               SA024-ITM-ITM-NBR NOT = PV-SAVE-ITM-NBR                          
               SET PS-AUDIT-OLD          TO TRUE                                
               PERFORM D3000-WRITE-SKU-UPC-AUDIT                                
                                                                                
               MOVE PV-SAVE-SKU-NBR      TO SA024-ITM-SKU-NBR                   
               MOVE PV-SAVE-ITM-NBR      TO SA024-ITM-ITM-NBR                   
                                                                                
               MOVE PV-SAVE-UPC-NBR      TO SA024-ITM-UPC-NBR                   
                                                                                
               MOVE PV-SAVE-AUDIT-CDE    TO SA033-AUD-AUDIT-CDE                 
                                                                                
               EVALUATE TRUE                                                    
                  WHEN SA033-AUD-INV-SKU-UPC                                    
                     ADD 1 TO PA-UPC-INVALID-COUNT                              
                  WHEN SA033-AUD-RENUM-SKU                                      
                     ADD 1 TO PA-SKU-RENUM-COUNT                                
                  WHEN SA033-AUD-GIFT                                           
                     ADD 1 TO PA-SKU-GIFT-CARD-COUNT                            
                  WHEN SA033-AUD-INVLD-DUMMY                                    
                     ADD 1 TO PA-SKU-INVLD-DUMMY-COUNT                          
                  WHEN SA033-AUD-DELTD-SKU                                      
                     ADD 1 TO PA-SKU-DELTD-COUNT                                
               END-EVALUATE                                                     
                                                                                
               SET PS-AUDIT-NEW          TO TRUE                                
               PERFORM D3000-WRITE-SKU-UPC-AUDIT                                
            END-IF.                                                             
122100******************************************************************10910000
122200* C1000 - VALIDATE UPC/SKU COMBINATION                            10920001
122300******************************************************************10930000
       C1000-VALIDATE-UPC.                                                      
                                                                                
            INITIALIZE PS-DUMMY-SKU-SW                                          
                       PS-TSKXREF-FOUND-SW                                      
                       PS-END-OF-SKU-UPC-SW                                     
                       PS-DEPT-CL-ERR-SW.                                       
                                                                                
            IF SA024-ITM-DEPT-NBR = '981' OR '983'                              
               MOVE '00'           TO SA024-ITM-SUB-CL-NBR                      
            END-IF.                                                             
                                                                                
            MOVE SA024-ITM-SKU-NBR TO PV-DUMMY-SKU-NBR.                         
                                                                                
            IF PV-DUMMY-SKU-LAST-3  NOT = '000'       AND                       
               SA024-ITM-DEPT-NBR   NOT = '099'       AND                       
               SA024-ITM-SUB-CL-NBR NOT = '10'        AND                       
               SA024-ITM-DEPT-CL-ERR-CDE > '3'                                  
               ADD 1               TO PA-DEPT-CL-INVALID-COUNT                  
               MOVE '000'          TO PV-DUMMY-SKU-LAST-3                       
               MOVE '099'          TO SA024-ITM-DEPT-NBR                        
               MOVE '10'           TO SA024-ITM-SUB-CL-NBR                      
               MOVE '000'          TO SA024-ITM-SKU-SEQ-NBR                     
               SET PS-DEPT-CL-ERR  TO TRUE                                      
            END-IF.                                                             
                                                                                
            IF PV-DUMMY-SKU-LAST-3 = '000'                                      
               IF NOT PS-DEPT-CL-ERR                                            
                  SET PS-DUMMY-SKU TO TRUE                                      
               END-IF                                                           
               PERFORM C2000-CREATE-INTERNAL-UPC                                
               IF NOT PS-AUDIT-OFF                                              
                  IF SA024-ITM-SKU-NBR = PC-GIFT-CARD OR                        
                     SA024-ITM-SKU-NBR = PC-GIFT-CERT                           
                     ADD 1 TO PA-SKU-GIFT-CARD-COUNT                            
                     SET SA033-AUD-GIFT TO TRUE                                 
                  ELSE                                                          
                     ADD 1 TO PA-SKU-INVLD-DUMMY-COUNT                          
                     SET SA033-AUD-INVLD-DUMMY TO TRUE                          
                  END-IF                                                        
               END-IF                                                           
            ELSE                                                                
               PERFORM C1500-VALIDATE-NON-DUMMY                                 
                                                                                
               IF SA024-ITM-SKU-NBR NOT = PV-SKXREF-SKU AND                     
                  NOT SA033-AUD-DELTD-SKU                                       
                  MOVE SA024-ITM-SKU-NBR    TO PV-PREV-SKU-NBR                  
                  MOVE SA024-ITM-UPC-NBR    TO PV-PREV-UPC-NBR                  
                  MOVE PV-SKXREF-SKU        TO SA024-ITM-SKU-NBR                
                                               PV-SAVE-SKU-NBR                  
                  MOVE PV-UPC-UPC-NBR       TO PV-SAVE-UPC-NBR                  
                  MOVE PV-SKXREF-ITM-NBR    TO PV-SAVE-ITM-NBR                  
                  ADD 1                     TO PA-SKU-RENUM-COUNT               
                  IF SA033-AUD-INV-SKU-UPC                                      
                     SUBTRACT 1 FROM PA-UPC-INVALID-COUNT                       
                  END-IF                                                        
                  SET SA033-AUD-RENUM-SKU   TO TRUE                             
               END-IF                                                           
            END-IF.                                                             
                                                                                
            MOVE SA033-AUD-AUDIT-CDE TO PV-SAVE-AUDIT-CDE.                      
                                                                                
122100******************************************************************10910000
122200* C1500 - VALIDATE NON-DUMMY UPC/SKU COMBINATION                  10920001
122300******************************************************************10930000
       C1500-VALIDATE-NON-DUMMY.                                                
                                                                                
            MOVE SA024-ITM-UPC-NBR TO UPC-UPC-NBR                               
            PERFORM S8000-OPEN-VLDT-SKU-UPC-CRSR                                
            PERFORM S8100-FETCH-VLDT-SKU-UPC-CRSR                               
                                                                                
            IF PS-END-OF-SKU-UPC                                                
               MOVE '000' TO PV-DUMMY-SKU-LAST-3                                
               PERFORM C2000-CREATE-INTERNAL-UPC                                
               IF NOT PS-AUDIT-OFF                                              
                  ADD 1 TO PA-SKU-DELTD-COUNT                                   
                  SET SA033-AUD-DELTD-SKU TO TRUE                               
               END-IF                                                           
            ELSE                                                                
                                                                                
               PERFORM                                                          
                  UNTIL PS-END-OF-SKU-UPC OR                                    
                        PS-TSKXREF-FOUND                                        
                  IF SA024-ITM-UPC-NBR = PV-UPC-UPC-NBR AND                     
                     UPC-UPC-TYP-CDE = 'V'                                      
                     SET PS-TSKXREF-FOUND TO TRUE                               
                  ELSE                                                          
                     IF SA024-ITM-UPC-NBR = PV-UPC-UPC-NBR AND                  
                        UPC-UPC-TYP-CDE = 'I'                                   
                        SET PS-TSKXREF-FOUND TO TRUE                            
                     END-IF                                                     
                     IF NOT PS-TSKXREF-FOUND                                    
                        PERFORM S8100-FETCH-VLDT-SKU-UPC-CRSR                   
                     END-IF                                                     
                  END-IF                                                        
               END-PERFORM                                                      
                                                                                
               IF NOT PS-TSKXREF-FOUND                                          
                  IF SA024-ITM-UPC-NBR NOT = PV-UPC-UPC-NBR                     
                     SET SA033-AUD-INV-SKU-UPC TO TRUE                          
                     MOVE SA024-ITM-SKU-NBR    TO PV-PREV-SKU-NBR               
                     MOVE SA024-ITM-UPC-NBR    TO PV-PREV-UPC-NBR               
                     MOVE PV-UPC-UPC-NBR       TO SA024-ITM-UPC-NBR             
                                                  PV-SAVE-UPC-NBR               
                     MOVE PV-SKXREF-SKU        TO PV-SAVE-SKU-NBR               
                     MOVE PV-SKXREF-ITM-NBR    TO PV-SAVE-ITM-NBR               
                     ADD 1                    TO PA-UPC-INVALID-COUNT           
                  END-IF                                                        
               END-IF                                                           
            END-IF                                                              
                                                                                
            PERFORM S8200-CLOSE-VLDT-SKU-UPC-CRSR.                              
                                                                                
      ******************************************************************        
      * C2000 -  CALL THE INTERNAL UPC CREATE ROUTINE PASSING IT THE            
      *          DEPT, CLASS AND SEQ #.                                         
      ******************************************************************        
       C2000-CREATE-INTERNAL-UPC.                                               
                                                                                
            INITIALIZE PS-AUDIT-OFF-SW.                                         
            SET  DP040I-GEN-INTERNAL-UPC-OPTION TO TRUE.                   CL**2
            MOVE SA024-ITM-DEPT-NBR       TO DP040I-DEPARTMENT-NUMBER.          
            MOVE SA024-ITM-SUB-CL-NBR     TO DP040I-CLASS-NUMBER.               
            MOVE PV-DUMMY-SKU-LAST-3      TO DP040I-SEQUENCE-NUMBER.            
            CALL DP040I-UPC-CHECK-DIGIT-WO-IUPC                                 
                            USING DP040I-UPC-CHECK-DIGIT-PARMS.                 
                                                                                
            IF DP040I-NO-ERROR-DETECTED                                         
                IF DP040I-UPC-CODE = SA024-ITM-UPC-NBR                          
                   SET PS-AUDIT-OFF          TO TRUE                            
                ELSE                                                            
                   MOVE DP040I-UPC-CODE      TO SA024-ITM-UPC-NBR               
                                                PV-SAVE-UPC-NBR                 
                                                PV-PREV-UPC-NBR                 
                   IF PS-DUMMY-SKU                                              
                      MOVE SA024-ITM-SKU-NBR TO PV-PREV-SKU-NBR                 
                                                PV-SAVE-SKU-NBR                 
                      MOVE SA024-ITM-ITM-NBR TO PV-SAVE-ITM-NBR                 
                   ELSE                                                         
                      MOVE DP040I-SKU-NUMBER TO SA024-ITM-SKU-NBR               
                                                PV-SAVE-SKU-NBR                 
                                                PV-PREV-SKU-NBR                 
                   END-IF                                                       
                END-IF                                                          
            ELSE                                                                
               DISPLAY '**** WARNING ****'                                      
               DISPLAY 'BAD RETURN CODE FROM UPC CHECK DIGIT ROUTINE'           
               DISPLAY 'FOR SKU NUMBER: ' DP040I-SKU-NUMBER                     
               DISPLAY 'UPC CHECK ERROR: ' DP040I-ERROR-INDICATOR               
TJHTMP*        MOVE +4001 TO ABEND-CODE                                         
TJHTMP*        CALL 'ILBOABN0' USING ABEND-CODE                                 
TJHTMP         MOVE 'Y' TO PV-SKU-VALIDATE-ERROR                                
            END-IF.                                                             
                                                                                
092100 D1000-READ-ITM-IN.                                               08530068
092200                                                                  08540001
092300     READ CW-EXTRACT-ITM-IN                                       08550030
092300        INTO SA024-CORP-WAREHOUSE-ITM-REC                         08550030
              AT END SET PS-END-OF-ITM-IN TO TRUE.                              
                                                                                
           IF NOT PS-END-OF-ITM-IN                                              
119800        ADD 1 TO PA-ITM-READ-COUNT                                10680037
           END-IF.                                                              
099300                                                                  08880001
115200 D2000-WRITE-ITM-OUT.                                             10260001
                                                                                
119700     WRITE CW-EXTRACT-ITM-OUT-REC FROM                            10670068
               SA024-CORP-WAREHOUSE-ITM-REC.                                    
                                                                                
119800     ADD 1 TO PA-ITM-WRITE-COUNT.                                 10680037
119900                                                                  10690001
115200 D3000-WRITE-SKU-UPC-AUDIT.                                       10260001
115300                                                                  10270001
            IF PS-AUDIT-OLD                                                     
                                                                                
115400         INITIALIZE SA033-CORP-WAREHOUSE-AUD-REC                  10280071
115500                                                                  10290045
               MOVE SA024-ITM-SLS-DTE      TO SA033-AUD-SLS-DTE                 
               MOVE SA024-ITM-COSA-KEY     TO SA033-AUD-COSA-KEY                
                                                                                
               MOVE SA024-ITM-UPC-NBR      TO SA033-AUD-OLD-UPC-NBR             
               MOVE SA024-ITM-ITM-NBR      TO SA033-AUD-OLD-ITM-NBR             
               MOVE SA024-ITM-SKU-NBR      TO SA033-AUD-OLD-SKU-NBR             
            ELSE                                                                
               MOVE SA024-ITM-UPC-NBR      TO SA033-AUD-NEW-UPC-NBR             
               MOVE SA024-ITM-ITM-NBR      TO SA033-AUD-NEW-ITM-NBR             
               MOVE SA024-ITM-SKU-NBR      TO SA033-AUD-NEW-SKU-NBR             
119700         WRITE CW-EXTRACT-AUD-REC                                 10670068
                  FROM SA033-CORP-WAREHOUSE-AUD-REC                             
119800         ADD 1 TO PA-AUD-WRITE-COUNT                              10680037
            END-IF.                                                             
                                                                                
132800******************************************************************11980028
132900* S8000 - OPEN VLDT UPC CURSOR                                    11990028
133000******************************************************************12000028
133100 S8000-OPEN-VLDT-SKU-UPC-CRSR.                                    12010028
133200                                                                  12020028
133300     EXEC SQL                                                     12030028
133400         OPEN VLDT-SKU-UPC-CRSR                                   12040028
133500     END-EXEC.                                                    12050028
133600                                                                  12060028
174500     IF SQLCODE NOT = PC-SQL-SUCCESSFUL                           14450028
              SET TUPC-TBL TO TRUE                                              
154100        MOVE 'S8000-OPEN-VLDT-SKU-UPC-CRSR' TO PV-PARAGRAPH-NAME  14120061
174800        PERFORM Z9000-DB2-ABEND                                   14480033
174900     END-IF.                                                      14490028
134100                                                                  12110028
151100******************************************************************13810028
151200* S8100 - FETCH VLDT UPC CURSOR                                   13820028
151300******************************************************************13830028
151400 S8100-FETCH-VLDT-SKU-UPC-CRSR.                                   13840030
151500                                                                  13850028
151600     EXEC SQL                                                     13860028
151700         FETCH VLDT-SKU-UPC-CRSR INTO                             13870028
151800           :UPC-UPC-NBR                                           13890028
151900          ,:UPC-UPC-TYP-CDE                                       13900028
                ,:SKXREF-ITM-NBR                                                
                ,:SKXREF-SKU                                                    
152300     END-EXEC.                                                    13940028
152400                                                                  13950028
175800     IF SQLCODE = PC-SQL-SUCCESSFUL                               14580028
178340        ADD 1                   TO PA-SKU-UPC-READ-COUNT          14734068
              MOVE UPC-UPC-NBR        TO PV-UPC-UPC-NBR                         
              MOVE SKXREF-ITM-NBR     TO PV-SKXREF-ITM-NBR                      
              MOVE SKXREF-SKU         TO PV-SKXREF-SKU                          
178700     ELSE                                                         14770028
178800        IF SQLCODE = PC-SQL-ROW-NOT-FOUND                         14780028
181340           SET PS-END-OF-SKU-UPC TO TRUE                          14931368
181700        ELSE                                                      14970028
                 SET TUPC-TBL TO TRUE                                           
181800           PERFORM Z9000-DB2-ABEND                                14980028
181900        END-IF                                                    14990028
182000     END-IF.                                                      15000028
152900                                                                  14000028
153000******************************************************************14010028
153100* S8200 - CLOSE VLDT UPC CURSOR                                   14020028
153200******************************************************************14030028
153300 S8200-CLOSE-VLDT-SKU-UPC-CRSR.                                   14040028
153400                                                                  14050028
153500     EXEC SQL                                                     14060028
153600         CLOSE VLDT-SKU-UPC-CRSR                                  14070028
153700     END-EXEC.                                                    14080028
154300                                                                  14140061
182200******************************************************************15020000
182300* E1000 - PROGRAM CONTROLS                                        15030000
182400******************************************************************15040000
182500 E1000-CONTROLS.                                                  15050000
182600                                                                  15060000
182700     MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 15070000
182800                                                                  15080000
182900     MOVE SYS-TIME (1:2) TO ST-END-HR.                            15090000
183000     MOVE SYS-TIME (3:2) TO ST-END-MN.                            15100000
183000     MOVE SYS-TIME (5:2) TO ST-END-SEC.                           15100000
183100                                                                  15110000
183200     DISPLAY '                    '.                              15120000
183300     DISPLAY '**********************'.                            15130000
183400     DISPLAY '   SAKUT056 CONTROLS  '.                            15140000
183500     DISPLAY '**********************'.                            15150000
183600     DISPLAY 'RUN DATE  = ' SD-SYS-DATE.                          15160000
183700     DISPLAY 'BEG TIME  = ' ST-BEG-TIME.                          15170000
183800     DISPLAY 'END TIME  = ' ST-END-TIME.                          15180000
183900     DISPLAY '======================'.                            15190000
                                                                                
187760     DISPLAY 'SKU UPC VALIDATION STATISTICS'                      15230000
           DISPLAY '--- --- ---------- ----------'.                             
184400     DISPLAY ' '.                                                 15230000
184500     DISPLAY 'ITEM READS    : ' PA-ITM-READ-COUNT.                        
184600     DISPLAY 'ITEM WRITES   : ' PA-ITM-WRITE-COUNT.                       
           DISPLAY 'SWPSTKS RJCT  : ' PA-KOHLS-SWPSTKS-ITM-COUNT.               
184700     DISPLAY ' '.                                                 15230000
           DISPLAY 'DEPT/CL INVALD: ' PA-DEPT-CL-INVALID-COUNT.                 
187760     DISPLAY ' '.                                                 15230000
           DISPLAY 'SKU/UPC READS : ' PA-SKU-UPC-READ-COUNT.                    
187760     DISPLAY ' '.                                                 15230000
           DISPLAY 'AUDIT WRITES  : ' PA-AUD-WRITE-COUNT.                       
187760     DISPLAY ' '.                                                 15230000
           DISPLAY 'INVLD CMBNTN (I): ' PA-UPC-INVALID-COUNT.                   
           DISPLAY 'RENUM        (R): ' PA-SKU-RENUM-COUNT.                     
           DISPLAY 'DUMMY        (X): ' PA-SKU-INVLD-DUMMY-COUNT.               
           DISPLAY 'GIFT         (G): ' PA-SKU-GIFT-CARD-COUNT.                 
           DISPLAY 'DLTD         (D): ' PA-SKU-DELTD-COUNT.                     
           DISPLAY ' '.                                                         
                                                                                
189600******************************************************************15240000
189700* Z9000 DB2 ABEND                                                 15250000
189800******************************************************************15260000
189900 Z9000-DB2-ABEND.                                                 15270000
190000                                                                  15280000
190100     MOVE SQLCODE TO SQLCODE2.                                    15290000
190200     MOVE SQLERRD(3) TO SQLERRD3.                                 15300000
190300                                                                  15310028
190400     DISPLAY 'Z9000-DB2-ABEND'.                                   15320000
190500     DISPLAY '** ABEND           ** = SQLABEND'.                  15330000
190600     DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          15340000
190700     DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        15350000
190800     DISPLAY '** TABLES AFFECTED ** = ' PV-DB2-TABLE-NAME.        15360000
190900     DISPLAY '** OPERATION       ** = ' PV-DB2-OPERATION-MSG.     15370000
191000     DISPLAY '** SQLCODE         ** = ' SQLCODE2.                 15380000
191100     DISPLAY '** ROWS PROCESSED  ** = ' SQLERRD3.                 15390000
191200     DISPLAY '** SQLERRM         ** = ' SQLERRM.                  15400000
191300     DISPLAY '** SQLERRMC        ** = ' SQLERRMC.                 15410000
191400                                                                  15420000
191500******************************************************************15430000
191600*  THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   15440000
191700*  MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        15450000
191800* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     15460000
191900*  FORMAT.                                                        15470000
192000******************************************************************15480000
192100     COPY DPPD004.                                                15490000
192200                                                                  15500000
192300     SET ABEND-4013-DB2-ERROR TO TRUE.                            15510000
192400                                                                  15520000
192500     CALL 'ILBOABN0' USING ABEND-CODE.                            15530000
