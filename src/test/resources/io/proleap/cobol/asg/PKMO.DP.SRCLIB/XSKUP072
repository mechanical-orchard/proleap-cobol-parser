      ***************************************************************** 00010017
       IDENTIFICATION DIVISION.                                         00020017
      ***************************************************************** 00030017
                                                                        00040017
       PROGRAM-ID.             XSKUP072.                                00050017
       AUTHOR.                 KOHLS.                                   00060017
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070017
       DATE-WRITTEN            JULY 2003.                               00080017
       DATE-COMPILED.                                                   00090017
                                                                        00100017
      ***************************************************************** 00110017
      *                                                                 00120017
      *  THIS PROGRAM IS RESPONSIBLE FOR UPDATING THE RFRSH_DTE         00130017
      *  FIELD ON THE EPT_STR_FIL_RFRSH TABLE. IT USES A CONTROL        00140017
      *  CARD THAT CONTAINS A STORE NUMBER ALONG WITH THE RFRSH_DTE     00150017
      *  THAT THE STORE WILL USE. IF THE STORE EXSISTS ON THE TABLE, AN 00160017
      *  UPDATE WILL BE DONE TO CHANGE THE REFRESH DATE TO THE VALUE ON 00161000
      *  THE CONTROL CARD, IF THE STORE DOES NOT EXSIST, AN INSERT WILL 00162000
      *  BE DONE TO ADD THE STORE ALONG WITH THE RFRSH_TYP_CDE,         00163000
      *  RFRSH_DTE, AND LAST_RFRSH_DTE. IF THE STORE CANNOT BE UPDATED  00164000
      *  OR INSERTED AN ERROR RECORD WILL BE WRITTEN OUT TO AN ERROR    00165000
      *  FILE.                                                          00166000
      *                                                                 00170017
      *                                                                 00171017
      *  THIS PROGRAM RUNS ON REQUEST AND WILL BE USED TO UPDATE STORES 00180017
      *  AS THEY ARE BROUGHT ON TO THE NEW PROMO SYSTEM.                00190017
      *                                                                 00200017
      *                                                                 00210017
      *  INPUT: INP01 - CONTROL CARD THAT CONTAINS: STR_NBR             00220017
      *                                             RFRSH_TYP_CDE       00230017
      *                                             RFRSH_DTE           00231000
      *                                             INDICATOR                   
      *  OUTPUT:OUT01 - CONTAINS ERROR RECORD(S).                       00240017
      *                                                                 00250017
      ***************************************************************** 00260017
      * HISTORY LOG:                                                    00270017
      ***************************************************************** 00280017
      * DATE: 08/13/2009                                                00290020
      * WR/IR#: INC000000911721                                         00300020
      * PROGRAMMER: DAVID WELLER                                        00310017
      * DESCRIPTION: DO NOT ALLOW FULL FILE REFRESHES TO BE SET UP      00320017
      * FOR CLOSED STORES OR FOR E STORES.  REMOVE THE ERROR FILE.      00330020
      ***************************************************************** 00340017
CR4339* DATE: 06/21/2011                                                00290020
CR4339* WR/IR#: CRQ4339                                                 00300020
CR4339* PROGRAMMER: COGNIZANT                                           00310017
CR4339* DESCRIPTION: ADDITIONAL LOGIC INCLUDED FOR FULL FILE REQUEST    00320017
CR4339*              TO FIND A FEASIBLE DATE  I;E NEXT AVAILABLE NO     00330020
CR4339*              EVENT SCHEDULE DATE FOLLOWED BY FULL FILE REQUEST          
CR4339*              DATE.PROMO,SIGN AND OFFERS WILL BE SEND ON THE             
CR4339*              NEXT DATE OF THE REQUESTED DATE.                           
CR4339*              AN INDICATOR FIELD IS INCLUDED TO EITHER FORCE THE         
CR4339*              FULL FILE TO STORES OR DO A NORMAL PROCESS TO SEND         
CR4339*              THE FILES TO STORES AS REQUESTED BY USER.                  
CR4339*              TRIGGER A MAIL TO SUPPORT GROUP IF THERE WILL BE           
CR4339*              ANY DELAY IN FULL FILE REQUEST FOR PRICING FILES.          
P17793***************************************************************** 00280017
P17793* DATE: 05/23/2012                                                        
P17793* WR/IR#: PBI17793                                                        
P17793* PROGRAMMER: COGNIZANT                                                   
P17793* DESCRIPTION: AN OUTPUT FILE IS WRITTEN WHEN FILE IS SET FOR ANY         
P17793*              CLOSED STORE OR WHEN XS FILE IS SET FOR E-STORE            
P17793*****************************************************************         
C99999* DATE: 11/05/2013                                                        
C99999* WR/IR#: CHG0099999                                                      
C99999* PROGRAMMER: DARYL WOELFEL                                               
C99999* DESCRIPTION: HANDLE NEW SI TYPE CODE FOR THE NEW SIGNING                
C99999*              SYSTEM THE SAME AS THE XS TYPE CODE FOR THE OLD            
C99999*              SIGNING SYSTEM.                                            
C99999*****************************************************************         
       ENVIRONMENT DIVISION.                                            00350017
      ***************************************************************** 00360017
                                                                        00370017
       CONFIGURATION SECTION.                                           00380017
                                                                        00390017
       SOURCE-COMPUTER.        IBM-3090.                                00400017
       OBJECT-COMPUTER.        IBM-3090.                                00410017
                                                                        00420017
       INPUT-OUTPUT SECTION.                                            00430017
                                                                        00440017
       FILE-CONTROL.                                                    00450017
                                                                        00460017
           SELECT CONTROL-CARD-CONV-STORES                              00470017
               ASSIGN TO INP01.                                         00480017
P17793     SELECT STORE-CLSE ASSIGN TO OUT01.                                   
                                                                        00520017
      ******************************************************************00530017
       DATA DIVISION.                                                   00540017
      ******************************************************************00550017
                                                                        00560017
       FILE SECTION.                                                    00570017
                                                                        00580017
       FD  CONTROL-CARD-CONV-STORES                                     00590017
           RECORDING MODE IS F                                          00600017
           BLOCK CONTAINS 0 RECORDS                                     00610017
           LABEL RECORDS ARE STANDARD.                                  00620017
                                                                        00630017
       01  CONTROL-CARD-STORE-RECORD        PIC X(080).                 00640017
                                                                        00710017
P17793 FD  STORE-CLSE                                                   00590017
P17793     RECORDING MODE IS F                                          00600017
P17793     BLOCK CONTAINS 0 RECORDS                                     00610017
P17793     LABEL RECORDS ARE STANDARD.                                  00620017
P17793                                                                  00630017
P17793 01  STORE-CLOSE-RECORD               PIC X(080).                 00640017
      ******************************************************************00720017
       WORKING-STORAGE SECTION.                                         00730017
      ******************************************************************00740017
                                                                        00750017
      ******************************************************************00760017
      * INPUT CONTROL CARD 1 CONTAINS STORE NUMBER, TYPE CODE,          00770017
      * REFRESH DATE AND INDICATOR                                      00771017
      ******************************************************************00780017
                                                                        00790017
       01  CC-CONTROL-CARD-1.                                           00800017
           10  PV-CONV-STORE                PIC 9(05)   VALUE 0.        00810017
           10  PV-RFRSH-TYP-CDE             PIC X(02)   VALUE SPACES.   00820017
           10  PV-RFRSH-DTE                 PIC X(10)   VALUE SPACES.   00830017
CR4339     10  PV-INDICATOR                 PIC X(01)   VALUE SPACES.           
P17793                                                                  00841000
P17793 01  HEADER-LINE.                                                         
P17793     05  COLUMN1          PIC X(05)  VALUE 'STORE'.                       
P17793     05  FILLER           PIC X(02)  VALUE SPACES.                        
P17793     05  COLUMN2          PIC X(09)  VALUE 'FILE-TYPE'.                   
P17793     05  FILLER           PIC X(02)  VALUE SPACES.                        
P17793     05  COLUMN3          PIC X(12)  VALUE 'REFRESH-DATE'.                
P17793     05  FILLER           PIC X(11)  VALUE SPACES.                        
P17793     05  COLUMN4          PIC X(06)  VALUE 'REASON'.                      
P17793     05  FILLER           PIC X(19)  VALUE SPACES.                        
P17793                                                                          
P17793 01  ST-STR-CLSE.                                                         
P17793     05  PV-STORE         PIC 9(05).                                      
P17793     05  FILLER           PIC X(05) VALUE SPACES.                         
P17793     05  PV-REFSH-TYPE    PIC X(02).                                      
P17793     05  FILLER           PIC X(07) VALUE SPACES.                         
P17793     05  PV-REFSH-DATE    PIC X(10).                                      
P17793     05  FILLER           PIC X(03) VALUE SPACES.                         
P17793     05  PV-REASON        PIC X(25).                                      
                                                                                
       01  AC-ABEND-CODE                    PIC S9(04) VALUE ZERO       00850017
                                                       COMP SYNC.       00860017
           88  AC-CONTROL-CARD-ERROR                   VALUE +4003.     00870017
           88  AC-DB2-ERROR                            VALUE +4013.     00880017
           88  AC-CALENDAR-ROUTINE-ERROR               VALUE +4019.     00890017
                                                                        00900017
       01  PS-PROGRAM-SWITCHES.                                         00910017
           05  PS-EOF-CC1-SW                PIC  X(01) VALUE 'N'.       00920017
               88  PS-EOF-CC1-YES                      VALUE 'Y'.       00930017
           05  PS-LOC-FOUND-SW              PIC  X(01) VALUE 'N'.       00931017
               88  PS-LOC-FOUND-YES                    VALUE 'Y'.       00932017
               88  PS-LOC-FOUND-NO                     VALUE 'N'.       00933017
           05  PS-SEND-RFRSH-SW             PIC  X(01) VALUE 'N'.       00934023
               88  PS-SEND-RFRSH-YES                   VALUE 'Y'.       00935023
               88  PS-SEND-RFRSH-NO                    VALUE 'N'.       00936023
           05  PS-UPDATE-SUCCESSFUL-SW      PIC  X(01) VALUE 'N'.       00937025
               88  PS-UPDATE-SUCCESSFUL-YES            VALUE 'Y'.       00938025
               88  PS-UPDATE-SUCCESSFUL-NO             VALUE 'N'.       00939025
CR4339     05  PS-FIND-RFRSH-DTE-SW         PIC  X(01) VALUE 'N'.       00939153
CR4339         88  PS-FIND-RFRSH-DTE-YES               VALUE 'Y'.       00939253
CR4339         88  PS-FIND-RFRSH-DTE-NO                VALUE 'N'.       00939353
CR4339     05  PS-SET-RETURN-4091-SW        PIC  X(01) VALUE 'N'.       00939453
CR4339         88  PS-SET-RETURN-4091-YES              VALUE 'Y'.       00939553
CR4339         88  PS-SET-RETURN-4091-NO               VALUE 'N'.       00939653
CR4339     05  PS-INDICATOR-VALUE-SW        PIC  X(01) VALUE 'N'.       00939453
CR4339         88  PS-INDT-NORMAL-YES                  VALUE 'Y'.       00939553
CR4339         88  PS-INDT-NORMAL-NO                   VALUE 'N'.               
                                                                                
       01  PC-CONSTANTS.                                                00950017
           05  PC-MAX-ATTEMPT-COUNT         PIC  9(01) VALUE 5.         00960017
           05  PC-CURRENT-PROGRAM-NAME      PIC  X(08) VALUE            00970017
                                            'XSKUP072'.                 00980017
           05  PC-NULL-IND                  PIC S9(4) COMP SYNC.        00982017
CR4339     05  PC-DAYS-FR                   PIC S9(3)  VALUE +1.        00983054
                                                                        00990017
       01  PV-MISC-FIELDS.                                              01000017
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACE.     01010017
           05  PV-ERROR-COUNT               PIC  9(04) VALUE ZERO.      01020020
           05  PV-OPERATION-MSG-1           PIC  X(40) VALUE SPACE.     01040017
           05  PV-OPERATION-MSG-2           PIC  X(40) VALUE SPACE.     01050017
           05  PV-DB2-OPERATION             PIC  X(40) VALUE SPACE.     01060017
           05  PV-ATTEMPT-COUNT             PIC  9(04) VALUE ZEROS.     01070017
           05  PV-HOLD-STORE-NBR            PIC  S9(04) COMP.           01080000
           05  PV-FOUND-E-BUS-IND           PIC  X(01) VALUE SPACE.     01090017
           05  PV-HOLD-RFRSH-TYP-CDE        PIC  X(02) VALUE SPACE.     01091017
           05  PV-HOLD-RFRSH-DTE            PIC  X(10) VALUE SPACE.     01100000
CR4339     05  PV-HOLD-INDICATOR            PIC  X(01) VALUE SPACE.             
           05  PV-HOLD-LAST-RFRSH-DTE       PIC  X(10) VALUE SPACE.     01101002
CR4339     05  PV-HOLD-RFRSH-NEXT-DTE       PIC  X(10) VALUE SPACE.     01101152
           05  PV-EVNT-COUNT                PIC  S9(4) VALUE +0 COMP.   01102029
CR4339     05  PV-RFRSH-COUNT               PIC  9(04) VALUE ZERO.      01103052
P17793     05  PV-WRITE-CNT                 PIC  9(01) VALUE ZERO.              
                                                                        01110017
           05  PV-CURRENT-DATE-YMD.                                     01111017
               10  PV-CURRENT-YEAR-YMD      PIC  9(02)  VALUE ZEROES.   01113017
               10  PV-CURRENT-MONTH-YMD     PIC  9(02)  VALUE ZEROES.   01114017
               10  PV-CURRENT-DAY-YMD       PIC  9(02)  VALUE ZEROES.   01116017
                                                                        01118017
           05  PV-CURRENT-DATE-CYMD.                                    01119017
               10  PV-CURRENT-CENT-CYMD     PIC  X(02)  VALUE '20'.     01119117
               10  PV-CURRENT-YEAR-CYMD     PIC  X(02)  VALUE SPACE.    01119217
               10  FILLER                   PIC  X(01)  VALUE '-'.      01119317
               10  PV-CURRENT-MONTH-CYMD    PIC  X(02)  VALUE SPACE.    01119417
               10  FILLER                   PIC  X(01)  VALUE '-'.      01119517
               10  PV-CURRENT-DAY-CYMD      PIC  X(02)  VALUE SPACE.    01119617
                                                                        01170000
      ******************************************************************01180017
      *  WS FIELDS FOR SQLCA MESSAGE ROUTINE                            01190017
      ******************************************************************01200017
                                                                        01210017
           COPY DPWS004.                                                01220017
                                                                        01230017
      ******************************************************************01231027
      * KOHLS DATE ROUTINE COPY MEMBERS.                                01232027
      ******************************************************************01233027
           COPY DPWS500.                                                01234027
                                                                        01235027
      ******************************************************************01240017
      *  DB2 COMMUNICATION AREA.                                        01250017
      ******************************************************************01260017
                                                                        01270017
           EXEC SQL                                                     01280017
                INCLUDE SQLCA                                           01290017
           END-EXEC.                                                    01300017
                                                                        01310017
CR4339******************************************************************01311052
CR4339*  DB2 TABLE XST00063 (XST_PRCHG)                                 01312052
CR4339******************************************************************01313052
CR4339                                                                  01313152
CR4339     EXEC SQL                                                     01314052
CR4339          INCLUDE XST00063                                        01315052
CR4339     END-EXEC.                                                    01316052
CR4339                                                                  01317052
CR4339******************************************************************01318052
CR4339*  DB2 TABLE XST00064 (XST_STR_GP_PRCHG)                          01319052
CR4339******************************************************************01319152
CR4339                                                                  01319252
CR4339     EXEC SQL                                                     01319352
CR4339          INCLUDE XST00064                                        01319452
CR4339     END-EXEC.                                                    01319552
CR4339                                                                  01319652
      ******************************************************************01320017
      *  DB2 TABLE DB2PDBA.EPT_STR_FIL_RFRSH                            01330017
      ******************************************************************01340017
                                                                        01350017
           EXEC SQL                                                     01360017
                INCLUDE EPT00010                                        01370017
           END-EXEC.                                                    01380017
                                                                        01390017
      ******************************************************************01391017
      *  DB2 TABLE DB2PDBA.XST_LOC                                      01392017
      ******************************************************************01393017
                                                                        01394017
           EXEC SQL                                                     01395017
                INCLUDE XST00001                                        01396017
           END-EXEC.                                                    01397017
                                                                        01398017
      ******************************************************************01399017
      *  DB2 TABLE DB2PDBA.XST_LOC_ATTR                                 01399117
      ******************************************************************01399217
                                                                        01399317
           EXEC SQL                                                     01399417
                INCLUDE XST00025                                        01399517
           END-EXEC.                                                    01399617
                                                                        01399717
      ***************************************************************** 01400017
       PROCEDURE DIVISION.                                              01410017
      ***************************************************************** 01420017
                                                                        01430017
      ******************************************************************01440017
      *  0000-MAINLINE-PARAGRAPH.                                       01450017
      ******************************************************************01460017
       0000-MAINLINE-PARAGRAPH.                                         01470017
                                                                        01480017
           PERFORM 1000-INITIALIZE.                                     01490017
                                                                        01500017
           PERFORM 1500-PROCESS-INPUT                                   01501021
             UNTIL PS-EOF-CC1-YES.                                      01520017
                                                                        01521015
           IF PV-ERROR-COUNT > 0                                        01522020
               PERFORM 9100-SQL-ABEND                                   01522115
           END-IF.                                                      01523015
                                                                        01530017
           PERFORM 9000-CLOSE-FILES.                                    01540056
CR4339     IF PS-SET-RETURN-4091-YES THEN                               01541052
CR4339         MOVE +4091              TO RETURN-CODE                   01542052
CR4339     END-IF.                                                      01550052
                                                                        01551052
           STOP RUN.                                                    01560017
                                                                        01570017
      ******************************************************************01600017
      *  1000-INITIALIZE.                                               01610017
      *     OPEN AND READS INPUT FILE AND PROCESSES CONTROL CARDS.      01620017
      ******************************************************************01630017
       1000-INITIALIZE.                                                 01640017
                                                                        01650017
           OPEN INPUT CONTROL-CARD-CONV-STORES.                         01660020
P17793     OPEN OUTPUT STORE-CLSE.                                      01680017
                                                                        01680017
           READ CONTROL-CARD-CONV-STORES INTO CC-CONTROL-CARD-1         01690017
              AT END                                                    01700017
                 SET PS-EOF-CC1-YES          TO TRUE.                   01710017
                                                                        01720017
           IF PS-EOF-CC1-YES                                            01760017
              MOVE '1000-INITIALIZE'        TO PV-PARAGRAPH-NAME        01770017
              MOVE 'CONTROL CARD IS EMPTY'  TO PV-OPERATION-MSG-1       01780017
              SET AC-CONTROL-CARD-ERROR     TO TRUE                     01790017
              PERFORM 9999-ABEND                                        01800017
           ELSE                                                         01800100
              MOVE PV-CONV-STORE         TO PV-HOLD-STORE-NBR           01801017
              MOVE PV-RFRSH-TYP-CDE      TO PV-HOLD-RFRSH-TYP-CDE       01802017
                                                                                
              IF PV-RFRSH-DTE = SPACES                                  01802100
                 MOVE -1                 TO PC-NULL-IND                 01802200
              ELSE                                                      01804000
                 MOVE 0                  TO PC-NULL-IND                 01804100
                 MOVE PV-RFRSH-DTE       TO PV-HOLD-RFRSH-DTE           01804200
              END-IF                                                    01810017
                                                                                
CR4339        MOVE PV-INDICATOR          TO PV-HOLD-INDICATOR                   
CR4339        PERFORM 1700-VALIDATE-INDICATOR.                                  
                                                                                
           DISPLAY 'PROGRAM NAME:       ' PC-CURRENT-PROGRAM-NAME.      01830017
           DISPLAY '---CONTROL CARD 1:     ' CC-CONTROL-CARD-1.         01840022
                                                                        01850017
           PERFORM 6000-GET-CURRENT-DATE.                               01851004
                                                                        01851121
      ******************************************************************01851221
      *  1500-PROCESS-INPUT.                                            01851321
      ******************************************************************01851521
       1500-PROCESS-INPUT.                                              01851621
                                                                        01851721
           PERFORM 4000-FIND-OPEN-LOCATION.                             01851921
           IF PS-LOC-FOUND-YES                                          01852021
               SET PS-SEND-RFRSH-YES       TO TRUE                      01852123
               IF PV-FOUND-E-BUS-IND = 'Y'                              01852222
C99999             IF (PV-HOLD-RFRSH-TYP-CDE = 'XS' OR                  01852322
C99999                 PV-HOLD-RFRSH-TYP-CDE = 'SI')                    01852322
                       SET PS-SEND-RFRSH-NO                             01852423
                                           TO TRUE                      01852523
P17793                 INITIALIZE ST-STR-CLSE                                   
P17793                 MOVE 'FILE SET FOR E-COM STORE ' TO PV-REASON            
P17793                 IF PV-WRITE-CNT = 0                                      
P17793                    WRITE STORE-CLOSE-RECORD FROM HEADER-LINE             
P17793                 END-IF                                                   
P17793                 PERFORM 7000-WRITE-FILE                                  
                   END-IF                                               01853123
               END-IF                                                   01853223
           ELSE                                                         01853323
               SET PS-SEND-RFRSH-NO        TO TRUE                      01853423
           END-IF.                                                      01853621
                                                                                
CR4339     IF PS-SEND-RFRSH-YES                                         01853952
CR4339       IF PS-INDT-NORMAL-YES                                              
CR4339         PERFORM 1600-VALIDATE-INPUT-DATE                         01851828
CR4339         IF (PV-HOLD-RFRSH-TYP-CDE  = 'PL' OR                     01854052
CR4339             PV-HOLD-RFRSH-TYP-CDE  = 'FP')                       01854152
CR4339             SET PS-FIND-RFRSH-DTE-NO  TO TRUE                    01854252
CR4339             INITIALIZE PV-RFRSH-COUNT                            01854352
CR4339             PERFORM 1900-INCREMENT-RFRSH-DATE                    01854552
CR4339             PERFORM 1800-FIND-RFRSH-DTE                          01854652
CR4339             UNTIL PS-FIND-RFRSH-DTE-YES                          01854752
CR4339             IF  PV-RFRSH-COUNT > 1                               01854852
CR4339                 SET PS-SET-RETURN-4091-YES TO TRUE               01854952
CR4339             END-IF                                               01855052
CR4339         END-IF                                                   01855052
CR4339       END-IF                                                     01855152
             PERFORM 2000-UPDATE-RFRSH-TABLE                            01855226
             IF PS-UPDATE-SUCCESSFUL-NO                                 01855326
                 PERFORM 2100-INSERT-RFRSH-TABLE-ROW                    01855426
             END-IF                                                     01855526
CR4339     END-IF.                                                              
                                                                        01855726
           READ CONTROL-CARD-CONV-STORES INTO CC-CONTROL-CARD-1         01855826
              AT END                                                    01855926
                 SET PS-EOF-CC1-YES          TO TRUE.                   01856026
                                                                        01856126
           IF NOT PS-EOF-CC1-YES                                        01856226
              DISPLAY '--- CONTROL CARD 1:   ' CC-CONTROL-CARD-1        01856326
              MOVE PV-CONV-STORE          TO PV-HOLD-STORE-NBR          01856426
              MOVE PV-RFRSH-TYP-CDE       TO PV-HOLD-RFRSH-TYP-CDE      01856526
              IF PV-RFRSH-DTE = SPACES                                  01856626
                 MOVE -1                  TO PC-NULL-IND                01856726
              ELSE                                                      01856826
                 MOVE 0                   TO PC-NULL-IND                01856926
                 MOVE PV-RFRSH-DTE        TO PV-HOLD-RFRSH-DTE          01857026
              END-IF                                                    01857126
                                                                                
CR4339        SET PS-INDT-NORMAL-NO      TO TRUE                                
CR4339        INITIALIZE PV-HOLD-INDICATOR                                      
CR4339        MOVE PV-INDICATOR          TO PV-HOLD-INDICATOR                   
CR4339        PERFORM 1700-VALIDATE-INDICATOR                                   
CR4339      END-IF.                                                             
                                                                        01858004
CR4339******************************************************************01880052
CR4339*1600-VALIDATE-INPUT-DATE.                                        01880152
CR4339******************************************************************01880252
CR4339 1600-VALIDATE-INPUT-DATE.                                        01880452
CR4339     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              01880752
CR4339     SET DPG51-ACTUAL-CALENDAR-ONLY TO TRUE.                      01880852
CR4339     SET DPG52-LK-DTE-ISO-FMT TO TRUE.                            01880952
CR4339     MOVE PV-HOLD-RFRSH-DTE  TO DPG52-LK-DATE-INPUT.              01881352
CR4339     CALL DP500-KOHLS-CALENDAR-ROUTINE                            01881452
CR4339     USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                   01881552
CR4339                                                                  01881652
CR4339      IF DPG54-DATE-INVALID                                       01881752
CR4339         DISPLAY '** PARAGRAPH: 1600-VALIDATE-INPUT-DATE'         01881852
CR4339         DISPLAY '** CALENDAR ROUTINE ERROR MESSAGE = '           01881952
CR4339                     DPG54-ERROR-MESSAGE                          01882052
CR4339         DISPLAY '** DPG51 = ' DPG51                              01882152
CR4339         DISPLAY '** DPG52 = ' DPG52                              01882252
CR4339         DISPLAY '** DPG53 = ' DPG53                              01882352
CR4339         DISPLAY '** DPG54 = ' DPG54                              01882452
CR4339         DISPLAY '** DPG55 = ' DPG55                              01882552
CR4339         DISPLAY '** DPG56 = ' DPG56                              01882652
CR4339         SET AC-CONTROL-CARD-ERROR TO TRUE                        01882752
CR4339         PERFORM 9999-ABEND                                       01882852
CR4339      ELSE                                                        01882952
CR4339         MOVE DPG55-DB2-ISO-DATE-FMT   TO PV-HOLD-RFRSH-DTE       01883052
CR4339      END-IF.                                                     01883152
CR4339******************************************************************01851221
CR4339*  1700-VALIDATE-INDICATOR.                                       01851321
CR4339******************************************************************01851521
CR4339 1700-VALIDATE-INDICATOR.                                         01851621
CR4339                                                                          
CR4339       EVALUATE TRUE                                              01885452
CR4339        WHEN  PV-HOLD-INDICATOR   = 'N'                                   
CR4339           SET PS-INDT-NORMAL-YES  TO TRUE                                
CR4339        WHEN  PV-HOLD-INDICATOR   = 'F'                                   
CR4339           SET PS-INDT-NORMAL-NO   TO TRUE                                
CR4339        WHEN  (PV-HOLD-INDICATOR   = SPACE AND                            
CR4339              PV-HOLD-RFRSH-DTE   = SPACE)                                
CR4339           SET PS-INDT-NORMAL-NO   TO TRUE                                
CR4339        WHEN OTHER                                                        
CR4339           MOVE '1700-VALIDATE-INDICATOR'  TO PV-PARAGRAPH-NAME   01770017
CR4339           MOVE 'MISSING INDICATOR IN CC'  TO PV-OPERATION-MSG-1  01780017
CR4339           SET AC-CONTROL-CARD-ERROR       TO TRUE                01790017
CR4339           PERFORM 9999-ABEND                                     01800017
CR4339        END-EVALUATE.                                                     
CR4339******************************************************************01883252
CR4339*1800-FIND-RFRSH-DTE.                                             01883352
CR4339******************************************************************01883452
CR4339 1800-FIND-RFRSH-DTE.                                             01883552
CR4339        MOVE '1800-FIND-RFRSH-DTE' TO PV-PARAGRAPH-NAME.          01883652
CR4339        ADD +1 TO  PV-RFRSH-COUNT.                                01883852
CR4339                                                                  01883952
CR4339        EXEC SQL                                                  01884052
CR4339          SELECT COUNT(*)                                         01884152
CR4339          INTO  :PV-EVNT-COUNT                                    01884252
CR4339          FROM   XST_PRCHG         A                              01884352
CR4339               , XST_STR_GP_PRCHG  B                              01884452
CR4339          WHERE  A.PRCHG_ID        =  B.PRCHG_ID                  01884555
CR4339            AND  B.PRCHG_EFF_DTE   = :PV-HOLD-RFRSH-NEXT-DTE      01884952
CR4339            AND  A.LAST_ACTN_CDE   <> 'D'                         01885052
CR4339            AND  B.LAST_ACTN_CDE   <> 'D'                         01885152
CR4339        END-EXEC.                                                 01885252
CR4339                                                                  01885352
CR4339        EVALUATE TRUE                                             01885452
CR4339           WHEN SQLCODE = +0                                      01885552
CR4339              IF PV-EVNT-COUNT = 0                                01885652
CR4339                 SET PS-FIND-RFRSH-DTE-YES TO TRUE                01885752
CR4339              ELSE                                                01885952
CR4339                 SET PS-FIND-RFRSH-DTE-NO TO TRUE                 01886152
CR4339              END-IF                                              01886252
CR4339           WHEN SQLCODE = +100                                    01886352
CR4339              SET PS-FIND-RFRSH-DTE-YES TO TRUE                   01886552
CR4339           WHEN SQLCODE = -304                                    01886652
CR4339              SET PS-FIND-RFRSH-DTE-NO  TO TRUE                   01886752
CR4339           WHEN OTHER                                             01886852
CR4339                DISPLAY 'ERROR WHILE FINDING REFRESH DATE'        01886952
CR4339                DISPLAY 'SQLCODE:'  SQLCODE                       01887052
CR4339                PERFORM 9100-SQL-ABEND                            01887152
CR4339        END-EVALUATE.                                             01887252
CR4339        IF PS-FIND-RFRSH-DTE-NO THEN                              01887352
CR4339              MOVE PV-HOLD-RFRSH-NEXT-DTE TO                      01887452
CR4339                   PV-HOLD-RFRSH-DTE                              01887552
CR4339              PERFORM 1900-INCREMENT-RFRSH-DATE                   01887652
CR4339        END-IF.                                                   01887752
CR4339******************************************************************01887852
CR4339*1900-INCREMENT-RFRSH-DATE.                                       01887952
CR4339******************************************************************01888052
CR4339 1900-INCREMENT-RFRSH-DATE.                                       01888152
CR4339      INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.             01888252
CR4339      SET DPG51-ACTUAL-CALENDAR-ONLY TO TRUE.                     01888352
CR4339      SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                      01888452
CR4339      MOVE PV-HOLD-RFRSH-DTE  TO DPG52-LK-DATE-INPUT.             01888552
CR4339      SET DPG51-INCREMENT-DATE      TO TRUE.                      01888652
CR4339      MOVE PC-DAYS-FR            TO DPG51-INCR-DECR-DAYS-9.       01888752
CR4339      CALL DP500-KOHLS-CALENDAR-ROUTINE                           01888852
CR4339         USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.               01888952
CR4339      IF DPG54-DATE-INVALID                                       01889052
CR4339         DISPLAY '** PARAGRAPH: 1900-INCREMENT-RFRSH-DATE'        01889152
CR4339         DISPLAY '** CALENDAR ROUTINE ERROR MESSAGE = '           01889252
CR4339                     DPG54-ERROR-MESSAGE                          01889352
CR4339         DISPLAY '** DPG51 = ' DPG51                              01889452
CR4339         DISPLAY '** DPG52 = ' DPG52                              01889552
CR4339         DISPLAY '** DPG53 = ' DPG53                              01889652
CR4339         DISPLAY '** DPG54 = ' DPG54                              01889752
CR4339         DISPLAY '** DPG55 = ' DPG55                              01889852
CR4339         DISPLAY '** DPG56 = ' DPG56                              01889952
CR4339         SET AC-CONTROL-CARD-ERROR TO TRUE                        01890052
CR4339         PERFORM 9999-ABEND                                       01890152
CR4339      ELSE                                                        01890252
CR4339         MOVE DPG55-DB2-ISO-DATE-FMT   TO PV-HOLD-RFRSH-NEXT-DTE  01890352
CR4339      END-IF.                                                     01890452
      ******************************************************************01890526
      * 2000-UPDATE-RFRSH-TABLE.                                        01891017
      ******************************************************************01900017
       2000-UPDATE-RFRSH-TABLE.                                         01901017
                                                                        01910017
           PERFORM                                                      01920021
             WITH TEST AFTER                                            01930021
             VARYING PV-ATTEMPT-COUNT                                   01940021
             FROM 1 BY 1                                                01950021
             UNTIL (PV-ATTEMPT-COUNT GREATER PC-MAX-ATTEMPT-COUNT       01960021
                       OR  SQLCODE = 0                                  01970021
                       OR  SQLCODE = +100)                              01980021
                                                                        01990021
              EXEC SQL                                                  02000021
                 UPDATE EPT_STR_FIL_RFRSH                               02010021
                    SET RFRSH_DTE = :PV-HOLD-RFRSH-DTE:PC-NULL-IND,     02020021
                        LAST_RFRSH_DTE = :PV-HOLD-LAST-RFRSH-DTE        02021021
                  WHERE STR_NBR   = :PV-HOLD-STORE-NBR                  02030021
                    AND RFRSH_TYP_CDE = :PV-HOLD-RFRSH-TYP-CDE          02031021
              END-EXEC                                                  02040021
                                                                        02050021
              EVALUATE SQLCODE                                          02060021
                 WHEN ZERO                                              02070021
                    SET PS-UPDATE-SUCCESSFUL-YES TO TRUE                02080025
                 WHEN +100                                              02090021
                    SET PS-UPDATE-SUCCESSFUL-NO  TO TRUE                02100025
                 WHEN -904                                              02130021
                 WHEN -911                                              02140021
                 WHEN -913                                              02150021
                    CONTINUE                                            02160021
                 WHEN OTHER                                             02170021
                    DISPLAY '=== DATABASE ERROR ON UPDATE ==='          02180021
                    DISPLAY 'STORE:         '  PV-CONV-STORE            02190021
                    DISPLAY 'REFRESH TYPE:  '  PV-RFRSH-TYP-CDE         02200021
                    DISPLAY 'REFRESH DATE:  '  PV-RFRSH-DTE             02201021
                    ADD 1                        TO PV-ERROR-COUNT      02210025
                    PERFORM 9100-SQL-ABEND                              02220021
              END-EVALUATE                                              02230021
           END-PERFORM.                                                 02240021
                                                                        02250000
           IF PV-ATTEMPT-COUNT         > PC-MAX-ATTEMPT-COUNT           02260021
              DISPLAY '=== DATABASE TIMEOUT ON UPDATE ==='              02270022
              DISPLAY 'STORE:         '  PV-CONV-STORE                  02271022
              DISPLAY 'REFRESH TYPE:  '  PV-RFRSH-TYP-CDE               02272022
              DISPLAY 'REFRESH DATE:  '  PV-RFRSH-DTE                   02273022
              ADD 1                              TO PV-ERROR-COUNT      02280025
              MOVE SQLCODE                       TO PV-DB2-OPERATION    02290025
              PERFORM 9100-SQL-ABEND                                    02300021
           END-IF.                                                      02310021
                                                                        02420000
      ******************************************************************02441017
      * 2100-INSERT-RFRSH-TABLE-ROW.                                    02442017
      ******************************************************************02443017
       2100-INSERT-RFRSH-TABLE-ROW.                                     02443117
                                                                        02444017
           PERFORM                                                      02445017
               WITH TEST AFTER                                          02446017
               VARYING PV-ATTEMPT-COUNT                                 02447017
               FROM 1 BY 1                                              02448017
               UNTIL (PV-ATTEMPT-COUNT GREATER PC-MAX-ATTEMPT-COUNT     02449017
                         OR  SQLCODE = 0                                02449117
                         OR  SQLCODE = +100)                            02449217
                                                                        02449317
                EXEC SQL                                                02449417
                   INSERT INTO EPT_STR_FIL_RFRSH                        02449517
                       (STR_NBR,                                        02449617
                        RFRSH_TYP_CDE,                                  02449717
                        RFRSH_DTE,                                      02449800
                        LAST_RFRSH_DTE)                                 02449900
                 VALUES (:PV-HOLD-STORE-NBR,                            02450000
                         :PV-HOLD-RFRSH-TYP-CDE,                        02450100
                         :PV-HOLD-RFRSH-DTE:PC-NULL-IND,                02450200
                         :PV-HOLD-LAST-RFRSH-DTE)                       02450500
                END-EXEC                                                02450617
                                                                        02450717
                EVALUATE SQLCODE                                        02450817
                   WHEN ZERO                                            02450917
                      CONTINUE                                          02451017
                   WHEN -904                                            02451717
                   WHEN -911                                            02451817
                   WHEN -913                                            02451917
                      CONTINUE                                          02452017
                   WHEN OTHER                                           02452117
                      DISPLAY '=== DATABASE ERROR ON INSERT ==='        02452220
                      DISPLAY 'STORE:         '  PV-CONV-STORE          02452320
                      DISPLAY 'REFRESH TYPE:  '  PV-RFRSH-TYP-CDE       02452420
                      DISPLAY 'REFRESH DATE:  '  PV-RFRSH-DTE           02452520
                      ADD 1                   TO PV-ERROR-COUNT         02452920
                      PERFORM 9100-SQL-ABEND                            02453017
                END-EVALUATE                                            02453117
           END-PERFORM.                                                 02453200
                                                                        02453300
           IF PV-ATTEMPT-COUNT         > PC-MAX-ATTEMPT-COUNT           02453417
              DISPLAY '=== DATABASE TIMEOUT ON INSERT ==='              02453522
              DISPLAY 'STORE:         '  PV-CONV-STORE                  02453622
              DISPLAY 'REFRESH TYPE:  '  PV-RFRSH-TYP-CDE               02453722
              DISPLAY 'REFRESH DATE:  '  PV-RFRSH-DTE                   02453822
              ADD 1                   TO PV-ERROR-COUNT                 02453920
              MOVE SQLCODE            TO PV-DB2-OPERATION               02454022
              PERFORM 9100-SQL-ABEND                                    02454117
           END-IF.                                                      02454217
                                                                        02454317
                                                                        02454609
      ******************************************************************02454717
      * 4000-FIND-OPEN-LOCATION.                                        02454817
      ******************************************************************02454917
       4000-FIND-OPEN-LOCATION.                                         02455017
                                                                        02455117
           PERFORM                                                      02455217
               WITH TEST AFTER                                          02455317
               VARYING PV-ATTEMPT-COUNT                                 02455417
               FROM 1 BY 1                                              02455517
               UNTIL (PV-ATTEMPT-COUNT GREATER PC-MAX-ATTEMPT-COUNT     02455617
                         OR  SQLCODE = 0                                02455717
                         OR  SQLCODE = +100)                            02455817
                                                                        02455917
                EXEC SQL                                                02456017
                  SELECT LOC.E_BUS_IND                                  02456117
                    INTO :PV-FOUND-E-BUS-IND                            02456217
                    FROM XST_LOC       LOC                              02456311
                        ,XST_LOC_ATTR  ATTR                             02456411
                   WHERE  LOC.LOC_NBR     =  :PV-HOLD-STORE-NBR         02456510
                     AND  LOC.LOC_NBR     =  ATTR.LOC_NBR               02456610
                     AND  LOC.LOC_TYP_CDE = 'DS'                        02456710
                     AND (ATTR.POS_CAP_LVL_ID > 12                      02456810
                      OR  ATTR.POS_CAP_LVL_ID = 5)                      02456910
                     AND (CURRENT DATE                                  02457010
                                 BETWEEN  LOC.OPN_DTE                   02457110
                                     AND  LOC.CLO_DTE                   02457210
                      OR   LOC.OPN_DTE                                  02457310
                                 BETWEEN CURRENT DATE                   02457410
                                     AND CURRENT DATE + 42 DAYS)        02457510
                  WITH UR                                               02457810
                END-EXEC                                                02457917
                                                                        02458017
                EVALUATE SQLCODE                                        02458117
                   WHEN ZERO                                            02458217
                      SET PS-LOC-FOUND-YES  TO TRUE                     02458322
                   WHEN +100                                            02460120
                      SET PS-LOC-FOUND-NO     TO TRUE                   02460220
P17793                INITIALIZE ST-STR-CLSE                                    
P17793                MOVE 'FILE SET FOR CLOSED STORE' TO PV-REASON             
P17793                IF PV-WRITE-CNT = 0                                       
P17793                   WRITE STORE-CLOSE-RECORD FROM HEADER-LINE              
P17793                END-IF                                                    
P17793                PERFORM 7000-WRITE-FILE                                   
                   WHEN -904                                            02461217
                   WHEN -911                                            02461317
                   WHEN -913                                            02461417
                      CONTINUE                                          02461517
                   WHEN OTHER                                           02461617
                      DISPLAY 'SQLCODE:' SQLCODE                        02461735
                      SET PS-LOC-FOUND-NO     TO TRUE                   02461819
                      DISPLAY '=== DATABASE ERROR ON LOC SELECT ==='    02461921
                      DISPLAY 'STORE:         '  PV-CONV-STORE          02462020
                      DISPLAY 'REFRESH TYPE:  '  PV-RFRSH-TYP-CDE       02462120
                      DISPLAY 'REFRESH DATE:  '  PV-RFRSH-DTE           02462220
                      ADD 1                   TO PV-ERROR-COUNT         02462520
                      PERFORM 9100-SQL-ABEND                            02462617
                END-EVALUATE                                            02462717
           END-PERFORM.                                                 02462817
                                                                        02462917
           IF PV-ATTEMPT-COUNT         > PC-MAX-ATTEMPT-COUNT           02463017
              DISPLAY '=== DATABASE TIMEOUT ON LOC SELECT ==='          02463122
              DISPLAY 'STORE:         '  PV-CONV-STORE                  02463222
              DISPLAY 'REFRESH TYPE:  '  PV-RFRSH-TYP-CDE               02463322
              DISPLAY 'REFRESH DATE:  '  PV-RFRSH-DTE                   02463422
              ADD 1                   TO PV-ERROR-COUNT                 02463520
              MOVE SQLCODE            TO PV-DB2-OPERATION               02463622
              PERFORM 9100-SQL-ABEND                                    02463717
           END-IF.                                                      02463817
                                                                        02463917
      ******************************************************************02464017
      * 6000-GET-CURRENT-DATE                                           02464117
      *   GET THE CURRENT DATE TO BE USED FOR LAST REFRSH DATE          02464217
      ******************************************************************02464317
       6000-GET-CURRENT-DATE.                                           02464417
                                                                        02464517
           ACCEPT PV-CURRENT-DATE-YMD FROM DATE.                        02464617
                                                                        02464717
           MOVE PV-CURRENT-YEAR-YMD  TO PV-CURRENT-YEAR-CYMD.           02464817
           MOVE PV-CURRENT-MONTH-YMD TO PV-CURRENT-MONTH-CYMD.          02464917
           MOVE PV-CURRENT-DAY-YMD   TO PV-CURRENT-DAY-CYMD.            02465017
           MOVE PV-CURRENT-DATE-CYMD TO PV-HOLD-LAST-RFRSH-DTE.         02465117
P17793*****************************************************************         
P17793* WRITE OUTPUT FILE                                                       
P17793***************************************************************** 02580017
P17793 7000-WRITE-FILE.                                                         
P17793     MOVE PV-CONV-STORE TO PV-STORE.                                      
P17793     MOVE PV-RFRSH-TYP-CDE TO PV-REFSH-TYPE.                              
P17793     MOVE PV-RFRSH-DTE TO PV-REFSH-DATE.                                  
P17793     ADD 1 TO PV-WRITE-CNT.                                               
P17793                                                                          
P17793     WRITE STORE-CLOSE-RECORD FROM ST-STR-CLSE.                           
P17793                                                                          
      ****************************************************************  02580017
      *CLOSES FILES                                                     02590017
      ****************************************************************  02600017
       9000-CLOSE-FILES.                                                02610000
                                                                        02620017
P17793     CLOSE CONTROL-CARD-CONV-STORES,                              02630020
P17793           STORE-CLSE.                                            02650017
                                                                        02650017
      ******************************************************************02680017
      * 9100-SQL-ABEND                                                  02690017
      *     ABEND PROGRAM WHEN AN SQL ERROR CODE OR WARNING CODE OCCURS.02700017
      ******************************************************************02710017
       9100-SQL-ABEND.                                                  02720017
           DISPLAY '** ABEND     **'.                                   02730017
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        02740017
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              02750017
           EXEC SQL                                                     02751024
               ROLLBACK                                                 02752024
           END-EXEC.                                                    02757024
                                                                        02760017
      ******************************************************************02770017
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     02780017
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      02790017
      ******************************************************************02800017
           COPY DPPD004.                                                02810017
                                                                        02820017
           SET AC-DB2-ERROR TO TRUE.                                    02830017
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         02840017
                                                                        02850017
      ******************************************************************02880017
      * 9999-ABEND                                                      02890017
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                  02900017
      ******************************************************************02910017
       9999-ABEND.                                                      02920017
           DISPLAY '** ABEND           **'.                             02930017
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  02940017
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        02950017
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       02960017
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       02970017
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         02980017
                                                                        02990000
