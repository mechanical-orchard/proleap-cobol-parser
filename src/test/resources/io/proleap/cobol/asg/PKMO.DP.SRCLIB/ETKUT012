       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    ETKUT012.                                                 
       AUTHOR.        JON FIEDLER.                                              
       DATE-WRITTEN.  07/02/2002.                                               
       DATE-COMPILED.                                                           
      *****************************************************************         
      * THIS PROGRAM CREATES THE LOCATION ADDRESS CHANGES FILE.                 
      *****************************************************************         
      *THIS PROGRAM WILL TAKE IN AN INPUT FILE OF STORE AND DC ADDRESS          
      *CHANGES. THESE CAN INCLUDE DELETES, INSERTS, AND CHANGES. COLUMN         
      *ONE OF THE INPUT FILE WILL INDICATE WHAT KIND OF CHANGE HAS TAKEN        
      *PLACE.                                                                   
      *                                                                         
      * ***THE FOLLOWING INDICATES A CHANGE IN ADDRESS INFORMATION              
      * I- (SAME STORE NUMBER)                                                  
      * D- (SAME STORE NUMBER)                                                  
      *                                                                         
      * ***THE FOLLOWING INDICATES A INSERT                                     
      * I- (DIFFERENT STORE NUMBER FOLLOWING THE I-)                            
      *                                                                         
      * ***THE FOLLOWING INDICATES A DELETE                                     
      * D- (DIFFERENT STORE NUMBER BEFORE THE D-)                               
      *                                                                         
      *THE PROGRAM WILL PROCESS THE INPUT FILE UNTIL THE EOF AND WILL           
      *OUTPUT FORMATTED RECORDS OF ADDRESS CHANGES FOR TRANSLATION.             
      *1000-PROCESS-FILE DOES THE MAIN PROCESSING. THE MAIN PROCESSING          
      *WILL HOLD A CURRENT RECORD AND THE RECORD BEFORE THE CURRENT REC         
      *(PREVIOUS RECORD). THE PREVIOUS RECORD WILL ALWAYS BE MOVED TO           
      *THE OUTPUT AND THE MAIN PROCESSING WILL DETERMINE WHETHER IT IS A        
      *CHANGE, INSERT, OR DELETE. IF THE RECORD IS A CHANGE 'C' WILL BE         
      *PUT IN THE ACTION CODE, A 'D' FOR DELETE, AND 'A' FOR AN ADD.            
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS V                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
VF0817 01 INPUT-RECORD                   PIC X(146).                            
                                                                                
       FD  OUTPUT-FILE                                                          
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01 OUTPUT-RECORD                  PIC X(150).                            
                                                                                
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01 END-OF-INPUT-FILE-SW           PIC X(1)    VALUE 'N'.                 
          88  END-OF-INPUT-FILE                      VALUE 'Y'.                 
                                                                                
       01  ABEND-CODE                    COMP SYNC PIC S9(4) VALUE ZERO.        
                                                                                
       01 PREV-INPUT-RECORD.                                                    
          05 PREV-REC-INDICATOR          PIC X(1).                              
          05 FILLER                      PIC X(13).                             
          05 PREV-ADDR-RECORD.                                                  
             10 PREV-TYPE-CODE           PIC X(2).                              
             10 PREV-STORE-NBR           PIC X(5).                              
             10 PREV-STORE-NAME          PIC X(25).                             
             10 PREV-ADDRESS1            PIC X(25).                             
             10 PREV-ADDRESS2            PIC X(25).                             
             10 PREV-CITY                PIC X(15).                             
             10 PREV-STATE               PIC X(2).                              
             10 PREV-ZIP-CODE1           PIC X(5).                              
             10 PREV-ZIP-CODE2           PIC X(5).                              
             10 PREV-OPENED-DATE         PIC X(10).                             
             10 PREV-CLOSED-DATE         PIC X(10).                             
                                                                                
       01 CURR-INPUT-RECORD.                                                    
          05 IN-REC-INDICATOR            PIC X(1).                              
          05 FILLER                      PIC X(13).                             
          05 IN-ADDR-RECORD.                                                    
             10 IN-TYPE-CODE             PIC X(2).                              
             10 IN-STORE-NBR             PIC X(5).                              
             10 IN-STORE-NAME            PIC X(25).                             
             10 IN-ADDRESS1              PIC X(25).                             
             10 IN-ADDRESS2              PIC X(25).                             
             10 IN-CITY                  PIC X(15).                             
             10 IN-STATE                 PIC X(2).                              
             10 IN-ZIP-CODE1             PIC X(5).                              
             10 IN-ZIP-CODE2             PIC X(5).                              
             10 IN-OPENED-DATE           PIC X(10).                             
             10 IN-CLOSED-DATE           PIC X(10).                             
                                                                                
      ******************************************************************        
      * STORE ADDRESS EXTRACT FILE COPYBOOK.                                    
      ******************************************************************        
           COPY ETRD033.                                                        
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN INPUT  INPUT-FILE.                                              
           OPEN OUTPUT OUTPUT-FILE.                                             
                                                                                
           PERFORM 2000-READ-INPUT-FILE                                         
              THRU 2000-READ-INPUT-FILE-EXIT.                                   
                                                                                
           PERFORM 1000-PROCESS-FILE                                            
              THRU 1000-PROCESS-FILE-EXIT                                       
                 UNTIL END-OF-INPUT-FILE.                                       
                                                                                
           CLOSE INPUT-FILE.                                                    
           CLOSE OUTPUT-FILE.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-PROCESS-FILE.                                                       
                                                                                
           MOVE INPUT-RECORD TO PREV-INPUT-RECORD.                              
           MOVE PREV-ADDR-RECORD TO ET033-STORE-ADDRESS-RECORD.                 
                                                                                
           PERFORM 2000-READ-INPUT-FILE                                         
              THRU 2000-READ-INPUT-FILE-EXIT.                                   
                                                                                
           IF PREV-STORE-NBR = IN-STORE-NBR                                     
              MOVE 'C' TO ET033-ACTION-CODE                                     
              PERFORM 2000-READ-INPUT-FILE                                      
                 THRU 2000-READ-INPUT-FILE-EXIT                                 
           ELSE                                                                 
              EVALUATE PREV-REC-INDICATOR                                       
                 WHEN 'I'                                                       
                    MOVE 'A' TO ET033-ACTION-CODE                               
                 WHEN 'D'                                                       
                    MOVE 'D' TO ET033-ACTION-CODE                               
                 WHEN OTHER                                                     
                    DISPLAY ' '                                                 
                    DISPLAY 'PROGRAM ABEND --- ETKUT012'                        
                    DISPLAY '1000-PROCESS-FILE'                                 
                    DISPLAY 'ERROR IN INPUT FILE'                               
                    DISPLAY 'PREV-REC-INDICATOR = ' PREV-REC-INDICATOR          
                    MOVE +4013 TO ABEND-CODE                                    
                    PERFORM 9000-ABEND                                          
                       THRU 9000-ABEND-EXIT                                     
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
           PERFORM 3000-WRITE-OUTPUT-FILE                                       
              THRU 3000-WRITE-OUTPUT-FILE-EXIT.                                 
                                                                                
       1000-PROCESS-FILE-EXIT.                                                  
           EXIT.                                                                
                                                                                
       2000-READ-INPUT-FILE.                                                    
                                                                                
           READ INPUT-FILE INTO CURR-INPUT-RECORD                               
              AT END                                                            
                 SET END-OF-INPUT-FILE TO TRUE                                  
                 MOVE SPACES TO CURR-INPUT-RECORD.                              
                                                                                
       2000-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
                                                                                
       3000-WRITE-OUTPUT-FILE.                                                  
                                                                                
            WRITE OUTPUT-RECORD                                                 
                FROM ET033-STORE-ADDRESS-RECORD.                                
                                                                                
       3000-WRITE-OUTPUT-FILE-EXIT.                                             
            EXIT.                                                               
                                                                                
       9000-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
       9000-ABEND-EXIT.                                                         
           EXIT.                                                                
