       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKUT212.                                       
       AUTHOR.                  LEE YANG.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            07/30/2001.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  INVENTORY LEDGER IN-TRANSIT REVERSAL ADJUSTMENT REFORMATTER   *        
      *                                                                *        
      *  THIS PROGRAM WILL TAKE THE INPUT EXTRACT, REFORMAT, AND FILL  *        
      *  IN SOME OTHER INFORMATION TO CREATE A NEW EXTRACT THAT WILL   *        
      *  FEED THE INVENTORY LEDGER TO ADJUST THE DOLLAR FOR IN-TRANSIT *        
      *  MERCHANDISE.                                                  *        
      *                                                                *        
      *  EACH INPUT RECORD WILL PRODUCE A SIMILAR OUTPUT RECORD.       *        
      *  DO NOT ATTEMPT TO SUM UP BY STORE/DEPT/CLASS, BECAUSE THE     *        
      *  RETAIL/COST AMOUNT MAY EXCEED THE 5 DIGITS THAT ARE DEFINED   *        
      *  IN THE MLRD015 EXTRACT.                                       *        
      *                                                                *        
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
W26600* W26600  04-05-2004 R. JANSSEN MERCH LEDGER STORE EXPANSION              
W27808* W27808  05-12-2005 S. JACKSON CONVERT PROCESS TO TAKE IN A STORE        
W27808*                    EXPECTED FEED AND CREATE REVERSING TRANSACTN         
W27808*                    IN MLRD015 FORMAT.                                   
W34309* W34309  04-25-2008 B.GRAHAM - STRATAGEM ADD INITIALIZE FOR              
W34309*                    MLRD015                                              
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT DATE-CONTROL-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT IN-TRANSIT-ADJ-FILE                                           
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT IN-TRANSIT-ADJ-REF-FILE                                       
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  DATE-CONTROL-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
W27808 01  DATE-CONTROL-REC                PIC X(80).                           
                                                                                
       FD  IN-TRANSIT-ADJ-FILE                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
W27808 01  IN-TRANSIT-ADJ-REC              PIC X(100).                          
                                                                                
       FD  IN-TRANSIT-ADJ-REF-FILE                                              
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 150 CHARACTERS                                       
           DATA RECORD IS IN-TRANSIT-REF-REC.                                   
W26600 01  IN-TRANSIT-REF-REC              PIC X(150).                          
Ù                                                                               
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** IN-TRANSIT INV LEDGER ADJ EXTR ***'.                            
                                                                                
      *01  INRD137-IN-TRANSIT-ADJ-REC.                                          
W26600     COPY INRD137.                                                        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** ML/IN ADJUSTMENT REFORMAT EXTR ***'.                            
                                                                                
      *01  ML015-PURCHASE-RECORD.                                               
W26600     COPY MLRD015.                                                        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING CONSTANT BEGIN ***'.                                
                                                                                
       01  CC-CONTROL-FILE.                                                     
           05  CC-CONTROL-DATE             PIC X(10)       VALUE SPACES.        
                                                                                
       01  PC-PROGRAM-CONSTANTS-AREA.                                           
           05  PC-COMPANY                  PIC X(02)       VALUE '03'.          
           05  PC-INCOMPLETE-DTE           PIC X(10)       VALUE                
                                                           '9999-09-09'.        
           05  PC-Y                        PIC X           VALUE 'Y'.           
           05  PC-N                        PIC X           VALUE 'N'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING SWITCHES BEGIN ***'.                                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
           05  PS-END-OF-ADJ-FILE          PIC X           VALUE 'N'.           
               88  END-OF-ADJ-FILE                         VALUE 'Y'.           
           05  PS-END-OF-CC-FILE           PIC X           VALUE 'N'.           
               88  END-OF-CC-FILE                          VALUE 'Y'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING VARIABLE BEGIN ***'.                                
                                                                                
       01  PV-PROGRAM-VARIABLES-AREA.                                           
           05  PV-OPER-ATTEMPTED           PIC X(25)       VALUE SPACES.        
W26600     05  PV-CURR-STORE-NBR           PIC X(5)        VALUE SPACES.        
           05  PV-CURR-STORE-NBR-9 REDEFINES PV-CURR-STORE-NBR                  
W26600                                     PIC 9(5).                            
           05  PV-CURR-BATCH               PIC X(03)       VALUE '400'.         
           05  FILLER REDEFINES PV-CURR-BATCH.                                  
               10  FILLER                  PIC X(02).                           
               10  PV-CURR-BATCH-WEEK      PIC X(01).                           
           05  PV-CURR-FSCL-MN-NBR         PIC X(02).                           
           05  PV-CURR-ADJ-DATE            PIC X(10)       VALUE SPACES.        
           05  FILLER REDEFINES PV-CURR-ADJ-DATE.                               
               10  PV-CURR-ADJ-DATE-YEAR   PIC X(04).                           
               10  FILLER REDEFINES PV-CURR-ADJ-DATE-YEAR.                      
                   15  PV-CURR-ADJ-DATE-CC PIC X(02).                           
                   15  PV-CURR-ADJ-DATE-YY PIC X(02).                           
               10  FILLER                  PIC X(01).                           
               10  PV-CURR-ADJ-DATE-MM     PIC X(02).                           
               10  FILLER                  PIC X(01).                           
               10  PV-CURR-ADJ-DATE-DD     PIC X(02).                           
           05  PV-CURR-DEPT-CLASS          PIC X(05)       VALUE SPACES.        
           05  FILLER REDEFINES PV-CURR-DEPT-CLASS.                             
               10  PV-CURR-DEPT-NBR        PIC 9(03).                           
               10  PV-CURR-CLASS-NBR       PIC 9(02).                           
           05  PV-CURR-ENT-ID              PIC S9(9) COMP  VALUE ZEROES.        
W27808     05  PV-TTL-ACCUM-TOTALS.                                             
               10  PV-TTL-ADJ-QTY          PIC S9(11)      VALUE ZEROES         
                                                           COMP.                
               10  PV-TTL-EXTD-RTL-AMT     PIC S9(13)V9(2) VALUE ZEROES         
                                                           COMP-3.              
W27808     05  PV-TTL-DISPLAY.                                                  
               10  PV-TTL-ADJ-QTY-DISP      PIC ZZ,ZZZ,ZZZ,ZZ9-.                
               10  PV-TTL-EXTD-RTL-AMT-DISP PIC $$,$$$,$$$,$$$.99-.             
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** SQL WORK AREA BEGIN ***'.                                       
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
      *01  DP004-DB2-ERROR-FIELDS.                                              
           COPY DPWS004.                                                        
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  ABEND-4003                                  VALUE +4003.         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TDTATTR - DATE ATTRIBUTE TABLE                             *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE END ***'.                                       
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN RPOCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
W27808     PERFORM Z100-READ-CONTROL-FILE.                                      
W27808                                                                          
W27808     IF NOT END-OF-CC-FILE                                                
W27808         PERFORM Z400-GET-FISCAL-INFO                                     
                                                                                
               PERFORM Z200-READ-ADJ-FILE                                       
                                                                                
               PERFORM B200-PROCESS-ADJ-FILE                                    
                   UNTIL END-OF-ADJ-FILE                                        
W27808     ELSE                                                                 
               MOVE 'INVALID CONTROL CARD' TO PV-OPER-ATTEMPTED                 
               PERFORM Z999-SQL-ABEND                                           
W27808     END-IF.                                                              
                                                                                
           PERFORM B900-FINALIZE                                                
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET              
      * THE REPORT NAME AND NUMBER.                                             
      ******************************************************************        
       B100-HOUSEKEEPING.                                                       
                                                                                
W27808     INITIALIZE PV-TTL-ACCUM-TOTALS.                                      
W34309     INITIALIZE ML015-PURCHASE-RECORD.                                    
                                                                                
           OPEN INPUT  DATE-CONTROL-FILE                                        
                       IN-TRANSIT-ADJ-FILE                                      
                OUTPUT IN-TRANSIT-ADJ-REF-FILE.                                 
                                                                                
      ******************************************************************        
      *    PROCESS IN-TRANSIT ADJUSTMENT FILE AND PRODUCE REPORT                
      ******************************************************************        
       B200-PROCESS-ADJ-FILE.                                                   
                                                                                
           SET ML015-VALID-PURCH-CDE    TO TRUE.                                
           MOVE SPACES                  TO ML015-PURCH-PO-TYP-CDE.              
W27808     MOVE '00000'                 TO ML015-PURCH-VENDOR-INHSE-NMBR        
W27808     MOVE ZEROES                  TO ML015-PURCH-DOC-5-NBR                
W27808                                     ML015-PURCH-DISCT-AMT                
                                           ML015-PURCH-FRGHT-AMT                
                                           ML015-PURCH-MERCH-AMT                
                                           ML015-PURCH-NET.                     
           MOVE '01'                    TO ML015-PURCH-TRANS-CDE                
                                           ML015-PURCH-AP-TRANS-CDE.            
W27808     MOVE IN137-STORE-NBR         TO ML015-PURCH-STORE-NBR.               
W27808     MOVE IN137-SKU-9             TO ML015-PURCH-SKU-NBR.                 
W27808     MOVE IN137-DEPT-9            TO ML015-PURCH-DEPT-NBR.                
W27808     MOVE IN137-CLASS-9           TO ML015-PURCH-CLASS-NBR                
W27808     MOVE IN137-ENT-ID            TO ML015-PURCH-ENT-ID.                  
W27808     MOVE IN137-PO-NBR            TO ML015-PURCH-PO-NBR-9.                
           MOVE PV-CURR-FSCL-MN-NBR     TO ML015-PURCH-KOHLS-MM.                
           MOVE PV-CURR-BATCH           TO ML015-PURCH-BATCH-NBR.               
           MOVE PV-CURR-ADJ-DATE-MM     TO ML015-PURCH-INV-DTE-MM               
                                           ML015-PURCH-DOC-DTE-MM.              
           MOVE PV-CURR-ADJ-DATE-DD     TO ML015-PURCH-INV-DTE-DD               
                                           ML015-PURCH-DOC-DTE-DD.              
           MOVE PV-CURR-ADJ-DATE-YY     TO ML015-PURCH-INV-DTE-YY               
W26600                                     ML015-PURCH-DOC-DTE-YY.              
W26600     MOVE PV-CURR-ADJ-DATE-CC     TO ML015-PURCH-INV-DTE-CC               
W26600                                     ML015-PURCH-DOC-DTE-CC.              
           MOVE IN137-EXTD-RTL-AMT      TO ML015-PURCH-RETAIL.                  
                                                                                
           PERFORM B300-FINALIZE-RECORD.                                        
                                                                                
      ******************************************************************        
      *    ACCUMMULATE THE TOTALS AND READ THE NEXT RECORD                      
      ******************************************************************        
       B300-FINALIZE-RECORD.                                                    
                                                                                
           ADD IN137-BAL-QTY            TO PV-TTL-ADJ-QTY.                      
           ADD IN137-EXTD-RTL-AMT       TO PV-TTL-EXTD-RTL-AMT.                 
                                                                                
           PERFORM Z300-WRITE-REF-ADJ-REC.                                      
                                                                                
           PERFORM Z200-READ-ADJ-FILE.                                          
                                                                                
      ******************************************************************        
      *    FINALIZE THE REPORT AND CLOSE OUT FILES                              
      ******************************************************************        
       B900-FINALIZE.                                                           
                                                                                
           MOVE PV-TTL-ADJ-QTY       TO PV-TTL-ADJ-QTY-DISP.                    
           MOVE PV-TTL-EXTD-RTL-AMT  TO PV-TTL-EXTD-RTL-AMT-DISP.               
                                                                                
           DISPLAY '*************************************'.                     
           DISPLAY '**  TRANSACTION DATE :', PV-CURR-ADJ-DATE.                  
           DISPLAY '*** INKUT212 RUN TOTALS ***'.                               
           DISPLAY '    QUANTITY - ', PV-TTL-ADJ-QTY-DISP.                      
           DISPLAY '    EXTD RTL - ', PV-TTL-EXTD-RTL-AMT-DISP.                 
           DISPLAY '*************************************'.                     
                                                                                
           CLOSE   IN-TRANSIT-ADJ-FILE                                          
                   IN-TRANSIT-ADJ-REF-FILE.                                     
                                                                                
      ******************************************************************        
      *    USE THE DATE CONTROL FILE SPECIFIYING RUN DATE (INV_DATE)            
      ******************************************************************        
W27808 Z100-READ-CONTROL-FILE.                                                  
                                                                                
           READ DATE-CONTROL-FILE                                               
               INTO CC-CONTROL-FILE                                             
                   AT END                                                       
                       SET END-OF-CC-FILE TO TRUE                               
                   NOT AT END                                                   
                       MOVE CC-CONTROL-DATE TO PV-CURR-ADJ-DATE.                
                                                                                
      ******************************************************************        
      *    READ THE NEXT RECORD FROM IN-TRANSIT-ADJ-FILE                        
      ******************************************************************        
       Z200-READ-ADJ-FILE.                                                      
                                                                                
           READ IN-TRANSIT-ADJ-FILE                                             
W27808         INTO IN137-STR-EXPTED-RECORD                                     
               AT END SET END-OF-ADJ-FILE TO TRUE.                              
                                                                                
      ******************************************************************        
      *    WRITE OUT THE REFORMATTED EXTRACT                                    
      ******************************************************************        
       Z300-WRITE-REF-ADJ-REC.                                                  
                                                                                
           WRITE IN-TRANSIT-REF-REC FROM ML015-PURCHASE-RECORD.                 
                                                                                
      ******************************************************************        
      *    CHECK AND OBTAIN THE FISCAL DATE, MONTH, AND WEEK                    
      ******************************************************************        
       Z400-GET-FISCAL-INFO.                                                    
                                                                                
           EXEC SQL                                                             
                SELECT   FSCL_MN_NBR                                            
                       , WK_IN_MN_NBR                                           
                   INTO  :DTATTR-FSCL-MN-NBR                                    
                        ,:DTATTR-WK-IN-MN-NBR                                   
                FROM   TDTATTR                                                  
W27808          WHERE  KY_DTE             = :CC-CONTROL-DATE                    
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   MOVE DTATTR-WK-IN-MN-NBR     TO PV-CURR-BATCH-WEEK           
                   MOVE DTATTR-FSCL-MN-NBR      TO PV-CURR-FSCL-MN-NBR          
               WHEN OTHER                                                       
                   MOVE 'GET FISCAL DATE INFO' TO  PV-OPER-ATTEMPTED            
                   PERFORM Z999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    SQL ERROR - ABEND                                                    
      ******************************************************************        
       Z999-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = INKUT212'.                              
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED                    
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
