       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         MLKUT619.                                            
       AUTHOR.             SHARON SZPISZAR - STRATAGEM.                         
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       03/19/2007.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *PURPOSE:  THIS MODULE IS USED FOR CREATING MARKDOWNS AND MARKDOWN        
      *          CANCELS.                                                       
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                             
      * -------  ----------  ----------------------------------------           
PM3403* PM3403   04-20-2012  LARGE RETAIL FOR MLRD017/MLRD019 RECOMPILE         
      *                                                                         
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT INPUT2-FILE                                                   
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT OUTPUT2-FILE                                                  
               ASSIGN TO OUT02.                                                 
                                                                                
           SELECT OUTPUT3-FILE                                                  
               ASSIGN TO OUT03.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS INPUT-TRANSFER-RECORD.                                
                                                                                
       01  INPUT-TRANSFER-RECORD    PIC X(80).                                  
                                                                        ETKUT004
       FD  INPUT2-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS INPUT-CONTROL-RECORD.                                 
                                                                                
       01  INPUT-CONTROL-RECORD     PIC X(80).                                  
                                                                        ETKUT004
       FD  OUTPUT-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 200 CHARACTERS                                       
           DATA RECORD IS OUTPUT-PRICE-CHANGE-RECORD.                           
                                                                                
       01  OUTPUT-PRICE-CHANGE-RECORD    PIC X(200).                            
                                                                                
       FD  OUTPUT2-FILE                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS OUTPUT-TRANSFER-RECORD.                               
                                                                                
       01  OUTPUT-TRANSFER-RECORD        PIC X(80).                             
                                                                                
       FD  OUTPUT3-FILE                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS OUTPUT-ERROR-RECORD.                                  
                                                                                
       01  OUTPUT-ERROR-RECORD           PIC X(80).                             
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                        ETKUT005
       01  HOLD-FIELDS.                                                         
           05  HOLD-PRICING-DATE            PIC X(10).                          
           05  HOLD-PRICING-STORE-NUMBER    PIC X(05).                          
           05  HOLD-PRICING-STORE-NUMBER-9 REDEFINES                            
               HOLD-PRICING-STORE-NUMBER    PIC 9(05).                          
                                                                                
       01  PRICING-DTE-IN.                                                      
           05  PRICING-DTE-CCYY             PIC X(04).                          
           05  FILLER                       PIC X(01)      VALUE '-'.           
           05  PRICING-DTE-MM               PIC X(02).                          
           05  FILLER                       PIC X(01)      VALUE '-'.           
           05  PRICING-DTE-DD               PIC X(02).                          
                                                                                
       01  IR-INPUT-RECORD-CC.                                          MLKUT002
           05  IR-STORE-NUMBER              PIC X(05).                          
           05  IR-STORE-NUMBER-9 REDEFINES                                      
               IR-STORE-NUMBER              PIC 9(05).                          
           05  FILLER                       PIC X(01).                          
           05  IR-BATCH-NUMBER              PIC X(03).                          
           05  FILLER                       PIC X(01).                          
           05  IR-MN-END-TIMESTAMP.                                             
               10  IR-TIMESTAMP-MN-END-MM   PIC X(02).                          
               10  FILLER                   PIC X(01).                          
               10  IR-TIMESTAMP-MN-END-DD   PIC X(02).                          
               10  FILLER                   PIC X(01).                          
               10  IR-TIMESTAMP-MN-END-CCYY PIC X(04).                          
           05  FILLER                       PIC X(01).                          
           05  IR-PC-KOHLS-MM-FSCL          PIC X(02).                          
           05  IR-REST-OF-RECORD            PIC X(57).                          
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                                                           COMP SYNC.           
           88  ABEND-4004                                  VALUE +4004.         
           88  ABEND-4013                                  VALUE +4013.         
           88  ABEND-4095                                  VALUE +4095.         
                                                                                
       01  PC-PROGRAM-CONSTANTS-AREA.                                           
           05  PC-PROGRAM-NAME         PIC X(08)  VALUE 'MLKUT619'.             
           05  PC-JOB-NAME             PIC X(08)  VALUE 'MLK923  '.             
                                                                                
       01  PE-PROGRAM-ERROR-MESSAGES.                                           
           05  PE-MSG-01                       PIC X(30) VALUE                  
                '4095-PRICING-INVALID KEY ERROR'.                               
           05  PE-MSG-02                       PIC X(30) VALUE                  
                '4095-PRICING-SKU EDIT ERROR   '.                               
                                                                                
       01  PROGRAM-SWITCHES.                                            ETKUT004
           05  END-OF-FILE-SW             PIC X(1)  VALUE 'N'.          ETKUT004
               88  END-OF-FILE                      VALUE 'Y'.          ETKUT004
           05  ERRORS-FOUND-SW            PIC X(1)  VALUE 'N'.                  
               88  ERRORS-FOUND                     VALUE 'Y'.                  
           05  PS-PRICE-FOUND-STATUS      PIC X(1)  VALUE 'N'.                  
               88  PS-PRICE-NOT-FOUND               VALUE 'N'.                  
               88  PS-PRICE-FOUND                   VALUE 'Y'.                  
                                                                                
      *****************************************************************         
      * PROGRAM VARIABLES                                                       
      *****************************************************************         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME          PIC X(30)   VALUE SPACE.              
           05  PV-OPER-ATTEMPTED          PIC X(30)   VALUE SPACES.             
           05  PV-EDIT-SKU-NBR            PIC 9(08).                            
                                                                                
       01  PROGRAM-VARIABLES.                                                   
           05  PV-MAJOR-CLASS            PIC 9(04) VALUE ZEROES.                
           05  FILLER REDEFINES PV-MAJOR-CLASS.                                 
               10  FILLER                PIC X(02).                             
               10  PV-MAJOR-CLASS-1      PIC X(01).                             
               10  PV-MAJOR-CLASS-2      PIC X(01).                             
           05  PV-PC-DTE-MMDD.                                                  
               10  PV-PC-DTE-MM     PIC 9(02).                                  
               10  PV-PC-DTE-DD     PIC 9(02).                                  
                                                                                
       01  WS-ACCUMULATOR-AREA.                                                 
           05  WS-RECS-READ-ACCUM         PIC S9(13) COMP VALUE ZEROS.          
           05  WS-RECS-WRITE-MC-ACCUM     PIC S9(13) COMP VALUE ZEROS.          
           05  WS-RECS-WRITE-MT-ACCUM     PIC S9(13) COMP VALUE ZEROS.          
           05  WS-RECS-WRITE-DROP-ACCUM   PIC S9(13) COMP VALUE ZEROS.          
           05  WS-RECS-WRITE-ERROR-ACCUM  PIC S9(13) COMP VALUE ZEROS.          
                                                                                
      *01  ML019-TRANSFER-RECORD.                                               
           COPY MLRD019.                                                        
                                                                                
      *01  ML019-TRANSFER-RECORD.                                               
           COPY MLRD019 REPLACING ML019 BY DRP19.                               
                                                                                
      *01  ML019-TRANSFER-RECORD.                                               
           COPY MLRD019 REPLACING ML019 BY ERR19.                               
                                                                                
      *01  ML017-PRICE-CHANGE-RECORD.                                           
           COPY MLRD017.                                                        
                                                                                
      ******************************************************************        
      * COPYBOOK - PRICING MODULE                                               
      ******************************************************************        
           COPY INWS400.                                                        
                                                                                
      ******************************************************************        
      * SQL COMMUNICATIONS WORK AREA                                            
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      /*****************************************************************        
      *FLOW:    THE MODULE SEQUENTIALLY READS THE STORE TRANSFER RECORD.        
      *                                                                         
      *         FORMAT THE INPUT FILE ROWS TO REFLECT THE LEDGER PRICE          
      *      CHANGE RECORD FORMAT FOUND IN THE COPYBOOK MLRD017.                
      *                                                                         
      *         IF A PROBLEM OCCURS WHILE, ABEND WITH A 4095 ERROR CODE.        
      *      THE ERROR CODE TRIGGERS AN EMAIL TO THE LEDGER TEAM FOR            
      *      ERROR CORRECTION.                                                  
      *                                                                         
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAINLINE-DRIVER-SECTION.                                            
                                                                                
           PERFORM 1000-INITIALIZATION                                          
              THRU 1000-INITIALIZATION-EXIT.                                    
                                                                                
           PERFORM 2000-PROCESS-INPUT-FILE                                      
              THRU 2000-PROCESS-INPUT-FILE-EXIT                                 
                 UNTIL END-OF-FILE.                                             
                                                                                
           PERFORM 9000-EOJ-TASKS                                               
              THRU 9000-EOJ-TASKS-EXIT.                                         
                                                                                
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZATION.                                                    
      ******************************************************************        
       1000-INITIALIZATION.                                                     
      *** GET READY TO PROCESS INFORMATION.....                                 
                                                                                
           OPEN  INPUT INPUT-FILE                                               
                 INPUT INPUT2-FILE                                              
                OUTPUT OUTPUT-FILE                                              
                OUTPUT OUTPUT2-FILE                                             
                OUTPUT OUTPUT3-FILE.                                            
                                                                                
           INITIALIZE ML019-TRANSFER-RECORD.                                    
           INITIALIZE INPUT-CONTROL-RECORD.                                     
                                                                                
           MOVE ZEROS          TO WS-RECS-READ-ACCUM.                           
           MOVE ZEROS          TO WS-RECS-WRITE-MC-ACCUM.                       
           MOVE ZEROS          TO WS-RECS-WRITE-MT-ACCUM.                       
           MOVE ZEROS          TO WS-RECS-WRITE-DROP-ACCUM.                     
           MOVE ZEROS          TO WS-RECS-WRITE-ERROR-ACCUM.                    
           MOVE 'N'            TO END-OF-FILE-SW.                               
           MOVE 'N'            TO ERRORS-FOUND-SW.                      ETKUT004
                                                                        ETKUT004
      *** READ THE INBOUND CONTROL CARD TO DETERMINE WHAT STORE WE ARE          
      *** PROCESSING FOR.......                                                 
                                                                                
           PERFORM 8000-READ-INPUT-FILE                                         
              THRU 8000-READ-INPUT-FILE-EXIT.                                   
                                                                                
           MOVE IR-STORE-NUMBER          TO HOLD-PRICING-STORE-NUMBER.          
           MOVE IR-TIMESTAMP-MN-END-MM   TO PRICING-DTE-MM.                     
           MOVE IR-TIMESTAMP-MN-END-DD   TO PRICING-DTE-DD.                     
           MOVE IR-TIMESTAMP-MN-END-CCYY TO PRICING-DTE-CCYY.                   
           MOVE PRICING-DTE-IN           TO HOLD-PRICING-DATE.                  
           MOVE IR-TIMESTAMP-MN-END-MM   TO PV-PC-DTE-MM.                       
           MOVE IR-TIMESTAMP-MN-END-DD   TO PV-PC-DTE-DD.                       
                                                                                
       1000-INITIALIZATION-EXIT.                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 2000-PROCESS INPUT FILE.                                                
      ******************************************************************        
       2000-PROCESS-INPUT-FILE.                                                 
      *** READ THE TRANSFER FILE                                                
                                                                                
           READ INPUT-FILE INTO ML019-TRANSFER-RECORD                           
               AT END  MOVE 'Y'         TO END-OF-FILE-SW                       
                       GO TO 2000-PROCESS-INPUT-FILE-EXIT.                      
                                                                                
           ADD 1                        TO WS-RECS-READ-ACCUM.                  
                                                                                
           INITIALIZE IN400-PV-INPUT-AREA.                                      
                                                                                
           SET IN400-PV-SKU-LOOKUP TO TRUE.                                     
                                                                                
           IF HOLD-PRICING-STORE-NUMBER = SPACES                                
               MOVE ML019-TRANSFER-TO-STORE-NBR   TO IN400-PV-LOC-IN            
               MOVE ML019-TRANSFER-TO-STORE-9     TO IN400-CONVERT-LOC          
           ELSE                                                                 
               MOVE HOLD-PRICING-STORE-NUMBER     TO IN400-PV-LOC-IN            
               MOVE HOLD-PRICING-STORE-NUMBER-9   TO IN400-CONVERT-LOC          
           END-IF.                                                              
                                                                                
           MOVE HOLD-PRICING-DATE               TO IN400-PV-EFF-DTE-IN.         
                                                                                
           IF ML019-TRANSFER-SEQ-NO = 000                                       
               PERFORM 3000-WRITE-DRP19-REC                                     
                  THRU 3000-WRITE-DRP19-REC-EXIT                                
           ELSE                                                                 
               PERFORM 4000-GET-PRICING-INFO                                    
                  THRU 4000-GET-PRICING-INFO-EXIT                               
           END-IF.                                                              
                                                                                
       2000-PROCESS-INPUT-FILE-EXIT.                                            
           EXIT.                                                                
                                                                                
       3000-WRITE-DRP19-REC.                                                    
           MOVE ML019-TRANSFER-RECORD TO DRP19-TRANSFER-RECORD.                 
                                                                                
           WRITE OUTPUT-TRANSFER-RECORD                                         
               FROM DRP19-TRANSFER-RECORD.                                      
                                                                                
           ADD 1                      TO WS-RECS-WRITE-DROP-ACCUM.              
                                                                                
       3000-WRITE-DRP19-REC-EXIT.                                               
           EXIT.                                                                
                                                                                
       4000-GET-PRICING-INFO.                                                   
           MOVE ML019-SKU                   TO IN400-PV-SKU-IN.                 
           PERFORM IN400-LOOKUP-RETAIL.                                         
                                                                                
      *** A.  INVALID KEY CONDITION..                                           
           IF IN400-PV-INVALID-KEY-INPUT                                        
              DISPLAY 'INVALID - ', PV-EDIT-SKU-NBR,                            
                      ' PRC RTN - ', IN400-PV-RETURN-CDE,                       
                      ' ATT RTN - ', IN400-PV-ATT-RTNCDE,                       
                      ' PRC SQL - ', IN400-PV-PRC-SQLCDE,                       
                      ' ATT SQL - ', IN400-PV-ATT-SQLCDE,                       
                      '  ', IN400-PV-RETURN-RSN                                 
              DISPLAY IN400-PV-PARAGRAPH                                        
              DISPLAY ' JOB:     ' PC-JOB-NAME                                  
                      ' PROGRAM: ' PC-PROGRAM-NAME                              
              DISPLAY ' ERROR  = ' PE-MSG-01                                    
              PERFORM 7000-WRITE-ERR19-REC                                      
                 THRU 7000-WRITE-ERR19-REC-EXIT                                 
                                                                                
      *** C.  REGULAR SKU EDITS.                                                
           ELSE                                                                 
               IF IN400-PS-RTL-FOUND                                            
                   SET PS-PRICE-FOUND              TO TRUE                      
                   PERFORM 5000-DETERMINE-OUTPUT-REC                            
                      THRU 5000-DETERMINE-OUTPUT-REC-EXIT                       
               ELSE                                                             
                   DISPLAY ' JOB:     ' PC-JOB-NAME                             
                           ' PROGRAM: ' PC-PROGRAM-NAME                         
                   DISPLAY ' ERROR  = ' PE-MSG-01                               
                   PERFORM 7000-WRITE-ERR19-REC                                 
                      THRU 7000-WRITE-ERR19-REC-EXIT                            
               END-IF.                                                          
                                                                                
       4000-GET-PRICING-INFO-EXIT.                                              
           EXIT.                                                                
                                                                                
       5000-DETERMINE-OUTPUT-REC.                                               
           INITIALIZE ML017-PRICE-CHANGE-RECORD.                                
                                                                                
           IF IN400-PRC-UNIT-RTL-OUT =                                          
              ML019-TRANSFER-PER-UNT-RTL-AMT                                    
      *** WRITE TRANSFER RECORD TO THE DROPPED FILE                             
                  PERFORM 3000-WRITE-DRP19-REC                                  
                     THRU 3000-WRITE-DRP19-REC-EXIT                             
           ELSE                                                                 
               IF IN400-PRC-UNIT-RTL-OUT <                                      
                  ML019-TRANSFER-PER-UNT-RTL-AMT                                
      *** MARKDOWN RECORD TO BE GENERATED                                       
                  MOVE '1' TO ML017-PC-TYP-CDE                                  
                  ADD 1 TO WS-RECS-WRITE-MT-ACCUM                               
                  PERFORM 6000-WRITE-PRICE-CHG-REC                              
                     THRU 6000-WRITE-PRICE-CHG-REC-EXIT                         
               ELSE                                                             
               IF IN400-PRC-UNIT-RTL-OUT >                                      
                  ML019-TRANSFER-PER-UNT-RTL-AMT                                
      *** MARKDOWN CANCEL RECORD TO BE GENERATED                                
                  MOVE '2' TO ML017-PC-TYP-CDE                                  
                  ADD 1 TO WS-RECS-WRITE-MC-ACCUM                               
                  PERFORM 6000-WRITE-PRICE-CHG-REC                              
                     THRU 6000-WRITE-PRICE-CHG-REC-EXIT                         
               ELSE                                                             
                  DISPLAY ' JOB:     ' PC-JOB-NAME                              
                          ' PROGRAM: ' PC-PROGRAM-NAME                          
                  DISPLAY ' ERROR = ' PE-MSG-02                                 
                  PERFORM 3000-WRITE-DRP19-REC                                  
                     THRU 3000-WRITE-DRP19-REC-EXIT                             
               END-IF.                                                          
                                                                                
       5000-DETERMINE-OUTPUT-REC-EXIT.                                          
           EXIT.                                                                
                                                                                
       6000-WRITE-PRICE-CHG-REC.                                                
           MOVE '4'                            TO ML017-PC-CDE.                 
           MOVE SPACE                          TO ML017-SLS-CORR-CDE.           
           MOVE ML019-TRANSFER-NBR             TO ML017-PC-DOC-5-NBR.           
                                                                                
           MOVE PV-PC-DTE-MMDD                 TO ML017-PC-DTE.                 
           MOVE IR-PC-KOHLS-MM-FSCL            TO ML017-PC-KOHLS-MM.            
                                                                                
           MOVE ML019-SKU                      TO ML017-PC-SKU.                 
           MOVE ML019-TRANSFER-DEPT-NBR        TO ML017-PC-DEPT-NBR.            
           MOVE ML019-TRANSFER-CLASS-NBR       TO ML017-PC-CLASS-NBR.           
           MOVE ML019-TRANSFER-CLASS-NBR(1:1)  TO PV-MAJOR-CLASS-1.             
           MOVE PV-MAJOR-CLASS                 TO ML017-PC-MAJ-CLASS.           
           MOVE '5'                            TO ML017-PC-RSN-CDE.             
           MOVE ML019-TRANSFER-PER-UNT-RTL-AMT TO ML017-PC-OLD-PR-AMT.          
           MOVE IN400-PRC-UNIT-RTL-OUT         TO ML017-PC-NEW-PR-AMT.          
           MOVE ML019-TRANSFER-QTY             TO ML017-PC-QTY.                 
                                                                                
           IF IR-BATCH-NUMBER IS GREATER THAN SPACES                            
               MOVE IR-BATCH-NUMBER            TO ML017-PC-BATCH-NBR            
           ELSE                                                                 
               MOVE ML019-TRANSFER-BATCH-NBR   TO ML017-PC-BATCH-NBR            
           END-IF.                                                              
                                                                                
           MOVE '502'                   TO ML017-PC-MKDN-MKUP-RSN-CDE.          
           SUBTRACT ML017-PC-NEW-PR-AMT FROM ML017-PC-OLD-PR-AMT                
               GIVING ML017-PC-DIFFER-AMT.                                      
           MOVE ML019-TRANSFER-QTY             TO ML017-PC-TOT-DOC-QTY.         
                                                                                
           IF IR-STORE-NUMBER > SPACES                                          
               MOVE IR-STORE-NUMBER            TO ML017-PC-STORE-NBR            
           ELSE                                                                 
               MOVE ML019-TRANSFER-TO-STORE-NBR TO ML017-PC-STORE-NBR           
           END-IF.                                                              
                                                                                
           MOVE '5'                            TO ML017-PC-CATEG-CDE.           
                                                                                
           STRING '20' DELIMITED BY SIZE                                        
                  ML019-TRANSFER-NBR DELIMITED BY SIZE                          
                                        INTO ML017-PC-DOC-7-NBR.                
                                                                                
           MOVE SPACES                  TO ML017-PC-VENDOR-INHSE-NMBR.          
           MOVE ZEROS                   TO ML017-PC-LINE-NBR.                   
                                                                                
           MOVE ZEROS                   TO ML017-RGST-ID.                       
           MOVE ZEROS                   TO ML017-TRN-NBR.                       
           MOVE ZEROS                   TO ML017-LNE-ITM-SEQ-NBR.               
           MOVE SPACES                  TO ML017-GMM.                           
           MOVE ZEROS                   TO ML017-DIVISION-NUMBER.               
           MOVE SPACES                  TO ML017-DMM.                           
           MOVE SPACES                  TO ML017-BUYER.                         
           MOVE ML019-TRANSFER-ENT-ID   TO ML017-PC-ENT-ID.                     
                                                                                
           WRITE OUTPUT-PRICE-CHANGE-RECORD                                     
               FROM ML017-PRICE-CHANGE-RECORD.                                  
                                                                                
       6000-WRITE-PRICE-CHG-REC-EXIT.                                           
           EXIT.                                                                
                                                                                
       7000-WRITE-ERR19-REC.                                                    
           MOVE 'Y'                      TO ERRORS-FOUND-SW.            ETKUT004
           MOVE '7000-WRITE-ERR19-REC  ' TO PV-PARAGRAPH-NAME.                  
           MOVE ML019-TRANSFER-RECORD    TO ERR19-TRANSFER-RECORD.              
                                                                                
           WRITE OUTPUT-ERROR-RECORD                                            
               FROM ERR19-TRANSFER-RECORD.                                      
                                                                                
           ADD 1                      TO WS-RECS-WRITE-ERROR-ACCUM.             
           SET ABEND-4095             TO TRUE.                                  
                                                                                
       7000-WRITE-ERR19-REC-EXIT.                                               
           EXIT.                                                                
                                                                                
      /*****************************************************************        
      * 8000-READ-INPUT-FILE.                                                   
      ******************************************************************        
       8000-READ-INPUT-FILE.                                                    
      *** READ IN THE CONTROL CARD WITH THE PASSED STORE NUMBER. IF WE          
      *** HAVE A PROBLEM OR HIT END OF FILE, MOVE "***" TO THE STORE            
      *** NUMBER TO IDENTIFY A PROBLEM. FLAG THE ABEND SWITCH TO SEND           
      *** AN EMAIL TO THE LEDGER TEAM OF A PROBLEM.....                         
                                                                        ETKUT004
           READ INPUT2-FILE NEXT                                        ETKUT004
             INTO IR-INPUT-RECORD-CC                                            
               AT END  MOVE '***' TO IR-STORE-NUMBER                            
                       MOVE 'Y'   TO ERRORS-FOUND-SW                    ETKUT004
                       CLOSE INPUT2-FILE                                ETKUT004
                       OPEN INPUT INPUT2-FILE.                          ETKUT004
                                                                                
       8000-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
                                                                                
      /*****************************************************************        
      * 9000-END OF JOB TASKS.                                                  
      ******************************************************************        
       9000-EOJ-TASKS.                                                          
      *** CLOSE THE FILES. SET THE RETURN CODE IF WE HAVE A PROBLEM...          
                                                                                
           DISPLAY '*** PROGRAM *** = ' PC-PROGRAM-NAME.                        
           DISPLAY '*** RECORD TOTALS ***'.                                     
           DISPLAY '*** NUMBER OF INPUT RECORDS READ              = '           
                WS-RECS-READ-ACCUM.                                             
           DISPLAY '*** NUMBER OF MARKDOWN CANCEL RECORDS WRITTEN = '           
                WS-RECS-WRITE-MC-ACCUM.                                         
           DISPLAY '*** NUMBER OF MARKDOWN TRANS RECORDS WRITTEN  = '           
                WS-RECS-WRITE-MT-ACCUM.                                         
           DISPLAY '*** NUMBER OF DROPPED RECORDS WRITTEN         = '           
                WS-RECS-WRITE-DROP-ACCUM.                                       
           DISPLAY '*** NUMBER OF ERROR RECORDS WRITTEN           = '           
                WS-RECS-WRITE-ERROR-ACCUM.                                      
                                                                                
           CLOSE INPUT-FILE                                                     
                 INPUT2-FILE                                                    
                 OUTPUT-FILE                                                    
                 OUTPUT2-FILE                                                   
                 OUTPUT3-FILE.                                                  
                                                                                
           IF ERRORS-FOUND                                                      
               MOVE 4095 TO RETURN-CODE                                         
           ELSE                                                                 
               MOVE 0    TO RETURN-CODE                                         
           END-IF.                                                              
                                                                                
       9000-EOJ-TASKS-EXIT.                                                     
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * PRICING MODULE.                                                         
      ******************************************************************        
           COPY INPD400.                                                        
                                                                                
