       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    MLKRP014.                                                 
       AUTHOR.        TESS BESTE.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  01-12-94.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *           MLKRP014 - ACCUM SALES                                        
      *                                                                         
      * THIS PROGRAM ACCUMULATES THE AMOUNTS FROM A FILE OF SALES               
      * TRANSACTIONS IN THE PSWS010 FORMAT, WHICH IS INPUT TO MLKUP020          
      * IN JOB MLK140, ON DD INP05. THE TRANSACTION COUNT AND TOTAL             
      * RETAIL ARE DISPLAYED.                                                   
      *                                                                         
      * THE INPUT FILE WILL NEED TO BE SORTED BY WHICH EVER BREAK               
      * CRITERIA THAT IS BEING REQUESTED IN THE OPTIONAL PARAMETER.             
      * BY DEFAULT, IT WILL BREAK BY DEPT/SUB-CLASS.                            
      *                                                                         
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      * LY0405   2004-05-06  CHANGE REPORT TO SHOW LEADING NEG SIGN    *        
      *                      INSTEAD OF TRAILING.                      *        
      ******************************************************************        
                                                                       *        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING  MODE IS F.                                                
       01  INPUT-RECORD                    PIC X(65).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      * INPUT RECORD LAYOUT                                                     
      ******************************************************************        
                                                                                
      *01  D040-RECORD.                                                         
           COPY PSWS010.                                                        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-INPUT-FILE-SW     PIC X           VALUE 'N'.           
               88  PS-END-OF-INPUT-FILE                    VALUE 'Y'.           
               88  PS-NOT-END-OF-INPUT-FILE                VALUE 'N'.           
           05  PS-PRODUCE-TOTAL-SW         PIC X           VALUE 'N'.           
               88  PS-PRODUCE-TOTAL                        VALUE 'Y'.           
               88  PS-NOT-PRODUCE-TOTAL                    VALUE 'N'.           
           05  PS-FIRST-TIME-THROUGH-SW    PIC X           VALUE 'Y'.           
               88  PS-FIRST-TIME-THROUGH                   VALUE 'Y'.           
               88  PS-NOT-FIRST-TIME-THROUGH               VALUE 'N'.           
                                                                                
       01  RP-REPORT-HEADER-1.                                                  
           05  FILLER                      PIC X(20)       VALUE SPACES.        
               88  RP-REPORT-BY-STORE                      VALUE                
                   ' STORE:             '.                                      
               88  RP-REPORT-BY-DEPT                       VALUE                
                   ' DEPT:              '.                                      
               88  RP-REPORT-BY-DEPT-CLASS                 VALUE                
                   ' DEPT/CLASS:        '.                                      
               88  RP-REPORT-BY-STORE-DEPT                 VALUE                
                   ' STORE/DEPT:        '.                                      
               88  RP-REPORT-BY-STORE-DEPT-CLASS           VALUE                
                   ' STORE/DEPT/CLASS:  '.                                      
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(19)       VALUE                
               '       SALES TOTAL '.                                           
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(17)       VALUE                
               '    NORMAL SALES '.                                             
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(17)       VALUE                
               '     AUDIT SALES '.                                             
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(17)       VALUE                
               ' LATE CORRECTION '.                                             
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(17)       VALUE                
               '           OTHER '.                                             
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(17)       VALUE SPACES.        
               88  RP-REPORT-NEG-SALES                     VALUE                
               '  NEGATIVE SALES '.                                             
           05  FILLER                      PIC X(02)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-HEADER-2.                                                  
           05  FILLER                      PIC X(20)      VALUE ALL '-'.        
           05  FILLER                      PIC X(01)      VALUE SPACES.         
           05  FILLER                      PIC X(19)      VALUE ALL '-'.        
           05  FILLER                      PIC X(01)      VALUE SPACES.         
           05  FILLER                      PIC X(17)      VALUE ALL '-'.        
           05  FILLER                      PIC X(01)      VALUE SPACES.         
           05  FILLER                      PIC X(17)      VALUE ALL '-'.        
           05  FILLER                      PIC X(01)      VALUE SPACES.         
           05  FILLER                      PIC X(17)      VALUE ALL '-'.        
           05  FILLER                      PIC X(01)      VALUE SPACES.         
           05  FILLER                      PIC X(17)      VALUE ALL '-'.        
           05  FILLER                      PIC X(01)      VALUE SPACES.         
           05  FILLER                      PIC X(17)      VALUE SPACES.         
               88  RP-REPORT-NEG-SALES                    VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
                                                                                
       01  RP-REPORT-DETAIL-1.                                                  
           05  RP-REPT-CRITERIA            PIC X(20)       VALUE SPACES.        
               88  RP-GRAND-RETAIL-TOTAL                   VALUE                
                   ' GRAND TOTAL:       '.                                      
           05  FILLER                      PIC X(01)       VALUE SPACES.        
LY0405*    05  RP-REPT-TOTAL               PIC ZZZ,ZZZ,ZZZ,ZZZ.99-.             
LY0405     05  RP-REPT-TOTAL               PIC -ZZZ,ZZZ,ZZZ,ZZZ.99.             
           05  FILLER                      PIC X(01)       VALUE SPACES.        
LY0405*    05  RP-NORMAL-REPT-TOTAL        PIC Z,ZZZ,ZZZ,ZZZ.99-.               
LY0405     05  RP-NORMAL-REPT-TOTAL        PIC -Z,ZZZ,ZZZ,ZZZ.99.               
           05  FILLER                      PIC X(01)       VALUE SPACES.        
LY0405*    05  RP-SALES-AUDIT-REPT-TOTAL   PIC Z,ZZZ,ZZZ,ZZZ.99-.               
LY0405     05  RP-SALES-AUDIT-REPT-TOTAL   PIC -Z,ZZZ,ZZZ,ZZZ.99.               
           05  FILLER                      PIC X(01)       VALUE SPACES.        
LY0405*    05  RP-LATE-CORR-REPT-TOTAL     PIC Z,ZZZ,ZZZ,ZZZ.99-.               
LY0405     05  RP-LATE-CORR-REPT-TOTAL     PIC -Z,ZZZ,ZZZ,ZZZ.99.               
           05  FILLER                      PIC X(01)       VALUE SPACES.        
LY0405*    05  RP-OTHER-REPT-TOTAL         PIC Z,ZZZ,ZZZ,ZZZ.99-.               
LY0405     05  RP-OTHER-REPT-TOTAL         PIC -Z,ZZZ,ZZZ,ZZZ.99.               
           05  FILLER                      PIC X(01)       VALUE SPACES.        
LY0405*    05  RP-NEG-REPT-TOTAL           PIC Z,ZZZ,ZZZ,ZZZ.99-.               
LY0405     05  RP-NEG-REPT-TOTAL           PIC -Z,ZZZ,ZZZ,ZZZ.99.               
           05  RP-NEG-REPT-TOTAL-X REDEFINES RP-NEG-REPT-TOTAL                  
                                           PIC X(17).                           
           05  FILLER                      PIC X(02)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-DETAIL-2.                                                  
           05  RP-COUNT-CRITERIA           PIC X(20)       VALUE SPACES.        
               88  RP-TRANSACTION-COUNT                    VALUE                
                   ' TRANSACTION COUNT: '.                                      
           05  FILLER                      PIC X(01)       VALUE SPACES.        
LY0405*    05  RP-COUNT-TOTAL              PIC ZZZ,ZZZ,ZZZ,ZZZ-.                
LY0405     05  RP-COUNT-TOTAL              PIC -ZZZ,ZZZ,ZZZ,ZZZ.                
           05  FILLER                      PIC X(04)       VALUE SPACES.        
LY0405*    05  RP-NORMAL-COUNT-TOTAL       PIC Z,ZZZ,ZZZ,ZZZ-.                  
LY0405     05  RP-NORMAL-COUNT-TOTAL       PIC -Z,ZZZ,ZZZ,ZZZ.                  
           05  FILLER                      PIC X(04)       VALUE SPACES.        
LY0405*    05  RP-SALES-AUDIT-COUNT-TOTAL  PIC Z,ZZZ,ZZZ,ZZZ-.                  
LY0405     05  RP-SALES-AUDIT-COUNT-TOTAL  PIC -Z,ZZZ,ZZZ,ZZZ.                  
           05  FILLER                      PIC X(04)       VALUE SPACES.        
LY0405*    05  RP-LATE-CORR-COUNT-TOTAL    PIC Z,ZZZ,ZZZ,ZZZ-.                  
LY0405     05  RP-LATE-CORR-COUNT-TOTAL    PIC -Z,ZZZ,ZZZ,ZZZ.                  
           05  FILLER                      PIC X(04)       VALUE SPACES.        
LY0405*    05  RP-OTHER-COUNT-TOTAL        PIC Z,ZZZ,ZZZ,ZZZ-.                  
LY0405     05  RP-OTHER-COUNT-TOTAL        PIC -Z,ZZZ,ZZZ,ZZZ.                  
           05  FILLER                      PIC X(04)       VALUE SPACES.        
LY0405*    05  RP-NEG-COUNT-TOTAL          PIC Z,ZZZ,ZZZ,ZZZ-.                  
LY0405     05  RP-NEG-COUNT-TOTAL          PIC -Z,ZZZ,ZZZ,ZZZ.                  
           05  RP-NEG-COUNT-TOTAL-X REDEFINES RP-NEG-COUNT-TOTAL                
                                           PIC X(14).                           
           05  FILLER                      PIC X(05)       VALUE SPACES.        
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-RECORD-COUNT             PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-NORMAL-REC-COUNT         PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-SALES-AUDIT-REC-COUNT    PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-LATE-CORR-REC-COUNT      PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-OTHER-REC-COUNT          PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-NEG-REC-COUNT            PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ACCUM-AMT                PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-NORMAL-ACCUM-AMT         PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-SALES-AUDIT-ACCUM-AMT    PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-LATE-CORR-ACCUM-AMT      PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-OTHER-ACCUM-AMT          PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-NEG-ACCUM-AMT            PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOTAL                    PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-NORMAL-TOTAL             PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-SALES-AUDIT-TOTAL        PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-LATE-CORR-TOTAL          PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-OTHER-TOTAL              PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-NEG-TOTAL                PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-PREV-STORE               PIC X(4)        VALUE SPACES.        
           05  PV-PREV-DEPT-CLASS.                                              
               10  PV-PREV-DEPT            PIC X(3)        VALUE SPACES.        
               10  PV-PREV-CLASS           PIC X(2)        VALUE SPACES.        
                                                                                
      ******************************************************************        
      * PROGRAM LINKAGE SECTION                                                 
      ******************************************************************        
       LINKAGE SECTION.                                                         
                                                                                
       01  PARM-DATA-AREA.                                                      
           05  PARM-LENGTH                  PIC S9(4)                           
                               COMP.                                            
           05  PARM-DATA.                                                       
               10  PRM-REPORT-BREAK-TYPE       PIC X(10)   VALUE SPACES.        
                   88  PRM-REPORT-BY-STORE                 VALUE                
                       'STORE     '.                                            
                   88  PRM-REPORT-BY-DEPT                  VALUE                
                       'DEPT      '.                                            
                   88  PRM-REPORT-BY-DEPT-CLASS            VALUE                
                       'DEPT/CLASS'.                                            
                   88  PRM-REPORT-BY-STORE-DEPT            VALUE                
                       'STORE/DEPT'.                                            
                   88  PRM-REPORT-BY-STORE-DEPT-CLASS      VALUE                
                       'STR/DPT/CL'.                                            
                   88  PRM-VALID-REPORT-BREAK-TYPE         VALUES ARE           
                       'STORE     '                                             
                       'DEPT      '                                             
                       'DEPT/CLASS'                                             
                       'STORE/DEPT'                                             
                       'STR/DPT/CL'.                                            
               10  FILLER                      PIC X(01)   VALUE SPACES.        
               10  PRM-REPORT-NEG-OPTION-IND   PIC X(01)   VALUE SPACES.        
                   88  PRM-REPORT-NEG-OPT                  VALUE 'Y'.           
               10  FILLER                      PIC X(01)   VALUE SPACES.        
               10  PRM-DISPLAY-DATA            PIC X(25)   VALUE SPACES.        
               10  FILLER                      PIC X(42)   VALUE SPACES.        
                                                                                
LY0405 PROCEDURE DIVISION USING PARM-DATA-AREA.                                 
                                                                                
                                                                                
       A100-PROGRAM-MAINLINE.                                                   
                                                                                
           OPEN INPUT INPUT-FILE.                                               
                                                                                
           INSPECT PARM-DATA                                                    
               REPLACING ALL LOW-VALUES  BY ' '                                 
                         ALL HIGH-VALUES BY ' '.                                
           IF PRM-VALID-REPORT-BREAK-TYPE                                       
               NEXT SENTENCE                                                    
           ELSE                                                                 
               SET PRM-REPORT-BY-DEPT-CLASS    TO TRUE                          
           END-IF.                                                              
                                                                                
LY0405     DISPLAY PARM-DATA.                                                   
                                                                                
           READ INPUT-FILE INTO D040-RECORD                                     
               AT END SET PS-END-OF-INPUT-FILE TO TRUE.                         
                                                                                
           MOVE D040-STORENO    TO PV-PREV-STORE.                               
           MOVE D040-DEPT-CLASS TO PV-PREV-DEPT-CLASS.                          
                                                                                
           SET PS-NOT-PRODUCE-TOTAL  TO TRUE.                                   
           SET PS-FIRST-TIME-THROUGH TO TRUE.                                   
                                                                                
           IF PRM-REPORT-NEG-OPT                                                
               SET RP-REPORT-NEG-SALES OF RP-REPORT-HEADER-1 TO TRUE            
               SET RP-REPORT-NEG-SALES OF RP-REPORT-HEADER-2 TO TRUE            
           END-IF.                                                              
                                                                                
           PERFORM UNTIL PS-END-OF-INPUT-FILE                                   
               PERFORM B100-DISPLAY-DETAIL                                      
                                                                                
               PERFORM B200-ACCUM-DETAIL                                        
                                                                                
               READ INPUT-FILE INTO D040-RECORD                                 
                   AT END SET PS-END-OF-INPUT-FILE TO TRUE                      
               END-READ                                                         
           END-PERFORM.                                                         
                                                                                
           PERFORM B100-DISPLAY-DETAIL.                                         
                                                                                
           COMPUTE PV-RECORD-COUNT                                              
                 = PV-NORMAL-REC-COUNT                                          
                 + PV-SALES-AUDIT-REC-COUNT                                     
                 + PV-LATE-CORR-REC-COUNT                                       
                 + PV-OTHER-REC-COUNT                                           
           MOVE PV-RECORD-COUNT TO RP-COUNT-TOTAL.                              
           MOVE PV-NORMAL-REC-COUNT TO RP-NORMAL-COUNT-TOTAL.                   
           MOVE PV-SALES-AUDIT-REC-COUNT TO RP-SALES-AUDIT-COUNT-TOTAL.         
           MOVE PV-LATE-CORR-REC-COUNT TO RP-LATE-CORR-COUNT-TOTAL.             
           MOVE PV-OTHER-REC-COUNT TO RP-OTHER-COUNT-TOTAL.                     
           IF PRM-REPORT-NEG-OPT                                                
               MOVE PV-NEG-REC-COUNT TO RP-NEG-COUNT-TOTAL                      
           ELSE                                                                 
               MOVE SPACES           TO RP-NEG-COUNT-TOTAL-X                    
           END-IF.                                                              
           SET RP-TRANSACTION-COUNT TO TRUE.                                    
                                                                                
           DISPLAY RP-REPORT-DETAIL-2.                                          
                                                                                
           COMPUTE PV-TOTAL                                                     
                 = PV-NORMAL-TOTAL                                              
                 + PV-SALES-AUDIT-TOTAL                                         
                 + PV-LATE-CORR-TOTAL                                           
                 + PV-OTHER-TOTAL.                                              
           MOVE PV-TOTAL       TO RP-REPT-TOTAL.                                
           MOVE PV-NORMAL-TOTAL TO RP-NORMAL-REPT-TOTAL.                        
           MOVE PV-SALES-AUDIT-TOTAL TO RP-SALES-AUDIT-REPT-TOTAL.              
           MOVE PV-LATE-CORR-TOTAL TO RP-LATE-CORR-REPT-TOTAL.                  
           MOVE PV-OTHER-TOTAL TO RP-OTHER-REPT-TOTAL.                          
           IF PRM-REPORT-NEG-OPT                                                
               MOVE PV-NEG-TOTAL TO RP-NEG-REPT-TOTAL                           
           ELSE                                                                 
               MOVE SPACES       TO RP-NEG-REPT-TOTAL-X                         
           END-IF.                                                              
           SET RP-GRAND-RETAIL-TOTAL TO TRUE.                                   
                                                                                
           DISPLAY RP-REPORT-DETAIL-1.                                          
                                                                                
           CLOSE INPUT-FILE.                                                    
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       B100-DISPLAY-DETAIL.                                                     
                                                                                
           EVALUATE TRUE                                                        
               WHEN PRM-REPORT-BY-STORE                                         
                   IF D040-STORENO NOT = PV-PREV-STORE                          
                       OR PS-END-OF-INPUT-FILE                                  
                       SET PS-PRODUCE-TOTAL   TO TRUE                           
                       SET RP-REPORT-BY-STORE TO TRUE                           
                       STRING PV-PREV-STORE                                     
                              ': '                                              
                           DELIMITED BY SIZE                                    
                           INTO RP-REPT-CRITERIA                                
                   END-IF                                                       
               WHEN PRM-REPORT-BY-DEPT                                          
                   IF D040-DEPT NOT = PV-PREV-DEPT                              
                       OR PS-END-OF-INPUT-FILE                                  
                       SET PS-PRODUCE-TOTAL   TO TRUE                           
                       SET RP-REPORT-BY-DEPT  TO TRUE                           
                       STRING PV-PREV-DEPT                                      
                              ': '                                              
                           DELIMITED BY SIZE                                    
                           INTO RP-REPT-CRITERIA                                
                   END-IF                                                       
               WHEN PRM-REPORT-BY-STORE-DEPT-CLASS                              
                   IF D040-STORENO NOT = PV-PREV-STORE                          
                       OR D040-DEPT-CLASS NOT = PV-PREV-DEPT-CLASS              
                       OR PS-END-OF-INPUT-FILE                                  
                       SET PS-PRODUCE-TOTAL   TO TRUE                           
                       SET RP-REPORT-BY-STORE-DEPT-CLASS TO TRUE                
                       STRING PV-PREV-STORE '/'                                 
                              PV-PREV-DEPT '/'                                  
                              PV-PREV-CLASS                                     
                              ': '                                              
                           DELIMITED BY SIZE                                    
                           INTO RP-REPT-CRITERIA                                
                   END-IF                                                       
               WHEN PRM-REPORT-BY-STORE-DEPT                                    
                   IF D040-STORENO NOT = PV-PREV-STORE                          
                       OR D040-DEPT NOT = PV-PREV-DEPT                          
                       OR PS-END-OF-INPUT-FILE                                  
                       SET PS-PRODUCE-TOTAL   TO TRUE                           
                       SET RP-REPORT-BY-STORE-DEPT TO TRUE                      
                       STRING PV-PREV-STORE '/'                                 
                              PV-PREV-DEPT                                      
                              ': '                                              
                           DELIMITED BY SIZE                                    
                           INTO RP-REPT-CRITERIA                                
                   END-IF                                                       
               WHEN OTHER                                                       
                   IF D040-DEPT-CLASS NOT = PV-PREV-DEPT-CLASS                  
                       OR PS-END-OF-INPUT-FILE                                  
                       SET PS-PRODUCE-TOTAL   TO TRUE                           
                       SET RP-REPORT-BY-DEPT-CLASS TO TRUE                      
                       STRING PV-PREV-DEPT-CLASS                                
                              ': '                                              
                           DELIMITED BY SIZE                                    
                           INTO RP-REPT-CRITERIA                                
                   END-IF                                                       
           END-EVALUATE.                                                        
                                                                                
           IF PS-PRODUCE-TOTAL                                                  
               IF PS-FIRST-TIME-THROUGH                                         
                   DISPLAY RP-REPORT-HEADER-1                                   
                   DISPLAY RP-REPORT-HEADER-2                                   
                   SET PS-NOT-FIRST-TIME-THROUGH TO TRUE                        
               END-IF                                                           
                                                                                
               COMPUTE PV-ACCUM-AMT                                             
                     = PV-NORMAL-ACCUM-AMT                                      
                     + PV-SALES-AUDIT-ACCUM-AMT                                 
                     + PV-LATE-CORR-ACCUM-AMT                                   
                     + PV-OTHER-ACCUM-AMT                                       
               MOVE PV-ACCUM-AMT TO RP-REPT-TOTAL                               
               MOVE PV-NORMAL-ACCUM-AMT TO RP-NORMAL-REPT-TOTAL                 
               MOVE PV-SALES-AUDIT-ACCUM-AMT                                    
                   TO RP-SALES-AUDIT-REPT-TOTAL                                 
               MOVE PV-LATE-CORR-ACCUM-AMT TO RP-LATE-CORR-REPT-TOTAL           
               MOVE PV-OTHER-ACCUM-AMT TO RP-OTHER-REPT-TOTAL                   
                                                                                
               IF PRM-REPORT-NEG-OPT                                            
                   MOVE PV-NEG-ACCUM-AMT TO RP-NEG-REPT-TOTAL                   
               ELSE                                                             
                   MOVE SPACES           TO RP-NEG-REPT-TOTAL-X                 
               END-IF                                                           
                                                                                
               DISPLAY RP-REPORT-DETAIL-1                                       
                                                                                
               ADD PV-NORMAL-ACCUM-AMT TO PV-NORMAL-TOTAL                       
               ADD PV-SALES-AUDIT-ACCUM-AMT TO PV-SALES-AUDIT-TOTAL             
               ADD PV-LATE-CORR-ACCUM-AMT TO PV-LATE-CORR-TOTAL                 
               ADD PV-OTHER-ACCUM-AMT TO PV-OTHER-TOTAL                         
               ADD PV-NEG-ACCUM-AMT TO PV-NEG-TOTAL                             
               MOVE ZERO TO PV-NORMAL-ACCUM-AMT                                 
                            PV-SALES-AUDIT-ACCUM-AMT                            
                            PV-LATE-CORR-ACCUM-AMT                              
                            PV-OTHER-ACCUM-AMT                                  
                            PV-NEG-ACCUM-AMT                                    
               MOVE D040-STORENO      TO PV-PREV-STORE                          
               MOVE D040-DEPT-CLASS   TO PV-PREV-DEPT-CLASS                     
               SET PS-NOT-PRODUCE-TOTAL TO TRUE                                 
           END-IF.                                                              
                                                                                
                                                                                
       B200-ACCUM-DETAIL.                                                       
                                                                                
           EVALUATE TRUE                                                        
               WHEN D040-NORMAL-TRANS                                           
                   ADD +1 TO PV-NORMAL-REC-COUNT                                
                   COMPUTE PV-NORMAL-ACCUM-AMT                                  
                         = PV-NORMAL-ACCUM-AMT                                  
                         + D040-SALE-AMT                                        
               WHEN D040-SALES-AUDIT-CORR                                       
                   ADD +1 TO PV-SALES-AUDIT-REC-COUNT                           
                   COMPUTE PV-SALES-AUDIT-ACCUM-AMT                             
                         = PV-SALES-AUDIT-ACCUM-AMT                             
                         + D040-SALE-AMT                                        
               WHEN D040-SYSTEM-LATE-CORR                                       
                   ADD +1 TO PV-LATE-CORR-REC-COUNT                             
                   COMPUTE PV-LATE-CORR-ACCUM-AMT                               
                         = PV-LATE-CORR-ACCUM-AMT                               
                         + D040-SALE-AMT                                        
               WHEN OTHER                                                       
                   ADD +1 TO PV-OTHER-REC-COUNT                                 
                   COMPUTE PV-OTHER-ACCUM-AMT                                   
                         = PV-OTHER-ACCUM-AMT                                   
                         + D040-SALE-AMT                                        
           END-EVALUATE.                                                        
                                                                                
           IF D040-SALE-AMT < 0                                                 
               ADD +1 TO PV-NEG-REC-COUNT                                       
               COMPUTE PV-NEG-ACCUM-AMT                                         
                     = PV-NEG-ACCUM-AMT                                         
                     + D040-SALE-AMT                                            
           END-IF.                                                              
                                                                                
