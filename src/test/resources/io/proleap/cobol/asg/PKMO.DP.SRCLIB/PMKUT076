      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
       PROGRAM-ID.             PMKUT076.                                        
       AUTHOR.                 AMANDA NEWHART                                   
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           FEBRUARY 2003.                                   
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *    PMKUT076 - PRICING BUSINESS EVENT INTEGRITY CHECK.          *        
      *                                                                *        
      *    -CURRENTLY CHECK SIX DIFFERENT AREAS FOR PRICING            *        
      *     (CLRNC, CMPTV, GRP, SIMPLE, PROMOITEMTIERED, ADMINTIERED)  *        
      *     A FUTURE CHANGE THOUGH WOULD BE TO INCLUDE TWO MORE        *        
      *     (ADDLAMTOFF AND BWPBUY).                                   *        
      *                                                                *        
      *     THIS PROGRAM CREATES THE PRICING BUSINESS FILE USED IN THE *        
      *     DATA INTEGRITY PROCESS.  FIRST OPEN TWO INPUT FILES AND    *        
      *     ONE OUTPUT FILE.  IF WHEN THE TWO INPUT FILES ARE READ     *        
      *     AND NOTHING IS IN EITHER OF THESE FILES ABEND THE PROGRAM  *        
      *     OTHERWISE, LOG ONTO ORACLE.  IF SUCCESSFUL THEN FIND THE   *        
      *     CURRENT SYSDATE USING A SELECT STATEMENT.  NEXT SELECT THE *        
      *     CLEARANCE RECORDS FROM THE PMV_STR_GP_CLRNC TABLE THAT     *        
      *     FALL WITHIN THE DATE RANGE FROM THE DATE RANGE FILE.  IF   *        
      *     THE LAST UPDATED TIMESTAMP IS LESS THEN THE TIMESTAMP FROM *        
      *     THE TIMESTAMP FILE THEN MOVE THE SELECTED RECORD TO THE    *        
      *     COPYBOOK.  MOVE THE COPYBOOK TO THE OUTPUT FILE.  WHEN     *        
      *     THE LAST CLEARANCE RECORD IS FOUND RUN THE AD EVENT CSR.   *        
      *     THIS WILL FIND ALL THE AD EVENT DTES THAT ARE WITHIN THE   *        
      *     DTE RANGE FROM THE DTE RANGE FILE.  AS LONG AS THE AD      *        
      *     EVENT DATE RANGE IS WITHIN THE FILE DATE RANGE USE THESE   *        
      *     NEW DATES AS THE DATE RANGE.  ALTHOUGH WHICHEVER DATES ARE *        
      *     USED CONCATINATE THE A TIMESTAMP TO THE END.  THEN SELECT  *        
      *     THE RECORDS FROM THE PMV_ADM_TIER_PRICG CURSOR THAT FALLS  *        
      *     WITHIN THE NEW DATE RANGE.  IF THE LAST UPDATE TIMESTAMP   *        
      *     IS LESS THEN THE TIMESTAMP FILE THEN MOVE THE SELECTED     *        
      *     VARIABLE TO THE COPYBOOK.  MOVE THE COPYBOOK TO THE OUTPUT *        
      *     FILE.  DO THE SAME THING WITH PMV_PROMO_ITM_TIER_PRICG AND *        
      *     PMV_CMPTV_PRICG.  WHEN THE LAST SELECT STATEMENT HAS BEEN  *        
      *     PROCESSED FETCH THE NEXT DATE RANGE FROM THE AD EVENT CSR  *        
      *     AND REPEAT THE PROCESS UNTIL THE LAST DATE HAS BEEN RUN.   *        
      *     AFTER THE LAST RECORD HAS BEEN WRITTEN TO THE OUTPUT FILE  *        
      *     WRITE A TOTAL LINE TO THE FILE.  FIANLLY CLOSE THE TWO     *        
      *     INPUT FILES AND THE ONE OUTPUT FILE.  AFTER THE LAST RECORD*        
      *     WAS WRITTEN TO THE FILE WRITE A TOTAL LINE TO THE FILE.    *        
      *     FINALLY CLOSE THE TWO INPUT FILES AND THE ONE OUTPUT FILE. *        
      *                                                                *        
      ******************************************************************        
      **   CHANGE LOG:                                                 *        
      ******************************************************************        
      *    MM/DD/YYYY - NAME HERE - WR NO. HERE                        *        
      *                                                                *        
      *    12/08/2003 - COLLEEN CUMMINGS - P000615021                  *        
      *    CHANGED 'E100-GET-STYLE-ID' PARAGRAPH TO NO LONGER ABEND    *        
      *    IF BAD RETURN CODE FROM PMKUT080.  INSTEAD MOVE ZEROES TO   *        
      *    STYL-ID AND CONTINUE ON - WILL SHOW UP ON DATA INTEGRITY    *        
      *    REPORT AS A DIFFERENCE.  THIS ALLOWS US TO COMPARE OTHER    *        
      *    PRICING RECORDS IF THERE IS ONE NOT-FOUND SITUATION IN VSM. *        
      *                                                                *        
      *    01/13/2004 - AMANDA NEWHART - P000616675                             
      *       TAKE OUT THE CALLS TO THE PRICESIMPLECSR AND THE         *        
      *       PRICEGROUPCSR CURSOR.  ADD DISPLAY OF TIMES.  ADDED VIEW *        
      *       TO THE CURSORS TO REPLACE THE DB2 PROGRAM CALL.  REMOVE  *        
      *       PMKUT080.                                                *        
      ******************************************************************        
      * DATE:         07/30/2018                                       *        
      * WR/IR#:       CHG0204126                                       *        
      * PROGRAMMER:   COGNIZANT                                        *        
      * DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE            *        
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT DATE-RANGE-FILE                                               
               ASSIGN                       TO INP01.                           
                                                                                
           SELECT TMST-FILE                                                     
               ASSIGN                       TO INP02.                           
                                                                                
070941     SELECT ORACLE-LOGON-FILE                                             
070941         ASSIGN                       TO ORALOGON.                        
                                                                                
           SELECT PRICING-FILE                                                  
               ASSIGN                       TO OUT01.                           
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  DATE-RANGE-FILE                                                      
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  DATE-RANGE-LINE                  PIC  X(80).                         
                                                                                
       FD  TMST-FILE                                                            
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  TMST-LINE                        PIC  X(80).                         
                                                                                
070941 FD  ORACLE-LOGON-FILE                                                    
070941     RECORDING MODE IS F                                                  
070941     LABEL RECORDS ARE STANDARD                                           
070941     BLOCK CONTAINS 0 RECORDS                                             
070941     RECORD CONTAINS 80 CHARACTERS                                        
070941     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
070941 01  OL-ORACLE-LOGON-RECORD           PIC X(80).                          
                                                                                
       FD  PRICING-FILE                                                         
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  PRICING-LINE                     PIC X(258).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0.         
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.           
           88  ABEND-4002-READ-ERROR                     VALUE +4002.           
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.           
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.           
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.           
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.           
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.           
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.           
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
           88  ABEND-4014-ORACLE-LOGON-ERROR             VALUE +4014.           
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.           
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.           
                                                                                
       01  CC-DATE-RANGE-FILE.                                                  
           05  CC-START-DATE                PIC X(10).                          
           05  FILLER                       PIC X(01).                          
           05  CC-END-DATE                  PIC X(10).                          
                                                                                
       01  CC-TMST-FILE.                                                        
           05  CC-LAST-UPD-TMST             PIC X(19).                          
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
070941     05  PS-END-OF-LOGON-FILE-SW          PIC X(01)  VALUE 'N'.           
070941         88  PS-END-OF-LOGON-FILE                    VALUE 'Y'.           
070941     05  PS-ORA-SRC-CTL-DONE-SW           PIC X(01)  VALUE 'N'.           
070941         88  PS-ORA-SRC-EMPTY                        VALUE 'Y'.           
           05  PS-DATE-RANGE-FILE-SW            PIC X(01)  VALUE 'N'.           
               88  PS-DATE-RANGE-FILE-END                  VALUE 'Y'.           
               88  PS-DATE-RANGE-FILE-START                VALUE 'N'.           
           05  PS-TMST-FILE-SW                  PIC X(01)  VALUE 'N'.           
               88  PS-TMST-FILE-END                        VALUE 'Y'.           
               88  PS-TMST-FILE-START                      VALUE 'N'.           
           05  PS-END-OF-PRCG-CLRNC-CSR-SW      PIC X(01)  VALUE 'N'.           
               88  PS-END-OF-PRCG-CLRNC-CSR                VALUE 'Y'.           
               88  PS-NOT-END-OF-PRCG-CLRNC-CSR            VALUE 'N'.           
           05  PS-END-PRCG-ADMTIER-CSR-SW       PIC X(01)  VALUE 'N'.           
               88  PS-END-OF-PRCG-ADMTIER-CSR              VALUE 'Y'.           
               88  PS-NOT-END-PRCG-ADMTIER-CSR             VALUE 'N'.           
           05  PS-END-PRCG-PROMTIER-CSR-SW      PIC X(01)  VALUE 'N'.           
               88  PS-END-OF-PRCG-PROMTIER-CSR             VALUE 'Y'.           
               88  PS-NOT-END-PRCG-PROMTIER-CSR            VALUE 'N'.           
           05  PS-END-OF-PRCG-CMPTV-CSR-SW      PIC X(01)  VALUE 'N'.           
               88  PS-END-OF-PRCG-CMPTV-CSR                VALUE 'Y'.           
               88  PS-NOT-END-OF-PRCG-CMPTV-CSR            VALUE 'N'.           
           05  PS-END-OF-ADEVNT-DTE-CSR-SW      PIC X(1)   VALUE 'N'.           
               88  PS-END-OF-ADEVNT-DTE-CSR                VALUE 'Y'.           
               88  PS-NOT-END-OF-ADEVNT-DTE-CSR            VALUE 'N'.           
           05  PS-TYPE                          PIC X(01).                      
               88  PS-CLRNC-TYP                            VALUE 'C'.           
               88  PS-PROMOTIER-TYP                        VALUE 'P'.           
               88  PS-ADMINTIER-TYP                        VALUE 'A'.           
               88  PS-CMPTV-TYP                            VALUE 'M'.           
                                                                                
070941 01  CC-ORACLE-CONNECTION.                                                
070941     05  CC-ORACLE-USERID             PIC X(40).                          
070941         88 CC-AUTO-LOGON                         VALUE '/'.              
070941     05  CC-ORACLE-PASSWORD           PIC X(40).                          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
070941*    05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)  VALUE                   
070941     05  PC-AUTO-LOGON                PIC  X(1)   VALUE '/'.              
070941     05  PC-PROGRAM-NAME              PIC  X(08)  VALUE                   
                                                           'PMKUT076'.          
           05  PC-BUSINESS-EVNT-ID          PIC  9(03)  VALUE 090.              
           05  PC-ACTION-CDE                PIC  X(01)  VALUE 'U'.              
           05  PC-DASH                      PIC  X(01)  VALUE '-'.              
           05  PC-SPACE                     PIC  X(01)  VALUE ' '.              
           05  PC-BEG-TMST                  PIC  X(08)  VALUE                   
                                                          '00:00:00'.           
           05  PC-END-TMST                  PIC  X(08)  VALUE                   
                                                          '23:59:59'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
070941     05  PV-ORACLE-USERID-LEN         PIC  S9(4)     VALUE ZEROS          
070941                                                     COMP SYNC.           
070941     05  PV-ORACLE-PASSWORD-LEN       PIC  S9(4)     VALUE ZEROS          
070941                                                     COMP SYNC.           
070941     05  PC-AUTO-LOGON                PIC  X(01)     VALUE '/'.           
           05  PV-SQL                       PIC S9(04)     VALUE ZEROS.         
           05  PV-TABLE-NAME                PIC  X(12)     VALUE SPACES.        
           05  PV-OPERATION-MSG             PIC  X(72)     VALUE SPACES.        
           05  PV-OPERATION-MSG-1           PIC  X(40)     VALUE SPACES.        
           05  PV-PARAGRAPH-NAME            PIC  X(30)     VALUE SPACES.        
           05  PV-SYS-DATE                  PIC  X(26)     VALUE SPACES.        
           05  PV-SYS-DATE-XS REDEFINES PV-SYS-DATE.                            
               10  PV-SYS-DATE-DATE         PIC  X(10).                         
               10  PV-PERIOD                PIC  X(01).                         
               10  PV-SYS-DATE-TIME         PIC  X(15).                         
           05  PV-NEW-STRT-DATE             PIC  X(26)     VALUE SPACES.        
           05  PV-NEW-END-DATE              PIC  X(26)     VALUE SPACES.        
                                                                                
       01  PV-ADEVNT-VARIABLES.                                                 
           05 PV-AD-EVNT-ID                 PIC  9(10)     VALUE ZEROS.         
           05 PV-AD-EVNT-STRT-DTE           PIC  X(10)     VALUE SPACES.        
           05 PV-AD-EVNT-END-DTE            PIC  X(10)     VALUE SPACES.        
                                                                                
       01  PV-SELECT-VARIABLES.                                                 
           05  PV-PROMO-INTFC-ID            PIC  9(10)     VALUE ZEROS.         
           05  PV-PROMO-INTFC-SUB-ID        PIC  9(10)     VALUE ZEROS.         
           05  PV-PRICG-TYP-CDE             PIC  X(03)     VALUE SPACES.        
           05  PV-PROMO-ID                  PIC  9(10)     VALUE ZEROS.         
           05  PV-PROMO-ITM-ID              PIC  9(10)     VALUE ZEROS.         
           05  PV-MDSE-SELN-LNE-ID          PIC  9(10)     VALUE ZEROS.         
           05  PV-MDSE-HIER-CDE             PIC  X(03)     VALUE SPACES.        
           05  PV-DEPT-NBR                  PIC  9(04)     VALUE ZEROS.         
           05  PV-MAJ-CL-NBR                PIC  9(04)     VALUE ZEROS.         
           05  PV-SUB-CL-NBR                PIC  9(04)     VALUE ZEROS.         
           05  PV-STYLE-ID                  PIC  9(09)     VALUE ZEROS.         
           05  PV-SKU-NBR                   PIC  9(08)     VALUE ZEROS.         
           05  PV-ADV-IND                   PIC  X(01)     VALUE SPACES.        
           05  PV-CLRNC-TYP-CDE             PIC  X(01)     VALUE SPACES.        
           05  PV-PRICG-AMT                 PIC S9(7)V9(3) VALUE ZEROS.         
           05  PV-GP-QTY                    PIC S9(5)      VALUE ZEROS.         
           05  PV-GP-AMT                    PIC S9(7)V9(2) VALUE ZEROS.         
           05  PV-GP-PCT                    PIC S9(3)V9(3) VALUE ZEROS.         
           05  PV-THRS-QTY                  PIC S9(5)      VALUE ZEROS.         
           05  PV-THRS-AMT                  PIC S9(7)V9(2) VALUE ZEROS.         
           05  PV-DISC-AMT                  PIC S9(7)V9(2) VALUE ZEROS.         
           05  PV-DISC-PCT                  PIC S9(3)V9(3) VALUE ZEROS.         
           05  PV-RCPT-CMMT-TXT             PIC  X(40)     VALUE SPACES.        
           05  PV-ASSN-LVL-CDE              PIC  X(01)     VALUE SPACES.        
           05  PV-ESL-TOPR-TYP              PIC  X(03)     VALUE SPACES.        
           05  PV-STAT-CDE                  PIC  X(01)     VALUE SPACES.        
           05  PV-LAST-UPD-TMST             PIC  X(19)     VALUE SPACES.        
           05  PV-INTFC-SUB-ID-NULL         PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-DEPT-NBR-NULL             PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-MAJ-CL-NBR-NULL           PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-SUB-CL-NBR-NULL           PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-STYLE-ID-NULL             PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-SKU-NBR-NULL              PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-ADV-IND-NULL              PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-CLRNC-TYP-CDE-NULL        PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-PRICG-AMT-NULL            PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-GP-QTY-NULL               PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-GP-AMT-NULL               PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-GP-PCT-NULL               PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-THRS-QTY-NULL             PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-THRS-AMT-NULL             PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-DISC-AMT-NULL             PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-DISC-PCT-NULL             PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-RCPT-CMMT-TXT-NULL        PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-ASSN-LVL-CDE-NULL         PIC S9(04)  COMP                    
                                                           VALUE ZEROS.         
           05  PV-ESL-TOPR-TYP-NULL         PIC S9(04)  COMP                    
                                                        VALUE ZEROS.            
       01  CT-CPU-TIME.                                                         
           05  CT-CPU-HR                    PIC  9(02)  VALUE ZERO.             
           05  CT-CPU-MIN                   PIC  9(02)  VALUE ZERO.             
           05  CT-CPU-SEC                   PIC  9(02)  VALUE ZERO.             
           05  CT-CPU-HSEC                  PIC  9(02)  VALUE ZERO.             
                                                                                
       01  PV-CPU-TIME.                                                         
           05  PV-CPU-HR                    PIC  9(02)  VALUE ZERO.             
           05  FILLER                       PIC  X(01)  VALUE ":".              
           05  PV-CPU-MIN                   PIC  9(02)  VALUE ZERO.             
           05  FILLER                       PIC  X(01)  VALUE ":".              
           05  PV-CPU-SEC                   PIC  9(02)  VALUE ZERO.             
           05  FILLER                       PIC  X(01)  VALUE ":".              
           05  PV-CPU-HSEC                  PIC  9(02)  VALUE ZERO.             
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECS-WRITTEN              PIC S9(11)  COMP-3                  
                                                           VALUE ZEROS.         
           05  PA-WRITE-PRCG-CLRNC-CNT      PIC S9(11)  COMP-3                  
                                                           VALUE ZEROS.         
           05  PA-WRITE-PRCG-ADMTIER-CNT    PIC S9(11)  COMP-3                  
                                                           VALUE ZEROS.         
           05  PA-WRITE-PRCG-PROMTIER-CNT   PIC S9(11)  COMP-3                  
                                                           VALUE ZEROS.         
           05  PA-WRITE-PRCG-CMPTV-CNT      PIC S9(11)  COMP-3                  
                                                           VALUE ZEROS.         
           05  PA-FETCH-PRCG-CLRNC-CNT      PIC S9(11)  COMP-3                  
                                                           VALUE ZEROS.         
           05  PA-FETCH-PRCG-ADMTIER-CNT    PIC S9(11)  COMP-3                  
                                                           VALUE ZEROS.         
           05  PA-FETCH-PRCG-PROMTIER-CNT   PIC S9(11)  COMP-3                  
                                                           VALUE ZEROS.         
           05  PA-FETCH-PRCG-CMPTV-CNT      PIC S9(11)  COMP-3                  
                                                           VALUE ZEROS.         
       01  PA-IND-PROGRAM-ACCUMULATORS.                                         
           05  PA-IND-WRITE-PRCG-CLRNC-CNT      PIC S9(11)  COMP-3              
                                                           VALUE ZEROS.         
           05  PA-IND-WRITE-PRCG-ADMTIER-CNT    PIC S9(11)  COMP-3              
                                                           VALUE ZEROS.         
           05  PA-IND-WRITE-PRCG-PROMTIER-CNT   PIC S9(11)  COMP-3              
                                                           VALUE ZEROS.         
           05  PA-IND-WRITE-PRCG-CMPTV-CNT      PIC S9(11)  COMP-3              
                                                           VALUE ZEROS.         
           05  PA-IND-FETCH-PRCG-CLRNC-CNT      PIC S9(11)  COMP-3              
                                                           VALUE ZEROS.         
           05  PA-IND-FETCH-PRCG-ADMTIER-CNT    PIC S9(11)  COMP-3              
                                                           VALUE ZEROS.         
           05  PA-IND-FETCH-PRCG-PROMTIER-CNT   PIC S9(11)  COMP-3              
                                                           VALUE ZEROS.         
           05  PA-IND-FETCH-PRCG-CMPTV-CNT      PIC S9(11)  COMP-3              
                                                           VALUE ZEROS.         
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE PRICING BUSINESS EVENT                              
      ******************************************************************        
           COPY PMRD706.                                                        
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE TOTAL RECORD                                        
      ******************************************************************        
           COPY PMRD700.                                                        
                                                                                
           EJECT                                                                
                                                                                
      *****************************************************************         
      *                      O  R  A  C  L  E                                   
      *================================================================         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *****************************************************************         
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *---------------------------------------------------------------*         
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *---------------------------------------------------------------*         
       01      PC-ENT01-TYPE-CDE          PIC X(02)      VALUE '01'.            
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
                                                                                
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
                                                                                
       01      ORACLE-RETURN              PIC X(200)     VALUE SPACES.          
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      ********************                                                      
       EJECT                                                                    
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      * ==> PROCESSES:                                                *         
      *  - RESET SWITCHES                                             *         
      *  - OPEN INPUT AND OUTPUT FILES                                *         
      *  - READ THE DATE RANGE FILE                                   *         
      *  - READ THE TIMESTAMP FILE                                    *         
      *  - SET UP ORACLE                                              *         
      *  - FIND THE CURRENT SYS DATE                                  *         
      *  - RUN THE CLEARANCE CURSOR                                   *         
      *  - RUN THE AD EVENT CURSOR                                    *         
      *  - WRITE THE TOTAL LINES                                      *         
      *  - CLOSE THE INPUT AND OUTPUT FILES                           *         
      * ==> PERFORMED FROM: START                                     *         
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
           SET PS-DATE-RANGE-FILE-START TO TRUE.                                
           SET PS-TMST-FILE-START TO TRUE.                                      
           PERFORM B100-OPEN-FILES.                                             
           PERFORM B200-READ-DATE-RANGE-FILE.                                   
           PERFORM B300-READ-TMST-FILE.                                         
                                                                                
070941*    PERFORM PM700-A000-ORACLE-SETUP.                                     
                                                                                
      *    PERFORM C100-FIND-SYSDATE.                                           
                                                                                
           PERFORM C220-DECLARE-PRCG-CLRNC-CSR.                                 
           PERFORM C320-OPEN-PRCG-CLRNC-CSR.                                    
            ACCEPT CT-CPU-TIME FROM TIME.                                       
            MOVE CT-CPU-HR     TO  PV-CPU-HR.                                   
            MOVE CT-CPU-MIN    TO  PV-CPU-MIN.                                  
            MOVE CT-CPU-SEC    TO  PV-CPU-SEC.                                  
            MOVE CT-CPU-HSEC   TO  PV-CPU-HSEC.                                 
            DISPLAY 'START FETCH/PROCESS CLRNC-CSR: ' PV-CPU-TIME.              
           PERFORM C420-FETCH-PRCG-CLRNC-CSR.                                   
           PERFORM C520-PROCESS-PRCG-CLRNC-CSR                                  
               UNTIL PS-END-OF-PRCG-CLRNC-CSR                                   
            ACCEPT CT-CPU-TIME FROM TIME.                                       
            MOVE CT-CPU-HR     TO  PV-CPU-HR.                                   
            MOVE CT-CPU-MIN    TO  PV-CPU-MIN.                                  
            MOVE CT-CPU-SEC    TO  PV-CPU-SEC.                                  
            MOVE CT-CPU-HSEC   TO  PV-CPU-HSEC.                                 
            DISPLAY 'FINISH FETCH/PROCESS CLRNC-CSR: ' PV-CPU-TIME.             
           PERFORM C620-CLOSE-PRCG-CLRNC-CSR.                                   
                                                                                
           PERFORM C260-DECLARE-ADEVNT-DTE-CSR                                  
           PERFORM C360-OPEN-ADEVNT-DTE-CSR.                                    
           PERFORM C460-FETCH-ADEVNT-DTE-CSR.                                   
           PERFORM C560-PROCESS-ADEVNT-DTE-CSR                                  
               UNTIL PS-END-OF-ADEVNT-DTE-CSR.                                  
           PERFORM C660-CLOSE-ADEVNT-DTE-CSR.                                   
                                                                                
           PERFORM W200-WRITE-TOTAL-LINE.                                       
                                                                                
           PERFORM B900-CLOSE-FILES.                                            
                                                                                
           DISPLAY 'RECORDS WRITTEN FROM PRICING CLRNC CSR = '          LE-CNT  
                                           PA-WRITE-PRCG-CLRNC-CNT.             
           DISPLAY 'RECORDS WRITTEN FROM PRICING ADMINTIERED CSR = '    LE-CNT  
                                           PA-WRITE-PRCG-ADMTIER-CNT.           
           DISPLAY 'RECORDS WRITTEN FROM PRICING PROMOTIERED CSR = '    LE-CNT  
                                           PA-WRITE-PRCG-PROMTIER-CNT.          
           DISPLAY 'RECORDS WRITTEN FROM PRICING CMPTV CSR = '          LE-CNT  
                                           PA-WRITE-PRCG-CMPTV-CNT.             
           DISPLAY 'RECORDS WRITTEN TO FILE = ' PA-RECS-WRITTEN.                
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * A200-PROCESS-CSRS.                                            *         
      * ==> PROCESSES:                                                *         
      *  - REINITIALIZE THE PROGRAM ACCUMULATORS                      *         
      *  - PROCESS ALL THE CURSORS                                    *         
      *    - SIMPLE, GROUP, ADMIN TIERED, PROMO TIERED, CPMTV         *         
      * ==> PERFORMED FROM: C560-PROCESS-ADEVNT-DTE-CSR               *         
      *****************************************************************         
       A200-PROCESS-CSRS.                                                       
                                                                                
           INITIALIZE PA-IND-PROGRAM-ACCUMULATORS.                              
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'START DATE = ' PV-NEW-STRT-DATE.                            
           DISPLAY 'END DATE   = ' PV-NEW-END-DATE.                             
           PERFORM C230-DECLARE-PRCG-ADMTIER-CSR.                               
           PERFORM C330-OPEN-PRCG-ADMTIER-CSR.                                  
            ACCEPT CT-CPU-TIME FROM TIME.                                       
            MOVE CT-CPU-HR     TO  PV-CPU-HR.                                   
            MOVE CT-CPU-MIN    TO  PV-CPU-MIN.                                  
            MOVE CT-CPU-SEC    TO  PV-CPU-SEC.                                  
            MOVE CT-CPU-HSEC   TO  PV-CPU-HSEC.                                 
            DISPLAY 'START FETCH/PROCESS PRCG-ADMTIER-CSR: '                    
                     PV-CPU-TIME.                                               
           PERFORM C430-FETCH-PRCG-ADMTIER-CSR.                                 
           PERFORM C530-PROCESS-PRCG-ADMTIER-CSR                                
               UNTIL PS-END-OF-PRCG-ADMTIER-CSR                                 
            ACCEPT CT-CPU-TIME FROM TIME.                                       
            MOVE CT-CPU-HR     TO  PV-CPU-HR.                                   
            MOVE CT-CPU-MIN    TO  PV-CPU-MIN.                                  
            MOVE CT-CPU-SEC    TO  PV-CPU-SEC.                                  
            MOVE CT-CPU-HSEC   TO  PV-CPU-HSEC.                                 
            DISPLAY 'FINISH FETCH/PROCESS PRCG-ADMTIER-CSR: '                   
                     PV-CPU-TIME.                                               
           PERFORM C630-CLOSE-PRCG-ADMTIER-CSR.                                 
                                                                                
           PERFORM C240-DECLARE-PRCG-PROMTIER-CSR.                              
           PERFORM C340-OPEN-PRCG-PROMTIER-CSR.                                 
            ACCEPT CT-CPU-TIME FROM TIME.                                       
            MOVE CT-CPU-HR     TO  PV-CPU-HR.                                   
            MOVE CT-CPU-MIN    TO  PV-CPU-MIN.                                  
            MOVE CT-CPU-SEC    TO  PV-CPU-SEC.                                  
            MOVE CT-CPU-HSEC   TO  PV-CPU-HSEC.                                 
            DISPLAY 'START FETCH/PROCESS PRCG-PROMOTIER-CSR: '                  
                     PV-CPU-TIME.                                               
           PERFORM C440-FETCH-PRCG-PROMTIER-CSR.                                
           PERFORM C540-PROCESS-PRCG-PROMTIER-CSR                               
               UNTIL PS-END-OF-PRCG-PROMTIER-CSR                                
            ACCEPT CT-CPU-TIME FROM TIME.                                       
            MOVE CT-CPU-HR     TO  PV-CPU-HR.                                   
            MOVE CT-CPU-MIN    TO  PV-CPU-MIN.                                  
            MOVE CT-CPU-SEC    TO  PV-CPU-SEC.                                  
            MOVE CT-CPU-HSEC   TO  PV-CPU-HSEC.                                 
            DISPLAY 'FINISH FETCH/PROCESS PRCG-PROMOTIER-CSR: '                 
                     PV-CPU-TIME.                                               
           PERFORM C640-CLOSE-PRCG-PROMTIER-CSR.                                
                                                                                
           PERFORM C250-DECLARE-PRCG-CMPTV-CSR.                                 
           PERFORM C350-OPEN-PRCG-CMPTV-CSR.                                    
            ACCEPT CT-CPU-TIME FROM TIME.                                       
            MOVE CT-CPU-HR     TO  PV-CPU-HR.                                   
            MOVE CT-CPU-MIN    TO  PV-CPU-MIN.                                  
            MOVE CT-CPU-SEC    TO  PV-CPU-SEC.                                  
            MOVE CT-CPU-HSEC   TO  PV-CPU-HSEC.                                 
            DISPLAY 'START FETCH/PROCESS CMPTV-CSR: ' PV-CPU-TIME.              
           PERFORM C450-FETCH-PRCG-CMPTV-CSR.                                   
           PERFORM C550-PROCESS-PRCG-CMPTV-CSR                                  
               UNTIL PS-END-OF-PRCG-CMPTV-CSR                                   
            ACCEPT CT-CPU-TIME FROM TIME.                                       
            MOVE CT-CPU-HR     TO  PV-CPU-HR.                                   
            MOVE CT-CPU-MIN    TO  PV-CPU-MIN.                                  
            MOVE CT-CPU-SEC    TO  PV-CPU-SEC.                                  
            MOVE CT-CPU-HSEC   TO  PV-CPU-HSEC.                                 
            DISPLAY 'FINISH FETCH/PROCESS CMPTV-CSR: ' PV-CPU-TIME.             
           PERFORM C650-CLOSE-PRCG-CMPTV-CSR.                                   
                                                                                
      *****************************************************************         
      * B100-OPEN-FILES.                                              *         
      * ==> PROCESSES:                                                *         
      *  - OPEN TWO INPUT FILES AND ONE OUTPUT FILE                   *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B100-OPEN-FILES.                                                         
                                                                                
070941     DISPLAY 'ORACLE FILE OPEN'.                                          
070941     OPEN INPUT ORACLE-LOGON-FILE.                                        
070941                                                                          
070941     DISPLAY 'ORACLE FILE READ'.                                          
070941     PERFORM 7000-READ-ORACLE-LOGON.                                      
070941                                                                          
070941     DISPLAY 'ORACLE FILE CLOSE'.                                         
070941     CLOSE ORACLE-LOGON-FILE.                                             
070941                                                                          
070941     IF ((PS-END-OF-LOGON-FILE)                                           
070941             OR (CC-ORACLE-CONNECTION = SPACES))                          
070941         DISPLAY '** ABEND **'                                            
070941         DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
070941                   'NOT PROVIDED'                                         
070941         SET PS-ORA-SRC-EMPTY TO TRUE                                     
070941         CALL 'ILBOABN0' USING ABEND-CODE                                 
070941     END-IF.                                                              
070941                                                                          
070941     IF CC-AUTO-LOGON                                                     
070941         DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
070941         EXEC SQL                                                         
070941              CONNECT :USERNAME                                           
070941         END-EXEC                                                         
070941     ELSE                                                                 
070941         INITIALIZE PV-ORACLE-USERID-LEN                                  
070941                    PV-ORACLE-PASSWORD-LEN                                
070941         INSPECT CC-ORACLE-USERID                                         
070941             TALLYING PV-ORACLE-USERID-LEN                                
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      INSPECT CC-ORACLE-PASSWORD                                          
070941          TALLYING PV-ORACLE-PASSWORD-LEN                                 
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
070941                USERNAME-ARR                                              
070941      MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
070941      MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
070941                PASSWD-ARR                                                
070941      MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
070941      DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '               
070941                USERNAME-ARR                                              
070941      EXEC SQL                                                            
070941        CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
070941      END-EXEC                                                            
070941      INITIALIZE CC-ORACLE-PASSWORD                                       
070941                 PASSWD                                                   
070941      END-IF.                                                             
070941                                                                          
070941     IF (SQLCODE NOT = 0)                                                 
070941         DISPLAY PV-PARAGRAPH-NAME                                        
070941         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
070941         PERFORM 9900-ORACLE-ABEND                                        
070941     END-IF.                                                              
                                                                                
           OPEN INPUT  DATE-RANGE-FILE                                          
                INPUT  TMST-FILE                                                
                OUTPUT PRICING-FILE.                                            
                                                                                
      *****************************************************************         
      * B200-READ-DATE-RANGE-FILE.                                    *         
      * ==> PROCESSES:                                                *         
      *  - READ IN THE DATE RANGE FILE                                *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B200-READ-DATE-RANGE-FILE.                                               
                                                                                
           READ DATE-RANGE-FILE INTO CC-DATE-RANGE-FILE                         
              AT END                                                            
                 SET PS-DATE-RANGE-FILE-END TO TRUE                             
           END-READ.                                                            
                                                                                
           IF PS-DATE-RANGE-FILE-END                                            
               MOVE 'NO DATE RANGE FILE TO PROCESS'                             
                                                 TO PV-OPERATION-MSG-1          
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM Z910-ABEND                                               
           ELSE                                                                 
               DISPLAY 'CC-START-DATE = ' CC-START-DATE                         
               DISPLAY 'CC-END-DATE   = ' CC-END-DATE                           
                                                                                
               STRING CC-START-DATE DELIMITED BY SPACE                          
                      PC-SPACE DELIMITED BY SIZE                                
                      PC-BEG-TMST   DELIMITED BY SIZE                           
                      INTO PV-NEW-STRT-DATE                                     
                                                                                
               STRING CC-END-DATE DELIMITED BY SPACE                            
                      PC-SPACE DELIMITED BY SIZE                                
                      PC-END-TMST   DELIMITED BY SIZE                           
                      INTO PV-NEW-END-DATE                                      
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B300-READ-TMST-FILE.                                          *         
      * ==> PROCESSES:                                                *         
      *  - READ IN THE TMST FILE                                      *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B300-READ-TMST-FILE.                                                     
                                                                                
           READ TMST-FILE INTO CC-TMST-FILE                                     
              AT END                                                            
                 SET PS-TMST-FILE-END TO TRUE                                   
           END-READ.                                                            
                                                                                
           IF PS-TMST-FILE-END                                                  
               MOVE 'NO TMST FILE TO PROCESS'                                   
                                                 TO PV-OPERATION-MSG-1          
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM Z910-ABEND                                               
           ELSE                                                                 
               DISPLAY 'CC-LAST-UPD-TMST = ' CC-LAST-UPD-TMST                   
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B900-CLOSE-FILES                                              *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE TWO INPUT FILES AND ONE OUTPUT FILE              *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B900-CLOSE-FILES.                                                        
                                                                                
           CLOSE  DATE-RANGE-FILE                                               
                  TMST-FILE                                                     
                  PRICING-FILE.                                                 
                                                                                
      *****************************************************************         
      * C100-FIND-SYSDATE:                                            *         
      * ==> PROCESSES:                                                *         
      *  - SINCE NO TABLE IS ACTUALLY USED WE USE THE DUAL TABLE      *         
      *  - FIND THE CURRENT DATE AND TIME                                       
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C100-FIND-SYSDATE.                                                       
           EXEC SQL                                                             
               SELECT                                                           
                   SYSDATE                                                      
               INTO :PV-SYS-DATE                                                
               FROM                                                             
                   DUAL                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
                 MOVE PC-DASH TO PV-PERIOD                                      
                 STRING PV-SYS-DATE-XS DELIMITED BY SPACE                       
                        '.000000'      DELIMITED BY SIZE                        
                        INTO PV-SYS-DATE-XS                                     
                 DISPLAY 'SYS DATE = ' PV-SYS-DATE-XS                           
                 CONTINUE                                                       
             WHEN OTHER                                                         
               MOVE SQLCODE               TO PV-SQL                             
               MOVE 'DUAL'                TO PV-TABLE-NAME                      
               MOVE 'C100-FIND-SYSDATE'   TO PV-PARAGRAPH-NAME                  
               PERFORM 9900-ORACLE-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C220-DECLARE-PRCG-CLRNC-CSR.                                  *         
      * ==> PROCESSES:                                                *         
      *  - USED A VIEW IN THE FROM CLAUSE INSTEAD OF IND TABLES       *         
      *  - DECLARE CSR THAT RETRIEVES CLEARANCE PRICING               *         
      *  - JOIN THE PMT_CLRNC_PROMO AND PMV_CLRNC_PRICG TABLE TO      *         
      *    GET THE START AND END DATES                                *         
      * ==> PERFORMED FROM: A200-PROCESS-CSRS                         *         
      *****************************************************************         
       C220-DECLARE-PRCG-CLRNC-CSR.                                             
                                                                                
           EXEC SQL                                                             
                DECLARE PRICECLRNCCSR                                           
                CURSOR FOR                                                      
                    SELECT /*+ FIRST_ROWS */                                    
                         A.CLRNC_PROMO_INTFC_ID                                 
                        ,A.PRICG_TYP_CDE                                        
                        ,A.CLRNC_PROMO_ID                                       
                        ,A.MDSE_SELN_LNE_ID                                     
                        ,A.MDSE_HIER_CDE                                        
                        ,A.DEPT_NBR                                             
                        ,A.MAJ_CL_NBR                                           
                        ,A.SUB_CL_NBR                                           
                        ,C.RMS_STYL_ID                                          
                        ,A.SKU_NBR                                              
                        ,A.CLRNC_TYP_CDE                                        
                        ,A.PRICG_AMT                                            
                        ,A.STAT_CDE                                             
                        ,TO_CHAR(A.LAST_UPD_TMST,                               
                                             'YYYY-MM-DD-HH24.MI.SS')           
                    FROM PMV_CLRNC_PRICG     A                                  
                        ,PMT_CLRNC_PROMO     B                                  
                        ,K_DBLINKS.V_TMDARVS C                                  
                    WHERE A.CLRNC_PROMO_ID = B.CLRNC_PROMO_ID                   
                      AND A.DEPT_NBR       = C.DEPT_NBR(+)                      
                      AND A.MAJ_CL_NBR     = C.MAJ_CL_NBR(+)                    
                      AND A.SUB_CL_NBR     = C.SUB_CL_NBR(+)                    
                      AND A.VS_ID          = C.VS_ID(+)                         
                      AND B.END_TMST >= TO_DATE(                                
                             :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')        
                      AND B.STRT_TMST <= TO_DATE(                               
                             :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')         
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C230-DECLARE-PRCG-ADMTIER-CSR.                                *         
      * ==> PROCESSES:                                                *         
      *  - USED A VIEW IN THE FROM CLAUSE INSTEAD OF IND TABLES       *         
      *  - DECLARE CSR THAT RETRIEVES ADMIN TIERED PRICING            *         
      *  - JOIN THE PMT_PROMO AND PMV_ADM_TIER_PRICG TABLE TO GET THE *         
      *    START AND END DATES                                        *         
      * ==> PERFORMED FROM: A200-PROCESS-CSRS                         *         
      *****************************************************************         
       C230-DECLARE-PRCG-ADMTIER-CSR.                                           
                                                                                
           EXEC SQL                                                             
                DECLARE PRICEADMINTIEREDCSR                                     
                CURSOR FOR                                                      
                    SELECT /*+ FIRST_ROWS */                                    
                         A.TIER_PROMO_INTFC_ID                                  
                        ,A.TIER_PROMO_INTFC_SUB_ID                              
                        ,A.PRICG_TYP_CDE                                        
                        ,A.PROMO_ID                                             
                        ,A.PROMO_TIER_MDSE_ID                                   
                        ,A.MDSE_HIER_CDE                                        
                        ,A.DEPT_NBR                                             
                        ,A.MAJ_CL_NBR                                           
                        ,A.SUB_CL_NBR                                           
                        ,C.RMS_STYL_ID                                          
                        ,A.SKU_NBR                                              
                        ,A.THRS_QTY                                             
                        ,A.THRS_AMT                                             
                        ,A.DISC_AMT                                             
                        ,A.DISC_PCT                                             
                        ,A.RCPT_CMMT_TXT                                        
                        ,A.ESL_TOPR_TYP_CDE                                     
                        ,A.STAT_CDE                                             
                        ,TO_CHAR(A.LAST_UPD_TMST,                               
                                             'YYYY-MM-DD-HH24.MI.SS')           
                    FROM PMV_ADM_TIER_PRICG  A                                  
                        ,PMT_PROMO           B                                  
                        ,K_DBLINKS.V_TMDARVS C                                  
                    WHERE A.PROMO_ID    = B.PROMO_ID                            
                      AND A.DEPT_NBR    = C.DEPT_NBR(+)                         
                      AND A.MAJ_CL_NBR  = C.MAJ_CL_NBR(+)                       
                      AND A.SUB_CL_NBR  = C.SUB_CL_NBR(+)                       
                      AND A.VS_ID       = C.VS_ID(+)                            
                      AND B.DFLT_END_TMST >= TO_DATE(                           
                             :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')        
                      AND B.DFLT_STRT_TMST <= TO_DATE(                          
                             :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')         
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C240-DECLARE-PRCG-PROMTIER-CSR.                               *         
      * ==> PROCESSES:                                                *         
      *  - USED A VIEW IN THE FROM CLAUSE INSTEAD OF IND TABLES       *         
      *  - DECLARE CSR THAT RETRIEVES PROMO TIERED PRICING            *         
      *  - JOIN THE PMT_PROMO AND PMV_PROMO_ITM_TIER_PRICG TABLE      *         
      *    TO GET THE START AND END DATES                             *         
      * ==> PERFORMED FROM: A200-PROCESS-CSRS                         *         
      *****************************************************************         
       C240-DECLARE-PRCG-PROMTIER-CSR.                                          
                                                                                
           EXEC SQL                                                             
                DECLARE PRCGPROMTIERCSR                                         
                CURSOR FOR                                                      
                    SELECT /*+ FIRST_ROWS */                                    
                         A.TIER_PROMO_INTFC_ID                                  
                        ,A.TIER_PROMO_INTFC_SUB_ID                              
                        ,A.PRICG_TYP_CDE                                        
                        ,A.PROMO_ID                                             
                        ,A.PROMO_ITM_ID                                         
                        ,A.MDSE_SELN_LNE_ID                                     
                        ,A.MDSE_HIER_CDE                                        
                        ,A.DEPT_NBR                                             
                        ,A.MAJ_CL_NBR                                           
                        ,A.SUB_CL_NBR                                           
                        ,C.RMS_STYL_ID                                          
                        ,A.SKU_NBR                                              
                        ,A.ADV_IND                                              
                        ,A.THRS_QTY                                             
                        ,A.THRS_AMT                                             
                        ,A.DISC_AMT                                             
                        ,A.DISC_PCT                                             
                        ,A.RCPT_CMMT_TXT                                        
                        ,A.ESL_TOPR_TYP_CDE                                     
                        ,A.STAT_CDE                                             
                        ,TO_CHAR(A.LAST_UPD_TMST,                               
                                             'YYYY-MM-DD-HH24.MI.SS')           
                    FROM PMV_PROMO_ITM_TIER_PRICG A                             
                        ,PMT_PROMO                B                             
                        ,K_DBLINKS.V_TMDARVS      C                             
                    WHERE                                                       
                          A.PROMO_ID    = B.PROMO_ID                            
                      AND A.DEPT_NBR    = C.DEPT_NBR(+)                         
                      AND A.MAJ_CL_NBR  = C.MAJ_CL_NBR(+)                       
                      AND A.SUB_CL_NBR  = C.SUB_CL_NBR(+)                       
                      AND A.VS_ID       = C.VS_ID(+)                            
                      AND B.DFLT_END_TMST >= TO_DATE(                           
                             :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')        
                      AND B.DFLT_STRT_TMST <= TO_DATE(                          
                             :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')         
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C250-DECLARE-PRCG-CMPTV-CSR.                                  *         
      * ==> PROCESSES:                                                *         
      *  - USED A VIEW IN THE FROM CLAUSE INSTEAD OF IND TABLES       *         
      *  - DECLARE CSR THAT RETRIEVES CMPTV PRICING                   *         
      *  - JOIN THE PMT_PROMO AND PMV_CMPTV_PRICG TABLE TO GET THE    *         
      *    START AND END DATES                                        *         
      * ==> PERFORMED FROM: A200-PROCESS-CSRS                         *         
      *****************************************************************         
       C250-DECLARE-PRCG-CMPTV-CSR.                                             
                                                                                
           EXEC SQL                                                             
                DECLARE PRICINGCMPTVCSR                                         
                CURSOR FOR                                                      
                    SELECT /*+ FIRST_ROWS */                                    
                         A.CMPTV_PROMO_INTFC_ID                                 
                        ,A.PRICG_TYP_CDE                                        
                        ,A.PROMO_ID                                             
                        ,A.PROMO_ITM_ID                                         
                        ,A.MDSE_SELN_LNE_ID                                     
                        ,A.MDSE_HIER_CDE                                        
                        ,A.DEPT_NBR                                             
                        ,A.MAJ_CL_NBR                                           
                        ,A.SUB_CL_NBR                                           
                        ,C.RMS_STYL_ID                                          
                        ,A.SKU_NBR                                              
                        ,A.ADV_IND                                              
                        ,A.PRICG_AMT                                            
                        ,A.ESL_TOPR_TYP_CDE                                     
                        ,A.STAT_CDE                                             
                        ,TO_CHAR(A.LAST_UPD_TMST,                               
                                             'YYYY-MM-DD-HH24.MI.SS')           
                    FROM PMV_CMPTV_PRICG     A                                  
                        ,PMT_PROMO           B                                  
                        ,K_DBLINKS.V_TMDARVS C                                  
                    WHERE                                                       
                          A.PROMO_ID    = B.PROMO_ID                            
                      AND A.DEPT_NBR    = C.DEPT_NBR(+)                         
                      AND A.MAJ_CL_NBR  = C.MAJ_CL_NBR(+)                       
                      AND A.SUB_CL_NBR  = C.SUB_CL_NBR(+)                       
                      AND A.VS_ID       = C.VS_ID(+)                            
                      AND B.DFLT_END_TMST >= TO_DATE(                           
                             :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')        
                      AND B.DFLT_STRT_TMST <= TO_DATE(                          
                             :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')         
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C260-DECLARE-ADEVNT-DTE-CSR.                                  *         
      * ==> PROCESSES:                                                *         
      *  - DECLARE CSR THAT RETRIEVES ADEVENT DATES                   *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C260-DECLARE-ADEVNT-DTE-CSR.                                             
                                                                                
           EXEC SQL                                                             
                DECLARE ADEVNTDTECSR                                            
                CURSOR FOR                                                      
                    SELECT                                                      
                         A.AD_EVNT_ID                                           
                        ,TO_CHAR(A.STRT_DTE, 'YYYY-MM-DD')                      
                        ,TO_CHAR(A.END_DTE, 'YYYY-MM-DD')                       
                    FROM                                                        
                        PMT_AD_EVNT A                                           
                    WHERE                                                       
                        A.END_DTE >= TO_DATE(                                   
                           :PV-NEW-STRT-DATE, 'YYYY-MM-DD HH24:MI:SS')          
                    AND A.STRT_DTE <= TO_DATE(                                  
                           :PV-NEW-END-DATE, 'YYYY-MM-DD HH24:MI:SS')           
                    ORDER BY                                                    
                        A.STRT_DTE                                              
                       ,A.END_DTE                                               
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C320-OPEN-PRCG-CLRNC-CSR.                                     *         
      * ==> PROCESSES:                                                *         
      *  - OPEN PRICECLRNCCSR                                         *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL OPEN                                       *         
      * ==> PERFORMED FROM: A200-PROCESS-CSRS                         *         
      *****************************************************************         
       C320-OPEN-PRCG-CLRNC-CSR.                                                
                                                                                
           EXEC SQL                                                             
                 OPEN PRICECLRNCCSR                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C320-OPEN-PRCG-CLRNC-CSR      '                        
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON PRICING-CLRNC-CSR '                      
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C330-OPEN-PRCG-ADMTIER-CSR                                    *         
      * ==> PROCESSES:                                                *         
      *  - OPEN PRICEADMINTIEREDCSR                                   *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL OPEN                                       *         
      * ==> PERFORMED FROM: A200-PROCESS-CSRS                         *         
      *****************************************************************         
       C330-OPEN-PRCG-ADMTIER-CSR.                                              
                                                                                
           EXEC SQL                                                             
                 OPEN PRICEADMINTIEREDCSR                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C330-OPEN-PRCG-ADMTIER-CSR '                           
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON PRICING-ADMTIER-CSR '                    
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C340-OPEN-PRCG-PROMTIER-CSR                                   *         
      * ==> PROCESSES:                                                *         
      *  - OPEN PRCGPROMTIERCSR                                       *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL OPEN                                       *         
      * ==> PERFORMED FROM: A200-PROCESS-CSRS                         *         
      *****************************************************************         
       C340-OPEN-PRCG-PROMTIER-CSR.                                             
                                                                                
           EXEC SQL                                                             
                 OPEN PRCGPROMTIERCSR                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C340-OPEN-PRCG-PROMTIER-CSR '                          
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON PRICING-PROMTIER-CSR '                   
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C350-OPEN-PRCG-CMPTV-CSR                                      *         
      * ==> PROCESSES:                                                *         
      *  - OPEN PRICINGCMPTVCSR                                       *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL OPEN                                       *         
      * ==> PERFORMED FROM: A200-PROCESS-CSRS                         *         
      *****************************************************************         
       C350-OPEN-PRCG-CMPTV-CSR.                                                
                                                                                
           EXEC SQL                                                             
                 OPEN PRICINGCMPTVCSR                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C350-OPEN-PRCG-CMPTV-CSR       '                       
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON PRICING-CMPTV-CSR   '                    
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C360-OPEN-ADEVNT-DTE-CSR                                      *         
      * ==> PROCESSES:                                                *         
      *  - OPEN ADEVNTDTECSR                                          *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = SUCCESSFULL OPEN                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C360-OPEN-ADEVNT-DTE-CSR.                                                
                                                                                
           EXEC SQL                                                             
                 OPEN ADEVNTDTECSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C360-OPEN-ADEVNT-DTE-CSR '                             
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON ADEVNTDTECSR '                           
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      ****************************************************************          
      * C420-FETCH-PRCG-CLRNC-CSR.                                   *          
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      *  - IF THE LST UPDATE DATE IS LESS THEN THE CURRENT LAST UPDATE*         
      *    DATE THEN                                                  *         
      *    - THEN MOVE TO THE COPYBOOK                                *         
      *    - WRITE TO THE FILE                                        *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1405 = FETCHED COLUMN VALUE IS NULL REASON FOR NULL VALUES *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM:                                           *         
      *          A200-PROCESS-CSRS, C520-PROCESS-PRCG-CLRNC-CSR       *         
      *****************************************************************         
       C420-FETCH-PRCG-CLRNC-CSR.                                               
                                                                                
           EXEC SQL                                                             
               FETCH PRICECLRNCCSR                                              
                INTO  :PV-PROMO-INTFC-ID                                        
                    , :PV-PRICG-TYP-CDE                                         
                    , :PV-PROMO-ID                                              
                    , :PV-MDSE-SELN-LNE-ID                                      
                    , :PV-MDSE-HIER-CDE                                         
                    , :PV-DEPT-NBR             :PV-DEPT-NBR-NULL                
                    , :PV-MAJ-CL-NBR           :PV-MAJ-CL-NBR-NULL              
                    , :PV-SUB-CL-NBR           :PV-SUB-CL-NBR-NULL              
                    , :PV-STYLE-ID             :PV-STYLE-ID-NULL                
                    , :PV-SKU-NBR              :PV-SKU-NBR-NULL                 
                    , :PV-CLRNC-TYP-CDE        :PV-CLRNC-TYP-CDE-NULL           
                    , :PV-PRICG-AMT            :PV-PRICG-AMT-NULL               
                    , :PV-STAT-CDE                                              
                    , :PV-LAST-UPD-TMST                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-FETCH-PRCG-CLRNC-CNT                             
                   ADD 1 TO PA-IND-FETCH-PRCG-CLRNC-CNT                         
                   IF PV-LAST-UPD-TMST <=  CC-LAST-UPD-TMST                     
                       ADD 1 TO PA-WRITE-PRCG-CLRNC-CNT                         
                       ADD 1 TO PA-IND-WRITE-PRCG-CLRNC-CNT                     
                       SET PS-CLRNC-TYP TO TRUE                                 
                       PERFORM D100-MOVE-TO-CPYBK                               
                       PERFORM W100-WRITE-DETAIL                                
                   END-IF                                                       
               WHEN SQLCODE = 1403                                              
                   DISPLAY ' RECORDS WRITTEN TO FILE SO FAR = '                 
                                                PA-RECS-WRITTEN                 
                   DISPLAY ' PRICING CLRNC COUNT FROM FETCH = '         LE-CNT  
                                               PA-FETCH-PRCG-CLRNC-CNT  T       
                   DISPLAY ' PRICING CLRNC COUNT FROM WRITE = '         LE-CNT  
                                               PA-WRITE-PRCG-CLRNC-CNT  T       
                   DISPLAY ' IND PRICING CLRNC COUNT FROM FETCH = '     LE-CNT  
                                            PA-IND-FETCH-PRCG-CLRNC-CNT T       
                   DISPLAY ' IND PRICING CLRNC COUNT FROM WRITE = '     LE-CNT  
                                            PA-IND-WRITE-PRCG-CLRNC-CNT T       
                   DISPLAY ' END OF PRICECLRNCCSR'                              
                   SET PS-END-OF-PRCG-CLRNC-CSR TO TRUE                         
               WHEN OTHER                                                       
                   DISPLAY ' PROMO-INTFC-ID = '  PV-PROMO-INTFC-ID              
                   MOVE 'C420-FETCH-PRCG-CLRNC-CSR        '                     
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON PRICING-CLRNC-CSR   '                   
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C430-FETCH-PRCG-ADMTIER-CSR                                   *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      *  - IF THE LST UPDATE DATE IS LESS THEN THE CURRENT LAST UPDATE*         
      *    DATE THEN                                                  *         
      *    - THEN MOVE TO THE COPYBOOK                                *         
      *    - WRITE TO THE FILE                                        *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1405 = FETCHED COLUMN VALUE IS NULL REASON FOR NULL VALUES *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM:                                           *         
      *          A200-PROCESS-CSRS, C530-PROCESS-PRCG-ADMTIER-CSR     *         
      *****************************************************************         
       C430-FETCH-PRCG-ADMTIER-CSR.                                             
                                                                                
           EXEC SQL                                                             
               FETCH PRICEADMINTIEREDCSR                                        
                INTO  :PV-PROMO-INTFC-ID                                        
                    , :PV-PROMO-INTFC-SUB-ID     :PV-INTFC-SUB-ID-NULL          
                    , :PV-PRICG-TYP-CDE                                         
                    , :PV-PROMO-ID                                              
                    , :PV-MDSE-SELN-LNE-ID                                      
                    , :PV-MDSE-HIER-CDE                                         
                    , :PV-DEPT-NBR               :PV-DEPT-NBR-NULL              
                    , :PV-MAJ-CL-NBR             :PV-MAJ-CL-NBR-NULL            
                    , :PV-SUB-CL-NBR             :PV-SUB-CL-NBR-NULL            
                    , :PV-STYLE-ID               :PV-STYLE-ID-NULL              
                    , :PV-SKU-NBR                :PV-SKU-NBR-NULL               
                    , :PV-THRS-QTY               :PV-THRS-QTY-NULL              
                    , :PV-THRS-AMT               :PV-THRS-AMT-NULL              
                    , :PV-DISC-AMT               :PV-DISC-AMT-NULL              
                    , :PV-DISC-PCT               :PV-DISC-PCT-NULL              
                    , :PV-RCPT-CMMT-TXT          :PV-RCPT-CMMT-TXT-NULL         
                    , :PV-ESL-TOPR-TYP           :PV-ESL-TOPR-TYP-NULL          
                    , :PV-STAT-CDE                                              
                    , :PV-LAST-UPD-TMST                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-FETCH-PRCG-ADMTIER-CNT                           
                   ADD 1 TO PA-IND-FETCH-PRCG-ADMTIER-CNT                       
                   IF PV-LAST-UPD-TMST <=  CC-LAST-UPD-TMST                     
                       ADD 1 TO PA-WRITE-PRCG-ADMTIER-CNT                       
                       ADD 1 TO PA-IND-WRITE-PRCG-ADMTIER-CNT                   
                       SET PS-ADMINTIER-TYP TO TRUE                             
                       PERFORM D100-MOVE-TO-CPYBK                               
                       PERFORM W100-WRITE-DETAIL                                
                   END-IF                                                       
               WHEN SQLCODE = 1403                                              
                   DISPLAY ' RECORDS WRITTEN TO FILE SO FAR = '                 
                                                PA-RECS-WRITTEN                 
                   DISPLAY ' PRICING ADMINTIERED COUNT FROM FETCH = '   LE-CNT  
                                           PA-FETCH-PRCG-ADMTIER-CNT            
                   DISPLAY ' PRICING ADMINTIERED COUNT FROM WRITE = '   LE-CNT  
                                           PA-WRITE-PRCG-ADMTIER-CNT            
                   DISPLAY ' IND PRICING ADMINTIERED CNT FROM FETCH = ' LE-CNT  
                                           PA-IND-FETCH-PRCG-ADMTIER-CNT        
                   DISPLAY ' IND PRICING ADMINTIERED CNT FROM WRITE = ' LE-CNT  
                                           PA-IND-WRITE-PRCG-ADMTIER-CNT        
                   DISPLAY ' END OF PRICEADMINTIEREDCSR'                        
                   SET PS-END-OF-PRCG-ADMTIER-CSR TO TRUE                       
               WHEN OTHER                                                       
                   DISPLAY ' PROMO-INTFC-ID = '  PV-PROMO-INTFC-ID              
                   MOVE 'C430-FETCH-PRCG-ADMTIER-CSR  '                         
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON PRICING-ADMTIER-CSR   '                 
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C440-FETCH-PRCG-PROMTIER-CSR                                  *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      *  - IF THE LST UPDATE DATE IS LESS THEN THE CURRENT LAST UPDATE*         
      *    DATE THEN                                                  *         
      *    - THEN MOVE TO THE COPYBOOK                                *         
      *    - WRITE TO THE FILE                                        *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1405 = FETCHED COLUMN VALUE IS NULL REASON FOR NULL VALUES *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM:                                           *         
      *          A200-PROCESS-CSRS, C540-PROCESS-PRCG-PROMTIER-CSR    *         
      *****************************************************************         
       C440-FETCH-PRCG-PROMTIER-CSR.                                            
                                                                                
           EXEC SQL                                                             
               FETCH PRCGPROMTIERCSR                                            
                INTO  :PV-PROMO-INTFC-ID                                        
                    , :PV-PROMO-INTFC-SUB-ID      :PV-INTFC-SUB-ID-NULL         
                    , :PV-PRICG-TYP-CDE                                         
                    , :PV-PROMO-ID                                              
                    , :PV-PROMO-ITM-ID                                          
                    , :PV-MDSE-SELN-LNE-ID                                      
                    , :PV-MDSE-HIER-CDE                                         
                    , :PV-DEPT-NBR                :PV-DEPT-NBR-NULL             
                    , :PV-MAJ-CL-NBR              :PV-MAJ-CL-NBR-NULL           
                    , :PV-SUB-CL-NBR              :PV-SUB-CL-NBR-NULL           
                    , :PV-STYLE-ID                :PV-STYLE-ID-NULL             
                    , :PV-SKU-NBR                 :PV-SKU-NBR-NULL              
                    , :PV-ADV-IND                 :PV-ADV-IND-NULL              
                    , :PV-THRS-QTY                :PV-THRS-QTY-NULL             
                    , :PV-THRS-AMT                :PV-THRS-AMT-NULL             
                    , :PV-DISC-AMT                :PV-DISC-AMT-NULL             
                    , :PV-DISC-PCT                :PV-DISC-PCT-NULL             
                    , :PV-RCPT-CMMT-TXT           :PV-RCPT-CMMT-TXT-NULL        
                    , :PV-ESL-TOPR-TYP            :PV-ESL-TOPR-TYP-NULL         
                    , :PV-STAT-CDE                                              
                    , :PV-LAST-UPD-TMST                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-FETCH-PRCG-PROMTIER-CNT                          
                   ADD 1 TO PA-IND-FETCH-PRCG-PROMTIER-CNT                      
                   IF PV-LAST-UPD-TMST <=  CC-LAST-UPD-TMST                     
                       ADD 1 TO PA-WRITE-PRCG-PROMTIER-CNT                      
                       ADD 1 TO PA-IND-WRITE-PRCG-PROMTIER-CNT                  
                       SET PS-PROMOTIER-TYP TO TRUE                             
                       PERFORM D100-MOVE-TO-CPYBK                               
                       PERFORM W100-WRITE-DETAIL                                
                   END-IF                                                       
               WHEN SQLCODE = 1403                                              
                   DISPLAY ' RECORDS WRITTEN TO FILE SO FAR = '                 
                                                PA-RECS-WRITTEN                 
                   DISPLAY ' PRICING PROMOTIERED COUNT FROM FETCH = '   LE-CNT  
                                           PA-FETCH-PRCG-PROMTIER-CNT           
                   DISPLAY ' PRICING PROMOTIERED COUNT FROM WRITE = '   LE-CNT  
                                           PA-WRITE-PRCG-PROMTIER-CNT           
                   DISPLAY ' IND PRICING PROMOTIERED CNT FROM FETCH = ' LE-CNT  
                                         PA-IND-FETCH-PRCG-PROMTIER-CNT         
                   DISPLAY ' IND PRICING PROMOTIERED CNT FROM WRITE = ' LE-CNT  
                                         PA-IND-WRITE-PRCG-PROMTIER-CNT         
                   DISPLAY ' END OF PRCGPROMTIERCSR'                            
                   SET PS-END-OF-PRCG-PROMTIER-CSR TO TRUE                      
               WHEN OTHER                                                       
                   DISPLAY ' PROMO-INTFC-ID = '  PV-PROMO-INTFC-ID              
                   MOVE 'C440-FETCH-PRCG-PROMTIER-CSR  '                        
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON PRICING-PROMTIER-CSR   '                
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C450-FETCH-PRCG-CMPTV-CSR                                     *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      *  - IF THE LST UPDATE DATE IS LESS THEN THE CURRENT LAST UPDATE*         
      *    DATE THEN                                                  *         
      *    - THEN MOVE TO THE COPYBOOK                                *         
      *    - WRITE TO THE FILE                                        *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1405 = FETCHED COLUMN VALUE IS NULL REASON FOR NULL VALUES *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM:                                           *         
      *          A200-PROCESS-CSRS, C550-PROCESS-PRCG-CMPTV-CSR       *         
      *****************************************************************         
       C450-FETCH-PRCG-CMPTV-CSR.                                               
                                                                                
           EXEC SQL                                                             
               FETCH PRICINGCMPTVCSR                                            
                INTO  :PV-PROMO-INTFC-ID                                        
                    , :PV-PRICG-TYP-CDE                                         
                    , :PV-PROMO-ID                                              
                    , :PV-PROMO-ITM-ID                                          
                    , :PV-MDSE-SELN-LNE-ID                                      
                    , :PV-MDSE-HIER-CDE                                         
                    , :PV-DEPT-NBR             :PV-DEPT-NBR-NULL                
                    , :PV-MAJ-CL-NBR           :PV-MAJ-CL-NBR-NULL              
                    , :PV-SUB-CL-NBR           :PV-SUB-CL-NBR-NULL              
                    , :PV-STYLE-ID             :PV-STYLE-ID-NULL                
                    , :PV-SKU-NBR              :PV-SKU-NBR-NULL                 
                    , :PV-ADV-IND              :PV-ADV-IND-NULL                 
                    , :PV-PRICG-AMT            :PV-PRICG-AMT-NULL               
                    , :PV-ESL-TOPR-TYP         :PV-ESL-TOPR-TYP-NULL            
                    , :PV-STAT-CDE                                              
                    , :PV-LAST-UPD-TMST                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-FETCH-PRCG-CMPTV-CNT                             
                   ADD 1 TO PA-IND-FETCH-PRCG-CMPTV-CNT                         
                   IF PV-LAST-UPD-TMST <=  CC-LAST-UPD-TMST                     
                       ADD 1 TO PA-WRITE-PRCG-CMPTV-CNT                         
                       ADD 1 TO PA-IND-WRITE-PRCG-CMPTV-CNT                     
                       SET PS-CMPTV-TYP TO TRUE                                 
                       PERFORM D100-MOVE-TO-CPYBK                               
                       PERFORM W100-WRITE-DETAIL                                
                   END-IF                                                       
               WHEN SQLCODE = 1403                                              
                   DISPLAY ' RECORDS WRITTEN TO FILE SO FAR = '                 
                                                PA-RECS-WRITTEN                 
                   DISPLAY ' PRICING CMPTV COUNT FROM FETCH = '         LE-CNT  
                                           PA-FETCH-PRCG-CMPTV-CNT              
                   DISPLAY ' PRICING CMPTV COUNT FROM WRITE = '         LE-CNT  
                                           PA-WRITE-PRCG-CMPTV-CNT              
                   DISPLAY ' IND PRICING CMPTV COUNT FROM FETCH = '     LE-CNT  
                                           PA-IND-FETCH-PRCG-CMPTV-CNT          
                   DISPLAY ' IND PRICING CMPTV COUNT FROM WRITE = '     LE-CNT  
                                           PA-IND-WRITE-PRCG-CMPTV-CNT          
                   DISPLAY ' END OF PRICINGCMPTVCSR'                            
                   SET PS-END-OF-PRCG-CMPTV-CSR TO TRUE                         
               WHEN OTHER                                                       
                   DISPLAY ' PROMO-INTFC-ID = '  PV-PROMO-INTFC-ID              
                   MOVE 'C450-FETCH-PRCG-CMPTV-CSR        '                     
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON PRICING-CMPTV-CSR   '                   
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C460-FETCH-ADEVNT-DTE-CSR                                     *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM:                                           *         
      *****************************************************************         
       C460-FETCH-ADEVNT-DTE-CSR.                                               
                                                                                
           EXEC SQL                                                             
               FETCH ADEVNTDTECSR                                               
                INTO  :PV-AD-EVNT-ID                                            
                    , :PV-AD-EVNT-STRT-DTE                                      
                    , :PV-AD-EVNT-END-DTE                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
                                                                                
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'END OF ADEVNTDTECSR'                                
                   SET PS-END-OF-ADEVNT-DTE-CSR TO TRUE                         
                                                                                
               WHEN OTHER                                                       
                   MOVE 'C460-FETCH-ADEVNT-DTE-CSR   '                          
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON ADEVNTDTECSR '                          
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      ****************************************************************          
      * C520-PROCESS-PRCG-CLRNC-CSR.                                 *          
      * ==> PROCESSES:                                                *         
      *  - REINITALIZE THE VARIABLES TO START FROM SCRATCH            *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT                  *         
      * ==> PERFORMED FROM: A200-PROCESS-CSRS                         *         
      ****************************************************************          
       C520-PROCESS-PRCG-CLRNC-CSR.                                             
                                                                                
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C420-FETCH-PRCG-CLRNC-CSR.                                   
                                                                                
      ****************************************************************          
      * C530-PROCESS-PRCG-ADMTIER-CSR                                *          
      * ==> PROCESSES:                                                *         
      *  - REINITALIZE THE VARIABLES TO START FROM SCRATCH            *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT                  *         
      * ==> PERFORMED FROM: A200-PROCESS-CSRS                         *         
      ****************************************************************          
       C530-PROCESS-PRCG-ADMTIER-CSR.                                           
                                                                                
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C430-FETCH-PRCG-ADMTIER-CSR.                                 
                                                                                
      ****************************************************************          
      * C540-PROCESS-PRCG-PROMTIER-CSR                               *          
      * ==> PROCESSES:                                                *         
      *  - REINITALIZE THE VARIABLES TO START FROM SCRATCH            *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT                  *         
      * ==> PERFORMED FROM: A200-PROCESS-CSRS                         *         
      ****************************************************************          
       C540-PROCESS-PRCG-PROMTIER-CSR.                                          
                                                                                
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C440-FETCH-PRCG-PROMTIER-CSR.                                
                                                                                
      ****************************************************************          
      * C550-PROCESS-PRCG-CMPTV-CSR                                  *          
      * ==> PROCESSES:                                                *         
      *  - REINITALIZE THE VARIABLES TO START FROM SCRATCH            *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT                  *         
      * ==> PERFORMED FROM: A200-PROCESS-CSRS                         *         
      ****************************************************************          
       C550-PROCESS-PRCG-CMPTV-CSR.                                             
                                                                                
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C450-FETCH-PRCG-CMPTV-CSR.                                   
                                                                                
      ****************************************************************          
      * C560-PROCESS-ADEVNT-DTE-CSR                                  *          
      * ==> PROCESSES:                                                *         
      *  - DETERMINE THE DATES TO BE USED IN THE CURSORS              *         
      *  - REINITALIZE THE VARIABLES TO START FROM SCRATCH            *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT                  *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      ****************************************************************          
       C560-PROCESS-ADEVNT-DTE-CSR.                                             
                                                                                
           IF PV-AD-EVNT-STRT-DTE < CC-START-DATE                               
               STRING CC-START-DATE DELIMITED BY SPACE                          
                      PC-SPACE DELIMITED BY SIZE                                
                      PC-BEG-TMST   DELIMITED BY SIZE                           
                      INTO PV-NEW-STRT-DATE                                     
           ELSE                                                                 
               STRING PV-AD-EVNT-STRT-DTE DELIMITED BY SPACE                    
                      PC-SPACE DELIMITED BY SIZE                                
                      PC-BEG-TMST   DELIMITED BY SIZE                           
                      INTO PV-NEW-STRT-DATE                                     
           END-IF.                                                              
                                                                                
           IF  PV-AD-EVNT-END-DTE > CC-END-DATE                                 
               STRING CC-END-DATE DELIMITED BY SPACE                            
                      PC-SPACE DELIMITED BY SIZE                                
                      PC-END-TMST   DELIMITED BY SIZE                           
                      INTO PV-NEW-END-DATE                                      
           ELSE                                                                 
               STRING PV-AD-EVNT-END-DTE DELIMITED BY SPACE                     
                      PC-SPACE DELIMITED BY SIZE                                
                      PC-END-TMST   DELIMITED BY SIZE                           
                      INTO PV-NEW-END-DATE                                      
           END-IF.                                                              
                                                                                
           DISPLAY 'START DATE = ' PV-NEW-STRT-DATE.                            
           DISPLAY 'END DATE   = ' PV-NEW-END-DATE.                             
           PERFORM A200-PROCESS-CSRS.                                           
                                                                                
           INITIALIZE PV-ADEVNT-VARIABLES.                                      
           PERFORM C460-FETCH-ADEVNT-DTE-CSR.                                   
                                                                                
      ****************************************************************          
      * C620-CLOSE-PRCG-CLRNC-CSR.                                   *          
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE PRICECLRNCCSR                                    *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = CLOSE WAS SUCCESSFULL                               *         
      * ==> PERFORMED FROM: A200-PROCESS-CSRS                         *         
      ****************************************************************          
       C620-CLOSE-PRCG-CLRNC-CSR.                                               
                                                                                
           EXEC SQL                                                             
                CLOSE PRICECLRNCCSR                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   SET PS-NOT-END-OF-PRCG-CLRNC-CSR TO TRUE                     
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C610-CLOSE-PRCG-CLRNC-CSR      '                        
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON PRICECLRNCCSR     '                      
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      ****************************************************************          
      * C630-CLOSE-PRCG-ADMTIER-CSR                                  *          
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE PRICEADMINTIEREDCSR                              *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = CLOSE WAS SUCCESSFULL                               *         
      * ==> PERFORMED FROM: A200-PROCESS-CSRS                         *         
      ****************************************************************          
       C630-CLOSE-PRCG-ADMTIER-CSR.                                             
                                                                                
           EXEC SQL                                                             
                CLOSE PRICEADMINTIEREDCSR                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   SET PS-NOT-END-PRCG-ADMTIER-CSR TO TRUE                      
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C630-CLOSE-PRCG-ADMTIER-CSR'                            
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON PRICEADMINTIEREDCSR'                     
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      ****************************************************************          
      * C640-CLOSE-PRCG-PROMTIER-CSR                                 *          
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE PRCGPROMTIERCSR                                  *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = CLOSE WAS SUCCESSFULL                               *         
      * ==> PERFORMED FROM: A200-PROCESS-CSRS                         *         
      ****************************************************************          
       C640-CLOSE-PRCG-PROMTIER-CSR.                                            
                                                                                
           EXEC SQL                                                             
                CLOSE PRCGPROMTIERCSR                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   SET PS-NOT-END-PRCG-PROMTIER-CSR TO TRUE                     
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C640-CLOSE-PRCG-PROMTIER-CSR'                           
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON PRCGPROMTIERCSR'                         
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      ****************************************************************          
      * C650-CLOSE-PRCG-CMPTV-CSR.                                   *          
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE PRICINGCMPTVCSR                                  *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = CLOSE WAS SUCCESSFULL                               *         
      * ==> PERFORMED FROM: A200-PROCESS-CSRS                         *         
      ****************************************************************          
       C650-CLOSE-PRCG-CMPTV-CSR.                                               
                                                                                
           EXEC SQL                                                             
                CLOSE PRICINGCMPTVCSR                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   SET PS-NOT-END-OF-PRCG-CMPTV-CSR TO TRUE                     
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C650-CLOSE-PRCG-CMPTV-CSR      '                        
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON PRICINGCMPTVCSR     '                    
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      ****************************************************************          
      * C660-CLOSE-ADEVNT-DTE-CSR.                                   *          
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE ADEVNTDTECSR                                     *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = CLOSE WAS SUCCESSFULL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      ****************************************************************          
       C660-CLOSE-ADEVNT-DTE-CSR.                                               
                                                                                
           EXEC SQL                                                             
                CLOSE ADEVNTDTECSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   SET PS-NOT-END-OF-ADEVNT-DTE-CSR TO TRUE                     
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C660-CLOSE-ADEVNT-DTE-CSR   '                           
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON ADEVNTDTECSR '                           
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * D100-MOVE-TO-CPYBK                                            *         
      * ==> PROCESSES:                                                *         
      *  - MOVE THE VARIABLES TO THE COPYBOOK                         *         
      * ==> PERFORMED FROM: C420-FETCH-PRCG-CLRNC-CSR,                *         
      *      C430-FETCH-PRCG-ADMTIER-CSR, C440-FETCH-PRCG-PROMTIER-CSR*         
      *      C450-FETCH-PRCG-CMPTV-CSR                                *         
      *****************************************************************         
       D100-MOVE-TO-CPYBK.                                                      
                                                                                
           INITIALIZE PM706-PRICING-RECORD.                                     
           MOVE PC-BUSINESS-EVNT-ID    TO PM706-BUSINESS-EVNT-ID.               
           MOVE PC-ACTION-CDE          TO PM706-ACTION-CDE.                     
      *    MOVE PV-SYS-DATE-XS         TO PM706-BUSINESS-EVNT-CRE-TMST.         
           MOVE '0000-00-00-00.00.00.000000'                                    
                                       TO PM706-BUSINESS-EVNT-CRE-TMST.         
           MOVE PV-PROMO-INTFC-ID      TO PM706-PROMO-INTFC-ID.                 
           MOVE PV-PRICG-TYP-CDE       TO PM706-PRCG-TYP-CDE.                   
           MOVE PV-PROMO-ID            TO PM706-PROMO-ID.                       
           MOVE PV-MDSE-SELN-LNE-ID    TO PM706-MDSE-LNE-ID.                    
           MOVE PV-MDSE-HIER-CDE       TO PM706-MDSE-HIER-CDE.                  
           MOVE PV-DEPT-NBR            TO PM706-DEPT-NBR.                       
           MOVE PV-MAJ-CL-NBR          TO PM706-MAJ-CL-NBR.                     
           MOVE PV-SUB-CL-NBR          TO PM706-SUB-CL-NBR.                     
           MOVE PV-SUB-CL-NBR          TO PM706-SUB-CL-NBR.                     
           MOVE PV-STYLE-ID            TO PM706-STYLE-ID.                       
           MOVE PV-SKU-NBR             TO PM706-SKU-NBR.                        
           MOVE PV-STAT-CDE          TO PM706-STAT-CDE.                         
           STRING PV-LAST-UPD-TMST DELIMITED BY SPACE                           
                  '.000000'     DELIMITED BY SIZE                               
                  INTO PM706-LAST-UPD-TMST.                                     
                                                                                
           EVALUATE TRUE                                                        
               WHEN PS-CLRNC-TYP                                                
                   MOVE PV-CLRNC-TYP-CDE        TO PM706-CLRNC-TYP-CDE          
                   MOVE PV-PRICG-AMT            TO PM706-PRICG-AMT              
               WHEN PS-ADMINTIER-TYP                                            
                   MOVE PV-PROMO-INTFC-SUB-ID                                   
                                            TO PM706-PROMO-INTFC-SUB-ID         
                   MOVE PV-THRS-QTY             TO PM706-THRS-QTY               
                   MOVE PV-THRS-AMT             TO PM706-THRS-AMT               
                   MOVE PV-DISC-AMT             TO PM706-DISC-AMT               
                   MOVE PV-DISC-PCT             TO PM706-DISC-PCT               
                   MOVE PV-RCPT-CMMT-TXT        TO PM706-RCPT-CMMT-TXT          
                   MOVE PV-ESL-TOPR-TYP         TO PM706-ESL-TOPR-TYP           
               WHEN PS-PROMOTIER-TYP                                            
                   MOVE PV-PROMO-INTFC-SUB-ID                                   
                                            TO PM706-PROMO-INTFC-SUB-ID         
                   MOVE PV-PROMO-ITM-ID         TO PM706-PROMO-ITM-ID           
                   MOVE PV-ADV-IND              TO PM706-ADV-IND                
                   MOVE PV-THRS-QTY             TO PM706-THRS-QTY               
                   MOVE PV-THRS-AMT             TO PM706-THRS-AMT               
                   MOVE PV-DISC-AMT             TO PM706-DISC-AMT               
                   MOVE PV-DISC-PCT             TO PM706-DISC-PCT               
                   MOVE PV-RCPT-CMMT-TXT        TO PM706-RCPT-CMMT-TXT          
                   MOVE PV-ESL-TOPR-TYP         TO PM706-ESL-TOPR-TYP           
               WHEN PS-CMPTV-TYP                                                
                   MOVE PV-PROMO-ITM-ID         TO PM706-PROMO-ITM-ID           
                   MOVE PV-ADV-IND              TO PM706-ADV-IND                
                   MOVE PV-PRICG-AMT            TO PM706-PRICG-AMT              
                   MOVE PV-ESL-TOPR-TYP         TO PM706-ESL-TOPR-TYP           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * W100-WRITE-DETAIL.                                            *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DETAIL LINE TO THE PRICING OUTPUT FILE           *         
      * ==> PERFORMED FROM:  C420-FETCH-PRCG-CLRNC-CSR,               *         
      *      C430-FETCH-PRCG-ADMTIER-CSR, C440-FETCH-PRCG-PROMTIER-CSR*         
      *      C450-FETCH-PRCG-CMPTV-CSR                                *         
      *****************************************************************         
       W100-WRITE-DETAIL.                                                       
                                                                                
           WRITE PRICING-LINE FROM PM706-PRICING-RECORD.                        
                                                                                
           ADD +1                    TO PA-RECS-WRITTEN.                        
                                                                                
      *****************************************************************         
      * W200-WRITE-TOTAL-LINE                                         *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE TOTAL LINE TO THE PRICING OUTPUT FILE            *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       W200-WRITE-TOTAL-LINE.                                                   
                                                                                
           MOVE PC-BUSINESS-EVNT-ID TO PM700-BUSINESS-EVNT-ID.                  
           MOVE PA-RECS-WRITTEN TO PM700-TOTAL-NBR-OF-RECS.                     
           WRITE PRICING-LINE FROM PM700-TOTAL-RECORD.                          
                                                                                
070941****************************************************************          
070941** READ THE ORACLE LOGON CONTROL CARD.                        **          
070941****************************************************************          
070941 7000-READ-ORACLE-LOGON.                                                  
070941                                                                          
070941     READ ORACLE-LOGON-FILE                                               
070941         INTO CC-ORACLE-CONNECTION                                        
070941         AT END                                                           
070941             SET PS-END-OF-LOGON-FILE TO TRUE                             
070941     END-READ.                                                            
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT     *        
      * LOGONS TO ORACLE.                                              *        
      ******************************************************************        
070941*    COPY PMPD700.                                                        
                                                                                
      *----------------------------------------------------------               
      * 9900-ORACLE-ABEND.                                                      
      *     PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE.                    
      *----------------------------------------------------------               
       9900-ORACLE-ABEND.                                                       
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
070941*    DISPLAY '** PROGRAM        ** = ' PC-CURRENT-PROGRAM-NAME.           
070941     DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' PV-SQL.                            
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *        
      ******************************************************************        
       Z910-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
070941*    DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
