       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              MLKUT909.                                       
       AUTHOR.                  JILL LIN.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            10/19/2007.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  PARTITION NUMBER CONTROL CARD CREATION FOR THE PROCESS OF     *        
      *  EXTRACT FILES OF ML'S WEEKLY SUMMARY TABLE FOR OTHER DOMAINS *         
      *                                                                *        
      *  THIS PROGRAM WILL EXTRACT PARTITION NUMBER FROM PARTITION     *        
      *  CONTROL TABLE FOR CREATING A CONTROL CARD FOR LATER BMC       *        
      *  UNLOAD PROCESS.                                               *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      * 3033D    07/21/2011   MDSE EDW WRITE OUT ACTUAL DATES USED FOR          
      * WR39512                FIELDS ML901-BEG-FSCL-WK-END-DTE AND             
      *                        ML901-END-FSCL-WK-END-DTE MARK FRAMNESS          
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT OPTIONAL REQUEST-CNTL-FILE                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT MLT-WKLY-SUM-CC                                               
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT MLT-DATES-USED-FILE-SUM                                       
               ASSIGN TO OUT02.                                                 
                                                                                
           SELECT MLT-DATES-USED-FILE-VND                                       
               ASSIGN TO OUT03.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  REQUEST-CNTL-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  REQUEST-CNTL-REC                PIC X(80).                           
                                                                                
       FD  MLT-WKLY-SUM-CC                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  MLT-WKLY-SUM-CC-REC             PIC X(80).                           
                                                                                
       FD  MLT-DATES-USED-FILE-SUM                                              
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  MLT-WKLY-DATE-OUT-REC-SUM       PIC X(34).                           
                                                                                
       FD  MLT-DATES-USED-FILE-VND                                              
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  MLT-WKLY-DATE-OUT-REC-VND       PIC X(34).                           
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
      ******************************************************************        
      *  COMMON EXTRACT DATA LAYOUT                                             
      ******************************************************************        
      *01  ML902-WKLY-SUM-COMMON-EXTRACT.                                       
           COPY MLRD902.                                                        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING CONSTANT BEGIN ***'.                                
                                                                                
       01  PC-PROGRAM-CONSTANTS-AREA.                                           
           05  PC-WEEKLY-TM-LVL-CDE        PIC X           VALUE 'W'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING SWITCHES BEGIN ***'.                                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
           05  PS-END-OF-REQUEST-CNTL      PIC X           VALUE 'N'.           
               88  END-OF-REQUEST-CNTL                     VALUE 'Y'.           
           05  PS-END-OF-WKLY-SUM          PIC X           VALUE 'N'.           
               88  NOT-END-OF-WKLY-SUM                     VALUE 'N'.           
               88  END-OF-WKLY-SUM                         VALUE 'Y'.           
           05  PS-END-OF-PARTN-CNTL        PIC X           VALUE 'N'.           
               88  NOT-END-OF-PARTN-CNTL                   VALUE 'N'.           
               88  END-OF-PARTN-CNTL                       VALUE 'Y'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** REPORT REQUEST CONTROL REC ***'.                                
                                                                                
      ******************************************************************        
      *  COMMON EXTRACT DATE CONTROL                                            
      ******************************************************************        
      *01  ML901-WKLY-SUM-EXTRACT-DATES.                                        
           COPY MLRD901.                                                        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING VARIABLE BEGIN ***'.                                
                                                                                
       01  OUT-DATE-RECORDS.                                                    
           05 OUT-SUM-BEG-DATE-REC.                                             
              10 FILLER                    PIC X(24)       VALUE                
                 "$$MLT_WKLY_SUM_BEG_DATE=".                                    
              10 OUT-SUM-BEG-DATE-VALUE    PIC X(10).                           
           05 OUT-SUM-END-DATE-REC.                                             
              10 FILLER                    PIC X(24)       VALUE                
                 "$$MLT_WKLY_SUM_END_DATE=".                                    
              10 OUT-SUM-END-DATE-VALUE    PIC X(10).                           
           05 OUT-VND-BEG-DATE-REC.                                             
              10 FILLER                    PIC X(24)       VALUE                
                 "$$MLT_WKLY_VND_BEG_DATE=".                                    
              10 OUT-VND-BEG-DATE-VALUE    PIC X(10).                           
           05 OUT-VND-END-DATE-REC.                                             
              10 FILLER                    PIC X(24)       VALUE                
                 "$$MLT_WKLY_VND_END_DATE=".                                    
              10 OUT-VND-END-DATE-VALUE    PIC X(10).                           
      **********************************************************                
       01  PT-PROGRAM-TABLE.                                                    
           05  PT-NUM-PARTN-TABLE      OCCURS 100 TIMES.                        
               15  PT-NUM-PARTN            PIC 9(04).                           
       01  PT-PARTN-INDEX                  PIC 9(03)       VALUE ZEROES.        
       01  PW-PROGRAM-WORK-REC.                                                 
           05  FILLER                      PIC X(14)       VALUE SPACES.        
           05  PW-BEG-PARTN-CNTL           PIC X(01)       VALUE SPACES.        
           05  PW-NUM-PARTN-CNTL           PIC 9(04)       VALUE ZEROES.        
           05  PW-END-PARTN-CNTL           PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(60)       VALUE SPACES.        
       01  PV-PROGRAM-VARIABLES-AREA.                                           
           05  PV-DEPT-NBR-9               PIC 9(03)       VALUE ZEROES.        
           05  PV-DEPT-NBR REDEFINES PV-DEPT-NBR-9                              
                                           PIC X(03).                           
           05  PV-MAJ-CL-NBR-9             PIC 9(02)       VALUE ZEROES.        
           05  PV-MAJ-CL-NBR REDEFINES PV-MAJ-CL-NBR-9                          
                                           PIC X(02).                           
           05  PV-RECORD-EXTRACTED         PIC S9(11)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-OPER-ATTEMPTED           PIC X(25)       VALUE SPACES.        
           05  PV-PROCESSING-DATE          PIC X(10)       VALUE SPACES.        
           05  PV-PROCESSING-DATE-RDF REDEFINES PV-PROCESSING-DATE.             
               10  PV-PROCESSING-CC        PIC X(02).                           
               10  PV-PROCESSING-YY        PIC X(02).                           
               10  PV-PROC-DSH1            PIC X(01).                           
               10  PV-PROCESSING-MM        PIC X(02).                           
               10  PV-PROC-DSH2            PIC X(01).                           
               10  PV-PROCESSING-DD        PIC X(02).                           
           05  PV-TODAYS-DATE.                                                  
               10  PV-TODAYS-DATE-YY       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-MM       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-DD       PIC 9(02)       VALUE ZEROES.        
           05  PV-TODAYS-TIME.                                                  
               10  PV-TODAYS-TIME-HR       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-MIN      PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-SEC      PIC 9(02)       VALUE ZEROES.        
               10  FILLER                  PIC 9(02)       VALUE ZEROES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** SQL WORK AREA BEGIN ***'.                                       
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
      *01  DP004-DB2-ERROR-FIELDS.                                              
           COPY DPWS004.                                                        
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  ABEND-4003                                  VALUE +4003.         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TMLCNTL - ML'S DATE CONTROL TABLE                          *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TDTATTR - ML'S DATE ATTRIBUTE TABLE                        *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    MLT_PARTN_BAL  - ML'S PARTITION BALANCE TABLE              *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE MLT00002                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    MLT_PARTN_CNTL - ML'S PARTITION CONTROL TABLE              *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE MLT00003                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    PARTITION CONTROL CURSOR BETWEEN REQUEST DATES             *         
      *---------------------------------------------------------------*         
           EXEC SQL   DECLARE PARTN-CNTL-CSR CURSOR FOR                         
                SELECT DISTINCT                                                 
                       PARTN_NBR                                                
                      ,FSCL_DTE                                                 
                FROM   MLT_PARTN_CNTL                                           
                WHERE  TBL_TM_LVL_CDE = :PC-WEEKLY-TM-LVL-CDE                   
                  AND  FSCL_DTE                                                 
                         BETWEEN :ML901-BEG-FSCL-WK-END-DTE                     
                             AND :ML901-END-FSCL-WK-END-DTE                     
                WITH UR                                                         
           END-EXEC.                                                            
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE END ***'.                                       
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM Z250-FETCH-PARTN-CNTL.                                       
           MOVE ZEROES TO PT-PARTN-INDEX.                                       
           PERFORM B200-PROCESS-PARTN-CNTL                                      
               UNTIL END-OF-PARTN-CNTL.                                         
                                                                                
           PERFORM B300-PROCESS-PARTN-TABLE                                     
               UNTIL PV-RECORD-EXTRACTED = PT-PARTN-INDEX                       
                  OR PT-PARTN-INDEX > +100.                                     
                                                                                
           PERFORM B400-FINALIZE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET              
      * THE REPORT NAME AND NUMBER.                                             
      ******************************************************************        
       B100-HOUSEKEEPING.                                                       
                                                                                
           ACCEPT PV-TODAYS-DATE FROM DATE.                                     
           ACCEPT PV-TODAYS-TIME FROM TIME.                                     
                                                                                
           MOVE PV-TODAYS-DATE-YY TO PV-PROCESSING-YY.                          
           MOVE PV-TODAYS-DATE-MM TO PV-PROCESSING-MM.                          
           MOVE PV-TODAYS-DATE-DD TO PV-PROCESSING-DD.                          
                                                                                
           MOVE '-' TO PV-PROC-DSH1.                                            
           MOVE '-' TO PV-PROC-DSH2.                                            
                                                                                
           IF PV-TODAYS-DATE-YY > '80'                                          
               MOVE '19' TO PV-PROCESSING-CC                                    
           ELSE                                                                 
               MOVE '20' TO PV-PROCESSING-CC                                    
           END-IF.                                                              
                                                                                
           OPEN INPUT  REQUEST-CNTL-FILE                                        
                OUTPUT MLT-WKLY-SUM-CC.                                         
                                                                                
           PERFORM C100-PROCESS-RQ-CNTL.                                        
                                                                                
           PERFORM Z200-OPEN-PARTN-CNTL.                                        
                                                                                
           MOVE ZEROES TO PV-RECORD-EXTRACTED.                                  
                                                                                
      ******************************************************************        
      *    PROCESS PARTITION CONTROL CURSOR                                     
      ******************************************************************        
       B200-PROCESS-PARTN-CNTL.                                                 
                                                                                
           ADD +1 TO PT-PARTN-INDEX.                                            
           MOVE MLT00003-PARTN-NBR TO PT-NUM-PARTN (PT-PARTN-INDEX).            
           PERFORM Z250-FETCH-PARTN-CNTL.                                       
                                                                                
      ******************************************************************        
      *    PROCESS PARTITION CONTROL TABLE                                      
      ******************************************************************        
       B300-PROCESS-PARTN-TABLE.                                                
                                                                                
           ADD +1 TO PV-RECORD-EXTRACTED.                                       
           MOVE PT-NUM-PARTN (PV-RECORD-EXTRACTED)                              
               TO PW-NUM-PARTN-CNTL.                                            
           IF PV-RECORD-EXTRACTED = +1                                          
               MOVE '(' TO PW-BEG-PARTN-CNTL                                    
               IF PT-PARTN-INDEX = +1                                           
                   MOVE ')' TO PW-END-PARTN-CNTL                                
               ELSE                                                             
                   MOVE ',' TO PW-END-PARTN-CNTL                                
               END-IF                                                           
           ELSE                                                                 
               MOVE SPACES TO PW-BEG-PARTN-CNTL                                 
               IF PT-PARTN-INDEX = PV-RECORD-EXTRACTED                          
                   MOVE ')' TO PW-END-PARTN-CNTL                                
               ELSE                                                             
                   MOVE ',' TO PW-END-PARTN-CNTL                                
               END-IF                                                           
           END-IF.                                                              
           PERFORM Z400-WRITE-EXTRACT.                                          
                                                                                
      ******************************************************************        
      *    FINALIZE THE REPORT AND CLOSE OUT FILES                              
      ******************************************************************        
       B400-FINALIZE.                                                           
                                                                                
           DISPLAY ' **** TOTAL EXTRACTED: ' PV-RECORD-EXTRACTED.               
                                                                                
           PERFORM Z275-CLOSE-PARTN-CNTL.                                       
                                                                                
           IF PV-RECORD-EXTRACTED > ZEROES                                      
               MOVE PV-RECORD-EXTRACTED TO ML901-RECORDS-EXTRACTED              
           END-IF.                                                              
                                                                                
      *NOW -- OPEN UP DATE FILE, WRITE OUT DATES, AND CLOSE                     
           OPEN OUTPUT MLT-DATES-USED-FILE-SUM                                  
           OPEN OUTPUT MLT-DATES-USED-FILE-VND                                  
           PERFORM B410-OUTPUT-DATES                                            
           CLOSE MLT-DATES-USED-FILE-SUM                                        
           CLOSE MLT-DATES-USED-FILE-VND                                        
      *ALL DONE WRITING OUT DATES!                                              
                                                                                
           CLOSE REQUEST-CNTL-FILE                                              
                 MLT-WKLY-SUM-CC.                                               
                                                                                
      ******************************************************************        
      *    FORMAT AND WRITE OUT DATES FOR MDSE EDW INFORMATICA STAGING          
      ******************************************************************        
       B410-OUTPUT-DATES.                                                       
           MOVE ML901-BEG-FSCL-WK-END-DTE  TO OUT-SUM-BEG-DATE-VALUE            
           MOVE ML901-BEG-FSCL-WK-END-DTE  TO OUT-VND-BEG-DATE-VALUE            
                                                                                
           MOVE ML901-END-FSCL-WK-END-DTE  TO OUT-SUM-END-DATE-VALUE            
           MOVE ML901-END-FSCL-WK-END-DTE  TO OUT-VND-END-DATE-VALUE            
                                                                                
           WRITE MLT-WKLY-DATE-OUT-REC-SUM FROM OUT-SUM-BEG-DATE-REC            
           WRITE MLT-WKLY-DATE-OUT-REC-SUM FROM OUT-SUM-END-DATE-REC            
                                                                                
           WRITE MLT-WKLY-DATE-OUT-REC-VND FROM OUT-VND-BEG-DATE-REC            
           WRITE MLT-WKLY-DATE-OUT-REC-VND FROM OUT-VND-END-DATE-REC.           
                                                                                
      ******************************************************************        
      *    PROCESS REQUEST CONTROL CARD AND/OR GET REQUEST DATES                
      ******************************************************************        
       C100-PROCESS-RQ-CNTL.                                                    
                                                                                
           DISPLAY 'EXTRACT REQUESTED:'.                                        
           DISPLAY '  ----+----1----+----2----+----3----+----4'.                
           PERFORM Z500-READ-REQUEST-CNTL.                                      
                                                                                
           PERFORM                                                              
               UNTIL END-OF-REQUEST-CNTL                                        
                   OR NOT ML901-COMMENT-RECORD                                  
                                                                                
               DISPLAY '  ' ML901-WKLY-SUM-EXTRACT-DATES                        
               PERFORM Z500-READ-REQUEST-CNTL                                   
                                                                                
           END-PERFORM.                                                         
                                                                                
           DISPLAY ' '.                                                         
           IF NOT ML901-COMMENT-RECORD                                          
               AND NOT END-OF-REQUEST-CNTL                                      
               DISPLAY '  ' ML901-WKLY-SUM-EXTRACT-DATES                        
               IF ML901-BEG-FSCL-DTE-CCYY IS NUMERIC                            
                   AND ML901-BEG-FSCL-DTE-MM IS NUMERIC                         
                   AND ML901-BEG-FSCL-DTE-DD IS NUMERIC                         
                   AND ML901-BEG-DTE-VALID-DASH-1                               
                   AND ML901-BEG-DTE-VALID-DASH-2                               
                   IF ML901-END-FSCL-DTE-CCYY IS NUMERIC                        
                       AND ML901-END-FSCL-DTE-MM IS NUMERIC                     
                       AND ML901-END-FSCL-DTE-DD IS NUMERIC                     
                       AND ML901-END-DTE-VALID-DASH-1                           
                       AND ML901-END-DTE-VALID-DASH-2                           
                       IF ML901-BEG-FSCL-WK-END-DTE                             
                           > ML901-END-FSCL-WK-END-DTE                          
                           DISPLAY ' *** SWAP REQUEST BEG AND END DATE'         
                           MOVE ML901-BEG-FSCL-WK-END-DTE                       
                               TO DTATTR-KY-DTE                                 
                           MOVE ML901-END-FSCL-WK-END-DTE                       
                               TO ML901-BEG-FSCL-WK-END-DTE                     
                           MOVE DTATTR-KY-DTE                                   
                               TO ML901-END-FSCL-WK-END-DTE                     
                       END-IF                                                   
                   ELSE                                                         
                       DISPLAY ' *** USE REQUESTED BEG AS END DATE'             
                       MOVE ML901-BEG-FSCL-WK-END-DTE                           
                           TO ML901-END-FSCL-WK-END-DTE                         
                   END-IF                                                       
               ELSE                                                             
                   MOVE SPACES TO ML901-BEG-FSCL-WK-END-DTE                     
                                  ML901-END-FSCL-WK-END-DTE                     
               END-IF                                                           
           ELSE                                                                 
               MOVE SPACES TO ML901-BEG-FSCL-WK-END-DTE                         
                              ML901-END-FSCL-WK-END-DTE                         
           END-IF.                                                              
           IF ML901-BEG-FSCL-WK-END-DTE = SPACES                                
               PERFORM C150-GET-ML-CONTROL                                      
               DISPLAY ' *** DEFAULT TO ML CONTROL DATES: '                     
                       ML901-BEG-FSCL-WK-END-DTE ' - '                          
                       ML901-END-FSCL-WK-END-DTE                                
           END-IF.                                                              
                                                                                
      *  GET THE BEGINNING FISCAL WEEKEND DATE FOR REQUESTED BEGIN DATE         
           EXEC SQL                                                             
                SELECT  X.FSCL_WK_END_DTE                                       
                  INTO :DTATTR-FSCL-WK-END-DTE                                  
                  FROM  TDTATTR    X                                            
                  WHERE X.KY_DTE = :ML901-BEG-FSCL-WK-END-DTE                   
                  WITH UR                                                       
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               MOVE DTATTR-FSCL-WK-END-DTE TO ML901-BEG-FSCL-WK-END-DTE         
           ELSE                                                                 
               MOVE 'GET REQ. FSCL BEG DATE' TO PV-OPER-ATTEMPTED               
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *  GET THE ENDING FISCAL WEEKEND DATE FOR REQUESTED END DATE              
           EXEC SQL                                                             
                SELECT  Y.FSCL_WK_END_DTE                                       
                  INTO :DTATTR-FSCL-WK-END-DTE                                  
                  FROM  TDTATTR    Y                                            
                  WHERE Y.KY_DTE = :ML901-END-FSCL-WK-END-DTE                   
                  WITH UR                                                       
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               MOVE DTATTR-FSCL-WK-END-DTE TO ML901-END-FSCL-WK-END-DTE         
           ELSE                                                                 
               MOVE 'GET REQ. FSCL END DATE' TO PV-OPER-ATTEMPTED               
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           SET ML901-NOT-COMMENT-RECORD    TO TRUE.                             
           DISPLAY ' USING FISCAL WEEKEND DATES: '                              
                   ML901-BEG-FSCL-WK-END-DTE ' - '                              
                   ML901-END-FSCL-WK-END-DTE.                                   
                                                                                
      ******************************************************************        
      *    GET DEFAULT DATES FROM ML CONTROL                                    
      ******************************************************************        
       C150-GET-ML-CONTROL.                                                     
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                       B.FSCL_MN_BEG_DTE                                        
                      ,B.FSCL_MN_END_DTE                                        
                      ,B.PFM_END_DTE                                            
                      ,B.WK_IN_MN_NBR                                           
                  INTO                                                          
                       :DTATTR-FSCL-MN-BEG-DTE                                  
                      ,:DTATTR-FSCL-MN-END-DTE                                  
                      ,:DTATTR-PFM-END-DTE                                      
                      ,:DTATTR-WK-IN-MN-NBR                                     
                  FROM                                                          
                       TMLCNTL  A                                               
                      ,TDTATTR  B                                               
                  WHERE                                                         
                       B.KY_DTE = A.OPN_FSCL_WK_DTE                             
                  WITH UR                                                       
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               IF DTATTR-WK-IN-MN-NBR = '1'                                     
                   DISPLAY ' 1ST FSCL WK, GET MN BEG DATE FOR LAST MN'          
                   EXEC SQL                                                     
                        SELECT                                                  
                               B.FSCL_MN_BEG_DTE                                
                          INTO                                                  
                               :DTATTR-FSCL-MN-BEG-DTE                          
                          FROM                                                  
                               TDTATTR    B                                     
                          WHERE                                                 
                               B.KY_DTE    = :DTATTR-PFM-END-DTE                
                          WITH UR                                               
                   END-EXEC                                                     
                   IF SQLCODE NOT = ZEROES                                      
                       MOVE 'GET LAST MN BEG DATE' TO PV-OPER-ATTEMPTED         
                       PERFORM Z900-SQL-ABEND                                   
                   END-IF                                                       
               END-IF                                                           
               MOVE DTATTR-FSCL-MN-BEG-DTE TO ML901-BEG-FSCL-WK-END-DTE         
               MOVE DTATTR-FSCL-MN-END-DTE TO ML901-END-FSCL-WK-END-DTE         
           ELSE                                                                 
               MOVE 'GET BEG/END REQ DATE' TO PV-OPER-ATTEMPTED                 
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    OPEN PARTITION CONTROL CURSOR FOR GIVEN DATES                        
      ******************************************************************        
       Z200-OPEN-PARTN-CNTL.                                                    
                                                                                
           EXEC SQL                                                             
               OPEN PARTN-CNTL-CSR                                              
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               SET NOT-END-OF-PARTN-CNTL  TO TRUE                               
           ELSE                                                                 
               DISPLAY '*** BEG DATE ' ML901-BEG-FSCL-WK-END-DTE                
                       ' END DATE '    ML901-END-FSCL-WK-END-DTE                
               MOVE 'OPEN PARTN-CNTL-CSR' TO PV-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    FETCH THE NEXT RECORD FROM PARTITION CONTROL CURSOR                  
      ******************************************************************        
       Z250-FETCH-PARTN-CNTL.                                                   
                                                                                
           EXEC SQL                                                             
             FETCH PARTN-CNTL-CSR                                               
              INTO :MLT00003-PARTN-NBR                                          
                  ,:MLT00003-FSCL-DTE                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   DISPLAY ' PROCESSING PARTITION '                             
                           MLT00003-PARTN-NBR                                   
                           ' FOR FISCAL DATE '                                  
                           MLT00003-FSCL-DTE                                    
                           ' ... ' FUNCTION CURRENT-DATE                        
               WHEN +100                                                        
                   SET END-OF-PARTN-CNTL TO TRUE                                
               WHEN OTHER                                                       
                   MOVE 'FETCH PARTN-CNTL-CSR' TO PV-OPER-ATTEMPTED             
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    CLOSE PARTITION CONTROL CURSOR FOR GIVEN REQUEST DATES               
      ******************************************************************        
       Z275-CLOSE-PARTN-CNTL.                                                   
                                                                                
           EXEC SQL                                                             
                CLOSE PARTN-CNTL-CSR                                            
           END-EXEC                                                             
           IF SQLCODE NOT = +0                                                  
               MOVE 'CLOSE PARTN-CNTL-CSR' TO PV-OPER-ATTEMPTED                 
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    WRITE OUT COMMON EXTRACT                                             
      ******************************************************************        
       Z400-WRITE-EXTRACT.                                                      
                                                                                
           WRITE MLT-WKLY-SUM-CC-REC FROM PW-PROGRAM-WORK-REC.                  
                                                                                
      ******************************************************************        
      *    READ CONTROL CARD                                                    
      ******************************************************************        
       Z500-READ-REQUEST-CNTL.                                                  
                                                                                
           READ REQUEST-CNTL-FILE                                               
               INTO ML901-WKLY-SUM-EXTRACT-DATES                                
               AT END SET END-OF-REQUEST-CNTL TO TRUE.                          
                                                                                
      ******************************************************************        
      *    SQL ERROR - ABEND                                                    
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  MLKUT909'.                             
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED                    
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           PERFORM Z999-ABEND.                                                  
                                                                                
      ******************************************************************        
      *    ABEND CALL                                                           
      ******************************************************************        
       Z999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
