         PRINT GEN                                                              
*.*********************************************************************         
*.                 KOHL'S DEPARTMENT STORES                           *         
*.                                                                    *         
*.*MODIFICATION HISTORY --------------------------------------------- *         
*.   - DATE -   - PROGRAMMER -            - DESCRIPTION -             *         
*.   03/12/98     D.DREW                    WRITE PROGRAM             *         
*.                                                                    *         
*.                                                                    *         
*.*PROGRAM LOCATION ------------------------------------------------- *         
*.         SOURCE LIBRARY: PKMO.DP.SRCLIB                             *         
*.          LOAD  LIBRARY: SYSL.LINKLIB                               *         
*.                                                                    *         
*.*FUNCTION OF PROGRAM:                                               *         
*.             PROGRAM CALLED FROM A FORTRAN/COBOL PROGRAM TO:        *         
*.               1) WRITE A MESSAGE ON THE CONSOLE                    *         
*.               2) BASED ON MESSAGE TYPE (E) HIGHLIGHT/NOROLL        *         
*.               3) BASED ON MESSAGE TYPE (I) JUST NORMAL MESSAGE     *         
*.                                                                    *         
*.                                                                    *         
*.*LIST OF ABEND AND/OR RETURN CODES - (COMPLETE IF APPLICABLE) ----- *         
*.      CODES         CAUSES AND REMEDIES                             *         
*.                                                                    *         
*.      0000          OK                                              *         
*.      4000          NEED 4 PARMS                                    *         
*.      4001          CODE NOT  "C"  OR  "D"                          *         
*.      4002          8 BYTE FIELD NOT Y,N OR BLANKS                  *         
*.      4003          8 BYTE FIELD ALL BLANKS FOR CHANGE              *         
*.                                                                    *         
*.*LINKAGE EDITOR ATTRIBUTES AND CONSIDERATIONS --------------------- *         
*.     NORMAL LKED ATTRIBUTES.                                        *         
*.            NONE                                                    *         
*.                                                                    *         
*.    PLACE IN "SYSL.LINKLIB"                                         *         
*. ENTRY POINTS:                                                      *         
*.    DPKUT175                                                        *         
*.                                                                    *         
*.                                                                    *         
*.*HOW TO USE:  (LIST JCL, CONTROL CARDS, ETC*) --------------------- *         
*.                                                                    *         
*.                                                                    *         
*. DOCUMENTATION FOR PRINTING FOLLOWS:                                *         
*.                                                                    *         
***********************************************************************         
*                                                                               
*                                                                               
* PARMS REQUIRED:                                                               
*                                                                               
*           COBOL                                  FORTRAN                      
*    ------------------------                 -----------------------           
*    01 MESSAGE  PIC X(55)                    CHARACTER*55 MESSAGE              
*                                                                               
*                                                                               
*                                                                               
* SAMPLE COBOL CALL:                                                            
*      CALL DPKUT175 USING MESSAGE.                                             
*                                                                               
***********************************************************************         
DPKUT175 $PROLOG R12,R11,LV=500                                                 
DPKUT175 AMODE 31                                                               
DPKUT175 RMODE 24                                                               
* TESTING                                                                       
*        LA    R8,MSG                                                           
*        B     ENTRY010                                                         
*MSG     DC    CL55'KTS88888I - TESTING, PLEASE IGNORE'                         
* TESTING                                                                       
         L     R8,0(R1)           R8 = MESSAGE FIELD                            
         TM    0(R1),LASTPARM     LAST PARM ??                                  
         BO    ENTRY010           START THE PROCESSING                          
BBB005   EQU   *                  NO  PARMS PASSED ??                           
         L     R15,=F'4000'       ERROR ?????                                   
         B     REALEOJ            RETURN                                        
         EJECT                                                                  
***********************************************************************         
*        CHECK PARMS                                                  *         
***********************************************************************         
ENTRY010 DS    0H                                                               
         LA    R0,72(R13)         ADDRESS OF 'TO' FIELD                         
         LA    R14,@WORK          ADDRESS OF 'FROM' FIELD                       
         LA    R1,@SIZE           SIZE ONE FOR MVCL                             
         LR    R15,R1             SIZE TWO FOR MVCL                             
         MVCL  R0,R14             MOVE DATA TO GOTTEN STORAGE                   
         USING @SAVE,R13          INFORM ASSEMBLER                              
         LA    R15,3               GET AUTH FOR SVC35                           
         LA    R1,=CL4'AUTH'       GET AUTH FOR SVC35                           
         SVC   255                 GPSVC                                        
         MODESET KEY=ZERO,MODE=SUP  GET KEY ZERO AND AUTH                       
*                                                                               
         CLI   8(R8),C'I'         INFORMATIONAL MESSAGE ??                      
         BE    INFOMSG                                                          
         MVC   WTOMSG+4(55),0(R8)     MOVE IN MESSAGE                           
         WTO   MF=(E,WTOMSG)                                                    
         B     RESETAUT                                                         
INFOMSG  MVC   WTOMSGI+4(55),0(R8)     MOVE IN MESSAGE                          
         WTO   MF=(E,WTOMSGI)                                                   
         B     RESETAUT                                                         
RESETAUT MODESET KEY=NZERO,MODE=PROB  RELEASE AUTHORIZATION                     
         LA    R15,4               REQ UNAUTH                                   
         SVC   255                 GPSVC                                        
         B     EOJ                                                              
         EJECT                                                                  
*********************************************************************           
*        END OF JOB PROCESSING                                      *           
*********************************************************************           
EOJ      XR    R15,R15            ZERO RETURN CODE                              
         B     REALEOJ                                                          
*                                                                               
*EALEOJ  ABEND 777,DUMP                                                         
REALEOJ  $EPILOG ,                RETURN TO CALLER                              
         LTORG                                                                  
LASTPARM EQU   X'80'                                                            
*********************************************************************           
@SAVE    DS    18F            REG SAVE AREA (MUST BE FIRST)                     
@WORK    EQU   *                                                                
WTOMSG   WTO   '1234567890123456789012345678901234567890123456789012345X        
               ',ROUTCDE=2,DESC=1,MF=L                                          
WTOMSGI  WTO   '1234567890123456789012345678901234567890123456789012345X        
               ',ROUTCDE=2,MF=L                                                 
@SIZE    EQU   *-@WORK        SIZE OF WORKAREA                                  
***********************************************************************         
*        WORK AREA                                                    *         
***********************************************************************         
         END                                                                    
