00001  IDENTIFICATION DIVISION.                                         04/24/95
00002                                                                   ETKUP033
00003                                                                      LV022
00004  PROGRAM-ID.    ETKUP033.                                            CL**7
00005  AUTHOR.        STEVE FLUNKER.                                       CL**7
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                             CL**7
00007  DATE-WRITTEN.  04-14-95.                                            CL**7
00008  DATE-COMPILED.                                                      CL**7
00009                                                                      CL**7
00010 ******************************************************************   CL**7
00011 *      ETKUP033 - EDI/PO RECYCLE (INSERT/UPDATE)                     CL**7
00012 *                                                                    CL**7
00013 * THIS PROGRAM RUNS UNDER THE DB2 CHECKPOINT RESTART SYSTEM.         CL**7
00014 * IT READS THE PREPARED EDI PO'S TO BE RECYLCLED FILE FROM           CL**7
00015 * ETKUT033. FOR EACH RECORD READ, IT INSERTS A ROW INTO THE          CL**7
00016 * APPROPRIATE TABLE.                                                 CL**7
00017 *                                                                    CL**7
00018 ******************************************************************   CL**7
260107*  DATE  08/19/21                                                         
260107*  CHANGE MADE:                                                           
260107*   ADD COPYBOOK DPWS991 TO MAKE CALL TO DPKUT901 DYNAMIC                 
260107*  MADE BY: JIM HUNTER              WR# : CHG0260107                      
260107******************************************************************   CL**7
00019                                                                      CL**7
00020                                                                      CL**7
00021                                                                      CL**7
00022  ENVIRONMENT DIVISION.                                               CL**7
00023                                                                      CL**7
00024                                                                      CL**7
00025  CONFIGURATION SECTION.                                              CL**7
00026                                                                      CL**7
00027  SOURCE-COMPUTER.    IBM-3090.                                       CL**7
00028  OBJECT-COMPUTER.    IBM-3090.                                       CL**7
00029                                                                      CL**7
00030                                                                      CL**7
00031  INPUT-OUTPUT SECTION.                                               CL**7
00032                                                                      CL**7
00033  FILE-CONTROL.                                                       CL**7
00034                                                                      CL**8
00035  DATA DIVISION.                                                      CL**7
00036                                                                      CL**7
00037                                                                      CL**8
00038  WORKING-STORAGE SECTION.                                            CL**7
00039                                                                      CL**7
00040  01  ABEND-CODE                       PIC S9(04)    COMP SYNC        CL**7
00041                                                     VALUE +0.        CL**7
00042  01  PO-INSERT-RECORD.                                               CL*12
00043      05 I-TABLE-NAME              PIC X(07).                         CL*12
00044      05 I-TP-DKEY                 PIC S9(9) COMP.                    CL*12
00045      05 I-DATA-AREA               PIC X(354).                        CL*12
00046      05 I-TFGTRHL-AREA  REDEFINES I-DATA-AREA.                       CL*12
00047         10  I-TRN-SET-CDE         PIC X(3).                          CL*12
00048         10  I-TRN-DTL-TXT         PIC 9(9).                          CL*12
00049         10  I-RLSE-IND            PIC X(01).                         CL*12
00050         10  I-RLSE-DTE            PIC X(10).                         CL*12
00051         10  I-USER-ID             PIC X(08).                         CL*12
00052         10  FILLER                PIC X(323).                        CL*12
00053      05 I-TTRPOHL-AREA  REDEFINES I-DATA-AREA.                       CL*12
00054         10  I-PO-NBR              PIC 9(9).                          CL*12
00055         10  I-DEPT-NBR            PIC X(03).                         CL*12
00056         10  I-PO-RLSE-IND         PIC X(01).                         CL*12
00057         10  FILLER                PIC X(341).                        CL*12
00058      05 I-TTRTRDT-AREA  REDEFINES I-DATA-AREA.                       CL*20
00059         10  I-TRN-SET-SEQ-NBR     PIC S9(9) COMP.                    CL*12
00060         10  I-TRN-SET-DATA        PIC X(350).                        CL*12
00061                                                                      CL**7
00062  01  WS-SAVE-TIMESTAMP                PIC X(26) VALUE SPACES.        CL*20
00063  01  WS-PO-NBR-CONVERT.                                              CL*20
00064      05  WS-PO-NBR                    PIC X(09) VALUE SPACES.        CL*14
00065      05  FILLER                       PIC X(21) VALUE SPACES.        CL*14
00066  01  WS-PO-NBR-TRPOHL REDEFINES WS-PO-NBR-CONVERT.                   CL*15
00067      05  WS-PO-NBR-22                 PIC X(22).                     CL*18
00068      05  FILLER                       PIC X(08).                     CL*18
00069                                                                      CL*14
00070  01  PC-PROGRAM-CONSTANTS.                                           CL*14
00071      05  PC-MAX-RETRIES               PIC S9(04)    COMP SYNC        CL*14
00072                                                     VALUE +3.        CL*14
00073      05  PC-MAX-DEADLOCKS             PIC S9(04)    COMP SYNC        CL**7
00074                                                     VALUE +3.        CL**7
00075      05  PC-CKPT-JOB-NAME             PIC  X(08)    VALUE            CL**7
00076                                                          'ETK160'.   CL**7
00077      05  PC-CKPT-PLAN-NAME            PIC  X(08)    VALUE            CL**7
00078                                                        'ETKUP033'.   CL**7
00079      05  PC-TRAN-PRES-FUNC-CODES.                                    CL**7
00080          10  PC-TRAN-PRES-FUNC-OPEN   PIC  X(05)    VALUE 'OPEN'.    CL**7
00081          10  PC-TRAN-PRES-FUNC-TRANS  PIC  X(05)    VALUE 'TRANS'.   CL**7
00082          10  PC-TRAN-PRES-FUNC-RSTRT  PIC  X(05)    VALUE 'RSTRT'.   CL**7
00083          10  PC-TRAN-PRES-FUNC-CLOSE  PIC  X(05)    VALUE 'CLOSE'.   CL**7
00084                                                                      CL**7
00085  01  PS-PROGRAM-SWITCHES.                                            CL**7
00086      05  PS-RESTART-REQUIRED-SW       PIC X         VALUE 'N'.       CL**7
00087          88  PS-RESTART-REQUIRED                    VALUE 'Y'.       CL**7
00088          88  PS-RESTART-NOT-REQUIRED                VALUE 'N'.       CL**7
00089                                                                      CL**7
00090  01  PV-PROGRAM-VARIABLES.                                           CL**7
00091      05  PV-SQL-SUB                   PIC 9(03)     VALUE ZERO.      CL**7
00092      05  PV-DEADLOCK-COUNTER          PIC 9(03)     VALUE ZERO.      CL**7
00093      05  PV-PARAGRAPH-NAME            PIC X(30)     VALUE SPACE.     CL**7
00094      05  PV-DB2-OPER-ATTEMPTED        PIC X(30)     VALUE SPACE.     CL*19
00095                                                                      CL**7
00096 *****************************************************************    CL**7
00097 *  DCLGEN FOR THE FUNCTIONAL GROUP HELD TABLE.                       CL**7
00098 *****************************************************************    CL**7
00099                                                                      CL**7
00100      EXEC SQL                                                        CL**7
00101          INCLUDE TFGTRHL                                             CL**7
00102      END-EXEC.                                                       CL**7
00103                                                                      CL**7
00104 *****************************************************************    CL**7
00105 *  DCLGEN FOR THE PO HELD TABLE.                                     CL**7
00106 *****************************************************************    CL**7
00107                                                                      CL**7
00108      EXEC SQL                                                        CL**7
00109          INCLUDE TTRPOHL                                             CL**7
00110      END-EXEC.                                                       CL**7
00111                                                                      CL**7
00112 *****************************************************************    CL**7
00113 *  DCLGEN FOR THE FUNCTIONAL GROUP HELD DATA TABLE.                  CL**7
00114 *****************************************************************    CL**7
00115                                                                      CL**7
00116      EXEC SQL                                                        CL**7
00117          INCLUDE TFGTRDT                                             CL**7
00118      END-EXEC.                                                       CL**7
00119                                                                      CL**7
00120 *****************************************************************    CL**7
00121 *  COMMUNICATIONS AREA FOR DB2                                       CL**7
00122 *****************************************************************    CL**7
00123                                                                      CL**7
00124      COPY SQLCA2.                                                    CL**7
00125                                                                      CL**7
00126                                                                      CL**7
00127 *****************************************************************    CL**7
00128 *  DB2 FORMATTED ERROR MESSAGE                                       CL**7
00129 *****************************************************************    CL**7
00130                                                                      CL**7
00131      COPY DPWS004.                                                   CL**7
00132                                                                      CL**7
00133                                                                      CL**7
00134 *****************************************************************    CL**7
00135 *  TRANSACTION PRESENTATION MODULE COMMUNICATIONS AREA               CL**7
00136 *****************************************************************    CL**7
00137                                                                      CL**7
260107     COPY DPWS991.                                                   CL**7
00137                                                                      CL**7
00138      COPY DPLS001.                                                   CL**7
00139      05  WORKING-STORAGE-PASSED.                                     CL**7
00140          10  FILLER                   PIC X.                         CL**7
00141                                                                      CL**7
00142  01  FILLER                           PIC  X(4096)  VALUE SPACE.     CL**7
00143                                                                      CL**7
00144 ******************************************************************   CL**7
00145 *  COMMUNICATIONS AREA FOR DB2                                   *   CL**7
00146 ******************************************************************   CL**7
00147                                                                      CL**7
00148      EXEC SQL                                                        CL**7
00149          INCLUDE SQLCA                                               CL**7
00150      END-EXEC.                                                       CL**7
00151  EJECT                                                               CL**7
00152  PROCEDURE DIVISION.                                                 CL**7
00153                                                                      CL**7
00154                                                                      CL**7
00155  A100-PROGRAM-MAINLINE.                                              CL**7
00156                                                                      CL**7
00157                                                                      CL**9
00158      MOVE PC-TRAN-PRES-FUNC-OPEN TO DP001-FUNC-CODE.                 CL**7
00159      PERFORM Z100-BUILD-COMM-AREA-TRAN-PRES.                         CL**7
00160                                                                      CL**7
00161      PERFORM B200-PROCESS-PO-RECYCLE-RECS                            CL**7
00162          UNTIL DP001-PREP-TRANS-EOF.                                 CL**7
00163                                                                      CL**7
00164      MOVE PC-TRAN-PRES-FUNC-CLOSE TO DP001-FUNC-CODE.                CL**7
00165      PERFORM Z100-BUILD-COMM-AREA-TRAN-PRES.                         CL**7
00166                                                                      CL**7
00167                                                                      CL**9
00168      STOP RUN.                                                       CL**7
00169                                                                      CL**7
00170                                                                      CL**7
00171 ******************************************************************   CL**7
00172 * PROCESS THE RECORDS ON THE PO RECLYCLE FILE.  FOR EACH             CL**7
00173 * RECORD ON THE FILE, BUILD AND INSERT A ROW IN THE APPROPRIATE      CL**7
00174 * TABLE.                                                             CL**7
00175 ******************************************************************   CL**7
00176  B200-PROCESS-PO-RECYCLE-RECS.                                       CL**7
00177                                                                      CL**7
00178      SET PS-RESTART-NOT-REQUIRED TO TRUE.                            CL**7
00179                                                                      CL**7
00180      PERFORM C100-BUILD-INSERT-ROW.                                  CL*13
00181                                                                      CL**7
00182 *    *------------------------------------------------------------   CL**7
00183 *    * IF A RESTART IS REQUIRED, ISSUE A RESTART CALL RATHER THAN    CL**7
00184 *    * A TRANS CALL. THIS WILL ALLOW PROCESSING TO RESUME WITH       CL**7
00185 *    * THE FIRST TRANSACTION AFTER THE LAST COMMIT.                  CL**7
00186 *    *------------------------------------------------------------   CL**7
00187      IF PS-RESTART-REQUIRED                                          CL**7
00188          MOVE PC-TRAN-PRES-FUNC-RSTRT TO DP001-FUNC-CODE             CL**7
00189      ELSE                                                            CL**7
00190          MOVE PC-TRAN-PRES-FUNC-TRANS TO DP001-FUNC-CODE             CL**7
00191      END-IF.                                                         CL**7
00192                                                                      CL**7
00193      PERFORM Z100-BUILD-COMM-AREA-TRAN-PRES.                         CL**7
00194 *                                                                    CL*13
00195 ******************************************************************   CL**7
00196 * DETERMINE WHICH TABLE THE ROW SHOULD BE INSERTED INTO AND          CL*13
00197 * PERFORM THE APPROPRIATE INSERT.                                    CL*13
00198 ******************************************************************   CL**7
00199  C100-BUILD-INSERT-ROW.                                              CL*13
00200                                                                      CL**7
00201      IF I-TABLE-NAME = 'TFGTRHL'                                     CL*13
00202         MOVE I-TRN-DTL-TXT TO WS-PO-NBR                              CL*17
00203                                                                      CL*20
00204         EXEC SQL                                                     CL*20
00205            SET :WS-SAVE-TIMESTAMP = CURRENT TIMESTAMP                CL*20
00206         END-EXEC                                                     CL*20
00207                                                                      CL*20
00208         EVALUATE TRUE                                                CL*20
00209            WHEN SQLCODE = 0                                          CL*20
00210               CONTINUE                                               CL*20
00211            WHEN OTHER                                                CL*20
00212               MOVE 'C100-BUILD-INSERT-ROW' TO                        CL*20
00213                    PV-PARAGRAPH-NAME                                 CL*20
00214               MOVE 'SELECT CURRENT TIMESTAMP' TO                     CL*20
00215                    PV-DB2-OPER-ATTEMPTED                             CL*21
00216               PERFORM Z900-SQL-ABEND                                 CL*20
00217         END-EVALUATE                                                 CL*20
00218                                                                      CL*20
00219         PERFORM Z200-INSERT-TFGTRHL-ROW                              CL*13
00220      ELSE                                                            CL*13
00221         IF I-TABLE-NAME = 'TTRPOHL'                                  CL*22
00222            MOVE I-PO-NBR TO WS-PO-NBR                                CL*14
00223            PERFORM Z300-INSERT-TTRPOHL-ROW                           CL*22
00224         ELSE                                                         CL*13
00225            IF I-TABLE-NAME = 'TFGTRDT'                               CL*22
00226               PERFORM Z400-INSERT-TFGTRDT-ROW                        CL*13
00227            END-IF                                                    CL*13
00228         END-IF                                                       CL*13
00229      END-IF.                                                         CL*13
00230 *                                                                    CL*13
00231 ******************************************************************   CL*13
00232 * BUILD THE COMMUNICATION AREA FOR A CALL TO THE TRANSACTION         CL*13
00233 * PRESENTATION MODULE.                                               CL*13
00234 ******************************************************************   CL*13
00235                                                                      CL*13
00236  Z100-BUILD-COMM-AREA-TRAN-PRES.                                     CL*13
00237                                                                      CL*13
00238      MOVE LENGTH OF WORKING-STORAGE-PASSED                           CL*11
00239                              TO DP001-WORK-STRG-LENGTH.              CL**7
00240      MOVE PC-CKPT-JOB-NAME   TO DP001-JOB-NAME.                      CL**7
00241      MOVE PC-CKPT-PLAN-NAME  TO DP001-PLAN-NAME.                     CL**7
00242      PERFORM Z900-CALL-TRANS-PRES-MODULE.                            CL**7
00243      MOVE DP001-TRANS-RECORD TO PO-INSERT-RECORD.                    CL*13
00244      IF DP001-CKPT-TAKEN                                             CL**7
00245          INITIALIZE PV-DEADLOCK-COUNTER                              CL**7
00246      END-IF.                                                         CL**7
00247                                                                      CL**7
00248 ******************************************************************   CL*10
00249 *  INSERT ROW INTO THE TFGTRHL TABLE                                 CL*10
00250 ******************************************************************   CL*10
00251  Z200-INSERT-TFGTRHL-ROW.                                            CL*10
00252 *                                                                    CL*10
00253      PERFORM                                                         CL*10
00254          WITH TEST AFTER                                             CL*10
00255          VARYING PV-SQL-SUB                                          CL*10
00256          FROM 1 BY 1                                                 CL*10
00257          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL*10
00258                    OR SQLCODE = -911                                 CL*10
00259                    OR SQLCODE = 0)                                   CL*10
00260 *                                                                    CL*10
00261          EXEC SQL                                                    CL*10
00262              INSERT INTO TFGTRHL                                     CL*10
00263                     (TP_DKEY,                                        CL*10
00264                      TP_DKEY_TMST,                                   CL*10
00265                      TRN_SET_CDE,                                    CL*10
00266                      TRN_DTL_TXT,                                    CL*10
00267                      HOLD_DTE,                                       CL*16
00268                      RLSE_IND,                                       CL*10
00269                      RLSE_DTE,                                       CL*10
00270                      USER_ID)                                        CL*10
00271              VALUES (:I-TP-DKEY,                                     CL*10
00272                      :WS-SAVE-TIMESTAMP,                             CL*20
00273                      :I-TRN-SET-CDE,                                 CL*10
00274                      :WS-PO-NBR-CONVERT,                             CL*14
00275                      CURRENT DATE,                                   CL*10
00276                      :I-RLSE-IND,                                    CL*10
00277                      :I-RLSE-DTE,                                    CL*10
00278                      :I-USER-ID)                                     CL*10
00279          END-EXEC                                                    CL*10
00280 *                                                                    CL*10
00281          EVALUATE TRUE                                               CL*10
00282              WHEN SQLCODE = -904                                     CL*10
00283              WHEN SQLCODE = 0                                        CL*10
00284                  CONTINUE                                            CL*10
00285              WHEN SQLCODE = -911                                     CL*10
00286                  ADD +1 TO PV-DEADLOCK-COUNTER                       CL*10
00287                  IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS           CL*10
00288                      MOVE 'Z200-INSERT-TFGTRHL-ROW'                  CL*14
00289                                      TO PV-PARAGRAPH-NAME            CL*10
00290                      MOVE 'MAX DEADLOCKS EXCEEDED'                   CL*10
00291                                      TO PV-DB2-OPER-ATTEMPTED        CL*19
00292                      PERFORM Z900-SQL-ABEND                          CL*10
00293                  ELSE                                                CL*10
00294                      SET PS-RESTART-REQUIRED TO TRUE                 CL*10
00295                  END-IF                                              CL*10
00296              WHEN SQLWARN NOT = SPACES                               CL*10
00297              WHEN SQLCODE NOT = ZERO                                 CL*10
00298                  MOVE 'Z200-INSERT-TFGTRHL-ROW'                      CL*10
00299                                TO  PV-PARAGRAPH-NAME                 CL*10
00300                  MOVE 'INSERT ROW INTO TFGTRHL'                      CL*10
00301                                TO  PV-DB2-OPER-ATTEMPTED             CL*10
00302                  PERFORM Z900-SQL-ABEND                              CL*10
00303          END-EVALUATE                                                CL*10
00304      END-PERFORM.                                                    CL*10
00305 *                                                                    CL*10
00306      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL*10
00307          MOVE 'Z200-INSERT-TFGTRHL-ROW'                              CL*10
00308                                TO  PV-PARAGRAPH-NAME                 CL*10
00309          MOVE 'VERIFY RETRIES EXCEEDED'                              CL*10
00310                                TO  PV-DB2-OPER-ATTEMPTED             CL*10
00311          PERFORM Z900-SQL-ABEND                                      CL*10
00312      END-IF.                                                         CL*10
00313 *                                                                    CL*10
00314 ******************************************************************   CL*10
00315 *  INSERT ROW INTO THE TTRPOHL TABLE                                 CL*16
00316 ******************************************************************   CL*10
00317  Z300-INSERT-TTRPOHL-ROW.                                            CL*22
00318 *                                                                    CL*10
00319      PERFORM                                                         CL*10
00320          WITH TEST AFTER                                             CL*10
00321          VARYING PV-SQL-SUB                                          CL*10
00322          FROM 1 BY 1                                                 CL*10
00323          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL*10
00324                    OR SQLCODE = -911                                 CL*10
00325                    OR SQLCODE = 0)                                   CL*10
00326 *                                                                    CL*10
00327          EXEC SQL                                                    CL*10
00328              INSERT INTO TTRPOHL                                     CL*16
00329                     (TP_DKEY,                                        CL*10
00330                      TP_DKEY_TMST,                                   CL*10
00331                      PO_NBR,                                         CL*10
00332                      DEPT_NBR,                                       CL*10
00333                      PO_RLSE_IND)                                    CL*10
00334              VALUES (:I-TP-DKEY,                                     CL*10
00335                      :WS-SAVE-TIMESTAMP,                             CL*20
00336                      :WS-PO-NBR-22,                                  CL*15
00337                      :I-DEPT-NBR,                                    CL*10
00338                      :I-PO-RLSE-IND)                                 CL*10
00339          END-EXEC                                                    CL*10
00340 *                                                                    CL*10
00341          EVALUATE TRUE                                               CL*10
00342              WHEN SQLCODE = -904                                     CL*10
00343              WHEN SQLCODE = 0                                        CL*10
00344                  CONTINUE                                            CL*10
00345              WHEN SQLCODE = -911                                     CL*10
00346                  ADD +1 TO PV-DEADLOCK-COUNTER                       CL*10
00347                  IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS           CL*10
00348                      MOVE 'Z300-INSERT-TTRPOHL-ROW'                  CL*16
00349                                      TO PV-PARAGRAPH-NAME            CL*10
00350                      MOVE 'MAX DEADLOCKS EXCEEDED'                   CL*10
00351                                      TO PV-DB2-OPER-ATTEMPTED        CL*19
00352                      PERFORM Z900-SQL-ABEND                          CL*10
00353                  ELSE                                                CL*10
00354                      SET PS-RESTART-REQUIRED TO TRUE                 CL*10
00355                  END-IF                                              CL*10
00356              WHEN SQLWARN NOT = SPACES                               CL*10
00357              WHEN SQLCODE NOT = ZERO                                 CL*10
00358                  MOVE 'Z300-INSERT-TTRPOHL-ROW'                      CL*16
00359                                TO  PV-PARAGRAPH-NAME                 CL*10
00360                  MOVE 'INSERT ROW INTO TTRPOHL'                      CL*16
00361                                TO  PV-DB2-OPER-ATTEMPTED             CL*10
00362                  PERFORM Z900-SQL-ABEND                              CL*10
00363          END-EVALUATE                                                CL*10
00364      END-PERFORM.                                                    CL*10
00365 *                                                                    CL*10
00366      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL*10
00367          MOVE 'Z300-INSERT-TTRPOHL-ROW'                              CL*16
00368                                TO  PV-PARAGRAPH-NAME                 CL*10
00369          MOVE 'VERIFY RETRIES EXCEEDED'                              CL*10
00370                                TO  PV-DB2-OPER-ATTEMPTED             CL*10
00371          PERFORM Z900-SQL-ABEND                                      CL*10
00372      END-IF.                                                         CL*10
00373 *                                                                    CL*10
00374 ******************************************************************   CL*10
00375 *  INSERT ROW INTO THE TFGTRDT TABLE                                 CL*10
00376 ******************************************************************   CL*10
00377  Z400-INSERT-TFGTRDT-ROW.                                            CL*10
00378 *                                                                    CL*10
00379      PERFORM                                                         CL*10
00380          WITH TEST AFTER                                             CL*10
00381          VARYING PV-SQL-SUB                                          CL*10
00382          FROM 1 BY 1                                                 CL*10
00383          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL*10
00384                    OR SQLCODE = -911                                 CL*10
00385                    OR SQLCODE = 0)                                   CL*10
00386 *                                                                    CL*10
00387          EXEC SQL                                                    CL*10
00388              INSERT INTO TFGTRDT                                     CL*10
00389                     (TP_DKEY,                                        CL*10
00390                      TP_DKEY_TMST,                                   CL*10
00391                      TRN_SET_SEQ_NBR,                                CL*10
00392                      TRN_SET_DATA)                                   CL*10
00393              VALUES (:I-TP-DKEY,                                     CL*10
00394                      :WS-SAVE-TIMESTAMP,                             CL*20
00395                      :I-TRN-SET-SEQ-NBR,                             CL*10
00396                      :I-TRN-SET-DATA)                                CL*10
00397          END-EXEC                                                    CL*10
00398 *                                                                    CL*10
00399          EVALUATE TRUE                                               CL*10
00400              WHEN SQLCODE = -904                                     CL*10
00401              WHEN SQLCODE = 0                                        CL*10
00402                  CONTINUE                                            CL*10
00403              WHEN SQLCODE = -911                                     CL*10
00404                  ADD +1 TO PV-DEADLOCK-COUNTER                       CL*10
00405                  IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS           CL*10
00406                      MOVE 'Z400-INSERT-TFGTRDT-ROW'                  CL*14
00407                                      TO PV-PARAGRAPH-NAME            CL*10
00408                      MOVE 'MAX DEADLOCKS EXCEEDED'                   CL*10
00409                                      TO PV-DB2-OPER-ATTEMPTED        CL*19
00410                      PERFORM Z900-SQL-ABEND                          CL*10
00411                  ELSE                                                CL*10
00412                      SET PS-RESTART-REQUIRED TO TRUE                 CL*10
00413                  END-IF                                              CL*10
00414              WHEN SQLWARN NOT = SPACES                               CL*10
00415              WHEN SQLCODE NOT = ZERO                                 CL*10
00416                  MOVE 'Z400-INSERT-TFGTRDT-ROW'                      CL*10
00417                                TO  PV-PARAGRAPH-NAME                 CL*10
00418                  MOVE 'INSERT ROW INTO TFGTRDT'                      CL*10
00419                                TO  PV-DB2-OPER-ATTEMPTED             CL*10
00420                  PERFORM Z900-SQL-ABEND                              CL*10
00421          END-EVALUATE                                                CL*10
00422      END-PERFORM.                                                    CL*10
00423 *                                                                    CL*10
00424      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL*10
00425          MOVE 'Z400-INSERT-TFGTRDT-ROW'                              CL*10
00426                                TO  PV-PARAGRAPH-NAME                 CL*10
00427          MOVE 'VERIFY RETRIES EXCEEDED'                              CL*10
00428                                TO  PV-DB2-OPER-ATTEMPTED             CL*10
00429          PERFORM Z900-SQL-ABEND                                      CL*10
00430      END-IF.                                                         CL*10
00431 *                                                                    CL*10
00432 ******************************************************************   CL*10
00433 * ABEND THE PROGRAM IN THE EVENT THAT AN SQL ERROR OR WARNING        CL*10
00434 * CODE OCCURS.                                                       CL*10
00435 ******************************************************************   CL*10
00436  Z900-SQL-ABEND.                                                     CL*10
00437                                                                      CL*10
00438      DISPLAY '** ABEND     **'.                                      CL**7
00439      DISPLAY '** PROGRAM   ** = ETKUP033'.                           CL**7
00440      DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                 CL**7
00441      DISPLAY '** OPERATION ** = ' PV-DB2-OPER-ATTEMPTED.             CL*19
00442                                                                      CL**7
00443                                                                      CL**7
00444 ******************************************************************   CL**7
00445 * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-       CL**7
00446 * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE            CL**7
00447 * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE        CL**7
00448 * FORMAT.                                                            CL**7
00449 ******************************************************************   CL**7
00450      COPY DPPD004.                                                   CL**7
00451                                                                      CL**7
00452      MOVE +4013 TO ABEND-CODE.                                       CL**7
00453      CALL 'ILBOABN0' USING ABEND-CODE.                               CL**7
00454                                                                      CL**7
00455 ******************************************************************   CL**7
00456 * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-       CL**7
00457 * MENTS THAT ARE REQUIRED TO INVOKE THE TRANSACTION PRESENTATION     CL**7
00458 * MODULE (DPKUT901) OF THE DB2 CHECKPOINT RESTART SYSTEM.            CL**7
00459 ******************************************************************   CL**7
00460      COPY DPPD001.                                                   CL**7
